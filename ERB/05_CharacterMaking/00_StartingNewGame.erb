@START_CHARA_MAKE
#DIM MEMBERS
#DIM CCOUNT

PRINTL

$MASTER_LOOP_CHARAMAKE
FIRSTLINE = LINECOUNT

IF FLAG:0 == 2 ;ソロモード
	MEMBERS = 1

ELSE
	MEMBERS = 3
	IF (GLOBAL:100 + GLOBAL:101 + GLOBAL:102) > 0 ;周回ボーナス

		CALL COLORLINE(1,"-")
		ALIGNMENT CENTER
		PRINTL
		CALL CPRINT("・・★   ",,1)
		CALL CPRINT("周回クリアボーナス",COLOR_YELLOW,1)
		CALL CPRINT("  ★・・",)
		CALL ADD_LINE(2)

		CALL CPRINT("☆　",COLOR_YELLOW,1)
		PRINTBUTTON @"%"1.　一人減らす　　",30,LEFT%%"(二人で開始)",40,RIGHT%",1
		PRINTL

		CALL CPRINT("☆　",COLOR_YELLOW,1)
		PRINTBUTTON @"%"2.　デフォルト　　",30,LEFT%%"(三人で開始)",40,RIGHT%",2
		PRINTL

		CALL CPRINT("☆　",COLOR_YELLOW,1)
		PRINTBUTTON @"%"3.　一人増やす　　",30,LEFT%%"(四人で開始)",40,RIGHT%",3
		PRINTL

		CALL CPRINT("☆　",COLOR_YELLOW,1)
		PRINTBUTTON @"%"4.　二人増やす　　",30,LEFT%%"(五人で開始)",40,RIGHT%",4
		PRINTL

		CALL CPRINT("☆　",COLOR_YELLOW,1)
		PRINTBUTTON @"%"5.　三人増やす　　",30,LEFT%%"(六人で開始)",40,RIGHT%",5
		PRINTL

		CALL CPRINT("☆　",COLOR_YELLOW,1)
		PRINTBUTTON @"%"9.　初期セットから選ぶ",75,LEFT%",9
		PRINTL

		CALL ADD_LINE(6)
		CALL COLORLINE(2,"-")
		ALIGNMENT LEFT

		PRINTFORML %INDENT(8)%[99] ゲームモード選択に戻る
		$INPUT_LOOP_CHARA_NUM
		INPUT

		CLEARLINE 1
		SELECTCASE RESULT
			CASE 1 TO 5
			MEMBERS = RESULT + 1
			CALL COLORLINE(2)
			PRINTL
			CALL STARPRINT(@"{MEMBERS}人で開始します")

			CASE 9
			GOTO SELECTED_SHOKISET

			CASE  99
			CLEARLINE LINECOUNT
			RETURN 99
		    CASEELSE
			GOTO INPUT_LOOP_CHARA_NUM
		ENDSELECT

	ENDIF

ENDIF
;ソロモード以外でデフォルト人数でなかった場合は制限日数が変動する
IF FLAG:0 != 2 
	FLAG:2 -= MEMBERS - 3
	SIF FLAG:0 != 10
		FLAG:1 = (FLAG:3 * FLAG:2) + (FLAG:4 * 10)
ENDIF
PRINTL 
CALL COLORLINE(1,"-")
CALL ADD_LINE(2)
ALIGNMENT CENTER
CALL CPRINT("キャラメイキングを行いますか？")
PRINTL
CALL INPUTYN(1,2,)
SIF (GLOBAL:100 + GLOBAL:101 + GLOBAL:102) == 0 && FLAG:0 != 2
CALL LINK("3.　初期セットから選ぶ　★", 3, 24,COLOR_BUTTON,,1)
PRINTL

CALL ADD_LINE(6)
CALL COLORLINE(2,"-")
ALIGNMENT LEFT

PRINTFORML %INDENT(8)%[99] ゲームモード選択に戻る

	$INPUT_LOOP_CHARA_MAKE
	INPUT
	CLEARLINE 1

	IF RESULT == 2
		REPEAT MEMBERS
			ADDCHARA 0
			FLAG:8 ++
		REND
		CALL COLORLINE(2)
		PRINTL
		CALL STARPRINT("デフォルト設定で遊びます")

		;相性データのコンバート
		CALL CONVERT_RELATION

	ELSEIF RESULT == 1
		REPEAT MEMBERS
			ADDCHARA 0
			FLAG:8 ++
		REND
		CALL COLORLINE()
		PRINTL
		CALL STARPRINT("キャラメイキングを行います")

		CALL CHARA_MAKE_MAIN, 0

	ELSEIF RESULT == 3 && (GLOBAL:100 + GLOBAL:101 + GLOBAL:102) == 0 && FLAG:0 != 2
		$SELECTED_SHOKISET
		TRYCALL CHARA_MAKE_DEFAULT_KAI
		IF RESULT < 0
			CLEARLINE LINECOUNT
			GOTO MASTER_LOOP_CHARAMAKE
		ENDIF

	ELSEIF RESULT == 99
		CLEARLINE LINECOUNT
		RETURN 99
		
	ELSE
		GOTO INPUT_LOOP_CHARA_MAKE
	ENDIF
PRINTL 

;**********************************************************
;キャラクターデフォルト設定
CALL CHARA_MAKE_DEFAULT

;**********************************************************
;全キャラ共通処理

;専用口上の番号を設定
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE

	CFLAG:CCOUNT:6 = NO:CCOUNT

	SIF NO:CCOUNT == 0 && TALENT:CCOUNT:オトコ > 0
		CFLAG:CCOUNT:6 = 1

	SIF NO:CCOUNT == 0 && TALENT:CCOUNT:ロボっ子 > 0
		CFLAG:CCOUNT:6 = 2

	IF TALENT:CCOUNT:口上設定 == -2
		CFLAG:CCOUNT:6 = -2

	ELSEIF TALENT:CCOUNT:口上設定 == 0 && TALENT:CCOUNT:オトコ > 0
		CFLAG:CCOUNT:6 = 1

	ELSEIF TALENT:CCOUNT:口上設定 == 0
		CFLAG:CCOUNT:6 = 0

	ELSEIF TALENT:CCOUNT:口上設定 == 1
		CFLAG:CCOUNT:6 = 2

	ELSEIF TALENT:CCOUNT:口上設定 == 2
		CFLAG:CCOUNT:6 = 3

	ENDIF

NEXT