;モード選択
@MODE_SELECT
$MASTER_LOOP
FIRSTLINE = LINECOUNT
CALL SETFONT_EX("メイリオ", "Arial")
CALL ADD_LINE(8)

ALIGNMENT CENTER
PRINTL
CALL CPRINT("★　ゲームモードを選んでください　★",COLOR_YELLOW,)
CALL ADD_LINE(2)

CALL SLINK("01.  N O R M A L ☆","ノーマルモード",1,"BLUE","ORANGE",,2)
PRINTFORML 複数人のキャラクターで敵の全滅を目指す通常プレイ
PRINTL


CALL SLINK("02.  S O L O P L A Y ☆","ソロモード",2,"AQUA","YELLOW",,2)
PRINTFORML １人のキャラクターで挑むシンプルモード。お手軽プレイ向け
PRINTL 

CALL SLINK("03.  S T O R Y ☆","ストーリーモード",3,"MINTGREEN","YELLOW",,2)
PRINTFORML 固有キャラを主人公にしてストーリーを楽しむモード
PRINTL 

CALL SLINK("04.  H A R D C O R E ☆","ハードコアモード",4,"GREEN","PINK",,2)
PRINTFORML 難易度の高い上級者向けのチャレンジモード
PRINTL

CALL SLINK("05.  E N D L E S S ☆","エンドレスモード",5,"ORANGE","AQUA",,2)
PRINTFORML 無限にボスが出現するモード、難易度はNORMAL並み
PRINTL


;PRINT [6] REVERSE	
;PRINTPLAIN 触手となって襲い来る女の子を撃退していくモードです
;PRINTL 


[IF_DEBUG]
CALL SLINK("99.  D E B U G","口上色チェック",99,"BUTTON","SELECT",,2)
PRINTFORML 口上の色を比較、作成するデバックモード、口上作者用
PRINTL
[ENDIF]

CALL ADD_MAXLINE(32) 

CALL LINK("<100>  ゲームの説明", 100, 10,COLOR_LGRAY,,1)
PRINTFORM %INDENT(30)%
CALL LINK("タイトルに戻る  <101>", 101, 10,COLOR_LGRAY,,1)
PRINTL 
CALL ADD_MAXLINE(36)
ALIGNMENT LEFT


$INPUT_LOOP
INPUT
CLEARLINE 1
SELECTCASE RESULT
    CASE 1
	FLAG:0 = 1
    CLEARLINE LINECOUNT - FIRSTLINE
	CALL COLORLINE(2,"-")
	PRINTL
	CALL STARPRINT("NORMALモードでプレイします")

    CASE 2
	FLAG:0 = 2
    CLEARLINE LINECOUNT - FIRSTLINE
    CALL COLORLINE(2,"-")
	PRINTL
	CALL STARPRINT("SOLOモードでプレイします")

    CASE 3
    ;FLAG:0 = 3
    ;RETURN 516
    PRINTL
    PRINTW 未実装です
    CLEARLINE LINECOUNT
    GOTO MASTER_LOOP

    CASE 4
	FLAG:0 = 4
    CLEARLINE LINECOUNT - FIRSTLINE
    CALL COLORLINE(2,"-")
	PRINTL 
	CALL STARPRINT("HARDCOREモードでプレイします")
    
    CASE 5
    CALL COLORLINE(2,"-")
	FLAG:0 = 10
    PRINTL
    ALIGNMENT CENTER
    CALL CPRINT("★　ENDLESSモードのオプションを選択してください ★",COLOR_VERMILION,)
	PRINTL 
	PRINTFORML 撃破成績に応じて周回プレイが可能な SURVIVAL と
	PRINTFORML 周回できないが撃破期限が存在しない FREEPLAY が選択可能です
    PRINTL
    CALL ADD_LINE(2)
	CALL LINK("10.　SURVIVAL　★", 10, 12,COLOR_BUTTON,,1)
    PRINTFORM %INDENT(20)%
    CALL LINK("★　FREEPLAY　.11", 11, 12,COLOR_BUTTON,,1)
    PRINTL 
    CALL ADD_LINE(4)
    CALL COLORLINE(2,"-")
	PRINTFORML %INDENT(2)%[102] モード選択に戻る
    ALIGNMENT LEFT
	PRINTL 
	PRINTL 
	$INPUT_LOOP2
	INPUT
    CLEARLINE 1
	IF RESULT == 10
		FLAG:906 = 0
		CALL COLORLINE(2,"-")
		PRINTL
        CALL STARPRINT("ENDLESS:SURVIVALモードでプレイします")
	ELSEIF RESULT == 11
		FLAG:906 = 1
		CALL COLORLINE(2,"-")
		PRINTL
	    CALL STARPRINT("ENDLESS:FREEPLAYモードでプレイします")
	ELSEIF RESULT == 102
        FLAG:0 = 0
		CLEARLINE LINECOUNT
		GOTO MASTER_LOOP
	ELSE
		GOTO INPUT_LOOP2
	ENDIF

    [IF_DEBUG]
    CASE 99
    CLEARLINE LINECOUNT
    ADDVOIDCHARA
	CALL KOJO_COLOR_SETTING
	DELALLCHARA
	GOTO MASTER_LOOP
    [ENDIF]

    CASE 100
    CLEARLINE LINECOUNT - FIRSTLINE
    CALL STARPRINT("チュートリアルを確認します")
    CLEARLINE 1
    CALL TUTORIAL

    CASE 101
    PRINTL 
	CALL STARPRINT("タイトルに戻ります")
    CLEARLINE LINECOUNT
    RETURN 101

    CASEELSE
    GOTO INPUT_LOOP

ENDSELECT


SIF FLAG:0 < 1
GOTO MASTER_LOOP

RETURN 0