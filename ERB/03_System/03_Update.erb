
; グローバルをアップデート
;-------------------
@UPDATE_GLOBAL

IF GLOBAL:3 < 364
	GLOBAL:3 = 364
	;プロフィール表示グローバルを消去
	GLOBAL:9 = 0
	SAVEGLOBAL
ENDIF
IF GLOBAL:3 < 371
	GLOBAL:3 = 371
	MOB_FLAG:0:1 = 100
	MOB_GLOBAL:0:1 = 100
	SAVEGLOBAL
ENDIF
IF GLOBAL:3 < 380
	GLOBAL:3 = 380
	GLOBAL:0 = 0
	GLOBAL:1 = 0
	GLOBAL:2 = 0
	;コンフィグを初期設定に変更
	GLOBAL:10 = 0
	GLOBAL:11 = 4
	GLOBAL:12 = 31
	GLOBAL:13 = 88
	GLOBAL:14 = 5
	SAVEGLOBAL
ENDIF
IF GLOBAL:3 < 390
	GLOBAL:3 = 390
	;拡張度フィルターの値を反転
	INVERTBIT GLOBAL:4,16
	INVERTBIT GLOBAL:4,17
ENDIF

@UPDATE
#DIM CCOUNT
PRINTL 

;グローバルデータ管理***************************
LOADGLOBAL
IF RESULT == 1
	;コンフィグデータ読み込み
	IF CONFIG_CHECK_MAIN_F(0) > 0
		SIF GLOBAL:0 + GLOBAL:1 + GLOBAL:2 != 0
			PRINTW 【コンフィグ設定を読み込みました】
		FLAG:801 = GLOBAL:11
		FLAG:802 = GLOBAL:12
		FLAG:803 = GLOBAL:13
		FLAG:804 = GLOBAL:14
	ENDIF
	;性嗜好フィルタは常に適用する
	FLAG:850 = GLOBAL:4
	;雑魚敵フィルタは常に適用する
	FOR LOCAL,0,MOB_CATEGORY
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{LOCAL * 100 + LOCAL:1}
				MOB_FLAG:LOCAL:(LOCAL:1) = MOB_GLOBAL:LOCAL:(LOCAL:1)
			CATCH
				MOB_FLAG:LOCAL:(LOCAL:1) = 0
			ENDCATCH
		NEXT
	NEXT
	;グローバルのアップデート
	CALL UPDATE_GLOBAL
ENDIF


IF LASTLOAD_VERSION < 372 && LASTLOAD_VERSION != -1
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF NAME:CCOUNT == "博麗 霊夢" || NAME:CCOUNT == "霧雨 魔理沙" || NAME:CCOUNT == "東風谷 早苗"
			NO:CCOUNT += 1000
		SIF NAME:CCOUNT == "守崎 美紅" || NAME:CCOUNT == "ソフィー・アンドレア・雪籐" || NAME:CCOUNT == "菱堂 藍胡" || NAME:CCOUNT == "桜坂 ひかり" || NAME:CCOUNT == "茜島 蘭牙"
			NO:CCOUNT += 1000
		SIF NO:CCOUNT < 700
			CONTINUE
		SIF NAME:CCOUNT == "高町 なのは" || NAME:CCOUNT == "フェイト=テスタロッサ" || NAME:CCOUNT == "フェイト・T・ハラオウン" || NAME:CCOUNT == "八神 はやて" || NAME:CCOUNT == "高町 ヴィヴィオ" || NAME:CCOUNT == "アインハルト・ストラトス" || NAME:CCOUNT == "オリヴィエ・ゼーゲブレヒト" || NAME:CCOUNT == "ヴィクトーリア・ダールグリュン"
			NO:CCOUNT += 1000
		SIF NAME:CCOUNT == "島村卯月" || NAME:CCOUNT == "渋谷凛" || NAME:CCOUNT == "本田未央"
			NO:CCOUNT += 1000
	NEXT
ENDIF
IF LASTLOAD_VERSION < 373 && LASTLOAD_VERSION != -1
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		;ボス触手が父親の場合、ＩＤのズレを修正
		SIF CFLAG:CCOUNT:230 >= 100
			CFLAG:CCOUNT:230 += 1
		SIF CFLAG:CCOUNT:9 >= 100
			CFLAG:CCOUNT:9 += 1
	NEXT
ENDIF
IF LASTLOAD_VERSION < 380 && LASTLOAD_VERSION != -1
	;オミットされたフラグを初期化
	FLAG:19 = 0
	FLAG:851 = 0
	;コンフィグを初期設定に変更
	FLAG:800 = 0
	FLAG:801 = 1
	FLAG:802 = 31
	FLAG:803 = 7
	FLAG:804 = 7
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		;刻印防止を初期化
		MARK:CCOUNT:快楽刻印防止 = 0
		MARK:CCOUNT:苦痛刻印防止 = 0
		MARK:CCOUNT:屈服刻印防止 = 0
		MARK:CCOUNT:恐怖刻印防止 = 0
		MARK:CCOUNT:恥辱刻印防止 = 0
	NEXT
ENDIF
IF LASTLOAD_VERSION < 381 && LASTLOAD_VERSION != -1
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		;素質基本値を新仕様に合わせて変更
		SIF GETBIT(BASE:CCOUNT:48, 0)
			BASE:CCOUNT:体格基本値 = -1
		SIF GETBIT(BASE:CCOUNT:48, 1)
			BASE:CCOUNT:体格基本値 = 1
		SIF GETBIT(BASE:CCOUNT:48, 2)
			BASE:CCOUNT:胸サイズ基本値 = -1
		SIF GETBIT(BASE:CCOUNT:48, 3)
			BASE:CCOUNT:胸サイズ基本値 = 1
		SIF GETBIT(BASE:CCOUNT:48, 4)
			BASE:CCOUNT:性別基本値 = 1
		;オミットされたBASEを初期化
		BASE:CCOUNT:42 = 0
		MAXBASE:CCOUNT:42 = 0
		BASE:CCOUNT:48 = 0
		MAXBASE:CCOUNT:48 = 0
	NEXT
ENDIF
IF LASTLOAD_VERSION < 382 && LASTLOAD_VERSION != -1
	;相関関係の全探索
	CALL CHECK_ALL_RELATION
ENDIF
IF LASTLOAD_VERSION < 394 && LASTLOAD_VERSION != -1
	;目つきの追加
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF CSTR:CCOUNT:18 == ""
			CSTR:CCOUNT:18 = 普通
	NEXT
ENDIF

IF LASTLOAD_VERSION < GAMEBASE_VERSION && LASTLOAD_VERSION != -1
	PRINTL 【セーブデータを最新バージョンにアップデートしました】
	LOCALS:0 = {1000 + LASTLOAD_VERSION}
	SUBSTRING LOCALS:0, 1, 3
	LOCALS:0 = {LASTLOAD_VERSION / 1000}.%RESULTS%
	LOCALS:1 = {1000 + GAMEBASE_VERSION}
	SUBSTRING LOCALS:1, 1, 3
	LOCALS:1 = {GAMEBASE_VERSION / 1000}.%RESULTS%
	PRINTFORMW 　ver %LOCALS:0% → %LOCALS:1%
	;０除算バグ用
	SIF LASTLOAD_VERSION != 100
		CALL ENDING
ENDIF
PRINTL 