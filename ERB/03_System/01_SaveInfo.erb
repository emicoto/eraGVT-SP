;SPセーブ仕様
@SAVEINFO_SP
#DIM BOSSNUM
#DIMS GAMEMODE
#DIMS SITUATION
#DIMS ALIVE
#DIMS DAYTIME
#DIMS SAVETIME
#DIMS GAMEVERSION
#DIMS VALUE

; 初期化
VARSET LOCALS
VARSET LOCAL

	; 新バージョンデータ
	; SPセーブ判断用文字列"SP", ゲーム内日付, ゲーム内時間
    
	VALUE = SP

	DAYTIME = {DAY,3}日目・%CONVERT_TIME()%

    ;殲滅状況を記録
    CALL TENTACLE_SURVIVE, "NUM"
    BOSSNUM = RESULT

    IF FLAG:64 > 0
	    SITUATION = ★GAME CLEAR★
    ELSEIF FLAG:0 == 11
        SITUATION = {FLAG:3,2}人目撃退中
    ELSEIF FLAG:10 == 0
        SITUATION = {FLAG:3 - BOSSNUM + 1,2}体目殲滅中
    ELSE
        SITUATION = {FLAG:3 + BOSSNUM,2}体目殲滅中
    ENDIF
	
    ;ゲームバージョン
    GAMEVERSION = {1000 + GAMEBASE_VERSION}
    SUBSTRING GAMEVERSION, 1, 3
    GAMEVERSION = {GAMEBASE_VERSION / 1000}.%RESULTS%
    GAMEVERSION = SP Ver %GAMEVERSION%

    ;生存状況記録
    ALIVE = 存命 {FLAG:31,2}人　幽閉 {FLAG:32,2}人　悪堕 {FLAG:33,2}人　死亡 {FLAG:34,2}人

    ;セーブ時間
    GETTIMES
    SAVETIME = %RESULTS%

	; ゲームモード記録
    SELECTCASE FLAG:0
        CASE 0
        GAMEMODE = ☆ GAMEOVER ☆
        CASE 1
        GAMEMODE = ☆ NORMAL ☆
        CASE 2
        GAMEMODE = ☆ SOLOPLAY ☆
        CASE 4
        GAMEMODE = ☆ HARDCORE ☆
        CASE 10
        IF FLAG:906 > 0
            GAMEMODE = ☆ FREEPLAY ☆
        ELSE
            GAMEMODE = ☆ SURVIVAL ☆
        ENDIF
        CASE 11
        GAMEMODE = ☆ REVERSE ☆
        CASEELSE
        GAMEMODE = SAVE_ERROR
    ENDSELECT
	
	[IF_DEBUG]
	GAMVERSION = < DEBUG >
	[ENDIF]

    VALUE = %VALUE%,%SAVETIME%,%DAYTIME%,%SITUATION%,%GAMEVERSION%,%ALIVE%,%GAMEMODE%
	RESULTS:0 = %VALUE%
RETURN



;旧式システムセーブ
@SAVEINFO
#DIM BOSSNUM
#DIMS GAMEMODE
#DIMS SITUATION
#DIMS DAYTIME
#DIMS GAMEVERSION
VARSET LOCALS
VARSET LOCAL

GETTIMES

;ゲームモード記録
SELECTCASE FLAG:0
    CASE 0
    GAMEMODE = GAMEOVER
    CASE 1
	GAMEMODE = NORMALモード
    CASE 2
	GAMEMODE = SOLOモード
    CASE 4
	GAMEMODE = HARDCOREモード
    CASE 10
	IF FLAG:906 > 0
		GAMEMODE = FREEPLAYモード
	ELSE
		GAMEMODE = SURVIVALモード
	ENDIF
    CASE 11
    GAMEMODE = REVERSEモード
    CASEELSE
    GAMEMODE = SAVE_ERROR
ENDSELECT

;殲滅状況を記録
CALL TENTACLE_SURVIVE, "NUM"
BOSSNUM = RESULT

IF FLAG:64 > 0
	SITUATION = GAMECLEAR
ELSEIF FLAG:0 == 11
	SITUATION = {FLAG:3,2}人目
ELSEIF FLAG:10 == 0
	SITUATION = {FLAG:3 - BOSSNUM + 1,2}体目
ELSE
	SITUATION = {FLAG:3 + BOSSNUM,2}体目
ENDIF

GAMEVERSION = {1000 + GAMEBASE_VERSION}
SUBSTRING GAMEVERSION, 1, 3
GAMEVERSION = {GAMEBASE_VERSION / 1000}.%RESULTS%
GAMEVERSION = SP Ver %GAMEVERSION%

LOCAL = TIME
IF LOCAL == 0
	LOCALS = 昼
ELSE
	LOCALS = 夜
ENDIF

DAYTIME = {DAY,3}日目・%LOCALS%


IF FLAG:64 > 0
	PUTFORM %GAMEMODE,14,LEFT% %DAYTIME,12,RIGHT%  %SITUATION,12,RIGHT%    %GAMEVERSION,12,RIGHT%
ELSEIF FLAG:0 == 11
	PUTFORM %GAMEMODE,14,LEFT% %DAYTIME,12,RIGHT%  %SITUATION,6,RIGHT%撃退中    %GAMEVERSION,12,RIGHT%
ELSE
	PUTFORM %GAMEMODE,14,LEFT% %DAYTIME,12,RIGHT%  %SITUATION,6,RIGHT%殲滅中    %GAMEVERSION,12,RIGHT%
ENDIF

