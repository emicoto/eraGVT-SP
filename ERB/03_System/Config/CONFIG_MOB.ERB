;雑魚敵出現フィルタ

;---------------------------------------------
;フィルタの処理
;---------------------------------------------
@CONFIG_M
#DIM LCOUNT, 2
#DIM CATEGORY
#DIM PAGE
#DIM SELECT
LCOUNT:0 = LINECOUNT
$MASTER_LOOP
CALL LB
DRAWLINE
PRINTL 雑魚敵出現フィルタ　カテゴリを選択してください
PRINTL
PRINTL [0]無形
PRINTL [1]スライム
PRINTL [2]植物
PRINTL  ---
PRINTL  ---
PRINTL  ---
PRINTL  ---
PRINTL [7]虫
PRINTL [8]物質
PRINTL [9]天使・悪魔系
PRINTL 
PRINTL [101]全カテゴリの雑魚出現率を全て0％に設定する
PRINTL [102]全カテゴリの雑魚出現率を全て100％に設定する
PRINTL [200]変更を保存して戻る
$INPUT_LOOP
INPUT
IF RESULT >= 0 && RESULT <= 9 && RESULT != 3 && RESULT != 4 && RESULT != 5 && RESULT != 6
	CATEGORY = RESULT
	PAGE = 1
	LCOUNT:1 = LINECOUNT
	$LOOP_0
	DRAWLINE
	PRINTFORML 雑魚敵出現フィルタ　PAGE({PAGE}/5)　　　　　　　　　　　　　　　　出現率
	PRINTL 
	FOR LOCAL,1,21
		SELECT = (PAGE - 1) * 20 + LOCAL
		TRYCCALLFORM TENTACLE_MOB_{CATEGORY * 100 + SELECT}
			TRYCALLFORM TENTACLE_MOB_{CATEGORY * 100 + SELECT}_GETNAME
			PRINTFORM [{SELECT,3,RIGHT}] %RESULTS,56,LEFT%　{MOB_FLAG:CATEGORY:SELECT}％
		CATCH
			PRINT  ---
		ENDCATCH
		PRINTL 
	NEXT
	PRINTL 
	PRINT このカテゴリの全出現率を 
	PRINTPLAIN 　　　
	PRINT [101]0％にする 
	PRINTPLAIN 　　　　
	PRINT [102]100％にする
	PRINTL 
	PRINT [200]戻る　　　
	PRINTPLAIN 　　　　　　　　
	IF PAGE >= 2
		PRINT [201]前のページ
	ELSE
		PRINT 　　　　　　　 
	ENDIF
	PRINTPLAIN 　　　　
	IF PAGE <= 4
		PRINT [202]次のページ
	ELSE
		PRINT 　　　　　　　 
	ENDIF
	PRINTL 
	$INPUT_LOOP_0
	INPUT
	SELECT = RESULT
	IF SELECT != 101 && SELECT != 102 && SELECT != 201 && SELECT != 202 && SELECT != 200
		TRYCCALLFORM TENTACLE_MOB_{CATEGORY * 100 + SELECT}
		CATCH
			SELECT = -1
		ENDCATCH
	ENDIF
	IF SELECT >= ((PAGE - 1) * 20 + 1) && SELECT <= ((PAGE - 1) * 20 + 20)
		TRYCALLFORM TENTACLE_MOB_{CATEGORY * 100 + SELECT}_GETNAME
		PRINTFORML %RESULTS%の出現率を入力してください （  0 - 100 ％）
		$INPUT_LOOP_1
		INPUT
		IF RESULT >= 0 && RESULT <= 100
			MOB_FLAG:CATEGORY:SELECT = RESULT
			CLEARLINE LINECOUNT - LCOUNT:1
			GOTO LOOP_0
		ELSE
			GOTO INPUT_LOOP_1
		ENDIF
	ELSEIF RESULT == 101
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{CATEGORY * 100 + LOCAL:1}
				MOB_FLAG:CATEGORY:(LOCAL:1) = 0
			CATCH
			ENDCATCH
		NEXT
		CLEARLINE LINECOUNT - LCOUNT:1
		GOTO LOOP_0
	ELSEIF RESULT == 102
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{CATEGORY * 100 + LOCAL:1}
				MOB_FLAG:CATEGORY:(LOCAL:1) = 100
			CATCH
			ENDCATCH
		NEXT
		CLEARLINE LINECOUNT - LCOUNT:1
		GOTO LOOP_0
	ELSEIF RESULT == 200
		CLEARLINE LINECOUNT - LCOUNT:0
		GOTO MASTER_LOOP
	ELSEIF RESULT == 201 && PAGE >= 2
		PAGE --
		CLEARLINE LINECOUNT - LCOUNT:1
		GOTO LOOP_0
	ELSEIF RESULT == 202 && PAGE <= 4
		PAGE ++
		CLEARLINE LINECOUNT - LCOUNT:1
		GOTO LOOP_0
	ELSE
		GOTO INPUT_LOOP_0
	ENDIF
ELSEIF RESULT == 101
	FOR LOCAL,0,MOB_CATEGORY
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{LOCAL * 100 + LOCAL:1}
				MOB_FLAG:LOCAL:(LOCAL:1) = 0
			CATCH
			ENDCATCH
		NEXT
	NEXT
	PRINTL 全カテゴリの雑魚敵出現率を0％に設定しました
	PRINTW 
	CLEARLINE LINECOUNT - LCOUNT:0
	GOTO MASTER_LOOP
ELSEIF RESULT == 102
	FOR LOCAL,0,MOB_CATEGORY
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{LOCAL * 100 + LOCAL:1}
				MOB_FLAG:LOCAL:(LOCAL:1) = 100
			CATCH
			ENDCATCH
		NEXT
	NEXT
	PRINTL 全カテゴリの雑魚敵出現率を100％に設定しました
	PRINTW 
	CLEARLINE LINECOUNT - LCOUNT:0
	GOTO MASTER_LOOP
ELSEIF RESULT == 200
	FOR LOCAL,0,MOB_CATEGORY
		FOR LOCAL:1,0,100
			MOB_GLOBAL:LOCAL:(LOCAL:1) = MOB_FLAG:LOCAL:(LOCAL:1)
		NEXT
	NEXT
	SAVEGLOBAL
	RETURN
ELSE
	GOTO INPUT_LOOP
ENDIF



