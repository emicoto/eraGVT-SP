;-------------------
; 画面デザインで空行を呼び出し
;;-------------------
@ADD_LINE(ARG)
FOR LOCAL, 0, ARG
	PRINTL 
NEXT
RETURN
;----------------------------------------------------------
;ARG行になるまでラインを追加
;----------------------------------------------------------
@ADD_MAXLINE(ARG) 
WHILE LINECOUNT < ARG
	PRINTL 
WEND

;-------------------------------------------------
;最大行数の定義関数
;-------------------------------------------------
@MAXLINE
#FUNCTION
RETURNF 36

;-------------------
; インデント（字下げ）
;	使用例：%INDENT(2)%
;	目的　：インデント幅を視覚的にする
;			インデントを一括置換しやすくする
;	引数　：APP_NUM……インデントする半角スペースの数
;	戻り値：文字列で半角スペースを返す
;	備考　：式中関数
;-------------------
@INDENT(SPACES_NUM)
	#FUNCTIONS
	#DIM SPACES_NUM
	#DIM SPACES_COUNTER
	#DIMS RETURN_STR

	RETURN_STR '= ""
	FOR SPACES_COUNTER, 0, SPACES_NUM
		RETURN_STR '= RETURN_STR + " "
	NEXT
	RETURNF RETURN_STR

@INDENT2(SPACES_NUM)
	#FUNCTIONS
	#DIM SPACES_NUM
	#DIM SPACES_COUNTER
	#DIMS RETURN_STR

	RETURN_STR '= ""
	FOR SPACES_COUNTER, 0, SPACES_NUM
		RETURN_STR '= RETURN_STR + "　"
	NEXT
	RETURNF RETURN_STR
;-------------------
;	フォントの変更
;	インストールされているか確認しながら
;	優先度１＞２＞３で表示する
;-------------------
@SETFONT_EX(FONT1, FONT2, FONT3)
	#DIMS FONT1
	#DIMS FONT2
	#DIMS FONT3

	IF FONT3 != ""
		CHKFONT FONT3
		IF RESULT
			SETFONT FONT3
		ENDIF
	ENDIF

	IF FONT2 != ""
		CHKFONT FONT2
		IF RESULT
			SETFONT FONT2
		ENDIF
	ENDIF

	IF FONT1 != ""
		CHKFONT FONT1
		IF RESULT
			SETFONT FONT1
		ENDIF
	ENDIF

	RETURN 0

;カラーコードの変換
@RGBCOLOR(ARG, ARG:1, ARG:2)
#FUNCTION
RETURNF (65536*ARG)+(256*ARG:1)+ARG:2

;Input buttom design
;Import Code from eraAWT,thanks for nice code
@LINK(LINK_TEXT = "", LINK_NUM = 0, LINK_WIDTH, LINK_COLOR,LINK_NOLINK,NOMARK,GORIGHT)
	#DIMS LINK_TEXT
	#DIM LINK_NUM
	#DIM LINK_WIDTH
	#DIM LINK_COLOR
	#DIM SAVE_COLOR
    #DIM LINK_NOLINK
    #DIM NOMARK
    #DIM GORIGHT

	GETCOLOR
	SAVE_COLOR = RESULT:0

	IF !LINK_WIDTH
		LINK_WIDTH = 1
	ENDIF

	IF LINK_COLOR && !LINK_NOLINK
		SETCOLOR LINK_COLOR
	ELSEIF LINK_NOLINK
		SETCOLOR COLOR_GRAY
	ELSE
		SETCOLOR COLOR_LINK
	ENDIF

	FONTSTYLE 8

    IF !GORIGHT
        IF !NOMARK
            PRINTBUTTON @"%LINK_TEXT, LINK_WIDTH, LEFT% ★", LINK_NUM
        ELSE
            PRINTBUTTON @"%LINK_TEXT, LINK_WIDTH, LEFT%", LINK_NUM
        ENDIF
    ELSE
        IF !NOMARK
            PRINTBUTTONC @"%LINK_TEXT, LINK_WIDTH% ★", LINK_NUM
        ELSE
            PRINTBUTTONC @"%LINK_TEXT, LINK_WIDTH%", LINK_NUM
        ENDIF
    ENDIF

	FONTSTYLE 0


	SETCOLOR SAVE_COLOR

	RETURN 0

@SLINK(LINK_TEXT = "",TITLE_TEXT = "",LINK_NUM = 0,LINK_COLOR,LINK_BCOLOR,FONT_FACE,ALIGN)
#DIM LINK_NUM
#DIM ALIGN
#DIMS fpos
#DIMS LINK_TEXT
#DIMS TITLE_TEXT
#DIMS LINK_COLOR
#DIMS LINK_BCOLOR
#DIMS FONT_FACE

fpos =
LINK_COLOR = #%PALETTE(@"%LINK_COLOR%")%
LINK_BCOLOR = #%PALETTE(@"%LINK_BCOLOR%")%


	IF FONT_FACE != ""
		CHKFONT FONT_FACE
		IF RESULT
			FONT_FACE = FONT_FACE
		ELSE
			FONT_FACE = ＭＳ ゴシック
		ENDIF
	ELSE
			CHKFONT "メイリオ"
		IF RESULT
			FONT_FACE = メイリオ
		ELSE
			FONT_FACE = ＭＳ ゴシック
		ENDIF
	ENDIF

	IF ALIGN == 1
	fpos = right
	ELSEIF ALIGN == 2
	fpos = center
	ELSE
	fpos = left
	ENDIF

HTML_PRINT @"<p align='%fpos%'><font face ='%FONT_FACE%' color='%LINK_COLOR%' bcolor='%LINK_BCOLOR%'><button value='{LINK_NUM}' title='%TITLE_TEXT%'>%LINK_TEXT%</button></font>"

@LABEL(TITLE_TEXT= "",TITLE_COLOR = COLOR_YELLOW)
#DIMS TITLE_TEXT
#DIM TITLE_COLOR

SETCOLOR COLOR_GRAY
DRAWLINE
SETCOLOR TITLE_COLOR
PRINTFORML %INDENT(2)%%TITLE_TEXT%
SETCOLOR COLOR_DARK
DRAWLINE
RESETCOLOR
PRINTL

@COLORLINE(LINECOLOR = 1,CUSTOM = "",FONT = "ＭＳ ゴシック")
#DIM LINECOLOR
#DIMS CUSTOM
#DIMS FONT
#DIMS FONT2

	GETFONT
	FONT2 = %RESULTS:0%

	IF LINECOLOR == 1
		LINECOLOR = COLOR_GRAY
	ELSEIF LINECOLOR == 2
		LINECOLOR = COLOR_DARK
	ELSE
		LINECOLOR = LINECOLOR
	ENDIF

	SETCOLOR LINECOLOR
	SETFONT FONT
	IF CUSTOM != ""
		DRAWLINEFORM %CUSTOM%
	ELSE
		DRAWLINE
	ENDIF
	RESETCOLOR
	SETFONT FONT2

@STARPRINT(INPUT_TEXT = "",FONT_COLOR = COLOR_ORANGE,NOWAIT = 0,ALIGN=0)
#DIMS INPUT_TEXT
#DIM FONT_COLOR
#DIM NOWAIT
#DIM ALIGN
#DIM SAVECOLOR

	GETCOLOR
	SAVECOLOR = RESULT:0
	IF ALIGN == 1
		ALIGNMENT RIGHT
	ELSEIF ALIGN == 2
		ALIGNMENT CENTER
	ENDIF

    SETCOLOR FONT_COLOR
    PRINTFORML ★ %INPUT_TEXT%	

	SETCOLOR SAVECOLOR
	SIF ALIGN > 0
	ALIGNMENT LEFT

SIF NOWAIT != 1
	TWAIT 1200,0


@CPRINT(INPUT_TEXT = "", FONT_COLOR = COLOR_ORANGE, NOL = 0,ALIGN = 0)
#DIM FONT_COLOR
#DIM NOL
#DIM ALIGN
#DIMS INPUT_TEXT

SETCOLOR FONT_COLOR

IF ALIGN == 1
ALIGNMENT RIGHT
ELSEIF ALIGN == 2
ALIGNMENT CENTER
ENDIF


PRINTFORM %INPUT_TEXT%

SIF NOL == 0
	PRINTL

RESETCOLOR

SIF ALIGN > 0
ALIGNMENT LEFT