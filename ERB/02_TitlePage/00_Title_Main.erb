; タイトル画面
;-------------------
@PRINT_TITLE
#DIMS GAMEVERSION

;Get Game Version
GAMEVERSION = {1000 + GAMEBASE_VERSION}
SUBSTRING GAMEVERSION, 1, 3
GAMEVERSION = {GAMEBASE_VERSION / 1000}.%RESULTS%

$START
FIRSTLINE = LINECOUNT ;GET NOW LINECOUNT

;PRINT TITLE PICTURE
CALL SETFONT_EX("メイリオ", "Arial")
CALL ADD_LINE(15)
    HTML_PRINT "<img src='Title' height='5000' ypos ='-2100'>"

;PRINT TITLE MENU
PRINTFORM %INDENT(20)%
CALL LINK("1. New Game", 1, 30,COLOR_PINK,)
PRINTL
PRINTL

PRINTFORM %INDENT(20)%
 CALL LINK("2. Load Game", 2, 25,COLOR_VERMILION,)
PRINTL
PRINTL

PRINTFORM %INDENT(20)%
CALL LINK("3. Gallery", 3, 24,COLOR_ORANGE)
PRINTL
PRINTL

PRINTFORM %INDENT(20)%
CALL LINK("4. Config", 4, 18,COLOR_YELLOW,)
PRINTL

;FILL UP SPACE LINE
CALL ADD_MAXLINE(34)
ALIGNMENT RIGHT
PRINTL

;PRINT GAME INFO
SETCOLOR COLOR_GRAY
CALL SETFONT_EX("Constantia", "Times New Roman")
PRINTFORM eraGVT+ SP Edition %GAMEVERSION%  by  脚長おじさん
RESETCOLOR

;PRINT CAUTION
PRINTFORM %INDENT(5)%
CALL LINK("VersionHistory", 5,,COLOR_VERMILION,,1)
PRINTFORM %INDENT(5)%
CALL LINK("Caution", 6,,COLOR_ORANGE,,1)
PRINTFORM %INDENT(5)%
CALL LINK("Thanks", 7,,COLOR_APPLEGREEN,,1)
PRINTFORM %INDENT(5)%
CALL LINK("License", 8,,COLOR_AQUA,,1)
PRINTFORM %INDENT(10)%
PRINTL

;RESET PRINT SETTING
ALIGNMENT LEFT
CALL SETFONT_EX("ＭＳ ゴシック")
PRINTL
INPUT

SELECTCASE RESULT
    CASE 1
    CALL STARPRINT("新しい物語を始まります")
    BEGIN FIRST
    CASE 2
    CALL STARPRINT("記憶を呼び覚まします")
    CALL LOADGAME_PAGE
    CASE 3
    CALL STARPRINT("本棚を確認します")
    PRINTW （未実装）
    ;CALL GALLERY
    CASE 4
    CALL STARPRINT("システム設定を行います")
    PRINTW （調整中）
    ;CALL SYSTEM_CONFIG
    CASE 5
    CALL VERUP_HISTORY
    CASE 6
    CALL CAUTION_PAGE
    CASE 7
    CALL THANKS_PAGE
    CASE 8  
    CALL LICENSE_PAGE 
    CASEELSE
ENDSELECT
CLEARLINE LINECOUNT - FIRSTLINE

SIF RESULT == 999 ;念の為リセットしておく
RESULT = 0

GOTO START

