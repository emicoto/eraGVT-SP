@EVENTSHOP
#DIM CCOUNT
#DIM LCOUNT
VARSET TEMP
REDRAW 0

FOR CCOUNT,0,CHARANUM
	SIF CCOUNT == MASTER
	CONTINUE

	SIF !GROUPMATCH(CFLAG:CCOUNT:0,1,2,3,4,9)
	TEMP:0 += 1 ;存命中人数記録
	SIF CFLAG:CCOUNT:0 == 1
	TEMP:1 += 1 ;幽閉中人数記録
	SIF GROUPMATCH(CFLAG:CCOUNT:0,2,3)
	TEMP:2 += 1 ;悪堕ち・洗脳人数記録
	SIF CFLAG:CCOUNT:0 == 9
	TEMP:3 += 1 ;死亡人数記録
NEXT
FLAG:31 = TEMP:0
FLAG:32 = TEMP:1
FLAG:33 = TEMP:2
FLAG:34 = TEMP:3



;パーティメンバー管理
CALL SET_PARTYMEMBER

;襲撃イベント後のスキップ
;RAID_HANTEIで判定を通過してイベントが開始されると$RAID_SKIPまで飛び、途中のイベントをスキップする（ターンが余分に経過してしまうため）
;戦闘が終わるとSHOPに戻ってくるので、元の位置$AFTER_RAIDまで飛ばして処理完了。ここまで１セット
IF FLAG:45 > 0
	LOCAL = TARGET
	;救出されたキャラの治療
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF CCOUNT >= CHARANUM
			BREAK

		IF CFLAG:CCOUNT:0 == -1
			TARGET = CCOUNT
			IF CFLAG:TARGET:6 == -1
				PRINTL 
				CALL RESCUE_CHILD,TARGET
				SIF RESULT < 0
					CCOUNT -= RESULT
			ELSE
				CALL AFTER_RESCUED,TARGET
			ENDIF
		ELSE
			CALL INMON_RECOVERY,CCOUNT
		ENDIF
	NEXT
	TARGET = LOCAL
	PRINTW 

	;襲撃フラグを折る
	FLAG:45 = 0
	GOTO AFTER_RAID
ENDIF


;全てのキャラが行動予定を終了しているか
;していない場合はSHOP_ACTIONに飛ばしてループを作っている
IF DAY == 0 || FLAG:799 > CHARANUM || FLAG:64 != 0
	SIF FLAG:64 == 0
		FLAG:799 = 0

	;ゲーム開始処理
	IF DAY == 0
		DAY = 1
		TIME = 0
		FLAG:60 = 10002
		FLAG:64 = 0
		FLAG:799 = 0
		TARGET = 1
		CALL LB
		PRINTL ゲームを開始します
		PRINTW 
		GOTO RAID_SKIP
	ELSE
		;前ターンで行動開始前に選択していたキャラにターゲットをあわせる
		;ターン中で使用不可になった場合は使用可能なキャラのうち一番若いキャラにあわせる
		IF CFLAG:(FLAG:798):0 != 0
			FOR CCOUNT, 0, CHARANUM
				SIF CCOUNT == MASTER
					CONTINUE
				
				IF CFLAG:CCOUNT:0 == 0
					TARGET = CCOUNT
					BREAK 
				ENDIF
			NEXT
		ELSE
			TARGET = FLAG:798
		ENDIF
	ENDIF
ELSE
	JUMP SHOP_ACTION
ENDIF

;エンディング判定
CALL ENDING
;強制的にタイトルに戻る場合
IF FLAG:999 == -999
	CLEARLINE LINECOUNT
	RESETDATA
	BEGIN TITLE
ELSEIF FLAG:64 != 0
	GOTO RAID_SKIP
ENDIF

PRINTL 
;救出されたキャラの治療と記録更新
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	SIF CCOUNT >= CHARANUM
		BREAK

	;ステータスの再計算
	CALL LEVELSTATUS,CCOUNT

	IF CFLAG:CCOUNT:0 == -1
		TARGET = CCOUNT
		IF CFLAG:TARGET:6 == -1
			PRINTL 
			CALL RESCUE_CHILD,TARGET
			SIF RESULT < 0
				CCOUNT -= RESULT
		ELSE
			CALL AFTER_RESCUED,TARGET
		ENDIF
		PRINTW 
	ELSE
		CALL INMON_RECOVERY,CCOUNT
	ENDIF
	;娘を施設に預けた場合の処理
	SIF CCOUNT > CHARANUM - 1
		BREAK

	;記録保持
	IF MAXBASE:CCOUNT:体力 > GLOBAL:103
		GLOBAL:103 = MAXBASE:CCOUNT:体力
		GLOBALS:0 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:レベル}）　{GLOBAL:103}
	ENDIF
	IF MAXBASE:CCOUNT:気力 > GLOBAL:104
		GLOBAL:104 = MAXBASE:CCOUNT:気力
		GLOBALS:1 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:レベル}）　{GLOBAL:104}
	ENDIF
	IF MAXBASE:CCOUNT:性耐性 > GLOBAL:105
		GLOBAL:105 = MAXBASE:CCOUNT:性耐性
		GLOBALS:2 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:レベル}）　{GLOBAL:105}
	ENDIF
	IF MAXBASE:CCOUNT:攻撃 > GLOBAL:106
		GLOBAL:106 = MAXBASE:CCOUNT:攻撃
		GLOBALS:10 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:レベル}）　{GLOBAL:106}
	ENDIF
	IF MAXBASE:CCOUNT:防御 > GLOBAL:107
		GLOBAL:107 = MAXBASE:CCOUNT:防御
		GLOBALS:11 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:レベル}）　{GLOBAL:107}
	ENDIF
	IF MAXBASE:CCOUNT:敏捷 > GLOBAL:108
		GLOBAL:108 = MAXBASE:CCOUNT:敏捷
		GLOBALS:12 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:レベル}）　{GLOBAL:108}
	ENDIF
	IF MAXBASE:CCOUNT:知性 > GLOBAL:109
		GLOBAL:109 = MAXBASE:CCOUNT:知性
		GLOBALS:13 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:レベル}）　{GLOBAL:109}
	ENDIF
	SAVEGLOBAL
	;実績
	CALL GET_STATE_TROPHY,CCOUNT
NEXT

;**********************************************************
;ターン終了後に起きるイベント各種

;ボス出現停止フラグを折る
FLAG:49 = 0

;ボス触手のダメージ蓄積値が回復
IF FLAG:10 == 1 && FLAG:11 == 2
ELSE
	SETCOLOR 128,128,128
	SIF FLAG:999 == 1
		PRINTL DEBUG ボスのキズが回復
	RESETCOLOR
	FOR LOCAL,1,FLAG:3 + 1
		FLAG:(300 + LOCAL) -= RAND:3000000 + 8000000
		SIF FLAG:(300 + LOCAL) < 0
			FLAG:(300 + LOCAL) = 0
	NEXT
	FOR LOCAL,1,FLAG:4 + 1
		;ラスボス強化時は回復量が低下
		CALL LASTBOSS_REST
		FLAG:(400 + LOCAL) -= (RAND:3000000 + 1000000) / (2 + RESULT)
		SIF FLAG:(400 + LOCAL) < 0
			FLAG:(400 + LOCAL) = 0
	NEXT
ENDIF



;口上のターン終了時処理を呼び出し
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE

	TARGET = CCOUNT
	CALL MESSAGE_TURNEND
NEXT

;TARGETの値を退避
FLAG:797 = TARGET

;クズ市民による脅迫あるいは誘拐監禁
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	IF GROUPMATCH(CFLAG:CCOUNT:0,0,5) && (CFLAG:CCOUNT:286 + CFLAG:CCOUNT:320 + CFLAG:CCOUNT:321) > 0 ;一度でもレイプされたことがあれば発生
	TARGET = CCOUNT
	CALL INTIMIDATION_EVENT
	ELSEIF CFLAG:CCOUNT:0 == 4
	TARGET = CCOUNT
	CALL KIDNAPPING
	ENDIF
NEXT

;触手幽閉
CALL PRISON
;判定出産
CALL BIRTH_HANTEI
;判定育児終了
CALL GROW_HANTEI

;悪堕ちキャラが暴れるイベント
CALL AKUOTI_ATTACK

;イチャックス
CALL LOVESEX_NIGHT
;夜間自慰
CALL SELF_NIGHT
;夜這い
SIF CONFIG_CHECK_EVENT_F(2) > 0
	CALL YOBAI


;ゲームオーバーモードでは防衛力が０になる
IF FLAG:0 == 0
	FLAG:852 = 0
	FLAG:853 = LIMIT(FLAG:853, -100, 100)
ELSEIF DAY > 0
	;防衛力の自動変動
	IF (FLAG:41 == 0 && FLAG:0 != 2) || FLAG:52
		SIF FLAG:41 == 0 && FLAG:0 != 2
			FLAG:42 ++
		DRAWLINE
		PRINTL 
		FONTBOLD
		PRINTL 防衛力の変動　　
		IF FLAG:42 < FLAG:52
			PRINTFORML 　防衛設備が効果的に機能しています
		ELSEIF FLAG:42 < 2 + FLAG:52
			PRINTFORML 　危険度レベル{FLAG:42 - FLAG:52}：まだ比較的安全です
		ELSEIF FLAG:42 < 3 + FLAG:52
			PRINTFORML 　危険度レベル{FLAG:42 - FLAG:52}：まだ無視できるレベルです
		ELSEIF FLAG:42 < 5 + FLAG:52
			PRINTFORML 　危険度レベル{FLAG:42 - FLAG:52}：触手の活動が活発化しています
		ELSEIF FLAG:42 < 7 + FLAG:52
			PRINTFORML 　危険度レベル{FLAG:42 - FLAG:52}：あちこちで被害が多発中！
		ELSE
			PRINTFORML 　危険度レベルMAX：かなり危険な状態です！
		ENDIF
		FONTREGULAR
		PRINTFORM 　防衛力( {FLAG:852} )　
		;このターンに誰も出撃していない場合、放棄ターン数に応じて防衛力が大きく低下
		LOCAL:852 = 0
		IF FLAG:41 == 0 && FLAG:0 != 2
			CALL TENTACLE_LEVEL
			IF FLAG:42 < FLAG:52
				LOCAL:852 = 0
			ELSEIF FLAG:42 < 2 + FLAG:52
				LOCAL:852 = RESULT + 5
			ELSEIF FLAG:42 == 2 + FLAG:52
				LOCAL:852 = FLAG:852 * 1 / 100 + RESULT + 5
			ELSEIF FLAG:42 == 3 + FLAG:52
				LOCAL:852 = FLAG:852 * 4 / 100 + RESULT + 10
			ELSEIF FLAG:42 == 4 + FLAG:52
				LOCAL:852 = FLAG:852 * 8 / 100 + RESULT + 20
			ELSEIF FLAG:42 == 5 + FLAG:52
				LOCAL:852 = FLAG:852 * 12 / 100 + RESULT + 40
			ELSEIF FLAG:42 == 6 + FLAG:52
				LOCAL:852 = FLAG:852 * 15 / 100 + RESULT + 80
			ELSEIF FLAG:42 == 7 + FLAG:52
				LOCAL:852 = FLAG:852 * 20 / 100 + RESULT + 160
			ELSE
				LOCAL:852 = FLAG:852 * 50 / 100 + RESULT + 320
			ENDIF
			PRINTFORM －　時間経過( {LOCAL:852} )　
			FLAG:852 = MAX(FLAG:852 - LOCAL:852, 0)
		ELSE
			;誰かが出撃していれば放棄ターン数リセット
			FLAG:42 = 0
		ENDIF
		LOCAL:852 = FLAG:52 * FLAG:52 * 5
		;防衛設備が自動で防衛
		FLAG:852 = FLAG:852 + LOCAL:852
		SIF LOCAL:852
			PRINTFORM ＋　防衛設備Lv{FLAG:52}( {LOCAL:852} )　
		SIF (FLAG:41 == 0 && FLAG:0 != 2) || FLAG:52
			PRINTFORM ＝　{FLAG:852}
		PRINTL 
	ELSEIF FLAG:41 > 0
		FLAG:42 = 0
	ENDIF
	;人気度の自動変動
	FLAG:853 = LIMIT(FLAG:853, -100, 100)
	DRAWLINE
	PRINTL 
	FONTBOLD
	PRINTL 人気度の変動　　
	FONTREGULAR
	PRINTFORM 　人気度( {FLAG:853} )　
	;一般評価
	LOCAL:853 = 0
	LOCAL:0 = FLAG:853
	IF FLAG:852 == 0
		SIF RAND:100 < 5
			LOCAL:853 -= 1
		SIF RAND:100 < 5
			LOCAL:853 -= 1
		SIF RAND:100 < 5
			LOCAL:853 -= 1
	ELSEIF FLAG:852 < 1250
		SIF RAND:100 < 5
			LOCAL:853 -= 1
		SIF RAND:100 < 5
			LOCAL:853 -= 1
	ELSEIF FLAG:852 < 2500
		SIF RAND:100 < 5
			LOCAL:853 -= 1
		SIF RAND:100 < 5
			LOCAL:853 += 1
	ELSEIF FLAG:852 < 5000
		SIF RAND:100 < 5
			LOCAL:853 += 1
	ELSEIF FLAG:852 < 7500
		SIF RAND:100 < 5
			LOCAL:853 += 1
		SIF RAND:100 < 5
			LOCAL:853 += 1
	ELSE
		SIF RAND:100 < 5
			LOCAL:853 += 1
		SIF RAND:100 < 5
			LOCAL:853 += 1
		SIF RAND:100 < 5
			LOCAL:853 += 1
	ENDIF
	IF LOCAL:853 < 0
		RESULT = ABS(LOCAL:853)
		PRINTFORM －　一般評価( {RESULT} )　
	ENDIF
	SIF LOCAL:853 > 0
		PRINTFORM ＋　一般評価( {LOCAL:853} )　
	FLAG:853 = FLAG:853 + LOCAL:853
	;処女ボーナス
	LOCAL:853 = 0
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		IF TALENT:CCOUNT:処女 > 0
			SIF RAND:100 < 5
				LOCAL:853 += 1
		ELSEIF CHARATALENT_F(CCOUNT,0,"オトコ") > 0 && CHARATALENT_F(CCOUNT,1,"オトコ") == 0 && TALENT:CCOUNT:変身時非処女 == 0
			SIF RAND:100 < 5
				LOCAL:853 += 1
		ENDIF
	NEXT
	SIF LOCAL:853 > 0
		PRINTFORM ＋　処女ボーナス( {LOCAL:853} )　
	FLAG:853 = FLAG:853 + LOCAL:853
	;ファン人気
	LOCAL:853 = 0
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		IF EXP:CCOUNT:魅了経験 >= 200
			SIF RAND:100 < 5
				LOCAL:853 += 1
			SIF EXP:CCOUNT:魅了経験 >= 500 && RAND:100 < 5
				LOCAL:853 += 1
			SIF EXP:CCOUNT:魅了経験 >= 3000 && RAND:100 < 5
				LOCAL:853 += 1
		ENDIF
		SIF TALENT:CCOUNT:清純派 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF RAND:100 < MIN(CFLAG:CCOUNT:284 / 4, 10)
			LOCAL:853 += 1
		;性格補正
		SIF TALENT:CCOUNT:母性的 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:小悪魔 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:目立ちたがり > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:上品 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:泣き虫 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		;FEATによる人気度への補正
		SIF TALENT:CCOUNT:獣性の証 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:人外の美貌 > 0 && RAND:100 < 10
			LOCAL:853 += 1
	NEXT
	SIF LOCAL:853 > 0
		PRINTFORM ＋　ファン人気( {LOCAL:853} )　
	FLAG:853 = FLAG:853 + LOCAL:853
	;悪いうわさ
	LOCAL:853 = 0
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF RAND:30 < MIN(CFLAG:825, 10)
			LOCAL:853 += 2
	NEXT
	SIF LOCAL:853 > 0
		PRINTFORM －　悪いうわさ( {LOCAL:853} )
	FLAG:853 = FLAG:853 - LOCAL:853
	FLAG:853 = LIMIT(FLAG:853, -100, 100)
	PRINTFORM ＝　{FLAG:853}　
	IF FLAG:853 < LOCAL:0
		PRINT (↓下降)
	ELSEIF FLAG:853 > LOCAL:0
		PRINT (↑上昇)
	ELSE
		PRINT (変動なし)
	ENDIF
	PRINTL 
	DRAWLINE
	PRINTL 
	SIF CONFIG_CHECK_SCREEN_F(3) == 0
		WAIT
ENDIF
LOCAL:852 = 0
LOCAL:853 = 0

;判定ボス触手襲来
SIF CONFIG_CHECK_EVENT_F(0) > 0
	CALL RAID_HANTEI
SIF FLAG:45 > 0
	GOTO RAID_SKIP
$AFTER_RAID

;寄生関係
CALL PARASITE
;子触手襲来
SIF CONFIG_CHECK_EVENT_F(1) > 0
	CALL SMALL_TENTACLE_HANTEI
;子触手の増殖
CALL AUTO_BIRTH

;同時出撃人数と戦闘支援人数をリセット
FLAG:41 = 0
FLAG:43 = 0
;TARGETを元に戻す
TARGET = FLAG:797

;**********************************************************

;昼夜の切り替え
INVERTBIT TIME, 0

;時間経過で自動的に体力,気力,性耐性,結界,淫紋が回復する
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	
	;一日が経過した直後（昼）だと回復量8%
	IF TIME == 0
		LOCAL:1 = 8
		IF TALENT:CCOUNT:回復早い == 1
			LOCAL:1 += 4
		ELSEIF TALENT:CCOUNT:回復遅い == 1
			LOCAL:1 -= 4
		ENDIF

		;自動回復アップの効果
		SIF FLAG:901
			LOCAL:1 += 4
	ELSE
		LOCAL:1 = 0
		;FEAT効果による回復量補正
		SIF TALENT:夜魔の貴族 > 0
			LOCAL:1 += 8
	ENDIF
    
		;射精しすぎて回復に影響
		SIF CFLAG:CCOUNT:37 > 2
		    LOCAL:1 -= (CFLAG:CCOUNT:37)/2
		SIF CFLAG:CCOUNT:38 > 0
		    LOCAL:1 -= 4
		
		;レイプや処女喪失など不幸事件により回復影響
		IF CFLAG:CCOUNT:15 > 0
		   LOCAL:1 -= 2*(CFLAG:CCOUNT:15)
		   CFLAG:CCOUNT:15 -= 1
		ENDIF

		SIF CFLAG:CCOUNT:72 > 0
		   CFLAG:CCOUNT:72 -= 1

	;射精回数回復
	IF TIME == 0
       SIF CFLAG:CCOUNT:37 > 0
	       CFLAG:CCOUNT:37 = 0
	   SIF CFLAG:CCOUNT:38 > 0
	       CFLAG:CCOUNT:38 = 0
	ELSE
	   SIF CFLAG:CCOUNT:37 >= 2 && CFLAG:CCOUNT:38 < 1
	       CFLAG:CCOUNT:37 -= 2
	ENDIF


	;休憩設備レベルに応じて自動回復
	IF FLAG:51 == 1
		LOCAL:1 += 0
	ELSEIF FLAG:51 == 2
		LOCAL:1 += 2
	ELSEIF FLAG:51 == 3
		LOCAL:1 += 4
	ELSEIF FLAG:51 == 4
		LOCAL:1 += 6
	ELSEIF FLAG:51 == 5
		LOCAL:1 += 8
	ENDIF

	SIF FLAG:54 && EXP:CCOUNT:魅了経験 >= 200
	    EXP:CCOUNT:魅了経験 += RAND:10

	;幽閉、監禁、洗脳、悪堕ち、死亡、消沈中のキャラは体力気力性耐性が０になる
	IF CFLAG:CCOUNT:15 > 3 || CFLAG:CCOUNT:0 == 1 || CFLAG:CCOUNT:0 == 2 || CFLAG:CCOUNT:0 == 3 || CFLAG:CCOUNT:0 == 4 || CFLAG:CCOUNT:0 == 5 || CFLAG:CCOUNT:0 == 9
		BASE:CCOUNT:体力 = 0
		BASE:CCOUNT:気力 = 0
		BASE:CCOUNT:性耐性 = 0
	;回復の処理
	ELSE
		LOCAL = MAXBASE:CCOUNT:体力
		LOCAL = LOCAL * LOCAL:1 / 100
		BASE:CCOUNT:体力 = LIMIT(LOCAL + BASE:CCOUNT:体力, 0, MAXBASE:CCOUNT:体力)

		LOCAL = MAXBASE:CCOUNT:気力
		LOCAL = LOCAL * LOCAL:1 / 100
		BASE:CCOUNT:気力 = LIMIT(LOCAL + BASE:CCOUNT:気力, 0, MAXBASE:CCOUNT:気力)

		LOCAL = MAXBASE:CCOUNT:性耐性	
		LOCAL = LOCAL * LOCAL:1 / 100
		BASE:CCOUNT:性耐性 = LIMIT(LOCAL + BASE:CCOUNT:性耐性, 0, MAXBASE:CCOUNT:性耐性)
	ENDIF

	;疲労度自動回復の処理
	IF CFLAG:CCOUNT:99 > 0 && CFLAG:CCOUNT:0 == 0
		LOCAL = 0
		SIF (FLAG:53 & 観葉植物) && RAND:2 == 0
			LOCAL += 1
		SIF (FLAG:53 & 心休まる風景画) && RAND:3 == 0
			LOCAL += 1
		SIF (FLAG:53 & オシャレなオブジェ) && RAND:4 == 0
			LOCAL += 1
		SIF (FLAG:53 & ゴージャスな噴水) && RAND:2 == 0
			LOCAL += 1
		SIF (FLAG:53 & マッサージチェア)
			LOCAL += 1
		SIF (FLAG:53 & ビューティサロン)
			LOCAL += 2

		SIF CFLAG:CCOUNT:100 == 102 && (FLAG:53 & シャワー設備)
			LOCAL += 1

		;FEAT効果による疲労の自動回復
		SIF TALENT:CCOUNT:不老長寿 > 0 && TIME == 0
			LOCAL += 1
		SIF TALENT:CCOUNT:不老長寿 > 0 && TIME == 0 && RAND:2 == 0
			LOCAL += 1
		SIF TALENT:CCOUNT:不老長寿 > 0 && TIME == 0 && RAND:2 == 0
			LOCAL += 1
		SIF TALENT:CCOUNT:不老長寿 > 0 && TIME == 1
			LOCAL += 1
		SIF TALENT:CCOUNT:不老長寿 > 0 && TIME == 1 && RAND:2 == 0
			LOCAL += 1

		SIF CFLAG:CCOUNT:99 <= 0
			LOCAL = 0

		;回復実行
		CFLAG:CCOUNT:99 -= LOCAL
		SIF CFLAG:CCOUNT:99 < 0
			CFLAG:CCOUNT:99 = 0

		IF CFLAG:CCOUNT:100 != 103 && LOCAL > 0
			IF CFLAG:CCOUNT:99 == 0
				PRINTFORML %CALLNAME:CCOUNT%の身体から疲労が完全に抜けた！
			ELSE
				PRINTFORML %CALLNAME:CCOUNT%の身体から疲労が少し抜けた…
			ENDIF
		ENDIF
	ENDIF

	;部位結界の回復
	FOR LCOUNT, 0, 感覚数
		LOCAL = 0
		IF CFLAG:CCOUNT:0 == 0
			LOCAL = 3
			SIF CFLAG:CCOUNT:100 == 103
				LOCAL += 3
		ENDIF
		LOCAL = MAXBASE:CCOUNT:(LCOUNT + 30) * LOCAL / 100
		SIF BASE:CCOUNT:(LCOUNT + 30) > 0
			BASE:CCOUNT:(LCOUNT + 30) = LIMIT(BASE:CCOUNT:(LCOUNT + 30) + LOCAL, 0, MAXBASE:CCOUNT:(LCOUNT + 30))
	NEXT

	;社交的な場合、低確率でストーカーフラグが増加
	SIF TALENT:CCOUNT:社交的 > 0 && RAND:100 < (4 + SQRT(EXP:CCOUNT:魅了経験))
		CFLAG:CCOUNT:285 += 1
	;小心者の場合、低確率でストーカーフラグが減少
	SIF TALENT:CCOUNT:社交的 > 0 && RAND:100 < 8 && CFLAG:CCOUNT:285 > 0
		CFLAG:CCOUNT:285 -= 1

	;避妊用ピルを処方していれば１つ消費
	SIF CFLAG:CCOUNT:241 > 0
		CFLAG:CCOUNT:241 -= 1
NEXT

;日数
DRAWLINE
IF TIME == 0
	DAY += 1
	IF DAY > 1
		PRINTL 一日が終了しました
		FLAG:60 = 0

		IF FLAG:0 != 0
			IF DAY == 7
				PRINTW 国から支援表明の通知が届きました。
				PRINTW これからは一週間ごとに政府からの支援と市民からの義援金が届きます。
				PRINTW 防衛成果に応じて金額が増えるので、防衛力に注意しましょう。
				PRINTL 
				PRINTL 最初の資金援助が届いています。
				PRINTW 
			ELSEIF DAY == 21
				PRINTL 国からの援助金が増額されました。
				PRINTW 
			ELSEIF DAY == 42
				PRINTL 不況の影響で、国からの援助金が大幅に減額されました。
				PRINTW 
			ELSEIF DAY == 63
				PRINTL 不況の影響で、国からの援助金が打ち切られました。
				PRINTW 
			ENDIF

			;資金援助
			IF DAY % 7 == 0
				LOCAL = 1000 + ((FLAG:852 +1) / 25)
				LOCAL:1 = RAND:((FLAG:852 +1) / 10 + 50) + 10
				IF FLAG:853 <= -50
					LOCAL:2 = 0
				ELSeIF FLAG:853 <= -25
					LOCAL:2 = 200
				ELSEIF FLAG:853 < 0
					LOCAL:2 = 600
				ELSEIF FLAG:853 < 25
					LOCAL:2 = 1200
				ELSEIF FLAG:853 < 50
					LOCAL:2 = 1800
				ELSEIF FLAG:853 < 75
					LOCAL:2 = 3600
				ELSE
					LOCAL:2 = 7200
				ENDIF
				FOR CCOUNT, 0, CHARANUM
					SIF CCOUNT == MASTER
						CONTINUE
					SIF EXP:CCOUNT:魅了経験 > 100
						LOCAL:2 += SQRT(MAX(EXP:CCOUNT:魅了経験 - 100, 0)) * 500 + RAND:(EXP:CCOUNT:魅了経験)
				NEXT
				SIF DAY >= 21
					LOCAL += 800
				SIF DAY >= 42
					LOCAL /= 4
				SIF DAY >= 63
					LOCAL = 0
				SIF DAY < 63
					PRINTFORML 政府からの援助金として資金${LOCAL}を得た！
				PRINTFORML 市民からの義援金として資金${LOCAL:1 + LOCAL:2}を得た！
				MONEY += LOCAL + LOCAL:1 + LOCAL:2
			ENDIF

			;支出
			CALL GET_DAILYFEE
			IF FLAG:30 > 0 && FLAG:854 > 0
			LOCAL:2 = 100 + 100 * CHARANUM_ALIVE()
			ELSE
			LOCAL:2 = 40 + 20 * CHARANUM_ALIVE()
			ENDIF
			
			SIF FLAG:0 == 4
				LOCAL:2 *= 3

			SIF FLAG:30 > 0 && FLAG:854 > 0
			    LOCAL:2 += MAINTECOST

			IF MONEY >= LOCAL:2
				PRINTFORML 維持費や生活費として資金${LOCAL:2}を支払った。
				MONEY -= LOCAL:2
				WAIT
			ELSE
				PRINTFORML 維持費や生活費のための資金が不足しています！
				PRINTFORML 体力と気力を消耗しました……
				WAIT
				MONEY = 0
				FOR CCOUNT, 0, CHARANUM
					TIMES BASE:CCOUNT:体力 , 0.6
					TIMES BASE:CCOUNT:気力 , 0.6
				NEXT
				IF FLAG:30 > 0 && FLAG:854 > 0 && (FLAG:50 + FLAG:51 + FLAG:52) > 0
				PRINTFORML 資金不足のため、施設のレベルが下がった。。
				FLAG:50 = LIMIT(FLAG:50-1,0,5)
				FLAG:51 = LIMIT(FLAG:51-1,0,5)
				FLAG:52 = LIMIT(FLAG:52-1,0,5)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSE
	PRINTL 夜になりました
	WAIT
ENDIF
;日数ごとに固定のニュースを表示
IF FLAG:0 == 0
	FLAG:60 = 10001
ELSEIF GETBIT(FLAG:101,1)
	FLAG:60 = 10000
ELSEIF DAY == 1
	FLAG:60 = 10002
ELSEIF DAY == 3
	FLAG:60 = 10003
ELSEIF DAY == 6
	FLAG:60 = 10004
ELSEIF DAY == 20
	FLAG:60 = 10005
ELSEIF DAY == 23
	FLAG:60 = 10006
ELSEIF DAY == 30
	FLAG:60 = 10007
ELSEIF DAY == 41
	FLAG:60 = 10008
ELSEIF DAY == 44
	FLAG:60 = 10009
ELSEIF DAY == 51
	FLAG:60 = 10010
ELSEIF DAY == 62
	FLAG:60 = 10011
ELSEIF DAY == 63
	FLAG:60 = 10012
ELSEIF DAY == 15
	FLAG:60 = 10013
ENDIF

;TARGETを元に戻す
TARGET = FLAG:798
$RAID_SKIP
;全キャラの結界状態をチェック
CALL CHECK_SHIELD_ALL