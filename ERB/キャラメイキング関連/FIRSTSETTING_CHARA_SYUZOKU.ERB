
;-------------------------------------------------------------------------------------------------------------------------------------------------------------------
;キャラの種族の設定
@FIRSTSETTING_CHARA_SYUZOKU, ARG
#DIM RACE
#DIM FEAT_NUM
#DIM FEAT_SELECT, 300
RACE = -1
;種族の開始番号
LOCAL:1 = 201
;フィートの開始番号
LOCAL:2 = 1000
;種族の数
LOCAL:3 = 11
$MASTER_LOOP
;フィートの初期化
VARSET FEAT_SELECT
FOR LOCAL, 100, 300
	;所持フィートの読み込み
	SIF TALENT:ARG:(LOCAL + LOCAL:2) > 0
		FEAT_SELECT:LOCAL = 1
NEXT
CALL LB
PRINTFORML ※種族ごとに取得可能なフィートが設定されており、
PRINTFORML 　合計で３つまで選択することができます。
PRINTFORML 　フィート以外の基本性能は同等で種族間の優劣は存在しません。
PRINTL 
PRINTFORML {ARG:0}人目のキャラ 『%CALLNAME:ARG%』 の種族を設定してください
PRINTL ･････････････････････････････････････････････････････
FOR LOCAL, 0, LOCAL:3
	SIF TALENT:ARG:(LOCAL + LOCAL:1) > 0
		SETCOLOR 128,128,255
	SIF LOCAL < 10
		PRINT  
	PRINTFORML [{LOCAL}]%TALENTNAME:(LOCAL + LOCAL:1)%
	RESETCOLOR
NEXT
$INPUT_LOOP
INPUT
IF RESULT < 0 || RESULT > LOCAL:3
	GOTO INPUT_LOOP
ELSE
	RACE = RESULT
	FOR LOCAL, 100, 300
		SIF FEAT_ABLE_F(RACE + LOCAL:1, LOCAL + LOCAL:2) == 0
			FEAT_SELECT:LOCAL = 0
	NEXT
	CALL LB
	$MASTER_LOOP_1
	;ヴァンパイアならば夜魔の貴族を必ず取得
	SIF RACE == 9
		FEAT_SELECT:110 = 1
	;フィートの数をカウント
	FEAT_NUM = 0
	FOR LOCAL, 100, 300
		SIF FEAT_SELECT:LOCAL > 0
			FEAT_NUM ++
	NEXT
	PRINTFORML %TALENTNAME:(RACE + LOCAL:1)%
	CALL SHORTLINE
	CALL SYUZOKU_INFO, (RACE + LOCAL:1)
	PRINTL 
	PRINTL (取得可能フィート)          選択
	FOR LOCAL, 100, 300
		SIF FEAT_ABLE_F(RACE + LOCAL:1, LOCAL + LOCAL:2) == 0
			CONTINUE
		IF LOCAL < 200
			PRINT 　★
		ELSE
			PRINT 　☆
		ENDIF
		PRINTFORM %TALENTNAME:(LOCAL + LOCAL:2),22,LEFT%  [{LOCAL}]  
		IF LOCAL == 110 && FEAT_SELECT:LOCAL > 0
			SETCOLOR 255,0,0
			PRINT ◎
			RESETCOLOR
			PRINTPLAIN  固定取得
		ELSEIF FEAT_SELECT:LOCAL > 0
			SETCOLOR 255,128,0
			PRINT ○
			RESETCOLOR
		ELSE
			PRINT ×
		ENDIF
		PRINTL 
	NEXT
	PRINTL 
	PRINTFORML 選択したフィート({FEAT_NUM}/3)
	SIF FEAT_NUM > 3
		SETCOLOR 128,128,128
	PRINTL [0]決定
	RESETCOLOR
	PRINTL [1]キャンセル
	$INPUT_LOOP_0
	INPUT
	IF RESULT == 0 && FEAT_NUM <= 3
	ELSEIF RESULT == 1
		GOTO MASTER_LOOP
	ELSEIF RESULT >= 100 && RESULT <= 300 && FEAT_ABLE_F(RACE + LOCAL:1, RESULT + LOCAL:2) > 0
		LOCAL = RESULT
		CALL LB
		CALL FEAT_INFO, (LOCAL + LOCAL:2)
		PRINTL 
		IF FEAT_SELECT:LOCAL > 0
			FEAT_SELECT:LOCAL = 0
		ELSE
			FEAT_SELECT:LOCAL = 1
		ENDIF
		GOTO MASTER_LOOP_1
	ELSE
		GOTO INPUT_LOOP_0
	ENDIF
	;種族の設定
	FOR LOCAL, LOCAL:1, LOCAL:1 + LOCAL:3
		TALENT:ARG:LOCAL = 0
	NEXT
	TALENT:ARG:(RACE + LOCAL:1) = 1
	PRINTFORML 種族を 『%TALENTNAME:(RACE + (LOCAL:1))%』 に設定しました
	;ロボっ子の場合
	IF RACE == 3
		IF TALENT:ARG:オトコ <= 0
			PRINTFORML 人工子宮を内蔵させることにより、%CALLNAME:ARG%を妊娠可能にすることができます
			PRINTFORML [0]妊娠機能を付ける
			PRINTFORML [1]妊娠機能を付けない（不妊になります）
			$INPUT_LOOP_1
			INPUT
			IF RESULT == 0
				PRINTFORML %CALLNAME:ARG%は妊娠可能になります
				TALENT:ARG:未熟 = 0
			ELSEIF RESULT == 1
				PRINTFORML %CALLNAME:ARG%に妊娠機能を付けません
				TALENT:ARG:未熟 = 2
			ELSE
				GOTO INPUT_LOOP_1
			ENDIF
		ENDIF
		IF TALENT:ARG:口上設定 != 1
			PRINTFORML 口上を『ロボ風』に変更しますか？
			PRINTFORML [0]変更する
			PRINTFORML [1]変更しない
			$INPUT_LOOP_2
			INPUT
			IF RESULT == 0
				PRINTFORML 口上をロボ風に設定しました
				TALENT:ARG:口上設定 = 1
			ELSEIF RESULT == 1
				PRINTFORML 設定を変更しません
			ELSE
				GOTO INPUT_LOOP_1
			ENDIF
		ENDIF
	ENDIF
	;フィートの設定
	FOR LOCAL, 100, 300 
		IF FEAT_SELECT:LOCAL > 0
			TALENT:ARG:(LOCAL + LOCAL:2) = 1
		ELSE
			TALENT:ARG:(LOCAL + LOCAL:2) = 0
		ENDIF
	NEXT
	;プロフィールに反映
	CALL SET_PROFILE, ARG
ENDIF
PRINTL 
PRINTL 

