;CSV書き出しバージョン0.394
@EXPORT_CSV
#DIM LCOUNT
#DIM SELECT
#DIM RELATION_LIST, CHARACSV最大数
VARSET LOCAL
VARSET LOCALS

PRINTL 注意：本機能はCSVからキャラクターデータを自作・改造できる人向けです。
PRINTL 
PRINTL 現在のキャラクターのステータスをCSVに対応した書式で出力します。
PRINTL データが表示されたらEMUERAの機能から「ログをクリップボードにコピー」を選択し、
PRINTL 空のCSVファイルにコピーペーストしてください。
PRINTL 一部保存されない情報もあります（修練Pやレベルの端数、妊娠状況など）。
PRINTL また、履歴ログが5000行以上あると失敗しますのでご注意ください。
PRINTL 

PRINTL 重複しないキャラクター番号を検索します。
PRINTL （連続してキャラクターデータを作製している場合、CSVの再読み込みが必要です）
;キャラクター番号で101〜300の間で空いている番号を探す
REPEAT 200
	LOCAL:1 = COUNT + 101
	;EXISTCSVが1なら設定されていないのでBREAKして決定
	IF EXISTCSV(LOCAL:1,0) != 1
		PRINTFORML キャラクター番号を「{LOCAL:1}」で出力します。
		BREAK
	ENDIF
	;LOCAL:1が300に達してもBREAKしていなかったら見つからなかったということで暫定的に100番を当てる
	IF LOCAL:1 == 300
		PRINTL 101〜300番の間に空いているキャラクター番号がないようです。
		PRINTL キャラクター番号を「101」で出力しますが、重複しているので後から変更をしてください。
		LOCAL:1 = 101
		BREAK
	ENDIF
REND
PRINTL

$INPUT_LOOP
PRINTL [0] 実行する
PRINT [1] 調教状況の保存範囲を変更する　現在の設定：
IF LOCAL:2 == 0
	PRINTL 感度・経験なども0にしてリセット
ELSEIF LOCAL:2 == 1
	PRINTL 感度・経験のみ保存
ELSEIF LOCAL:2 == 2
	PRINTL 感度・経験・刻印・珠を保存
ENDIF
PRINTFORML [2] キャラクター番号を手動で変更する　現在の番号：{LOCAL:1}
PRINTL [999] やめる
INPUT
IF RESULT == 999
	RETURN
ELSEIF RESULT == 1
	LOCAL:2 += 1
	LOCAL:2 = LOCAL:2 % 3
	GOTO INPUT_LOOP
ELSEIF RESULT == 2
	PRINTL 100〜998の数値を入力してください
	INPUT
	LOCAL:1 = RESULT
	IF EXISTCSV(LOCAL:1,0) == 1
		PRINTW 注意：そのキャラクター番号のキャラはすでに設定されています
	ENDIF
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	
ELSE
	GOTO INPUT_LOOP
ENDIF

;ログを全消去
CLEARLINE LINECOUNT

;出力開始
;番号は事前に調べた空番号に
PRINTFORML 番号,{LOCAL:1},
PRINTFORML 名前,%NAME:TARGET%,
PRINTFORML 呼び名,%CALLNAME:TARGET%,
;苗字
SIF CSTR:10 != ""
	PRINTFORML CSTR,10,%CSTR:10%,;　苗字
;一人称
SIF CSTR:4 != ""
	PRINTFORML CSTR,4,%CSTR:4%,;　一人称
SIF CFLAG:8 > 0
	PRINTFORML フラグ,8,{CFLAG:8},;　一人称設定
PRINTL
IF CSTR:204 != "" || CSTR:205 != "" || CSTR:206 != ""
	PRINTL ;-------年齢指定----------------
	SIF CSTR:204 != ""
		PRINTFORML CSTR,204,%CSTR:204%,;実年齢指定
	SIF CSTR:205 != ""
		PRINTFORML CSTR,205,%CSTR:205%,;年齢指定
	SIF CSTR:206 != ""
		PRINTFORML CSTR,206,%CSTR:206%,;変身時年齢指定
	PRINTL
ELSEIF CFLAG:34 > 0
	PRINTL ;-------年齢指定----------------
	PRINTFORML CSTR,204,{BASE:実年齢},;実年齢指定
	PRINTFORML CSTR,205,{BASE:年齢},;年齢指定
	PRINTFORML CSTR,206,{MAXBASE:年齢},;変身時年齢指定
	PRINTL
ENDIF
PRINTL ;-------初期ステータス----------
PRINTL ;ステータスにおける基礎値
PRINTFORML 基礎,体力,{SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 0, CSVBASE_F(NO:TARGET,0))},
PRINTFORML 基礎,気力,{SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 1, CSVBASE_F(NO:TARGET,1))},
PRINTFORML 基礎,性耐性,{SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 2, CSVBASE_F(NO:TARGET,2))},
PRINTFORML 基礎,攻撃,{SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 10, CSVBASE_F(NO:TARGET,10))},
PRINTFORML 基礎,防御,{SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 11, CSVBASE_F(NO:TARGET,11))},
PRINTFORML 基礎,敏捷,{SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 12, CSVBASE_F(NO:TARGET,12))},
PRINTFORML 基礎,知性,{SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 13, CSVBASE_F(NO:TARGET,13))},
PRINTFORML 基礎,射精,{MAXBASE:射精},
PRINTFORML 基礎,噴乳,{MAXBASE:噴乳},
PRINTFORML 能力,レベル,{ABL:レベル},
PRINTFORML 基礎,空中ダッシュ,{MAXBASE:空中ダッシュ + TALENT:空中苦手 - TALENT:空中得意},
PRINTL
LOCAL:3 = LINECOUNT
SIF ABL:近距離 > 0
	PRINTFORML 能力,近距離,{ABL:近距離},
SIF ABL:中距離 > 0
	PRINTFORML 能力,中距離,{ABL:中距離},
SIF ABL:遠距離 > 0
	PRINTFORML 能力,遠距離,{ABL:遠距離},
SIF ABL:戦闘基礎 > 0
	PRINTFORML 能力,戦闘基礎,{ABL:戦闘基礎},
SIF LINECOUNT > LOCAL:3
	PRINTL
PRINTL ;-------素質--------------------
REPEAT 1000
	;COUNT200変身能力、250〜299のオプション系素質、COUNT900以降の記述を飛ばす
	IF TALENT:COUNT != 0 && COUNT != 200 && COUNT < 900 && (COUNT < 250 || COUNT > 299)
		PRINTFORM 素質,%TALENTNAME:COUNT%,{TALENT:COUNT},
		IF COUNT == 800
			IF TALENT:800 == 1
				PRINT ;　片思い
			ELSEIF TALENT:800 == 2
				PRINT ;　彼氏持ち
			ELSEIF TALENT:800 == 3
				PRINT ;　婚約
			ELSEIF TALENT:800 == 4
				PRINT ;　人妻
			ELSEIF TALENT:800 == 5
				PRINT ;　未亡人
			ENDIF
		ELSEIF COUNT == 801
			IF TALENT:801 == 1
				PRINT ;　小学生
			ELSEIF TALENT:801 == 2
				PRINT ;　中学生
			ELSEIF TALENT:801 == 3
				PRINT ;　高校生
			ELSEIF TALENT:801 == 4
				PRINT ;　大学生
			ENDIF
		ELSEIF COUNT == 810
			IF TALENT:810 == 1
				PRINT ;　弟持ち
			ELSEIF TALENT:810 == 2
				PRINT ;　妹持ち
			ELSEIF TALENT:810 == 3
				PRINT ;　兄持ち
			ELSEIF TALENT:810 == 4
				PRINT ;　姉持ち
			ELSEIF TALENT:810 == 5
				PRINT ;　幼い息子
			ELSEIF TALENT:810 == 6
				PRINT ;　幼い娘
			ELSEIF TALENT:810 == 7
				PRINT ;　年頃の息子
			ELSEIF TALENT:810 == 8
				PRINT ;　年頃の娘
			ENDIF
		ELSEIF COUNT == 811
			IF TALENT:811 == 1
				PRINT ;　メガネっ娘
			ELSEIF TALENT:811 == 2
				PRINT ;　アホ毛
			ELSEIF TALENT:811 == 3
				PRINT ;　お嬢様
			ENDIF
		ELSEIF COUNT == 812
			IF TALENT:812 == 1
				PRINT ;　安産型
			ELSEIF TALENT:812 == 2
				PRINT ;　むちむち
			ELSEIF TALENT:812 == 3
				PRINT ;　イカ腹
			ELSEIF TALENT:812 == 4
				PRINT ;　スレンダー
			ENDIF
		ENDIF
		PRINTL
	ENDIF
REND
PRINTL ;-------FEAT--------------------
FOR LCOUNT, 1000, 1300
	IF TALENT:LCOUNT > 0
		PRINTFORML 素質,%TALENTNAME:LCOUNT%,{TALENT:LCOUNT},
	ENDIF
NEXT
;感度・経験はLOCAL:2が1以上のときのみ
IF LOCAL:2 >= 1
	PRINTL
	PRINTL ;-------感覚など----------------
	REPEAT 25
		IF ABL:COUNT >= 1
			PRINTFORML 能力,%ABLNAME:COUNT%,{ABL:COUNT},
		ENDIF
	REND
	PRINTL
	PRINTL ;-------経験--------------------
	REPEAT 60
		IF EXP:COUNT >= 1
			PRINTFORML 経験,%EXPNAME:COUNT%,{EXP:COUNT},
		ENDIF
	REND
	;拡張度も経験扱いとして出力
	IF FILTER_CHECK_F(16) == 0
		FOR COUNT, 35, 37
			SUBSTRINGU ABLNAME:(COUNT - 34), 0, 1
			LOCALS = %RESULTS%拡張度
			PRINTFORML フラグ,{COUNT},{CFLAG:COUNT},;　%LOCALS%
		NEXT
	ENDIF
ENDIF
;珠・刻印はLOCAL:2が2以上のときのみ
IF LOCAL:2 >= 2
	PRINTL
	PRINTL ;-------刻印--------------------
	REPEAT 100
		IF MARK:COUNT >= 1
			PRINTFORML MARK,{COUNT},{MARK:COUNT},
		ENDIF
	REND
	PRINTL
	PRINTL ;-------珠----------------------
	REPEAT 20
		IF JUEL:COUNT >= 1
			PRINTFORML JUEL,{COUNT},{JUEL:COUNT},
		ENDIF
	REND
ENDIF
PRINTL
PRINTL ;-------衣装--------------------
PRINTFORML フラグ,40,{CFLAG:40},;　%ITEMNAME:(CFLAG:40)%
PRINTFORML フラグ,41,{CFLAG:41},;　%ITEMNAME:(CFLAG:41)%　
PRINTFORML フラグ,42,{CFLAG:42},;　%ITEMNAME:(CFLAG:42)%
SIF CSTR:8 != ""
	PRINTFORML CSTR,8,%CSTR:8%,
SIF CSTR:9 != ""
	PRINTFORML CSTR,9,%CSTR:9%,
PRINTL
PRINTL ;-------髪型--------------------
PRINTFORML CSTR,12,%CSTR:12%,
PRINTFORML CSTR,13,%CSTR:13%,
PRINTFORML CSTR,14,%CSTR:14%,
PRINTL
PRINTL ;-------目つき--------------------
PRINTFORML CSTR,18,%CSTR:18%,
PRINTL 
PRINTL ;-------プロフィール-------------
PRINTL ;髪の色
PRINTFORML CSTR,30,%CSTR:30%,
IF TALENT:変身能力 == 0
	PRINTL ;変身時の髪の色
	PRINTFORML CSTR,31,%CSTR:30%,
ELSE
	PRINTL ;変身時の髪の色
	PRINTFORML CSTR,31,%CSTR:31%,
ENDIF
PRINTL ;右目の色
PRINTFORML CSTR,32,%CSTR:32%,
PRINTL ;左目の色
PRINTFORML CSTR,33,%CSTR:33%,
IF TALENT:変身能力 == 0
	PRINTL ;変身時の右目の色
	PRINTFORML CSTR,34,%CSTR:32%,
	PRINTL ;変身時の左目の色
	PRINTFORML CSTR,35,%CSTR:33%,
ELSE
	PRINTL ;変身時の右目の色
	PRINTFORML CSTR,34,%CSTR:34%,
	PRINTL ;変身時の左目の色
	PRINTFORML CSTR,35,%CSTR:35%,
ENDIF
PRINTL ;肌の色
PRINTFORML CSTR,36,%CSTR:36%,
IF TALENT:変身能力 == 0
	PRINTL ;変身時の肌の色
	PRINTFORML CSTR,37,%CSTR:36%,
ELSE
	PRINTL ;変身時の肌の色
	PRINTFORML CSTR,37,%CSTR:37%,
ENDIF
PRINTL
PRINTL ;-------武器定義-----------------
FOR LOCAL, 5, 8
	LOCALS:(LOCAL - 4) += @"%CSTR:LOCAL%" + "// //" + @"%FSTYLE_NAME_F(TARGET,LOCAL - 4)%"
NEXT
PRINTFORML CSTR,15,%LOCALS:1%,
PRINTFORML CSTR,16,%LOCALS:2%,
PRINTFORML CSTR,17,%LOCALS:3%,
PRINTL
PRINTL ;-------キャラフラグ------------
PRINTFORM フラグ,10,{CFLAG:10},
PRINTL ;　変身能力のあるキャラの変身前の能力(デフォルトは25%)
PRINTFORM フラグ,11,{CFLAG:11},
PRINTL ;　変身能力のあるキャラのSP変身後の能力(デフォルトは110%)
PRINTL
;トロフィー『陰鬱ハネムーン』達成時、最初から悪堕ちが解禁
IF GLOBAL:162 > 0
	SIF CFLAG:0 == 3 || CFLAG:32 > 0
		PRINTL ;-------悪堕ち関係------------
	IF CFLAG:0 == 3
		PRINTFORM ;フラグ,0,{CFLAG:0},
		PRINTL ;　最初から悪堕ち
	ENDIF
	IF CFLAG:32 > 0
		PRINTFORM ;フラグ,32,{CFLAG:32},
		PRINTL ;　淫紋
	ENDIF
	SIF CFLAG:0 == 3 || CFLAG:32 > 0
		PRINTL
ENDIF
IF TALENT:変身能力 == 1
	PRINTL ;-------変身能力関連------------
	PRINTL 素質,変身能力,1,;　変身能力あり
	IF CFLAG:2 > 0
		PRINTL
		PRINTL ;変身後の名前
		PRINTFORML CSTR,0,%CSTR:0%,
		PRINTFORML フラグ,2,1,
	ENDIF
	IF CFLAG:3 > 0
		PRINTL
		PRINTL ;変身後の呼び名
		PRINTFORML CSTR,1,%CSTR:1%,
		PRINTFORML フラグ,3,1,
	ENDIF
	IF CFLAG:4 > 0
		PRINTL
		PRINTL ;変身時のかけ声
		PRINTFORML CSTR,2,%CSTR:2%,
		PRINTFORML フラグ,4,1,
	ENDIF
	IF CFLAG:5 > 0
		PRINTL
		PRINTL ;変身時の名乗り
		PRINTFORML CSTR,3,%CSTR:3%,
		PRINTFORML フラグ,5,1,
	ENDIF
	PRINTL
ELSEIF TALENT:変身能力 == -1
	PRINTL ;-------変身能力関連------------
	PRINTL 素質,変身能力,-1,;　非戦闘員
	PRINTL
ENDIF
IF TALENT:固有キャラ != 0
	PRINTL ;-------固有キャラ----------
	PRINTFORML 素質,固有キャラ,{TALENT:固有キャラ},
	PRINTL
ENDIF
;RELATIONの値をリストにコピー
LOCAL = 0
VARSET RELATION_LIST
FOR SELECT, 0, CHARANUM
	SIF SELECT == MASTER
		CONTINUE
	IF NO:SELECT > 0 && RELATION:TARGET:SELECT > 0
		RELATION_LIST:(NO:SELECT) = RELATION:TARGET:SELECT
		LOCAL ++
	ENDIF
NEXT
IF LOCAL > 0
	PRINTL ;-------相関関係----------
	FOR SELECT, 0, CHARANUM
		SIF SELECT == MASTER
			CONTINUE
		IF NO:SELECT > 0 && RELATION:TARGET:SELECT > 0
			CALL GET_RELATION, TARGET, SELECT
			PRINTFORML 相性,{NO:SELECT},{RELATION_LIST:(NO:SELECT)},;%RESULTS%
		ENDIF
	NEXT
	PRINTL
ENDIF
IF CSTR:40 != "" || CSTR:41 != "" || CSTR:42 != ""
	PRINTL ;-------パーソナリティ----------
	FOR SELECT, 40, 43
		SIF CSTR:SELECT != ""
			PRINTFORML CSTR,{SELECT},%CSTR:SELECT%,
	NEXT
	PRINTL
ENDIF
IF CFLAG:50 || CFLAG:51 || CFLAG:52 || CFLAG:53 || CFLAG:54 || CFLAG:55 || CFLAG:56
	PRINTL ;-------キャラメイクボーナス----------
	SIF CFLAG:50
		PRINTFORML フラグ,50,{CFLAG:50},;体力
	SIF CFLAG:51
		PRINTFORML フラグ,51,{CFLAG:51},;気力
	SIF CFLAG:52
		PRINTFORML フラグ,52,{CFLAG:52},;性耐性
	SIF CFLAG:53
		PRINTFORML フラグ,53,{CFLAG:53},;攻撃
	SIF CFLAG:54
		PRINTFORML フラグ,54,{CFLAG:54},;防御
	SIF CFLAG:55
		PRINTFORML フラグ,55,{CFLAG:55},;敏捷
	SIF CFLAG:56
		PRINTFORML フラグ,56,{CFLAG:56},;知性
	PRINTL
ENDIF
IF CFLAG:60 || CFLAG:61 || CFLAG:62 || CFLAG:63 || CFLAG:64 || CFLAG:65 || CFLAG:66
	PRINTL ;-------遺伝ボーナス----------
	SIF CFLAG:60
		PRINTFORML フラグ,60,{CFLAG:60},;体力
	SIF CFLAG:61
		PRINTFORML フラグ,61,{CFLAG:61},;気力
	SIF CFLAG:62
		PRINTFORML フラグ,62,{CFLAG:62},;性耐性
	SIF CFLAG:63
		PRINTFORML フラグ,63,{CFLAG:63},;攻撃
	SIF CFLAG:64
		PRINTFORML フラグ,64,{CFLAG:64},;防御
	SIF CFLAG:65
		PRINTFORML フラグ,65,{CFLAG:65},;敏捷
	SIF CFLAG:66
		PRINTFORML フラグ,66,{CFLAG:66},;知性
	PRINTL
ENDIF
LOCAL = 0
FOR COUNT, 250, 260
	SIF TALENT:COUNT != 0
		LOCAL ++
NEXT
IF LOCAL > 0
	PRINTL ;-------オプション系素質----------
	FOR COUNT, 250, 260
		IF TALENT:COUNT != 0
			PRINTFORM 素質,%TALENTNAME:COUNT%,{TALENT:COUNT},
			PRINTL
		ENDIF
	NEXT
ENDIF
LOCAL = 0
FOR COUNT, 260, 272
	SIF TALENT:COUNT != 0
		LOCAL ++
NEXT
IF LOCAL > 0
	PRINTL ;-------体型数値指定----------
	FOR COUNT, 260, 272
		IF TALENT:COUNT != 0
			PRINTFORM 素質,%TALENTNAME:COUNT%,{TALENT:COUNT},
			PRINTL
		ENDIF
	NEXT
	PRINTL
ENDIF
IF CFLAG:34 > 0
	IF TALENT:体型乱数値指定 == 0 || TALENT:体型成長曲線指定 == 0
		PRINTL ;-------現在の体型乱数----------
		SIF TALENT:体型乱数値指定 == 0
			PRINTFORML 素質,体型乱数値指定,{CFLAG:33},
		SIF TALENT:体型成長曲線指定 == 0
			PRINTFORML 素質,体型成長曲線指定,{CFLAG:34},
		PRINTL
	ENDIF
	PRINTL ;キャラの体型をCSVに記述する場合は上記２つあればＯＫだが、
	PRINTL ;以下の乱数指定を利用することもできる
	PRINTL ;不要であれば削除すること
	PRINTL ;-------体型乱数指定----------
	IF TALENT:280 > 0
		PRINTFORM 素質,%TALENTNAME:280%,{TALENT:280},
	ELSE
		PRINTFORM 素質,%TALENTNAME:280%,{CFLAG:33 % 11 + 1},
	ENDIF
	PRINTL
	IF TALENT:281 > 0
		PRINTFORM 素質,%TALENTNAME:281%,{TALENT:281},
	ELSE
		PRINTFORM 素質,%TALENTNAME:281%,{CFLAG:33 % 31 + 1},
	ENDIF
	PRINTL
	IF TALENT:282 > 0
		PRINTFORM 素質,%TALENTNAME:282%,{TALENT:282},
	ELSE
		PRINTFORM 素質,%TALENTNAME:282%,{CFLAG:33 % 59 + 1},
	ENDIF
	PRINTL
	IF TALENT:283 > 0
		PRINTFORM 素質,%TALENTNAME:283%,{TALENT:283},
	ELSE
		PRINTFORM 素質,%TALENTNAME:283%,{CFLAG:33 % 15 + 1},
	ENDIF
	PRINTL
	IF TALENT:284 > 0
		PRINTFORM 素質,%TALENTNAME:284%,{TALENT:284},
	ELSE
		PRINTFORM 素質,%TALENTNAME:284%,{CFLAG:33 % 16 + 1},
	ENDIF
	PRINTL
	IF TALENT:285 > 0
		PRINTFORM 素質,%TALENTNAME:285%,{TALENT:285},
	ELSE
		PRINTFORM 素質,%TALENTNAME:285%,{CFLAG:33 % 13 + 1},
	ENDIF
	PRINTL
	IF TALENT:286 > 0
		PRINTFORM 素質,%TALENTNAME:286%,{TALENT:286},
	ELSE
		PRINTFORM 素質,%TALENTNAME:286%,{CFLAG:33 % 23 + 1},
	ENDIF
	PRINTL
	IF TALENT:287 > 0
		PRINTFORM 素質,%TALENTNAME:287%,{TALENT:287},
	ELSE
		PRINTFORM 素質,%TALENTNAME:287%,{CFLAG:33 % 7 + 1},
	ENDIF
	PRINTL
	IF TALENT:288 > 0
		PRINTFORM 素質,%TALENTNAME:288%,{TALENT:288},
	ELSE
		PRINTFORM 素質,%TALENTNAME:288%,{CFLAG:33 % 53 + 1},
	ENDIF
	PRINTL
	PRINTL
ELSE
	LOCAL = 0
	FOR COUNT, 280, 289
		SIF TALENT:COUNT != 0
			LOCAL ++
	NEXT
	IF LOCAL > 0
		PRINTL ;-------体型乱数指定----------
		FOR COUNT, 280, 289
			IF TALENT:COUNT != 0
				PRINTFORM 素質,%TALENTNAME:COUNT%,{TALENT:COUNT},
				PRINTL
			ENDIF
		NEXT
		PRINTL
	ENDIF
ENDIF
PRINTL ;-------------------------------------------------------------------------------------
PRINTL ;EXPORT VER 0.390
PRINTL ;生成終了=============================================================================
WAIT

