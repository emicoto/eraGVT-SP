;**********************************************************
;主題の設定
@FIRSTSETTING_TITLE
PRINTL 主題を設定しますか？
PRINTL （『魔法少女』や『美少女戦隊』など。変身後の名乗り口上に使用されます）
DRAWLINE
PRINTL [0]主題なし
PRINTL [1]ランダムに設定
PRINTL [2]自分で設定
PRINTL [99]もどる
$INPUT_LOOP
INPUT
IF RESULT == 0
	FLAG:5 = 0
	PRINTL 主題を設定しませんでした
ELSEIF RESULT == 1
	FLAG:5 = 1
	$INPUT_LOOP_CHILD_1_1
	CALL FIRSTSETTING_TITLE_RANDOM
	LOCAL = RESULT
	IF RESULTS == ""
		PRINTFORML 主題を設定しません。よろしいですか？
	ELSE
		PRINTFORML 主題を 『%RESULTS%』 とします。よろしいですか？
	ENDIF
	PRINTL [0]もう一度選びなおす
	PRINTL [1]はい
	SIF SAVESTR:10 != ""
		PRINTFORML [2]変更しない（%SAVESTR:10%）
	$INPUT_LOOP_CHILD_1_2
	INPUT
	IF RESULT == 0
		GOTO INPUT_LOOP_CHILD_1_1
	ELSEIF RESULT == 1
		SAVESTR:10 = %RESULTS%
		IF RESULTS == ""
			FLAG:5 = 0
			PRINTFORML 主題を設定しませんでした
			RESULTS = 
		ELSE
			PRINTFORML 主題を 『%SAVESTR:10%』 に設定しました
		ENDIF
	ELSEIF RESULT == 2 && SAVESTR:10 != ""
	ELSE
		GOTO INPUT_LOOP_CHILD_1_2
	ENDIF
ELSEIF RESULT == 2
	FLAG:5 = 1
	PRINTL 主題を入力してください。
	SIF SAVESTR:10 != ""
		PRINTFORML [999]変更しない（%SAVESTR:10%）
	$INPUT_LOOP_CHILD_2
	INPUTS
	IF RESULTS == ""
		GOTO INPUT_LOOP_CHILD_2
	ELSEIF RESULTS == "999"
	ELSE
		SAVESTR:10 = %RESULTS%
		PRINTFORML 主題を 『%SAVESTR:10%』 に設定しました
	ENDIF
ELSEIF RESULT == 99
	RETURN 99
ELSE
	GOTO INPUT_LOOP
ENDIF
PRINTL 

;----------------------------------------------------------
;主題のランダム設定
@FIRSTSETTING_TITLE_RANDOM
$REGENERATE_TITLE
PRINTL [0]日本語の単語で構成
PRINTL [1]外国語の単語で構成
PRINTL [2]完全ランダム
PRINTL [99]もどる
$INPUT_LOOP
INPUT
IF RESULT == 99
	RESULTS = 
	RETURN 99
ENDIF
LOCAL:0 = RESULT
RESULTS = ""
PRINTL
WHILE RESULTS != ""
	FOR LOCAL:1, 0, 20
		IF LOCAL:0 < 0 || LOCAL:0 > 2
			GOTO INPUT_LOOP
		ELSEIF LOCAL:0 == 0
			CALL FIRSTSETTING_TITLE_RANDOM_TOP, 0
			LOCAL:(LOCAL:1 + 10) = RESULT
			CALL FIRSTSETTING_TITLE_RANDOM_BOTTOM, 0
			LOCAL:(LOCAL:1 + 30) = RESULT
			LOCAL:(LOCAL:1 + 50) = 0
		ELSEIF LOCAL:0 == 1
			CALL FIRSTSETTING_TITLE_RANDOM_TOP, 1
			LOCAL:(LOCAL:1 + 10) = RESULT
			CALL FIRSTSETTING_TITLE_RANDOM_BOTTOM, 1
			LOCAL:(LOCAL:1 + 30) = RESULT
			LOCAL:(LOCAL:1 + 50) = 0
		ELSEIF LOCAL:0 == 2
			CALL FIRSTSETTING_TITLE_RANDOM_TOP, RAND:2
			LOCAL:(LOCAL:1 + 10) = RESULT
			CALL FIRSTSETTING_TITLE_RANDOM_BOTTOM, RAND:2
			LOCAL:(LOCAL:1 + 30) = RESULT
			;ラストにひらがながつく場合もある
			LOCAL:(LOCAL:1 + 50) = 0
			IF RAND:2 == 0
				CALL FIRSTSETTING_TITLE_RANDOM_LAST, 0
				LOCAL:(LOCAL:1 + 50) = RESULT
			ENDIF
		ENDIF
	NEXT

	REPEAT 20
		PRINTFORML [{COUNT, 2}] %STR:(LOCAL:(COUNT + 10))%%STR:(LOCAL:(COUNT + 30))%\@LOCAL:(COUNT + 50) ?%STR:(LOCAL:(COUNT + 50))%#\@
	REND

	PRINTL 
	PRINTL [100]構成から選びなおす
	PRINTL [200]再生成

	$INPUT_LOOP2
	INPUT
	IF RESULT >= 0 && RESULT < 20
		RESULTS = %STR:(LOCAL:(RESULT + 10))%%STR:(LOCAL:(RESULT + 30))%\@LOCAL:(RESULT + 50) ?%STR:(LOCAL:(RESULT + 50))%#\@
		RETURN LOCAL:0
	ELSEIF RESULT == 100
		PRINTL
		GOTO REGENERATE_TITLE
	ELSEIF RESULT == 200
		CLEARLINE 24
	ELSE
		GOTO INPUT_LOOP2
	ENDIF
WEND

;----------------------------------------------------------
;上の句
@FIRSTSETTING_TITLE_RANDOM_TOP, ARG:0
IF ARG:0 == 0
	LOCAL = RAND:100
	WHILE STR:LOCAL == ""
		LOCAL = RAND:100
	WEND
	LOCALS = %STR:LOCAL%
ELSEIF ARG:0 == 1
	LOCAL = RAND:100 + 100
	WHILE STR:LOCAL == ""
		LOCAL = RAND:100 + 100
	WEND
	LOCALS = %STR:LOCAL%
ENDIF
RESULTS:1 = %LOCALS%
RETURN LOCAL

;----------------------------------------------------------
;下の句
@FIRSTSETTING_TITLE_RANDOM_BOTTOM, ARG:0
IF ARG:0 == 0
	LOCAL = RAND:100 + 200
	WHILE STR:LOCAL == ""
		LOCAL = RAND:100 + 200
	WEND
	LOCALS = %STR:LOCAL%
ELSEIF ARG:0 == 1
	LOCAL = RAND:100 + 300
	WHILE STR:LOCAL == ""
		LOCAL = RAND:100 + 300
	WEND
	LOCALS = %STR:LOCAL%
ENDIF
RESULTS:2 = %LOCALS%
RETURN LOCAL

;----------------------------------------------------------
;ひらがな
@FIRSTSETTING_TITLE_RANDOM_LAST, ARG:0
IF ARG:0 == 0
	LOCAL = RAND:100 + 400
	WHILE STR:LOCAL == ""
		LOCAL = RAND:100 + 400
	WEND
	LOCALS = %STR:LOCAL%
ELSE
ENDIF
RESULTS:3 = %LOCALS%
RETURN LOCAL
