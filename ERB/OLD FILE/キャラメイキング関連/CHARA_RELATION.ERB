;--------------------------------------------------
;キャラの相関関係に関する処理
;--------------------------------------------------
;固有番号ARGのキャラがARG:1のキャラをどう思っているか返す
@GET_RELATION, ARG:0, ARG:1
LOCALS = 

;印象系---------------------------------

IF RELATION:(ARG:0):(ARG:1) < POWER(2, 20)
	IF GETBIT(RELATION:(ARG:0):(ARG:1),疎遠な) && GETBIT(RELATION:(ARG:0):(ARG:1),親しい)
		LOCALS += "微妙な距離感の知人"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),親しい)
		LOCALS += "親しい間柄"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),疎遠な)
		LOCALS += "顔見知り"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),愛する) && GETBIT(RELATION:(ARG:0):(ARG:1),憎悪する)
		LOCALS += "複雑な関係"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),愛する)
		LOCALS += "大事な人"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),憎悪する)
		LOCALS += "仇敵"
	ENDIF
ELSE
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),生き別れ)
		LOCALS += "生き別れの"
	IF GETBIT(RELATION:(ARG:0):(ARG:1),疎遠な) && GETBIT(RELATION:(ARG:0):(ARG:1),親しい)
		LOCALS += "微妙な距離感の"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),親しい) && GETBIT(RELATION:(ARG:0):(ARG:1),友人) == 0
		LOCALS += "親しい"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),疎遠な)
		LOCALS += "疎遠な"
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1),愛する) && GETBIT(RELATION:(ARG:0):(ARG:1),憎悪する)
		LOCALS += "愛憎渦巻く"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),愛する)
		LOCALS += "愛する"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),憎悪する)
		LOCALS += "憎悪する"
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),義理の)
		LOCALS += "義理の"
ENDIF

;関係---------------------------------

LOCAL = 0
IF GETBIT(RELATION:(ARG:0):(ARG:1),親子)
	IF TOSHIUE_F(ARG:0, ARG:1) > 0
		IF TALENT:(ARG:1):オトコ > 0
			LOCALS += "息子"
			LOCAL ++
		ELSE
			LOCALS += "娘"
			LOCAL ++
		ENDIF
	ELSE
		IF TALENT:(ARG:1):オトコ > 0
			LOCALS += "父親"
			LOCAL ++
		ELSE
			LOCALS += "母親"
			LOCAL ++
		ENDIF
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),兄弟姉妹) || GETBIT(RELATION:(ARG:0):(ARG:1),祖父祖母孫) || GETBIT(RELATION:(ARG:0):(ARG:1),おじおば) || GETBIT(RELATION:(ARG:0):(ARG:1),甥姪) || GETBIT(RELATION:(ARG:0):(ARG:1),いとこ)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),兄弟姉妹)
	IF TOSHIUE_F(ARG:0, ARG:1)
		IF TALENT:(ARG:1):オトコ > 0
			LOCALS += "弟"
			LOCAL ++
		ELSE
			LOCALS += "妹"
			LOCAL ++
		ENDIF
	ELSE
		IF TALENT:(ARG:1):オトコ > 0
			LOCALS += "兄"
			LOCAL ++
		ELSE
			LOCALS += "姉"
			LOCAL ++
		ENDIF
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),祖父祖母孫) || GETBIT(RELATION:(ARG:0):(ARG:1),おじおば) || GETBIT(RELATION:(ARG:0):(ARG:1),甥姪) || GETBIT(RELATION:(ARG:0):(ARG:1),いとこ)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),祖父祖母孫)
	IF TOSHIUE_F(ARG:0, ARG:1)
		LOCALS += "孫"
		LOCAL ++
	ELSE
		IF TALENT:(ARG:1):オトコ > 0
			LOCALS += "祖父"
			LOCAL ++
		ELSE
			LOCALS += "祖母"
			LOCAL ++
		ENDIF
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),おじおば) || GETBIT(RELATION:(ARG:0):(ARG:1),甥姪) || GETBIT(RELATION:(ARG:0):(ARG:1),いとこ)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),おじおば)
	IF TALENT:(ARG:1):オトコ > 0
		LOCALS += "おじ"
		LOCAL ++
	ELSE
		LOCALS += "おば"
		LOCAL ++
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),いとこ)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),甥姪)
	IF TALENT:(ARG:1):オトコ > 0
		LOCALS += "甥"
		LOCAL ++
	ELSE
		LOCALS += "姪"
		LOCAL ++
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),いとこ)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),いとこ)
	LOCALS += "いとこ"
	LOCAL ++
ENDIF
SIF LOCAL && (GETBIT(RELATION:(ARG:0):(ARG:1),友人) || GETBIT(RELATION:(ARG:0):(ARG:1),片思い) || GETBIT(RELATION:(ARG:0):(ARG:1),恋人) || GETBIT(RELATION:(ARG:0):(ARG:1),婚約者) || GETBIT(RELATION:(ARG:0):(ARG:1),配偶者))
	LOCALS += "かつ"
IF GETBIT(RELATION:(ARG:0):(ARG:1),親しい) && GETBIT(RELATION:(ARG:0):(ARG:1),疎遠な) == 0 && GETBIT(RELATION:(ARG:0):(ARG:1),友人)
	LOCALS += "親友"
	SIF LOCAL && (GETBIT(RELATION:(ARG:0):(ARG:1),片思い) || GETBIT(RELATION:(ARG:0):(ARG:1),恋人) || GETBIT(RELATION:(ARG:0):(ARG:1),婚約者) || GETBIT(RELATION:(ARG:0):(ARG:1),配偶者))
		LOCALS += "で"
ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),友人)
	LOCALS += "友人"
	SIF LOCAL && (GETBIT(RELATION:(ARG:0):(ARG:1),片思い) || GETBIT(RELATION:(ARG:0):(ARG:1),恋人) || GETBIT(RELATION:(ARG:0):(ARG:1),婚約者) || GETBIT(RELATION:(ARG:0):(ARG:1),配偶者))
		LOCALS += "で"
ENDIF
SIF GETBIT(RELATION:(ARG:0):(ARG:1),片思い)
	LOCALS += "片思いの相手"
SIF GETBIT(RELATION:(ARG:0):(ARG:1),恋人)
	LOCALS += "恋人"
SIF GETBIT(RELATION:(ARG:0):(ARG:1),婚約者)
	LOCALS += "婚約者"
IF GETBIT(RELATION:(ARG:0):(ARG:1),配偶者)
	IF TALENT:(ARG:1):オトコ > 0
		LOCALS += "夫"
	ELSE
		LOCALS += "妻"
	ENDIF
ENDIF

;文字列を返す
RESULTS = %LOCALS%


;--------------------------------------------------
;キャラの相関関係の付与
;--------------------------------------------------
;固有番号ARG:0のキャラに対するARG:1の関係を設定する
;一部の関係は双方向処理
@SET_RELATION, ARG:0, ARG:1
#DIM LCOUNT
#DIM SELECT, 64
#DIM CHECK
#DIM LOSE_VIRGIN, 2
#DIM WAITFLAG
VARSET LOCAL
VARSET SELECT
FOR LOCAL,0,64
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),LOCAL) > 0
		SELECT:LOCAL = 1
NEXT
LCOUNT = LINECOUNT
$INPUT_LOOP
CALL LB
PRINTFORML {ARG:0}人目のキャラ『%CALLNAME:(ARG:0)%』と{ARG:1}人目のキャラ『%CALLNAME:(ARG:1)%』の関係を設定してください
DRAWLINE
LOCAL = 0
PRINTL 　印象
SIF SELECT:生き別れ > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{生き別れ}]　生き別れ
RESETCOLOR
SIF SELECT:親しい > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{親しい}]　親しい
RESETCOLOR
SIF SELECT:疎遠な > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{疎遠な}]　疎遠な
RESETCOLOR
SIF SELECT:愛する > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{愛する}]　愛する
RESETCOLOR
SIF SELECT:憎悪する > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{憎悪する}]　憎悪する
RESETCOLOR
SIF SELECT:義理の > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{義理の}]　義理の
RESETCOLOR
DRAWLINE
PRINTL 　関係
SIF SELECT:親子 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{親子}] 親子
RESETCOLOR
SIF SELECT:兄弟姉妹 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{兄弟姉妹}] 兄/弟/姉/妹
RESETCOLOR
SIF SELECT:祖父祖母孫 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{祖父祖母孫}] 祖父/祖母/孫
RESETCOLOR
SIF SELECT:おじおば > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{おじおば}] おじ/おば
RESETCOLOR
SIF SELECT:甥姪 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{甥姪}] 甥/姪
RESETCOLOR
SIF SELECT:友人 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{友人}] 友人
RESETCOLOR
SIF SELECT:片思い > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{片思い}] 片思いの相手
RESETCOLOR
SIF SELECT:恋人 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{恋人}] 恋人
RESETCOLOR
SIF SELECT:婚約者 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{婚約者}] 婚約者
RESETCOLOR
SIF SELECT:配偶者 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{配偶者}] 配偶者
RESETCOLOR
DRAWLINE
CHECK = 0
LOSE_VIRGIN:0 = 0
LOSE_VIRGIN:1 = 0
IF SELECT:親子 == 0 && SELECT:兄弟姉妹 == 0 && SELECT:祖父祖母孫 == 0 && SELECT:おじおば == 0 && SELECT:甥姪 == 0 && SELECT:いとこ == 0
	IF SELECT:生き別れ > 0
		PRINTL  家族関係が無いキャラクターに「生き別れ」は付けられません
		CHECK = 1
	ENDIF
	IF SELECT:義理の > 0
		PRINTL  家族関係が無いキャラクターに「義理の」は付けられません
		CHECK = 1
	ENDIF
ENDIF
IF SELECT:親子 > 0 && SELECT:義理の == 0
	IF TOSHIUE_F(ARG:0, ARG:1) > 0 && TALENT:(ARG:0):処女 > 0 && TALENT:(ARG:0):オトコ <= 0
		PRINTFORML  %CALLNAME:(ARG:0)%の方が年上ですが処女なので実の母親にはなれません
		IF TALENT:(ARG:0):固有キャラ == 0
			PRINTFORML  決定した場合は%CALLNAME:(ARG:0)%の処女を消去します
		ELSE
			CHECK = 1
		ENDIF
		LOSE_VIRGIN:0 = 1
	ELSEIF TOSHIUE_F(ARG:1, ARG:0) > 0 && TALENT:(ARG:1):処女 > 0 && TALENT:(ARG:1):オトコ <= 0
		PRINTFORML  %CALLNAME:(ARG:1)%の方が年上ですが処女なので実の母親にはなれません
		IF TALENT:(ARG:1):固有キャラ == 0
			PRINTFORML  決定した場合は%CALLNAME:(ARG:1)%の処女を消去します
		ELSE
			CHECK = 1
		ENDIF
		LOSE_VIRGIN:1 = 1
	ENDIF
ENDIF
IF SELECT:祖父祖母孫 > 0 && SELECT:義理の == 0
	IF TOSHIUE_F(ARG:0, ARG:1) > 0 && TALENT:(ARG:0):処女 > 0 && TALENT:(ARG:0):オトコ <= 0
		PRINTFORML  %CALLNAME:(ARG:0)%の方が年上ですが処女なので実の祖母にはなれません
		IF TALENT:(ARG:0):固有キャラ == 0
			PRINTFORML  決定した場合は%CALLNAME:(ARG:0)%の処女を消去します
		ELSE
			CHECK = 1
		ENDIF
		LOSE_VIRGIN:0 = 1
	ELSEIF TOSHIUE_F(ARG:1, ARG:0) > 0 && TALENT:(ARG:1):処女 > 0 && TALENT:(ARG:1):オトコ <= 0
		PRINTFORML  %CALLNAME:(ARG:1)%の方が年上ですが処女なので実の祖母にはなれません
		IF TALENT:(ARG:1):固有キャラ == 0
			PRINTFORML  決定した場合は%CALLNAME:(ARG:1)%の処女を消去します
		ELSE
			CHECK = 1
		ENDIF
		LOSE_VIRGIN:1 = 1
	ENDIF
ENDIF
SIF CHECK > 0
	SETCOLOR 96,96,96
PRINTL 　[200]決定
RESETCOLOR
PRINTL 　[999]キャンセル
INPUT 
SIF RESULT != 200
	CLEARLINE LINECOUNT - LCOUNT
IF RESULT == 200 && CHECK == 0
	RELATION:(ARG:0):(ARG:1) = 0
	FOR LOCAL, 1, 64
		SIF SELECT:LOCAL > 0
			SETBIT RELATION:(ARG:0):(ARG:1), LOCAL
	NEXT
	;処女を消す処理
	SIF LOSE_VIRGIN:0 > 0
		TALENT:(ARG:0):処女 = 0
	SIF LOSE_VIRGIN:1 > 0
		TALENT:(ARG:1):処女 = 0

	WAITFLAG = 0

	;関係の双方向化
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 生き別れ) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 生き別れ) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 生き別れ
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 生き別れ) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 生き別れ
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 義理の) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 義理の) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 義理の
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 義理の) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 義理の
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 親子) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 親子) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 親子
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 親子) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 親子
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 兄弟姉妹) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 兄弟姉妹) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 兄弟姉妹
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 兄弟姉妹) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 兄弟姉妹
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 祖父祖母孫) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 祖父祖母孫) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 祖父祖母孫
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 祖父祖母孫) > 0
		WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 祖父祖母孫
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), おじおば) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 甥姪) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 甥姪
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 甥姪) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 甥姪
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 甥姪) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), おじおば) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), おじおば
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), おじおば) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), おじおば
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), いとこ) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), いとこ) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), いとこ
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), いとこ) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), いとこ
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 恋人) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 恋人) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 恋人
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 恋人) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 恋人
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 婚約者) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 婚約者) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 婚約者
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 婚約者) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 婚約者
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 配偶者) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 配偶者) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 配偶者
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 配偶者) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 配偶者
	ENDIF

	IF RELATION:(ARG:0):(ARG:1) > 0
		CALL GET_RELATION, ARG:0, ARG:1
		PRINTFORML 『%CALLNAME:(ARG:0)%』にとって『%CALLNAME:(ARG:1)%』は【%RESULTS%】です。
	ELSE
		PRINTFORML 『%CALLNAME:(ARG:0)%』と『%CALLNAME:(ARG:1)%』の間に相関関係はありません。
	ENDIF
	;相手との関係に変動があった場合、メッセージ表示
	IF RELATION:(ARG:1):(ARG:0) > 0 && WAITFLAG > 0
		CALL GET_RELATION, (ARG:1), (ARG:0)
		PRINTFORML 『%CALLNAME:(ARG:1)%』にとって『%CALLNAME:(ARG:0)%』は【%RESULTS%】になりました。
	ELSEIF WAITFLAG > 0
		PRINTFORML 『%CALLNAME:(ARG:1)%』と『%CALLNAME:(ARG:0)%』の相関関係は無くなりました。
	ENDIF
	PRINTW 

	RETURN
ELSEIF RESULT == 999
	RETURN
ELSEIF (RESULT >= 1 && RESULT <= 義理の) || (RESULT >= 親子 && RESULT <= いとこ) || (RESULT >= 友人 && RESULT <= 配偶者)
	IF SELECT:RESULT > 0
		SELECT:RESULT = 0
	ELSE
		SELECT:RESULT = 1
	ENDIF
	SIF RESULT == おじおば
		SELECT:甥姪 = 0
	SIF RESULT == 甥姪
		SELECT:おじおば = 0
	IF RESULT == 片思い
		SELECT:恋人 = 0
		SELECT:婚約者 = 0
		SELECT:配偶者 = 0
	ENDIF
	IF RESULT == 恋人
		SELECT:片思い = 0
		SELECT:婚約者 = 0
		SELECT:配偶者 = 0
	ENDIF
	IF RESULT == 婚約者
		SELECT:片思い = 0
		SELECT:恋人 = 0
		SELECT:配偶者 = 0
	ENDIF
	IF RESULT == 配偶者
		SELECT:片思い = 0
		SELECT:恋人 = 0
		SELECT:婚約者 = 0
	ENDIF
ENDIF
GOTO INPUT_LOOP





;--------------------------------------------------
;相関関係の全キャラ探索
;--------------------------------------------------
@CHECK_ALL_RELATION
#DIM CHARA
#DIM SELECT
#DIM RELATE
#DIM WAITFLAG, 2
;キャラ２人未満なら中断
SIF CHARANUM < 3
	RETURN
;自キャラの全探索
FOR CHARA, 0, CHARANUM
	SIF CHARA == MASTER
		CONTINUE

	;両親が仲間に居る場合
	IF CFLAG:CHARA:9 <= -100 || CFLAG:CHARA:7 > 0
		FOR SELECT, 0, CHARANUM
			SIF SELECT == MASTER
				CONTINUE
			SIF SELECT == CHARA
				CONTINUE
			;親子
			IF (CFLAG:CHARA:9 <= -100 && CFLAG:SELECT:240 == CFLAG:CHARA:9 * -1 - 100) || (CFLAG:CHARA:7 > 0 && CFLAG:SELECT:240 == CFLAG:CHARA:7)
				SETBIT RELATION:CHARA:SELECT, 親子
				SETBIT RELATION:SELECT:CHARA, 親子
			ENDIF
		NEXT
	ENDIF

	;相手キャラの探索
	FOR SELECT, 0, CHARANUM
		SIF SELECT == MASTER
			CONTINUE
		SIF SELECT == CHARA
			CONTINUE
		WAITFLAG:0 = 0
		WAITFLAG:1 = 0

		;自分に親か子が居る場合
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			;親が居る場合
			IF GETBIT(RELATION:CHARA:RELATE, 親子) && TOSHIUE_F(RELATE, CHARA) > 0
				PRINTL 
				;同じ親を持つ相手なら兄弟姉妹
				IF GETBIT(RELATION:SELECT:RELATE, 親子) && TOSHIUE_F(RELATE, SELECT) > 0
					SIF GETBIT(RELATION:CHARA:SELECT, 兄弟姉妹) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 兄弟姉妹
					SIF GETBIT(RELATION:SELECT:CHARA, 兄弟姉妹) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 兄弟姉妹
					;義理の場合
					IF GETBIT(RELATION:CHARA:RELATE, 義理の)
						SETBIT RELATION:CHARA:SELECT, 義理の
						SETBIT RELATION:SELECT:CHARA, 義理の
					ENDIF
				ENDIF
				;親の親で年上なら祖父母
				IF GETBIT(RELATION:RELATE:SELECT, 親子) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(SELECT, CHARA) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 祖父祖母孫) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 祖父祖母孫
						SIF GETBIT(RELATION:SELECT:CHARA, 祖父祖母孫) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 祖父祖母孫
						;義理の場合
						IF GETBIT(RELATION:CHARA:RELATE, 義理の)
							SETBIT RELATION:CHARA:SELECT, 義理の
							SETBIT RELATION:SELECT:CHARA, 義理の
						ENDIF
					ENDIF
				ENDIF
				;親の兄弟姉妹ならおじおば
				IF GETBIT(RELATION:RELATE:SELECT, 兄弟姉妹)
					SIF GETBIT(RELATION:CHARA:SELECT, おじおば) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, おじおば
					SIF GETBIT(RELATION:SELECT:CHARA, 甥姪) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 甥姪
					;義理の場合
					IF GETBIT(RELATION:CHARA:RELATE, 義理の)
						SETBIT RELATION:CHARA:SELECT, 義理の
						SETBIT RELATION:SELECT:CHARA, 義理の
					ENDIF
				ENDIF
			ENDIF
			;子が居る場合
			IF GETBIT(RELATION:CHARA:RELATE, 親子) && TOSHIUE_F(CHARA, RELATE) > 0
				;子の子で年下なら孫
				IF GETBIT(RELATION:RELATE:SELECT, 親子) && TOSHIUE_F(RELATE, SELECT) > 0
					IF TOSHIUE_F(CHARA, SELECT) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 祖父祖母孫) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 祖父祖母孫
						SIF GETBIT(RELATION:SELECT:CHARA, 祖父祖母孫) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 祖父祖母孫
						;義理の場合
						IF GETBIT(RELATION:CHARA:RELATE, 義理の)
							SETBIT RELATION:CHARA:SELECT, 義理の
							SETBIT RELATION:SELECT:CHARA, 義理の
						ENDIF
					ENDIF
				ENDIF
				;子の祖父母で相手が年上なら親
				IF GETBIT(RELATION:RELATE:SELECT, 祖父祖母孫) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(SELECT, CHARA) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 親子) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 親子
						SIF GETBIT(RELATION:SELECT:CHARA, 親子) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 親子
						;義理の場合
						IF GETBIT(RELATION:CHARA:RELATE, 義理の)
							SETBIT RELATION:CHARA:SELECT, 義理の
							SETBIT RELATION:SELECT:CHARA, 義理の
						ENDIF
					ENDIF
				ENDIF
			ENDIF
		NEXT
		;自分に兄弟姉妹が居る場合
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			IF GETBIT(RELATION:CHARA:RELATE, 兄弟姉妹)
				;兄弟姉妹の兄弟姉妹は兄弟姉妹
				IF GETBIT(RELATION:RELATE:SELECT, 兄弟姉妹)
					SIF GETBIT(RELATION:CHARA:SELECT, 兄弟姉妹) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 兄弟姉妹
					SIF GETBIT(RELATION:SELECT:CHARA, 兄弟姉妹) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 兄弟姉妹
				ENDIF
				;兄弟姉妹の親で年上なら親
				IF GETBIT(RELATION:RELATE:SELECT, 親子) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(SELECT, CHARA) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 親子) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 親子
						SIF GETBIT(RELATION:SELECT:CHARA, 親子) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 親子
					ENDIF
				ENDIF
				;兄弟姉妹の祖父母で年上なら祖父母
				IF GETBIT(RELATION:RELATE:SELECT, 祖父祖母孫) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(SELECT, CHARA) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 祖父祖母孫) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 祖父祖母孫
						SIF GETBIT(RELATION:SELECT:CHARA, 祖父祖母孫) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 祖父祖母孫
					ENDIF
				ENDIF
				;兄弟姉妹の子は甥姪
				IF GETBIT(RELATION:RELATE:SELECT, 親子) && TOSHIUE_F(RELATE, SELECT) > 0
					SIF GETBIT(RELATION:CHARA:SELECT, 甥姪) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 甥姪
					SIF GETBIT(RELATION:SELECT:CHARA, おじおば) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, おじおば
				ENDIF
			ENDIF
		NEXT
		;自分に祖父母か孫が居る場合
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			;祖父母が居る場合
			IF GETBIT(RELATION:CHARA:RELATE, 祖父祖母孫) && TOSHIUE_F(RELATE, CHARA) > 0
				;親ではない祖父母の子はおじおば
				IF GETBIT(RELATION:RELATE:SELECT, 親子) && TOSHIUE_F(RELATE, SELECT) > 0 && GETBIT(RELATION:CHARA:SELECT, 親子) == 0
					SIF GETBIT(RELATION:CHARA:SELECT, おじおば) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, おじおば
					SIF GETBIT(RELATION:SELECT:CHARA, 甥姪) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 甥姪
					;義理の場合
					IF GETBIT(RELATION:CHARA:RELATE, 義理の)
						SETBIT RELATION:CHARA:SELECT, 義理の
						SETBIT RELATION:SELECT:CHARA, 義理の
					ENDIF
				ENDIF
			ENDIF
			;孫が居る場合
			IF GETBIT(RELATION:CHARA:RELATE, 祖父祖母孫) && TOSHIUE_F(CHARA, RELATE) > 0
				;孫の親が年下なら子
				IF GETBIT(RELATION:RELATE:SELECT, 親子) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(CHARA, SELECT) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 親子) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 親子
						SIF GETBIT(RELATION:SELECT:CHARA, 親子) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 親子
					ENDIF
				ENDIF
			ENDIF
		NEXT
		;自分におじおばが居る場合
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			IF GETBIT(RELATION:CHARA:RELATE, おじおば)
				;おじおばの子はいとこ
				IF GETBIT(RELATION:RELATE:SELECT, 親子) && TOSHIUE_F(RELATE, SELECT) > 0
					SIF GETBIT(RELATION:CHARA:SELECT, いとこ) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, いとこ
					SIF GETBIT(RELATION:SELECT:CHARA, いとこ) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, いとこ
					;義理の場合
					IF GETBIT(RELATION:CHARA:RELATE, 義理の)
						SETBIT RELATION:CHARA:SELECT, 義理の
						SETBIT RELATION:SELECT:CHARA, 義理の
					ENDIF
				ENDIF
			ENDIF
		NEXT
		;自分にいとこが居る場合
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			IF GETBIT(RELATION:CHARA:RELATE, いとこ)
				;いとこの親はおじおば
				IF GETBIT(RELATION:RELATE:SELECT, 親子) && TOSHIUE_F(SELECT, RELATE) > 0
					SIF GETBIT(RELATION:CHARA:SELECT, おじおば) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, おじおば
					SIF GETBIT(RELATION:SELECT:CHARA, 甥姪) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 甥姪
					;義理の場合
					IF GETBIT(RELATION:CHARA:RELATE, 義理の)
						SETBIT RELATION:CHARA:SELECT, 義理の
						SETBIT RELATION:SELECT:CHARA, 義理の
					ENDIF
				ENDIF
			ENDIF
		NEXT

		;関係の双方向化
		IF GETBIT(RELATION:CHARA:SELECT, 生き別れ) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 生き別れ) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 生き別れ
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA,生き別れ) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 生き別れ
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 義理の) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 義理の) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 義理の
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 義理の) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 義理の
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 親子) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 親子) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 親子
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 親子) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 親子
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 兄弟姉妹) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 兄弟姉妹) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 兄弟姉妹
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 兄弟姉妹) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 兄弟姉妹
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 祖父祖母孫) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 祖父祖母孫) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 祖父祖母孫
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 祖父祖母孫) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 祖父祖母孫
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, おじおば) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 甥姪) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 甥姪
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 甥姪) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 甥姪
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 甥姪) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, おじおば) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, おじおば
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, おじおば) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, おじおば
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, いとこ) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, いとこ) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, いとこ
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, いとこ) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, いとこ
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 恋人) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 恋人) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 恋人
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 恋人) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 恋人
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 婚約者) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 婚約者) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 婚約者
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 婚約者) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 婚約者
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 配偶者) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 配偶者) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 配偶者
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 配偶者) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 配偶者
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 親子) == 0 && GETBIT(RELATION:CHARA:SELECT, 兄弟姉妹) == 0 && GETBIT(RELATION:CHARA:SELECT, 祖父祖母孫) == 0 && GETBIT(RELATION:CHARA:SELECT, おじおば) == 0 && GETBIT(RELATION:CHARA:SELECT, 甥姪) == 0
			SIF GETBIT(RELATION:SELECT:CHARA, 生き別れ) || GETBIT(RELATION:SELECT:CHARA, 義理の)
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 生き別れ
			CLEARBIT RELATION:SELECT:CHARA, 義理の
		ENDIF
		;相手との関係に変動があった場合、メッセージ表示
		IF RELATION:CHARA:SELECT > 0 && WAITFLAG:0 > 0
			CALL GET_RELATION, CHARA, SELECT
			PRINTFORML 『%CALLNAME:CHARA%』にとって『%CALLNAME:SELECT%』は【%RESULTS%】になりました。
		ELSEIF WAITFLAG:0 > 0
			PRINTFORML 『%CALLNAME:CHARA%』と『%CALLNAME:SELECT%』の相関関係は無くなりました。
		ENDIF
		IF RELATION:SELECT:CHARA > 0 && WAITFLAG:1 > 0
			CALL GET_RELATION, SELECT, CHARA
			PRINTFORML 『%CALLNAME:SELECT%』にとって『%CALLNAME:CHARA%』は【%RESULTS%】になりました。
		ELSEIF WAITFLAG:1 > 0
			PRINTFORML 『%CALLNAME:SELECT%』と『%CALLNAME:CHARA%』の相関関係は無くなりました。
		ENDIF
		SIF WAITFLAG:0 || WAITFLAG:1
			PRINTL 
		;子供か孫を持つキャラは処女を失う
		IF GETBIT(RELATION:CHARA:SELECT, 義理の) == 0 && (GETBIT(RELATION:CHARA:SELECT, 親子) || GETBIT(RELATION:CHARA:SELECT, 祖父祖母孫))
			IF TOSHIUE_F(CHARA, SELECT) > 0
				IF TALENT:CHARA:処女 > 0
					TALENT:CHARA:処女 = 0
					PRINTFORML 　　関係の変化により『%CALLNAME:CHARA%』の処女を消去しました
					PRINTL 
				ENDIF
			ENDIF
		ENDIF
	NEXT
NEXT
;両親ＩＤを設定
FOR CHARA, 0, CHARANUM
	SIF CHARA == MASTER
		CONTINUE
	;父親の探索
	FOR SELECT, 0, CHARANUM
		SIF SELECT == MASTER
			CONTINUE
		SIF SELECT == CHARA
			CONTINUE
		;実の親子かつ男性かふたなりなら父親
		SIF GETBIT(RELATION:CHARA:SELECT, 親子) && GETBIT(RELATION:CHARA:SELECT, 義理の) == 0 && TOSHIUE_F(SELECT, CHARA) > 0 && (TALENT:SELECT:オトコ > 0 || TALENT:SELECT:ふたなり > 0) && CFLAG:CHARA:7 != CFLAG:SELECT:240 && CFLAG:CHARA:9 == 0
			CFLAG:CHARA:9 = CFLAG:SELECT:240 * -1 - 100
	NEXT
	FOR SELECT, 0, CHARANUM
		SIF SELECT == MASTER
			CONTINUE
		SIF SELECT == CHARA
			CONTINUE
		;実の親子かつ男性でないなら母親
		SIF GETBIT(RELATION:CHARA:SELECT, 親子) && GETBIT(RELATION:CHARA:SELECT, 義理の) == 0 && TOSHIUE_F(SELECT, CHARA) > 0 && TALENT:SELECT:オトコ <= 0 && CFLAG:CHARA:9 * -1 - 100 != CFLAG:SELECT:240 && CFLAG:CHARA:7 == 0
			CFLAG:CHARA:7 = CFLAG:SELECT:240
	NEXT
NEXT



;年上判定
;-------------------------------------------------
;ARG:0がARG:1より年上なら1を返す
@TOSHIUE_F(ARG:0, ARG:1)
#FUNCTION
IF BASE:(ARG:0):実年齢 > BASE:(ARG:1):実年齢
	LOCAL = 1
ELSEIF BASE:(ARG:0):実年齢 == BASE:(ARG:1):実年齢 && CFLAG:(ARG:0):240 < CFLAG:(ARG:1):240
	LOCAL = 1
ELSE
	LOCAL = 0
ENDIF
RETURNF LOCAL


;近親相姦判定
;------------------------------------------------
;ARG:0とARG:1が近親関係にあるなら1を返す
;ARG:2が0なら仲間キャラ、1ならそれ以外
@INCEST_F(ARG:0, ARG:1, ARG:2 = 0)
#FUNCTION
IF ARG:2 == 0
	SIF GETBIT(RELATION:(ARG:0):(ARG:1), 義理の)
		RETURNF 0
	SIF GETBIT(RELATION:(ARG:0):(ARG:1), 親子) || GETBIT(RELATION:(ARG:0):(ARG:1), 兄弟姉妹) || GETBIT(RELATION:(ARG:0):(ARG:1), 祖父祖母孫) || GETBIT(RELATION:(ARG:0):(ARG:1), おじおば) || GETBIT(RELATION:(ARG:0):(ARG:1), 甥姪)
		RETURNF 1
ELSE
	SIF CFLAG:(ARG:0):9 >= 200
		RETURNF 0
	IF ARG:1 == CFLAG:(ARG:0):9
		RETURNF 1
	ENDIF
ENDIF
RETURNF 0



;--------------------------------------------------
;恋愛判定
;ARG:0にとってARG:1番目が愛する相手なら1を返す
@LOVER_F, ARG, ARG:1
#FUNCTION
SIF (GETBIT(RELATION:(ARG:0):(ARG:1),片思い) || GETBIT(RELATION:(ARG:0):(ARG:1),恋人) || GETBIT(RELATION:(ARG:0):(ARG:1),婚約者) || GETBIT(RELATION:(ARG:0):(ARG:1),配偶者))
	RETURNF 1
RETURNF 0

