;**********************************************************
;接敵関連
;--------------------------------------------------
;ザコ触手と戦闘（自動勝利）
@MESSAGE_ENCOUNT_MOB
PRINTFORML %STR:2501% と遭遇した！
PRINTL ・・・・・・・・・・・・・・・
PRINTFORML %STR:2501% を倒した!
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "ENCOUNT_MOB")
PRINTW 

;--------------------------------------------------
;雑魚触手と戦闘
@MESSAGE_ENCOUNT_MOB_BATTLE
CALL TENTACLE_ACCESS, "NAME"
CALL TENTACLE_LEVEL
PRINTFORML  Lv.{RESULT} と遭遇した！
TRYCALLFORM TENTACLE_MOB_{FLAG:11}_DEFENITION
PRINTL ・・・・・・・・・・・・・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "ENCOUNT_MOB_BATTLE")
PRINTW 

;--------------------------------------------------
;ボス触手と戦闘
@MESSAGE_ENCOUNT_BOSS
CALL TENTACLE_ACCESS, "NAME"
CALL TENTACLE_LEVEL
PRINTFORML  Lv.{RESULT} と遭遇した！
TRYCALLFORM TENTACLE_BOSS_{FLAG:11}_DEFENITION
PRINTL ・・・・・・・・・・・・・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "ENCOUNT_BOSS")
PRINTW 

;--------------------------------------------------
;ボス触手襲来
@MESSAGE_RAID_BOSS
CALL TENTACLE_ACCESS, "NAME"
CALL TENTACLE_LEVEL
PRINTFORML  Lv.{RESULT} と遭遇した！
TRYCALLFORM TENTACLE_BOSS_{FLAG:11}_DEFENITION
PRINTL ・・・・・・・・・・・・・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "RAID_BOSS")
PRINTW 

;--------------------------------------------------
;ラスボス触手と戦闘
@MESSAGE_ENCOUNT_LASTBOSS
CALL TENTACLE_ACCESS, "NAME"
CALL TENTACLE_LEVEL
PRINTFORML  Lv.{RESULT} と遭遇した！
TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_DEFENITION
PRINTL ・・・・・・・・・・・・・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "ENCOUNT_LASTBOSS")
PRINTW 

;--------------------------------------------------
;ラスボス触手襲来
@MESSAGE_RAID_LASTBOSS
CALL TENTACLE_ACCESS, "NAME"
CALL TENTACLE_LEVEL
PRINTFORML  Lv.{RESULT} と遭遇した！
TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_DEFENITION
PRINTL ・・・・・・・・・・・・・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "RAID_LASTBOSS")
PRINTW 

;--------------------------------------------------
;初めて洗脳キャラと対峙した
@MESSAGE_ENCOUNT_SENNOU_FIRST
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は周囲を包む異様な空気に気付き警戒心を強める。
PRINTFORML いつの間にか、前方に一つの人影が浮かび上がっている。
PRINTFORML そのシルエットに見覚えのあった%PRINT_TRANSCALLNAME(TARGET)%は、
PRINTFORML 不審に思いつつもそれに呼び掛ける。
PRINTFORML 久方ぶりに見る仲間の顔・・・しかしその瞳は意志の光を宿さず、無機質にこちらを見据えた。
PRINTFORML そうして、ゆっくりと身構える「元」仲間───
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は苦渋に眉を顰めつつも、仕方なく敵と相対するために戦闘体勢をとった・・・
PRINTL 
PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%と遭遇した！
PRINTL ・・・・・・・・・・・・・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "ENCOUNT_SENNOU_FIRST")
PRINTW 

;--------------------------------------------------
;洗脳キャラと対峙
@MESSAGE_ENCOUNT_SENNOU
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は独特の雰囲気に息を呑む・・・
PRINTFORML 視界の先に現れた影は、%PRINT_TRANSCALLNAME(TARGET)%にとって決して遭遇したい相手ではない。
PRINTFORML しかして%PRINT_TRANSCALLNAME(TARGET)%は静かに頷く。
PRINTFORML これもまた、戦う者の運命であると、%PRINT_TRANSCALLNAME(TARGET)%は覚悟を改めるのであった・・・
PRINTL 
PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%と遭遇した！
PRINTL ・・・・・・・・・・・・・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "ENCOUNT_SENNOU")
PRINTW 

;--------------------------------------------------
;始めて悪堕ちキャラと対峙した
@MESSAGE_ENCOUNT_AKUOTI_FIRST
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は周囲を包む異様な空気に気付き警戒心を強める。
PRINTFORML いつの間にか、前方に一つの人影が浮かび上がっている。
PRINTFORML そのシルエットに見覚えのあった%PRINT_TRANSCALLNAME(TARGET)%は、
PRINTFORML 不審に思いつつもそれに呼び掛ける。
PRINTFORML 久方ぶりに見る仲間の顔・・・しかしその表情は加虐的な愉悦に歪んでいる。
PRINTFORML そうして、怪しげに微笑み身構える「元」仲間───
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は動揺を隠しきれずも、何とか敵と相対するために戦闘体勢をとった・・・
PRINTL 
PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%と遭遇した！
PRINTL ・・・・・・・・・・・・・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "ENCOUNT_AKUOTI_FIRST")
PRINTW 

;--------------------------------------------------
;悪堕ちキャラと対峙
@MESSAGE_ENCOUNT_AKUOTI
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は独特の雰囲気に息を呑む・・・
PRINTFORML 視界の先に現れた影は、%PRINT_TRANSCALLNAME(TARGET)%にとって決して遭遇したい相手ではない。
PRINTFORML しかして%PRINT_TRANSCALLNAME(TARGET)%は静かに頷く。
PRINTFORML これもまた、戦う者の運命であると、%PRINT_TRANSCALLNAME(TARGET)%は覚悟を改めるのであった・・・
PRINTL 
PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%と遭遇した！
PRINTL ・・・・・・・・・・・・・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "ENCOUNT_AKUOTI")
PRINTW 


;**********************************************************
;戦闘関連の地の文

;**********************************************************
;キャラ側の行動

;変身関連
;**********************************************************
;変身前のかけ声
@MESSAGE_BATTLE_CHARA_TRANSFORMCALL
PRINTFORML 身構えた%PRINT_TRANSCALLNAME(TARGET)%の周囲を眩い光が包み込む！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_TRANSFORMCALL")
IF RESULT == -1
	PRINTL 
	SIF CFLAG:4 == 1
		PRINTFORML 「%CSTR:2%」
ENDIF

;変身後の名乗り
@MESSAGE_BATTLE_CHARA_NANORI
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_NANORI")
IF RESULT == -1
	PRINTL 
	SIF CFLAG:5 == 1
		PRINTFORML 「%CSTR:3%」
ENDIF

;気力が尽きて変身が強制的に解けた
@MESSAGE_BATTLE_CHARA_TRANSRELEASE
FONTBOLD
IF FLAG:110 == 0
	PRINTFORML %STR:2500%の猛攻によって、限界を越えた%PRINT_TRANSCALLNAME(TARGET)%の変身が解けてしまった！
ELSEIF FLAG:110 == 1
	PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%の猛攻によって、限界を越えた%PRINT_TRANSCALLNAME(TARGET)%の変身が解けてしまった！
ENDIF
FONTREGULAR
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_TRANSRELEASE")
PRINTW 

;絶頂で変身が強制的に解けた
@MESSAGE_BATTLE_CHARA_TRANSRELEASE_ECS
FONTBOLD
PRINTFORML 絶頂によって%PRINT_TRANSCALLNAME(TARGET)%の変身が解けてしまった！
FONTREGULAR
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_TRANSRELEASE_ECS")
PRINTW 

;撤退関連
;**********************************************************
;撤退成功
@MESSAGE_BATTLE_CHARA_TETTAI_SUCCESS
PRINTFORML 分が悪いと見た%PRINT_TRANSCALLNAME(TARGET)%は大きく後退し、
IF (FLAG:70 + FLAG:71)
	PRINTL 避難誘導をしながらその場から撤退した・・・
ELSEIF TALENT:主観視点 > 0
	PRINTL すぐさまその場から撤退した・・・
ELSE
	PRINTL 悔しさを噛み殺しながらその場から撤退した・・・
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_TETTAI_SUCCESS")
PRINTW 

;撤退失敗
@MESSAGE_BATTLE_CHARA_TETTAI_FALSE
IF FLAG:110 == 0
	PRINTFORML 複数の%STR:2500%に回り込まれて逃げられない！！
ELSEIF FLAG:110 == 1
	PRINTFORML しかし一足先に動いた%PRINT_TRANSCALLNAME(FLAG:111)%に素早く回り込まれてしまった！！
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_TETTAI_FALSE")
PRINTW 



;攻撃関連
;**********************************************************
;近距離攻撃開始
@MESSAGE_BATTLE_CHARA_ATTACK_RANGE_SHORT
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%の近距離攻撃！
IF FLAG:110 == 0
	PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は%PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
IF GETBIT(TCVAR:216, 1)
	PRINTL の頭上から強烈な一撃を叩きこんだ！
ELSE
	SELECTCASE RAND:5
		CASE 0
			PRINTL の懐に潜り込み全力の一撃を叩き込んだ！
		CASE 1
			PRINTL に対して息もつかせぬ連続攻撃を放った！
		CASE 2
			PRINTL の死角から電光石火の一撃を加え、すかさず飛びずさった！
		CASE 3
			PRINTL に肉薄し、強烈な一撃を叩き込んだ！
		CASE 4
			PRINTL の一瞬の隙を突いて飛び掛かり、側面から攻撃を叩き込んだ！
	ENDSELECT
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_ATTACK_RANGE_SHORT")
PRINTW 

;中距離攻撃開始
@MESSAGE_BATTLE_CHARA_ATTACK_RANGE_MIDDLE
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%の中距離攻撃！
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%
IF GETBIT(TCVAR:216, 1)
	PRINTL は空中に飛び上がり、上空から猛攻撃を仕掛けた！
ELSE
	SELECTCASE RAND:5
		CASE 0
			PRINTL はミドルレンジから強力な攻撃を放った！
		CASE 1
			PRINTL は力を集中させ、一気に解き放って攻撃した！
		CASE 2
			PRINTL は付かず離れずの位置から連続攻撃を仕掛けた！
		CASE 3
			PRINTL は障害物を利用して回避しながら、付かず離れずの距離から攻撃した！
		CASE 4
			PRINTL は接近すると見せかけて、中距離からそのまま攻撃した！
	ENDSELECT
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_ATTACK_RANGE_MIDDLE")
PRINTW 

;遠距離攻撃開始
@MESSAGE_BATTLE_CHARA_ATTACK_RANGE_LONG
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%の遠距離攻撃！
IF FLAG:110 == 0
	PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は%PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
IF GETBIT(TCVAR:216, 1)
	PRINTL に狙いを定め、高空から遠距離攻撃を仕掛けた！
ELSE
	SELECTCASE RAND:5
		CASE 0
			PRINTL から離れた位置から遠距離攻撃を仕掛けた！
		CASE 1
			PRINTL に遠距離攻撃を放ち、ヒットアンドアウェイの要領で後退した！
		CASE 2
			PRINTL の射程外から照準を定め、急所を狙い撃った！
		CASE 3
			PRINTL の動きを先読みし、連続で攻撃を放った！
		CASE 4
			PRINTL にロングレンジから狙い澄ました一撃を加えた！
	ENDSELECT
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_ATTACK_RANGE_LONG")
PRINTW 

;攻撃がクリティカルヒット
@MESSAGE_BATTLE_CHARA_ATTACK_CRITICAL_HIT,ARG = 0
SETCOLOR 255,0,0
FONTBOLD
PRINTL CRITICAL HIT!
FONTREGULAR
RESETCOLOR
;口上
SIF ARG == 0
	TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_ATTACK_CRITICAL_HIT")
SIF RESULT == 999
	RETURN 999

;攻撃が通常ヒット
@MESSAGE_BATTLE_CHARA_ATTACK_HIT,ARG = 0
SETCOLOR 255,255,0
PRINTL HIT!
RESETCOLOR
;口上
SIF ARG == 0
	TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_ATTACK_HIT")
SIF RESULT == 999
	RETURN 999

;攻撃失敗
@MESSAGE_BATTLE_CHARA_ATTACK_FALSE,ARG = 0
SETCOLOR 255,0,255
PRINTL MISS!
RESETCOLOR
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%の攻撃は回避されてしまった・・・
;口上
SIF ARG == 0
	TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_ATTACK_FALSE")
PRINTW 

;カス当たり
@MESSAGE_BATTLE_CHARA_ATTACK_GUARD,ARG = 0
SETCOLOR 0,255,255
PRINTL GUARD...
RESETCOLOR
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%の攻撃は直撃しなかったようだ・・・
;口上
SIF ARG == 0
	TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_ATTACK_FALSE")


;防御
;**********************************************************
@MESSAGE_BATTLE_CHARA_DEFENSE
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は集中して身を固めた！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_DEFENSE")
PRINTW 



;距離をとる
;**********************************************************
@MESSAGE_BATTLE_CHARA_TAKEAWAY
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は一度距離をとって体勢を立て直し、心を落ち着けた・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_TAKEAWAY")
PRINTW 

;背後に回る
;**********************************************************
@MESSAGE_BATTLE_CHARA_STEPIN
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%はあえて踏み込み、相手の背後に回った！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_STEPIN")
PRINTW 

;見切り
;**********************************************************
@MESSAGE_BATTLE_CHARA_SEETHROUGH
IF TALENT:主観視点 > 0
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は油断なく相手の動きを観察した！
ELSE
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は相手の動きを見切ろうとしている！
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_SEETHROUGH")
PRINTW 

;何もしない
;**********************************************************
@MESSAGE_BATTLE_CHARA_NOACTION
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は何もしなかった
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_NOACTION")
PRINTW 



;振り解く関連
;**********************************************************
;振り解く開始
@MESSAGE_BATTLE_CHARA_HURIHODOKU
IF FLAG:110 == 0
	IF FLAG:10 == 2
		PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は
		CALL TENTACLE_ACCESS, "NAME"
		PRINTFORML を振り解こうと全力で暴れた・・・
	ELSE
		PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は手足に絡みついた触手を振り解こうと全力で暴れた・・・
	ENDIF
ELSEIF FLAG:110 == 1
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は%PRINT_TRANSCALLNAME(FLAG:111)%の拘束を振り解こうと全力で暴れた・・・
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_HURIHODOKU")
PRINTW 

;振り解く成功
@MESSAGE_BATTLE_CHARA_HURIHODOKU_SUCCESS
PRINT すると一瞬
IF FLAG:110 == 0
	IF FLAG:10 == 2
		CALL TENTACLE_ACCESS, "NAME"
	ELSE
		PRINT 触手
	ENDIF
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
PRINTL の拘束が緩み、
PRINTFORML その隙を突いた%PRINT_TRANSCALLNAME(TARGET)%は辛くも窮地を脱することに成功した！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_HURIHODOKU_SUCCESS")
PRINTW 

;振り解く失敗
@MESSAGE_BATTLE_CHARA_HURIHODOKU_FALSE
PRINT しかし
IF FLAG:110 == 0
	IF FLAG:10 == 2
		CALL TENTACLE_ACCESS, "NAME"
	ELSE
		PRINT 触手
	ENDIF
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
IF TALENT:感情乏しい > 0
	PRINTL の拘束はびくともしなかった・・・
ELSEIF TALENT:主観視点 > 0
	PRINTL の拘束はびくともせず、
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%の眉が苦渋に歪む・・・
ELSE
	PRINTL の拘束はびくともせず、
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は苦渋に眉を顰めた・・・
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_HURIHODOKU_FALSE")
PRINTW 



;暴れる
;**********************************************************
@MESSAGE_BATTLE_CHARA_ABARERU
IF FLAG:110 && TALENT:(FLAG:111):寄生 == 0
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は%PRINT_TRANSCALLNAME(FLAG:111)%に拘束された状態からでも
ELSEIF FLAG:10 == 2
	PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は
	CALL TENTACLE_ACCESS, "NAME"
	PRINTFORML に拘束された状態からでも
ELSE
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は触手に絡まれた状態からでも
ENDIF
PRINTL なんとかダメージを与えようと苦し紛れにもがいた！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_ABARERU")
PRINTW 



;耐える
;**********************************************************
@MESSAGE_BATTLE_CHARA_TAERU
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は
PRINT ぐっと唇を噛んで
IF FLAG:110 == 0
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
PRINTL の陵辱に耐えている・・・
PRINTL 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_TAERU")
PRINTW 



;なすがまま
;**********************************************************
@MESSAGE_BATTLE_CHARA_NASUGAMAMA
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は身を竦ませ、
IF FLAG:110 == 0
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
PRINTL の陵辱になすがままにされている・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_NASUGAMAMA")
PRINTW 



;受け入れる
;**********************************************************
@MESSAGE_BATTLE_CHARA_UKEIRERU
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は
IF FLAG:110 == 0
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
PRINTL の与える快楽をすすんで受け入れた・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_UKEIRERU")
PRINTW 



;奉仕する
;**********************************************************
@MESSAGE_BATTLE_CHARA_HOUSI
IF FLAG:110 == 0
	IF FLAG:10 == 2
		PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は自分から
		CALL TENTACLE_ACCESS, "NAME"
		PRINTFORML への熱心な奉仕を始めた・・・
	ELSE
		PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は近くにあった触手を手に取り熱心な奉仕を始めた・・・
	ENDIF
ELSEIF FLAG:110 == 1
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は自分から%PRINT_TRANSCALLNAME(FLAG:111)%への熱心な奉仕を始めた・・・
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_HOUSI")
PRINTW 



;Ｖ挿入防御
;**********************************************************
@MESSAGE_BATTLE_CHARA_ANTI_V
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%はせめて膣への挿入だけでも防ごうと身構えた・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_ANTI_V")
PRINTW 



;引き剥がす
;**********************************************************
@MESSAGE_BATTLE_CHARA_HIKIHAGASU
IF (FLAG:110 == 0)
	LOCALS = 触手
	CALL TENTACLE_ACCESS, "GETNAME"
	LOCALS:1 = %RESULTS%
ELSEIF (FLAG:110 == 1)
	IF (TALENT:(FLAG:111):寄生 == 0 || FILTER_CHECK_F(13) > 0) && (TALENT:(FLAG:111):ふたなり > 0 || TALENT:(FLAG:111):オトコ > 0)
		LOCALS = ペニス
	ELSE
		LOCALS = 触手
	ENDIF
	LOCALS:1 = %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は%LOCALS:1%に犯されながらも激しく抵抗した・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_HIKIHAGASU")
PRINTW 

;引き剥がす成功
@MESSAGE_BATTLE_CHARA_HIKIHAGASU_SUCCESS
IF (FLAG:110 == 0)
	LOCALS = 触手
	CALL TENTACLE_ACCESS, "GETNAME"
	LOCALS:1 = %RESULTS%
ELSEIF (FLAG:110 == 1)
	IF (TALENT:(FLAG:111):寄生 == 0 || FILTER_CHECK_F(13) > 0) && (TALENT:(FLAG:111):ふたなり > 0 || TALENT:(FLAG:111):オトコ > 0)
		LOCALS = ペニス
	ELSE
		LOCALS = 触手
	ENDIF
	LOCALS:1 = %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
IF LOCALS == "ペニス" || LOCALS:1 == "Ｃ触手" || (LOCALS:1 == "Ｂ触手" && TFLAG:20 == 1011)
	PRINTFORM 何とか%LOCALS:1%を突き飛ばすことに成功した！
ELSE
	PRINTFORML 触手を両手で掴んで抑え込み、力尽くで引き抜くことに成功した！
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_HIKIHAGASU_SUCCESS")
PRINTW 


;引き剥がす失敗
@MESSAGE_BATTLE_CHARA_HIKIHAGASU_FALSE
IF (FLAG:110 == 0)
	LOCALS = 触手
	CALL TENTACLE_ACCESS, "GETNAME"
	LOCALS:1 = %RESULTS%
ELSEIF (FLAG:110 == 1)
	IF (TALENT:(FLAG:111):寄生 == 0 || FILTER_CHECK_F(13) > 0) && (TALENT:(FLAG:111):ふたなり > 0 || TALENT:(FLAG:111):オトコ > 0)
		LOCALS = ペニス
	ELSE
		LOCALS = 触手
	ENDIF
	LOCALS:1 = %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
IF LOCALS == "ペニス" || LOCALS:1 == "Ｃ触手" || LOCALS:1 == "Ｂ触手" && TFLAG:20 == 1011
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は何とかして%LOCALS:1%を跳ね除けようとするが、
	PRINTFORML 激しいピストンで姿勢を崩されて思うように抵抗できない・・・
ELSEIF LOCALS:1 == "Ａ触手"
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は何とかして%LOCALS:1%を引き離そうとするが、
	PRINTFORML 相手が不定形では空しい抵抗でしかない・・・
ELSE
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は自分を犯している触手を何とか掴んで引き抜こうとしたが、
	IF RAND:3 == 0
		PRINTFORML 絶妙なタイミングでズンと突き上げられて力が抜けてしまった・・・
	ELSEIF RAND:2 == 0
		PRINTFORML 伸ばした手を別の触手に拘束され失敗してしまった・・・
	ELSE
		PRINTFORML ぬるぬるした粘液で滑る触手を上手く掴むことができなかった・・・
	ENDIF
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_HIKIHAGASU_FALSE")
PRINTW 


;睨みつける
;**********************************************************
@MESSAGE_BATTLE_CHARA_NIRAMI
IF TALENT:臆病 || TALENT:恥ずかしがり屋 || TALENT:悲観的
	LOCALS = 涙目で
	LOCALS:1 = 勇気を振り絞り
ELSE
	LOCALS = キッと
	LOCALS:1 = 好きにさせまいと
ENDIF
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は
;射精暴発、手に射精
IF (TFLAG:5 & 暴発) > 0
	PRINTFORM 精液で全身を汚されながらも%LOCALS%
;前遊系
ELSEIF TFLAG:20 == 0 || TFLAG:20 == 1 || TFLAG:20 == 2 || TFLAG:20 == 4 || TFLAG:20 == 6 || TFLAG:20 == 7 || TFLAG:20 == 8 || TFLAG:20 == 1007
	PRINTFORM 手も足も出ない悔しさを堪えながら%LOCALS%
;口辱系
ELSEIF TFLAG:20 == 11 || TFLAG:20 == 12
	IF (TFLAG:5 & 口) > 0
		PRINTFORM 口元から精液と唾液の混合物を垂れ流しながらも%LOCALS%
	ELSE
		PRINTFORM 口を犯されながらも%LOCALS:1%
	ENDIF
;ＳＭ系
ELSEIF TFLAG:20 == 9 || TFLAG:20 == 1004 || TFLAG:20 == 1008 || TFLAG:20 == 1015
	PRINTFORM 苦痛を伴う責めに息を荒げながらも%LOCALS%
;本番系
ELSEIF TFLAG:20 == 3 || TFLAG:20 == 5 || TFLAG:20 == 15 || TFLAG:20 == 16 || TFLAG:20 == 1001 || TFLAG:20 == 1002 || TFLAG:20 == 1009 || TFLAG:20 == 1010 || TFLAG:20 == 1011 || TFLAG:20 == 1014
	IF (TFLAG:5 & 膣) > 0 && (TFLAG:5 & アナル) > 0
		PRINTFORM 前後の穴に精液を注ぎこまれながらも%LOCALS%
	ELSEIF (TFLAG:5 & 膣) > 0
		PRINTFORM 無残に中出しされ精液を垂れ流しながらも%LOCALS%
	ELSEIF (TFLAG:5 & アナル) > 0
		PRINTFORM アナルに精液を注ぎこまれながらも%LOCALS%
	ELSE
		PRINTFORM 為すすべなく犯されながらも戦意を失わずに%LOCALS%
	ENDIF
;激しい凌辱
ELSEIF TFLAG:20 == 1000 || TFLAG:20 == 1003 || TFLAG:20 == 1005 || TFLAG:20 == 1012
	PRINTFORM 激しい凌辱に悶え乱れながらも%LOCALS%
ELSE
	PRINTFORM 自分を鼓舞し奮い立たせるかのように
ENDIF
PRINTFORML 相手を睨みつけた！
PRINTL 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_NIRAMI")
PRINTW 


;罵倒する
;**********************************************************
@MESSAGE_BATTLE_CHARA_BATOU
IF (FLAG:110 == 0)
	LOCALS = 触手
	CALL TENTACLE_ACCESS, "GETNAME"
	LOCALS:1 = %RESULTS%
ELSEIF (FLAG:110 == 1)
	IF (TALENT:(FLAG:111):寄生 == 0 || FILTER_CHECK_F(13) > 0) && (TALENT:(FLAG:111):ふたなり > 0 || TALENT:(FLAG:111):オトコ > 0)
		LOCALS = ペニス
	ELSE
		LOCALS = 触手
	ENDIF
	LOCALS:1 = %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
IF (MESSAGE_BRANCH_F(TARGET) & 絶望)
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は%LOCALS:1%を罵ったが言葉に勢いがなく、
	PRINTFORML 虚勢を張っているだけなのは誰の目にも明らかだ・・・
ELSEIF (MESSAGE_BRANCH_F(TARGET) & 快楽蕩け)
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は%LOCALS:1%を口汚く罵倒したが
	IF TALENT:主観視点 > 0
		PRINTFORML 快楽に蕩けさせられた舌では呂律が回らない・・・
	ELSE
		PRINTFORML 快楽に蕩けさせられ呂律が回っていないため強がりが筒抜けだ・・・
	ENDIF
ELSEIF (MESSAGE_BRANCH_F(TARGET) & 苦戦)
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は%LOCALS:1%を口汚く罵り何とか戦意を奮い起した！
ELSE
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は負けないという意志を込めて%LOCALS:1%を口汚く罵った！
ENDIF
PRINTL 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_BATOU")
PRINTW 


;反抗する
;**********************************************************
@MESSAGE_BATTLE_CHARA_RESISTANCE
IF (FLAG:110 == 0)
	LOCALS = 触手
	CALL TENTACLE_ACCESS, "GETNAME"
	LOCALS:1 = %RESULTS%
ELSEIF (FLAG:110 == 1)
	IF (TALENT:(FLAG:111):寄生 == 0 || FILTER_CHECK_F(13) > 0) && (TALENT:(FLAG:111):ふたなり > 0 || TALENT:(FLAG:111):オトコ > 0)
		LOCALS = あなた
	ELSE
		LOCALS = 触手
	ENDIF
	LOCALS:1 = %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
IF (MESSAGE_BRANCH_F(TARGET) & 絶望)
	PRINTFORML 心を折られた%PRINT_TRANSCALLNAME(TARGET)%はもはや形だけの抵抗をしている・・・
ELSEIF (MESSAGE_BRANCH_F(TARGET) & 快楽蕩け)
	PRINTFORML 快楽漬けにされた%PRINT_TRANSCALLNAME(TARGET)%はもはや形だけの抵抗をしている・・・
ELSEIF (MESSAGE_BRANCH_F(TARGET) & 苦戦)
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%はまだ諦めていないことを態度で示した！
ELSE
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%はまだ凌辱に屈していないことを態度で示した！
ENDIF
PRINTL 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_RESISTANCE")
PRINTW 


;説得する
;**********************************************************
@MESSAGE_BATTLE_CHARA_PERSUADE
IF (FLAG:110 == 0)
	LOCALS = 触手
	CALL TENTACLE_ACCESS, "GETNAME"
	LOCALS:1 = %RESULTS%
ELSEIF (FLAG:110 == 1)
	IF (TALENT:(FLAG:111):寄生 == 0 || FILTER_CHECK_F(13) > 0) && (TALENT:(FLAG:111):ふたなり > 0 || TALENT:(FLAG:111):オトコ > 0)
		LOCALS = ペニス
	ELSE
		LOCALS = 触手
	ENDIF
	LOCALS:1 = %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
IF TALENT:主観視点 > 0
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%はなんとか言葉で説得しようとした・・・
ELSE
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%はこんなことを止めるように説得しようとしている・・・
ENDIF
PRINTL 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_PERSUADE")
PRINTW 


;ゲージ関連
;**********************************************************
;SP変身
;**********************************************************
@MESSAGE_BATTLE_CHARA_SP_TRANSFORMCALL
PRINTFORML 力を開放した%PRINT_TRANSCALLNAME(TARGET)%の周囲に、\@(CFLAG:1 == 1) ? 再び # \@光が集う…！
;口上
PRINTL 
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_SP_TRANSFORMCALL")
IF RESULT == -1
	SIF CFLAG:4 == 1
		PRINTL 「はぁぁあああっ！！」
ENDIF
PRINTL 
PRINT 輝きが散り、その下から
IF CFLAG:1 == 1 && CFLAG:41 != 0
	PRINT 傷一つない衣装
ELSE
	PRINT 今まで以上の力
ENDIF
PRINTFORML を纏った%PRINT_TRANSCALLNAME(TARGET)%が姿を現した！

PRINTW 

;SPバースト
;**********************************************************
@MESSAGE_BATTLE_CHARA_SP_BURST
PRINTL 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_SP_BURST")
IF RESULT == -1
	SIF CFLAG:4 == 1
		PRINTL 「いい加減に……！！」
ENDIF
PRINTL 
IF FLAG:110 == 0
	PRINTFORML %PRINT_CALLNAME(TARGET)%は身体に秘められた力を解き放ち、周囲の触手を吹き飛ばした！
ELSE
	PRINTFORML %PRINT_CALLNAME(TARGET)%は身体に秘められた力を解き放ち、拘束を吹き飛ばした！
ENDIF
PRINTW 




;**********************************************************
;敵側の行動

;敵の戦闘行動関連
;**********************************************************
;攻撃開始
@MESSAGE_BATTLE_TENTACLE_ATTACK
TRYCCALLFORM  MESSAGE_BATTLE_MOB_{FLAG:11}_TENTACLE_ATTACK
CATCH
	IF FLAG:110 == 0
		CALL TENTACLE_ACCESS, "NAME"
	ELSEIF FLAG:110 == 1
		PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
	ENDIF
	IF TFLAG:12 == 1
		PRINT の攻撃！
	ELSEIF TFLAG:12 == 2
		PRINT の二点同時攻撃！
	ELSE
		PRINT の全方位攻撃！
	ENDIF
	PRINT （
	IF TFLAG:11 & 1
		PRINT 近
	ELSE
		PRINT 　
	ENDIF
	IF TFLAG:11 & 2
		PRINT 中
	ELSE
		PRINT 　
	ENDIF
	IF TFLAG:11 & 4
		PRINT 遠
	ELSE
		PRINT 　
	ENDIF
	PRINT ）
	IF TFLAG:11 & 8
		PRINT 対空
	ELSE
		PRINT 　　
	ENDIF
	PRINTL 
	;口上
	TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_ATTACK")
	PRINTL 
ENDCATCH

;攻撃がクリティカルヒット
@MESSAGE_BATTLE_TENTACLE_ATTACK_CRITICAL_HIT
IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "撹乱" || (GETBIT(TCVAR:217, 0) && FSTYLE_NAME_F(TARGET,TCVAR:0) == "全力")
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は防御に不安のある体勢で直撃を受けてしまった！
	PRINTL 
ELSEIF FSTYLE_NAME_F(TARGET,TCVAR:0) == "装甲"
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は咄嗟に身構え、ダメージを軽減した！
	PRINTL 
ENDIF
SETCOLOR 255,0,0
FONTBOLD
PRINTL CRITICAL HIT!
FONTREGULAR
RESETCOLOR
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_ATTACK_CRITICAL_HIT")
PRINTL 

;攻撃が通常ヒット
@MESSAGE_BATTLE_TENTACLE_ATTACK_HIT
IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "撹乱" || (GETBIT(TCVAR:217, 0) && FSTYLE_NAME_F(TARGET,TCVAR:0) == "全力")
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は防御に不安のある体勢で直撃を受けてしまった！
	PRINTL 
ELSEIF FSTYLE_NAME_F(TARGET,TCVAR:0) == "装甲"
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は咄嗟に身構え、ダメージを軽減した！
	PRINTL 
ENDIF
SETCOLOR 255,255,0
PRINTL HIT!
RESETCOLOR
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_ATTACK_HIT")
PRINTL 

;攻撃を回避
@MESSAGE_BATTLE_TENTACLE_ATTACK_FALSE
SETCOLOR 255,0,255
PRINTL MISS!
RESETCOLOR
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は攻撃を回避した！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_ATTACK_FALSE")
PRINTL 

;攻撃がカス当たり
@MESSAGE_BATTLE_TENTACLE_ATTACK_GRAZE
SETCOLOR 0,255,255
PRINTL GRAZE!
RESETCOLOR
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は紙一重で攻撃を回避したが、衣装の一部を引き裂かれた！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_ATTACK_GRAZE")
PRINTL 



;触手絡みつく関連
;**********************************************************
;絡みつく開始
@MESSAGE_BATTLE_TENTACLE_KARAMITUKU
IF FLAG:110 == 0 && FLAG:10 == 2
	CALLFORM MESSAGE_BATTLE_MOB_{FLAG:11}_KARAMITUKU
ELSE
	IF FLAG:110 == 0
		CALL TENTACLE_ACCESS, "NAME"
	ELSEIF FLAG:110 == 1
		PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
	ENDIF
	IF TFLAG:12 == 1
		PRINT は触手を伸ばして絡みつこうとしてきた！
	ELSE
		PRINT は広範囲に触手を伸ばして絡みつこうとしてきた！
	ENDIF
	PRINT （
	IF TFLAG:11 & 1
		PRINT 近
	ELSE
		PRINT 　
	ENDIF
	IF TFLAG:11 & 2
		PRINT 中
	ELSE
		PRINT 　
	ENDIF
	IF TFLAG:11 & 4
		PRINT 遠
	ELSE
		PRINT 　
	ENDIF
	PRINT ）
	IF TFLAG:11 & 8
		PRINT 対空
	ELSE
		PRINT 　　
	ENDIF
	PRINTL 
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_KARAMITUKU")
PRINTL 

;絡みつくが成功
@MESSAGE_BATTLE_TENTACLE_KARAMITUKU_SUCCESS
IF FLAG:110 == 0 && FLAG:10 == 2
	CALLFORM MESSAGE_BATTLE_MOB_{FLAG:11}_KARAMITUKU_SUCCESS
ELSE
	LOCAL = RAND:5
	IF LOCAL == 4
		PRINTFORML 素早く伸びた触手が足に絡み付き、そのまま上下逆さまに空高く吊し上げられる！
	ELSEIF LOCAL == 3
		PRINTFORML 足元から無数の触手が飛び出し、逃げる間もなく全身に絡み付いていく！
	ELSEIF LOCAL == 2
		PRINTFORML 正面から押し寄せてきた触手の波に飲まれ、その場に押し倒される！
	ELSEIF LOCAL == 1
		PRINTFORML 触手に吹き飛ばされて壁にぶつかり、地面に倒れたところをズルズルと引き摺られていく！
	ELSE
		PRINTFORML 全身を触手に巻き付かれて人外の筋力で壁際に押し付けられる！
	ENDIF
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は触手に拘束されてしまった！
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_KARAMITUKU_SUCCESS")
PRINTL 

;絡みつくを回避
@MESSAGE_BATTLE_TENTACLE_KARAMITUKU_FALSE
SETCOLOR 255,0,255
PRINTL MISS!
RESETCOLOR
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は拘束攻撃を回避した！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_KARAMITUKU_FALSE")
PRINTL 



;触手が体液を吐く
;**********************************************************
;体液を吐く開始
@MESSAGE_BATTLE_TENTACLE_TAIEKI
TRYCCALLFORM  MESSAGE_BATTLE_MOB_{FLAG:11}_TENTACLE_TAIEKI
CATCH
	IF FLAG:110 == 0
		CALL TENTACLE_ACCESS, "NAME"
		PRINT は大きく身震いすると先端から腐臭のする体液を
	ELSEIF FLAG:110 == 1
		PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
		PRINT は大きく身震いすると触手の先端から腐臭のする体液を
	ENDIF
	IF TFLAG:12 == 1
		PRINT 直線状に放ってきた！
	ELSEIF TFLAG:12 == 2
		PRINT 放ってきた！
	ELSE
		PRINT 広範囲に放ってきた！
	ENDIF
	PRINT （
	IF TFLAG:11 & 1
		PRINT 近
	ELSE
		PRINT 　
	ENDIF
	IF TFLAG:11 & 2
		PRINT 中
	ELSE
		PRINT 　
	ENDIF
	IF TFLAG:11 & 4
		PRINT 遠
	ELSE
		PRINT 　
	ENDIF
	PRINT ）
	IF TFLAG:11 & 8
		PRINT 対空
	ELSE
		PRINT 　　
	ENDIF
	PRINTL 
	;口上
	TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_TAIEKI")
	PRINTL 
ENDCATCH

;体液を吐くがヒット
@MESSAGE_BATTLE_TENTACLE_TAIEKI_HIT
IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "撹乱"
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は防御に不安のある体勢で直撃を受けてしまった！
	PRINTL 
ELSEIF FSTYLE_NAME_F(TARGET,TCVAR:0) == "装甲"
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は咄嗟に身構え、ダメージを軽減した！
	PRINTL 
ENDIF
SETCOLOR 255,255,0
PRINTL HIT!
RESETCOLOR
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_TAIEKI_HIT")
PRINTL 

;体液を吐くを回避
@MESSAGE_BATTLE_TENTACLE_TAIEKI_FALSE
SETCOLOR 255,0,255
PRINTL MISS!
RESETCOLOR
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は体液を回避した！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_TAIEKI_FALSE")
PRINTL 



;その距離にはいない
;**********************************************************
@MESSAGE_BATTLE_TENTACLE_ABSENCE
SETCOLOR 255,0,255
PRINTL MISS!
RESETCOLOR
IF TCVAR:0 == 0
	PRINTFORMW ERROR ! 拘束中に回避エラー
ELSEIF GETBIT(TCVAR:216,1) && GETBIT(TFLAG:11,TCVAR:0 - 1)
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は空中でうまく身をかわした！
ELSE
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%はそこにはいない！
ENDIF
PRINTL 



;距離をとる
;**********************************************************
@MESSAGE_BATTLE_TENTACLE_TAKEAWAY
IF FLAG:110 == 0
	CALL TENTACLE_ACCESS, "NAME"
	PRINTL は一度距離をとって力を温存させているようだ・・・
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
	PRINTL は一度距離をとって力を温存させているようだ・・・
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_TAKEAWAY")
PRINTL 



;押し倒す関連
;**********************************************************
;敵キャラが押し倒すを開始
@MESSAGE_BATTLE_TENTACLE_OSHITAOSU
PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
PRINTFORML は怪しげな足取りで%PRINT_TRANSCALLNAME(TARGET)%ににじり寄る！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_OSHITAOSU")
PRINTL 

;敵キャラの押し倒す成功
@MESSAGE_BATTLE_TENTACLE_OSHITAOSU_SUCCESS
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は%PRINT_TRANSCALLNAME(FLAG:111)%に勢い良く押し倒されてしまった！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_OSHITAOSU_SUCCESS")
PRINTL 

;押し倒す回避
@MESSAGE_BATTLE_TENTACLE_OSHITAOSU_FALSE
SETCOLOR 255,0,255
PRINTL MISS!
RESETCOLOR
PRINTFORML 危機を感じた%PRINT_TRANSCALLNAME(TARGET)%はなんとか%PRINT_TRANSCALLNAME(FLAG:111)%から距離をとった！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_OSHITAOSU_FALSE")
PRINTL 



;邪悪な波動関連
;**********************************************************
;敵キャラが邪悪な波動を開始
@MESSAGE_BATTLE_TENTACLE_HADOU
TRYCCALLFORM  MESSAGE_BATTLE_MOB_{FLAG:11}_TENTACLE_HADOU
CATCH
	IF FLAG:110 == 0
		CALL TENTACLE_ACCESS, "NAME"
		PRINTFORM は邪悪な波動を
	ELSEIF FLAG:110 == 1
		PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%は邪悪な波動を
	ENDIF
	IF TFLAG:12 == 1
		PRINT 直線状に放ってきた！
	ELSEIF TFLAG:12 == 2
		PRINT 放ってきた！
	ELSE
		PRINT 広範囲に放ってきた！
	ENDIF
	PRINT （
	IF TFLAG:11 & 1
		PRINT 近
	ELSE
		PRINT 　
	ENDIF
	IF TFLAG:11 & 2
		PRINT 中
	ELSE
		PRINT 　
	ENDIF
	IF TFLAG:11 & 4
		PRINT 遠
	ELSE
		PRINT 　
	ENDIF
	PRINT ）
	IF TFLAG:11 & 8
		PRINT 対空
	ELSE
		PRINT 　　
	ENDIF
	PRINTL 
	;口上
	TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_HADOU")
	PRINTL 
ENDCATCH

;邪悪な波動がヒット
@MESSAGE_BATTLE_TENTACLE_HADOU_HIT
IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "撹乱"
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は防御に不安のある体勢で直撃を受けてしまった！
	PRINTL 
ELSEIF FSTYLE_NAME_F(TARGET,TCVAR:0) == "装甲"
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は咄嗟に身構え、ダメージを軽減した！
	PRINTL 
ENDIF
SETCOLOR 255,255,0
PRINTL HIT!
RESETCOLOR
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_HADOU_HIT")
PRINTL 

;邪悪な波動を回避
@MESSAGE_BATTLE_TENTACLE_HADOU_FALSE
SETCOLOR 255,0,255
PRINTL MISS!
RESETCOLOR
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は波動を回避した！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_TENTACLE_HADOU_FALSE")
PRINTL 



;衣装の損傷が酷くなった
;**********************************************************
;地の文：アウターが破れ始めた
@MESSAGE_BATTLE_CLOTH_OUTERDAMAGE
IF FLAG:110 == 0
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
PRINTFORML の攻撃により、%PRINT_TRANSCALLNAME(TARGET)%の%COSTUME_NAME(TARGET)%の端々が破れ始めた・・・
IF CLOTH_NO_INNER > 0
	PRINTFORML アウターの挿入抵抗機能が失われた！
ELSE
	PRINTFORML アウターの下着防護機能が失われた！
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CLOTH_OUTERDAMAGE")
PRINTL 

;地の文：アウターが破れた
@MESSAGE_BATTLE_CLOTH_OUTERBREAK
IF FLAG:110 == 0
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
PRINTFORML の攻撃により、%PRINT_TRANSCALLNAME(TARGET)%の%COSTUME_NAME(TARGET)%は完全に布切れと化してしまった・・・
PRINTFORML アウターの機能が完全に失われた！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CLOTH_OUTERBREAK")
PRINTL 

;地の文：インナーが破れ始めた
@MESSAGE_BATTLE_CLOTH_INNERDAMAGE
IF FLAG:110 == 0
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
PRINTFORM の攻撃により、%PRINT_TRANSCALLNAME(TARGET)%の%ITEMNAME:(CFLAG:42)%
IF CFLAG:42 == 398
	PRINTL が今にも剥がれそうになっている・・・
ELSE
	PRINTL までも破れ始めてしまった・・・
ENDIF
PRINTFORML インナーの挿入抵抗機能が失われた！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CLOTH_INNERDAMAGE")
PRINTL 

;地の文：インナーが破れた
@MESSAGE_BATTLE_CLOTH_INNERBREAK
IF FLAG:110 == 0
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
IF CFLAG:42 == 398
	PRINTFORM に%ITEMNAME:(CFLAG:42)%を剥ぎ取られ、%PRINT_TRANSCALLNAME(TARGET)%の
	PRINTL 秘部が露わにされてしまっている・・・
ELSE
	PRINTFORM の攻撃により、%PRINT_TRANSCALLNAME(TARGET)%の%ITEMNAME:(CFLAG:42)%
	PRINTL はもはや下着としての機能を果たしていない・・・
ENDIF
PRINTFORML インナーの機能が完全に失われた！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CLOTH_INNERBREAK")
PRINTL 



;戦闘終了関連
;**********************************************************
;勝利
@MESSAGE_BATTLE_END_WIN
CALL TENTACLE_ACCESS, "NAME"
PRINTL は力尽きた！
PRINTL 
PRINT 戦闘結果：勝利 - 
CALL TENTACLE_ACCESS, "NAME"
PRINTL の殲滅完了！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_WIN")
PRINTW 

;敵キャラに勝利
@MESSAGE_BATTLE_END_WIN_ENEMY
PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%は力尽きた！
PRINTL 
PRINTFORM 戦闘結果：勝利 - %PRINT_TRANSCALLNAME(FLAG:111)%の奪還完了！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_WIN_ENEMY")
PRINTW 

;ラスボス触手出現
@MESSAGE_BATTLE_END_LASTBOSSAPPEAR
PRINTL 
PRINTFORML 全ての%STR:2502%を殲滅しました！
PRINTFORML %STR:2503%が出現しました！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_LASTBOSSAPPEAR")
PRINTW 

;完全殲滅
@MESSAGE_BATTLE_END_PERFECT
PRINTL 
PRINTFORML 全ての%STR:2500%を完全殲滅しました！！
PRINTL 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_PERFECT")
PRINTW 

;裏ボス出現
@MESSAGE_BATTLE_END_LASTBOSSAPPEAR_2
PRINTL 
PRINTFORML 全ての%STR:2500%を完全殲滅しました！！
PRINTL 
PRINTW …
PRINTW ……
PRINTW ………
PRINTL 
PRINTW なにやら様子がおかしい……
PRINTL 
PRINTFORML 突如大きな地響きが起こったかと思った次の瞬間、
PRINTFORML 目の前の触手の巨大な屍体が弾け飛び、地中から無数の肉の柱が出現した！
PRINTFORML それらは互いに絡まり合い、巨大な一本の"樹"の形を成していく……
PRINTW 
PRINTFORML 際限なく生長をつづける"肉の樹"は大地を砕き、ビルを倒壊させ、周囲を取り込んでいく。
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は危ういところで身を避わし、何とか撤退に成功した……
PRINTW 

;触手に捕らわれたキャラの救出
@MESSAGE_BATTLE_END_RESCUE
PRINTL 
PRINTFORML 殲滅した触手の屍から捕らわれていた仲間達が吐き出された！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_RESCUE")
PRINTW 

;悪堕ちキャラに捕らわれたキャラの救出
@MESSAGE_BATTLE_END_RESCUE_AKUOTI
PRINTL 
PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%に捕らわれていた仲間達が吐き出された！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_RESCUE_AKUOTI")
PRINTW 

;洗脳中キャラの救出
@MESSAGE_BATTLE_END_RESCUE_SENNOU
PRINTL 
IF FLAG:110 == 0
	PRINTFORML 触手が倒されたことで洗脳状態から解放された仲間がいるようです
ELSEIF FLAG:110 == 1
	PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%が倒されたことで洗脳状態から解放された仲間がいるようです
ENDIF
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_RESCUE_SENNOU")
PRINTW 

;敵キャラの救出
@MESSAGE_BATTLE_END_RESCUE_ENEMY
PRINTL 
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は倒れた%PRINT_TRANSCALLNAME(FLAG:111)%に駆け寄った！
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_RESCUE_ENEMY")
PRINTW 

;敗北
@MESSAGE_BATTLE_END_LOSS
IF FLAG:110 == 0
	CALL LOSE_SITUATION(TARGET)
	IF RESULT == 0
		PRINTFORML 精根尽き果てて動けなくなった%PRINT_TRANSCALLNAME(TARGET)%に、%STR:2500%が殺到する・・・
		PRINTFORML 大量の触手に絡みつかれた%PRINT_TRANSCALLNAME(TARGET)%は叫び声を上げることすら出来ずに
		PRINTFORML そのまま%STR:2500%に取り込まれていってしまった・・・
	ENDIF
ELSEIF FLAG:110 == 1
	PRINTFORML 精根尽き果てて動けなくなった%PRINT_TRANSCALLNAME(TARGET)%に、%PRINT_TRANSCALLNAME(FLAG:111)%が舌なめずりしながら這い寄る・・・
	PRINTFORML 何処からともなく現れた大量の触手に絡みつかれた%PRINT_TRANSCALLNAME(TARGET)%は叫び声を上げることすら出来ずに
	PRINTFORML そのまま%STR:2500%に取り込まれていってしまった・・・
ENDIF
PRINTL 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_LOSS")
PRINTW 

;時間切れ
@MESSAGE_BATTLE_END_TIMEUP
IF FLAG:45 == 0
	PRINTL どうやら戦闘が長引き過ぎた様だ
ELSE
	PRINTL 何とか襲撃を耐えきったようだ・・・
ENDIF
IF FLAG:110 == 0
	CALL TENTACLE_ACCESS, "NAME"
ELSEIF FLAG:110 == 1
	PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
ENDIF
PRINT は
SIF TCVAR:0 == 0
	PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%を離し、
;裏ボスの場合
IF FLAG:10 == 1 && FLAG:11 == 2
	PRINTL 地響きと共に姿を消してしまった・・・
;襲撃中の場合
ELSEIF FLAG:45 == 0
	PRINTL 大きく後退してそのまま逃げ出してしまった・・・
ELSE
	PRINTL 大きく後退してそのまま姿を消した・・・
ENDIF
PRINTL 
PRINT 戦闘結果：タイムアップ - 
CALL TENTACLE_ACCESS, "NAME"
PRINTL の殲滅失敗
PRINTL 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_END_TIMEUP")
PRINTW 



;心境変化関連
;**********************************************************
;心境変化：倒錯
@MESSAGE_SHINKYOU_CHANGE_TOUSAKU
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は 
CALL SHINKYOU_CHECK, "PRINT", 0
PRINTL 状態 になった
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_SHINKYOU_CHANGE_TOUSAKU")
PRINTW 

;心境変化：怒り
@MESSAGE_SHINKYOU_CHANGE_IKARI
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は 
CALL SHINKYOU_CHECK, "PRINT", 0
PRINTL 状態 になった
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_SHINKYOU_CHANGE_IKARI")
PRINTW 

;心境変化：諦観
@MESSAGE_SHINKYOU_CHANGE_TEIKAN
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は 
CALL SHINKYOU_CHECK, "PRINT", 0
PRINTL 状態 になった
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_SHINKYOU_CHANGE_TEIKAN")
PRINTW 

;心境変化：冷静
@MESSAGE_SHINKYOU_CHANGE_REISEI
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は 
CALL SHINKYOU_CHECK, "PRINT", 0
PRINTL 状態 になった
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_SHINKYOU_CHANGE_REISEI")

;心境変化：動揺
@MESSAGE_SHINKYOU_CHANGE_DOUYOU
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は 
CALL SHINKYOU_CHECK, "PRINT", 0
PRINTL 状態 になった
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_SHINKYOU_CHANGE_DOUYOU")
PRINTW 

;心境変化：高揚
@MESSAGE_SHINKYOU_CHANGE_KOUYOU
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は 
CALL SHINKYOU_CHECK, "PRINT", 0
PRINTL 状態 になった
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_SHINKYOU_CHANGE_KOUYOU")
PRINTW 

;心境変化：消沈
@MESSAGE_SHINKYOU_CHANGE_SYOUTIN
PRINTFORM %PRINT_TRANSCALLNAME(TARGET)%は 
CALL SHINKYOU_CHECK, "PRINT", 0
PRINTL 状態 になった
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_SHINKYOU_CHANGE_SYOUTIN")
PRINTW 


;体が竦んだ関連
;**********************************************************
;触手の虜で体が竦む
@MESSAGE_BATTLE_DISACTION_TORIKO
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は不意にこれまでの数々の陵辱を思い出してしまい
PRINTL 体が竦んでしまった・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_DISACTION_TORIKO")
PRINTW 

;快楽で体が竦む
@MESSAGE_BATTLE_DISACTION_KAIRAKU
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は不意にこれまでに与えられた快楽を思い出してしまい
PRINTL 体が竦んでしまった・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_DISACTION_KAIRAKU")
PRINTW 

;苦痛で体が竦む
@MESSAGE_BATTLE_DISACTION_KUTUU
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は不意にこれまでに与えられた苦痛を思い出してしまい
PRINTL 体が竦んでしまった・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_DISACTION_KUTUU")
PRINTW 

;屈辱で体が竦む
@MESSAGE_BATTLE_DISACTION_KUTUZYOKU
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は不意にこれまでに与えられた屈辱を思い出してしまい
PRINTL 体が竦んでしまった・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_DISACTION_KUTUZYOK")
PRINTW 

;恐怖で体が竦む
@MESSAGE_BATTLE_DISACTION_KYOUHU
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は不意にこれまでに与えられた恐怖を思い出してしまい
PRINTL 体が竦んでしまった・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_DISACTION_KYOUHU")
PRINTW 

;恥辱で体が竦む
@MESSAGE_BATTLE_DISACTION_TIZYOKU
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は不意にこれまでに与えられた恥辱を思い出してしまい
PRINTL 体が竦んでしまった・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_DISACTION_TIZYOKU")
PRINTW 

;寄生の影響で体が竦む
@MESSAGE_BATTLE_DISACTION_PARASITE
PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は体内に寄生する触手が不意に暴れだそうとして
PRINTL 体が竦んでしまった・・・
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_DISACTION_TIZYOKU")
PRINTW 


