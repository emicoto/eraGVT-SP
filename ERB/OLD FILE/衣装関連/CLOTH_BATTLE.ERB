;**********************************************************
;戦闘中の衣装に関する処理

;----------------------------------------------------------
;衣装の耐久度を設定
@CLOTH_BATTLE_SETHP
TRYCCALLFORM CLOTH_HP_{CFLAG:40}
	TCVAR:20 = RESULT
	TCVAR:21 = RESULT
CATCH
	TCVAR:20 = 0
	TCVAR:21 = 0
ENDCATCH

CFLAG:1 = 1
TRYCCALLFORM CLOTH_HP_{CFLAG:41}
	CALL CLOTH_CUSTOMIZE_COMMON_CAL, RESULT, "HP"
	TCVAR:22 = RESULT
	TCVAR:23 = RESULT
CATCH
	TCVAR:22 = 0
	TCVAR:23 = 0
ENDCATCH
CFLAG:1 = 0

TRYCCALLFORM CLOTH_HP_{CFLAG:42}
	TCVAR:24 = RESULT
	TCVAR:25 = RESULT
CATCH
	TCVAR:24 = 0
	TCVAR:25 = 0
ENDCATCH


;----------------------------------------------------------
;衣装の耐久度表示
@CLOTH_BATTLE_DISPHP
PRINTL 
CALL CLOTH_NO_INNER, TARGET
SIF COSTUME_NAME(TARGET) != ""
	PRINTFORM %COSTUME_NAME(TARGET)%
SIF COSTUME_NAME(TARGET) == "" && (ITEMNAME:(CFLAG:42) == "" || CFLAG:42 == 398)
	PRINTFORM ハダカ
SIF (COSTUME_NAME(TARGET) != "" || CFLAG:42 == 398) && ITEMNAME:(CFLAG:42) != "" && RESULT == 0
	PRINT  / 
SIF ITEMNAME:(CFLAG:42) != "" && RESULT == 0
	PRINTFORM %ITEMNAME:(CFLAG:42)%
PRINTL 
LOCALS = アウター
IF CFLAG:1 == 0
	IF TCVAR:20 < 0
		PRINTFORM %LOCALS,10,LEFT%
		BAR 0, 1, 5
		PRINTFORM （─） 
	ELSEIF TCVAR:20 == 0
		SIF CFLAG:40 != 0
			SETCOLOR 255, 0, 0
		PRINTFORM %LOCALS,10,LEFT%
		BAR 0, 1, 5
		PRINTFORM （なし）
	ELSEIF TCVAR:21 > 0
		IF PERCENT_CAL_F(TCVAR:21, TCVAR:20) <= CLOTH_OUTER_DEF
			SETCOLOR 255, 123, 0
		ELSEIF PERCENT_CAL_F(TCVAR:21, TCVAR:20) <= CLOTH_OUTER_DEF + (100 - CLOTH_OUTER_DEF) / 2
			SETCOLOR 255, 255, 0
		ENDIF
		PRINTFORM %LOCALS,10,LEFT%
		BAR TCVAR:21, TCVAR:20, 5
		PRINTFORM （{TCVAR:21}/{TCVAR:20}）
	ELSE
		SETCOLOR 255, 0, 0
		PRINTFORM %LOCALS,10,LEFT%
		BAR 0, TCVAR:20, 5
		PRINTFORM （0/{TCVAR:20}）
	ENDIF
ELSE
	IF TCVAR:22 < 0
		PRINTFORM %LOCALS,10,LEFT%
		BAR 0, 1, 5
		PRINTFORM （─） 
	ELSEIF TCVAR:22 == 0
		SIF CFLAG:41 != 0
			SETCOLOR 255, 0, 0
		PRINTFORM %LOCALS,10,LEFT%
		BAR 0, 1, 5
		PRINTFORM （なし）
	ELSEIF TCVAR:23 > 0
		IF PERCENT_CAL_F(TCVAR:23, TCVAR:22) <= CLOTH_INNER_DEF
			SETCOLOR 255, 123, 0
		ELSEIF PERCENT_CAL_F(TCVAR:23, TCVAR:22) <= CLOTH_INNER_DEF + (100 - CLOTH_INNER_DEF) / 2
			SETCOLOR 255, 255, 0
		ENDIF
		PRINTFORM %LOCALS,10,LEFT%
		BAR TCVAR:23, TCVAR:22, 5
		PRINTFORM （{TCVAR:23}/{TCVAR:22}）
	ELSE
		SETCOLOR 255, 0, 0
		PRINTFORM %LOCALS,10,LEFT%
		BAR 0, TCVAR:22, 5
		PRINTFORM （0/{TCVAR:22}）
	ENDIF
ENDIF
RESETCOLOR

LOCALS = インナー
CALL CLOTH_NO_INNER, TARGET
IF TCVAR:24 < 0 || RESULT > 0
	PRINTFORM %LOCALS,10,LEFT%
	BAR 0, 1, 5
	PRINTFORM （─）
ELSE
	IF TCVAR:24 == 0
		SIF CFLAG:42 != 0
			SETCOLOR 255, 0, 0
		PRINTFORM %LOCALS,10,LEFT%
		BAR 0, 1, 5
		PRINTFORM （なし）
	ELSEIF TCVAR:25 > 0
		IF PERCENT_CAL_F(TCVAR:25, TCVAR:24) < 25
			SETCOLOR 255, 123, 0
		ELSEIF PERCENT_CAL_F(TCVAR:25, TCVAR:24) < 50
			SETCOLOR 255, 255, 0
		ENDIF
		PRINTFORM %LOCALS,10,LEFT%
		BAR TCVAR:25, TCVAR:24, 5
		PRINTFORM （{TCVAR:25}/{TCVAR:24}）
	ELSE
		SETCOLOR 255, 0, 0
		PRINTFORM %LOCALS,10,LEFT%
		BAR 0, TCVAR:24, 5
		PRINTFORM （0/{TCVAR:24}）
	ENDIF
ENDIF

;----------------------------------------------------------
;衣装へのダメージ
@CLOTH_BATTLE_DAMAGE, ARG, ARG:1 = 0
LOCAL:0 = 0
LOCAL:1 = 0
LOCAL:2 = 50
LOCAL:3 = ARG
;前貼りに対するダメージは非常に小さくなる
IF CFLAG:42 == 398 && ARG > 0
	IF TCVAR:2 == 体勢：Ｖ防御
		LOCAL:3 = MAX(LOCAL:3 / 6, 1)
	ELSE
		LOCAL:3 = MAX(LOCAL:3 / 3, 1)
	ENDIF
ENDIF
IF CFLAG:1 == 0
	TRYCCALLFORM CLOTH_DEF_{CFLAG:40}
		LOCAL:2 = RESULT
	CATCH
		LOCAL:2 = DEFAULT_OUTER_DEF
	ENDCATCH
	IF TCVAR:20 > 0 || TCVAR:24 > 0
		IF TCVAR:21 > 0 || TCVAR:25 > 0
			IF TCVAR:21 == 0 || PERCENT_CAL_F(TCVAR:21,TCVAR:20) < LOCAL:2
				;インナー兼用衣装の場合はインナーにダメージを与えない
				CALL CLOTH_NO_INNER, TARGET
				IF TCVAR:25 > 0 && RESULT == 0
					SIF TCVAR:0 != 0 && ARG:1 == 0
						LOCAL:3 = LOCAL:3 / 3
					LOCAL:1 = PERCENT_CAL_F(TCVAR:25,TCVAR:24)
					TCVAR:25 = LIMIT(TCVAR:25 - LOCAL:3, 0, TCVAR:25)
					SIF ARG:1 == 0 && TCVAR:25 > 0
						ARG = ARG / 3
				ELSEIF ARG:1 == 0 && TCVAR:25 > 0
					ARG = ARG / 6
				ENDIF
			ENDIF
			LOCAL:0 = PERCENT_CAL_F(TCVAR:21,TCVAR:20)
			TCVAR:21 = LIMIT(TCVAR:21 - ARG, 0, TCVAR:21)
		ENDIF
	ENDIF
ELSE
	TRYCCALLFORM CLOTH_DEF_{CFLAG:41}
		LOCAL:2 = RESULT
	CATCH
		LOCAL:2 = DEFAULT_OUTER_DEF
	ENDCATCH
	IF TCVAR:22 > 0 || TCVAR:24 > 0
		IF TCVAR:23 > 0 || TCVAR:25 > 0
			IF TCVAR:23 == 0 || PERCENT_CAL_F(TCVAR:23,TCVAR:22) < LOCAL:2
				;インナー兼用衣装の場合はインナーにダメージを与えない
				CALL CLOTH_NO_INNER, TARGET
				IF TCVAR:25 > 0 && RESULT == 0
					SIF TCVAR:0 != 0 && ARG:1 == 0
						LOCAL:3 = LOCAL:3 / 3
					LOCAL:1 = PERCENT_CAL_F(TCVAR:25,TCVAR:24)
					TCVAR:25 = LIMIT(TCVAR:25 - LOCAL:3, 0, TCVAR:25)
					SIF ARG:1 == 0 && TCVAR:25 > 0
						ARG = ARG / 3
				ELSEIF ARG:1 == 0 && TCVAR:25 > 0
					ARG = ARG / 6
				ENDIF
			ENDIF
			LOCAL:0 = PERCENT_CAL_F(TCVAR:23,TCVAR:22)
			TCVAR:23 = LIMIT(TCVAR:23 - ARG, 0, TCVAR:23)
		ENDIF
	ENDIF
ENDIF

;耐久力関連の情報を更新
CALL REFRESH_CLOTH_DATA

;地の文
IF CFLAG:1 == 0
	IF LOCAL:0 > 0 && TCVAR:21 <= 0 && TCVAR:20 > 0
		;地の文：アウターが破れた
		CALL MESSAGE_BATTLE_CLOTH_OUTERBREAK
		;地の文：洗脳/悪堕ちキャラが操作キャラのアウターが破れたのを見た
		SIF FLAG:110 == 1
			CALL MESSAGE_OTHER_BATTLE_CLOTH_OUTERBREAK
			
	ELSEIF LOCAL:0 >= CLOTH_OUTER_DEF && PERCENT_CAL_F(TCVAR:21,TCVAR:20) < CLOTH_OUTER_DEF && TCVAR:20 > 0
		;地の文：アウターが破れ始めた
		CALL MESSAGE_BATTLE_CLOTH_OUTERDAMAGE
		;地の文：洗脳/悪堕ちキャラが操作キャラのアウターが破れ始めたのを見た
		SIF FLAG:110 == 1
			CALL MESSAGE_OTHER_BATTLE_CLOTH_OUTERDAMAGE
			
	ENDIF
ELSE
	IF LOCAL:0 > 0 && TCVAR:23 <= 0 && TCVAR:22 > 0
		;地の文：アウターが破れた
		CALL MESSAGE_BATTLE_CLOTH_OUTERBREAK
		;地の文：洗脳/悪堕ちキャラが操作キャラのアウターが破れたのを見た
		SIF FLAG:110 == 1
			CALL MESSAGE_OTHER_BATTLE_CLOTH_OUTERBREAK
			
	ELSEIF LOCAL:0 >= CLOTH_OUTER_DEF && PERCENT_CAL_F(TCVAR:23,TCVAR:22) < CLOTH_OUTER_DEF && TCVAR:22 > 0
		;地の文：アウターが破れ始めた
		CALL MESSAGE_BATTLE_CLOTH_OUTERDAMAGE
		;地の文：洗脳/悪堕ちキャラが操作キャラのアウターが破れ始めたのを見た
		SIF FLAG:110 == 1
			CALL MESSAGE_OTHER_BATTLE_CLOTH_OUTERDAMAGE

	ENDIF
ENDIF

IF LOCAL:1 && TCVAR:25 <= 0 && TCVAR:24 > 0
	;地の文：インナーが破れた
	CALL MESSAGE_BATTLE_CLOTH_INNERBREAK
	;地の文：洗脳/悪堕ちキャラが操作キャラのインナーが破れたのを見た
	SIF FLAG:110 == 1
		CALL MESSAGE_OTHER_BATTLE_CLOTH_INNERBREAK
		
ELSEIF LOCAL:1 >= CLOTH_INNER_DEF && PERCENT_CAL_F(TCVAR:25,TCVAR:24) < CLOTH_INNER_DEF && TCVAR:24 > 0
	;地の文：インナーが破れ始めた
	CALL MESSAGE_BATTLE_CLOTH_INNERDAMAGE
	;地の文：洗脳/悪堕ちキャラが操作キャラのインナーが破れ始めたのを見た
	SIF FLAG:110 == 1
		CALL MESSAGE_OTHER_BATTLE_CLOTH_INNERDAMAGE
		
ENDIF




;----------------------------------------------------------
;衣装による補正
@CLOTH_BATTLE_HOSEI, ARGS, ARG = -999
#DIM KEEP_TARGET
KEEP_TARGET = TARGET
SIF ARG != -999
	TARGET = ARG
LOCAL = 100
SIF ARGS == "CRITICAL" || ARGS == "ANTIBUP" || ARGS == "ANTICUP" || ARGS == "HEALUP" || ARGS == "EXBOOST" || ARGS == "AIRPLUS" || ARGS == "HIT" || ARGS == "AVOID"
	LOCAL = 0
IF CFLAG:1 == 0
	IF FLAG:700 && (TCVAR:21 == 0 || PERCENT_CAL_F(TCVAR:21,TCVAR:20) < 1) && KEEP_TARGET == TARGET
	ELSE
		TRYCCALLFORM CLOTH_HOSEI_%ARGS%_{CFLAG:40}
			IF ARGS == "CRITICAL" || ARGS == "ANTIBUP" || ARGS == "ANTICUP" || ARGS == "HEALUP" || ARGS == "EXBOOST" || ARGS == "AIRPLUS" || ARGS == "HIT" || ARGS == "AVOID"
				LOCAL += RESULT
			ELSE
				LOCAL = LOCAL * RESULT / 100
			ENDIF
		CATCH
		ENDCATCH
	ENDIF
ELSE
	IF FLAG:700 && (TCVAR:23 == 0 || PERCENT_CAL_F(TCVAR:23,TCVAR:22) < 1) && KEEP_TARGET == TARGET
	ELSE
		TRYCCALLFORM CLOTH_HOSEI_%ARGS%_{CFLAG:41}
			IF ARGS == "CRITICAL" || ARGS == "ANTIBUP" || ARGS == "ANTICUP" || ARGS == "HEALUP" || ARGS == "EXBOOST" || ARGS == "AIRPLUS" || ARGS == "HIT" || ARGS == "AVOID"
				LOCAL += RESULT
			ELSE
				LOCAL = LOCAL * RESULT / 100
			ENDIF
		CATCH
		ENDCATCH
	ENDIF
ENDIF
IF FLAG:700 && (TCVAR:25 == 0 || PERCENT_CAL_F(TCVAR:25,TCVAR:24) < 1) || CLOTH_NO_INNER > 0 && KEEP_TARGET == TARGET
ELSE
	TRYCCALLFORM CLOTH_HOSEI_%ARGS%_{CFLAG:42}
		IF ARGS == "CRITICAL" || ARGS == "ANTIBUP" || ARGS == "ANTICUP" || ARGS == "HEALUP" || ARGS == "EXBOOST" || ARGS == "AIRPLUS" || ARGS == "HIT" || ARGS == "AVOID"
			LOCAL += RESULT
		ELSE
			LOCAL = LOCAL * RESULT / 100
		ENDIF
	CATCH
	ENDCATCH
ENDIF
IF CFLAG:1 > 0
	CALL CLOTH_CUSTOMIZE_COMMON_CAL, LOCAL, "ARGS"
	LOCAL = RESULT
ENDIF
TARGET = KEEP_TARGET
RETURN LOCAL



;----------------------------------------------------------
;衣装の状態判定
;着衣が破壊されていなければビットを立てる
@CLOTH_CHECK(ARG)
#FUNCTION
LOCAL = 0
IF CFLAG:ARG:1 == 0 && TCVAR:ARG:21 > 0
	LOCAL |= 2
ELSEIF TCVAR:ARG:23 > 0
	LOCAL |= 4
ENDIF
SIF TCVAR:ARG:25 > 0
	LOCAL |= 1

RETURNF LOCAL



;--------------------------------------------------
;アウターの名称を返す式中関数
@COSTUME_NAME(ARG)
#FUNCTIONS
IF CFLAG:ARG:1 == 0
	IF CFLAG:ARG:40 == 0
		LOCALS = 
	ELSEIF CSTR:ARG:8 != ""
		LOCALS = %CSTR:ARG:8%
	ELSE
		LOCALS = %ITEMNAME:(CFLAG:ARG:40)%
	ENDIF
ELSE
	IF CFLAG:ARG:41 == 0
		LOCALS = 
	ELSEIF CSTR:ARG:9 != ""
		LOCALS = %CSTR:ARG:9%
	ELSE
		LOCALS = %ITEMNAME:(CFLAG:ARG:41)%
	ENDIF
ENDIF
RETURNF LOCALS



;--------------------------------------------------
;インナー兼用の判定
@CLOTH_NO_INNER, ARG
RESULT = 0
IF CFLAG:ARG:1 > 0
	TRYCALLFORM CLOTH_NO_INNER_{CFLAG:ARG:41}
ELSE
	TRYCALLFORM CLOTH_NO_INNER_{CFLAG:ARG:40}
ENDIF
RETURN RESULT




;--------------------------------------------------
;衣装耐久力参照に関する間数
@REFRESH_CLOTH_DATA
;衣装の状態を見る
IF CFLAG:1 == 0
	CALL PERCENT_CAL, TCVAR:21, TCVAR:20
	CLOTH_OUTER_PER = RESULT
	CALL PERCENT_CAL, TCVAR:25, TCVAR:24
	CLOTH_INNER_PER = RESULT
	TRYCCALLFORM CLOTH_DEF_{CFLAG:40}
		CLOTH_OUTER_DEF = RESULT
	CATCH
		CLOTH_OUTER_DEF = DEFAULT_OUTER_DEF
	ENDCATCH
	;変身後のアウターの耐久力を同期
	SIF TCVAR:22 > 0
		TCVAR:23 = TCVAR:22 * CLOTH_OUTER_PER / 100
ELSE
	CALL PERCENT_CAL, TCVAR:23, TCVAR:22
	CLOTH_OUTER_PER = RESULT
	CALL PERCENT_CAL, TCVAR:25, TCVAR:24
	CLOTH_INNER_PER = RESULT
	TRYCCALLFORM CLOTH_DEF_{CFLAG:41}
		CLOTH_OUTER_DEF = RESULT
	CATCH
		CLOTH_OUTER_DEF = DEFAULT_OUTER_DEF
	ENDCATCH
	;変身前のアウターの耐久力を同期
	SIF TCVAR:20 > 0
		TCVAR:21 = TCVAR:20 * CLOTH_OUTER_PER / 100
ENDIF
SIF CFLAG:1 == 0 && TCVAR:20 < 0
	CLOTH_OUTER_PER = 0
SIF CFLAG:1 > 0 && TCVAR:22 < 0
	CLOTH_OUTER_PER = 0
SIF TCVAR:24 < 0
	CLOTH_INNER_PER = 0
;インナー兼用の処理
CALL CLOTH_NO_INNER, TARGET
IF RESULT > 0
	CLOTH_INNER_PER = CLOTH_OUTER_PER
	CLOTH_NO_INNER = 1
	;挿入抵抗力の設定
	IF CFLAG:1 == 0
		LOCAL = 40
	ELSE
		LOCAL = 41
	ENDIF
	TRYCCALLFORM CLOTH_DEF_{CFLAG:LOCAL}
		CLOTH_INNER_DEF = RESULT
	CATCH
		CLOTH_INNER_DEF = DEFAULT_OUTER_DEF
	ENDCATCH
	;インナーの耐久力をアウターに同期
	SIF TCVAR:24 > 0
		TCVAR:25 = TCVAR:24 * CLOTH_OUTER_PER / 100
ELSE
	CLOTH_NO_INNER = 0
	;挿入抵抗力の設定
	TRYCCALLFORM CLOTH_DEF_{CFLAG:42}
		CLOTH_INNER_DEF = RESULT
	CATCH
		CLOTH_INNER_DEF = DEFAULT_INNER_DEF
	ENDCATCH
ENDIF