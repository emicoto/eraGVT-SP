;**********************************************************
;衣装の購入処理
;**********************************************************
@SHOW_SHOP_CLOTH
#LOCALSIZE 1
#LOCALSSIZE 1
CALL SHOW_CLOTH("購入画面")
RETURN 1

;----------------------------------------------------------
;衣装表示UI
;----------------------------------------------------------
@SHOW_CLOTH(SHOW_TYPE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS SHOW_TYPE

#DIM LCOUNT
#DIM KEEP_REDRAW
#DIM LIST_CLOTH, 100
#DIM LIST_CLOTH_LAST_NO
#DIM INPUT_NO
#DIMS CATEGORY = "アウター", "カスタマイズパーツ", "インナー", "その他"
#DIM SELECT_CATEGORY
#DIM カタログ
#DIM 簡易情報
#DIM PAGE
#DIM MAXPAGE
#DIM LIST_CLOTH_SELECT_NO
#DIMS DRAW_TYPE
#DIM CONST FILTER_INPUT_OFFSET = 50

;1ページに表示する衣装の数(リスト, 単体, カタログ, 簡易情報)
#DIM SHOW_COUNT_LIST = 44, 1, 4, 22
;フィルタの中身 ビットで管理
#DIM FILTER
;フィルタボタンの表示文字
{
#DIMS LABEL =
"攻", "防", "敏", "知", "体",
"気", "性", "油", "撃", "避",
"ク", "粘", "邪", "羞", "Ｂ",
"Ｃ", "癒", "EX", "空"
}
;フィルタボタンの効果指定
{
#DIMS HOSEI_NAME =
"KOUGEKI", "BOUGYO", "BINSYOU", "CHISEI", "TAIRYOKU",
"KIRYOKU", "SEITAISEI", "YUDAN", "HIT", "AVOID",
"CRITICAL", "LIQUID", "WAVE", "SHYNESS", "ANTIBUP",
"ANTICUP", "HEALUP", "EXBOOST", "AIRPLUS"
}
;有効にするフィルタの番号
#DIM CLOTH_HOSEI_ACTIVE = 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18

;初期化
INPUT_NO = -1
SELECT_CATEGORY = 0
カタログ = 0
簡易情報 = 0
PAGE = 0
MAXPAGE = 0
LIST_CLOTH_SELECT_NO = 0
DRAW_TYPE '= "リスト"

;現在の表示方法を保存し表示を止める
CURRENTREDRAW
KEEP_REDRAW = RESULT
REDRAW 0

$INPUT_LOOP

;戻る
IF INPUT_NO == 99
	IF DRAW_TYPE == "リスト" || DRAW_TYPE == "簡易情報"
		IF SHOW_TYPE == "購入画面"
			;実績
			LOCAL = 0
			FOR LCOUNT,100,400
				SIF LCOUNT == 100 || LCOUNT == 200 || LCOUNT == 300
					CONTINUE
				LOCAL += ITEM:LCOUNT
			NEXT
			IF LOCAL >= 10 && GLOBAL:169 == 0
				PRINTL 【実績：バトルレイヤー】を達成しました！
				PRINTW 
				GLOBAL:169 = 1
				SAVEGLOBAL
			ENDIF
		ENDIF
		REDRAW KEEP_REDRAW
		RETURN 1
	ELSE
		DRAW_TYPE '= 簡易情報 ? "簡易情報" # "リスト"
		PAGE = 0
		LIST_CLOTH_SELECT_NO = 0
	ENDIF
;前へ
ELSEIF INPUT_NO == 90
	PAGE = !PAGE ? MAXPAGE # PAGE - 1
	IF DRAW_TYPE == "単体"
		IF 0 < LIST_CLOTH_SELECT_NO
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_SELECT_NO - 1
		ELSE
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_LAST_NO - 1
		ENDIF
	ELSEIF DRAW_TYPE == "カタログ"
		IF 0 < LIST_CLOTH_SELECT_NO - SHOW_COUNT_LIST:2
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_SELECT_NO - SHOW_COUNT_LIST:2
		ELSE
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_LAST_NO - 1
		ENDIF
	ENDIF
;次へ
ELSEIF INPUT_NO == 91
	PAGE = 0 < MAXPAGE - PAGE ? PAGE + 1 # 0
	IF DRAW_TYPE == "単体"
		IF LIST_CLOTH_SELECT_NO < LIST_CLOTH_LAST_NO
			LIST_CLOTH_SELECT_NO ++
		ELSE
			LIST_CLOTH_SELECT_NO = 0
		ENDIF
	ELSEIF DRAW_TYPE == "カタログ"
		IF LIST_CLOTH_SELECT_NO + SHOW_COUNT_LIST:2 < LIST_CLOTH_LAST_NO
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_SELECT_NO + SHOW_COUNT_LIST:2
		ELSEIF LIST_CLOTH_SELECT_NO == LIST_CLOTH_LAST_NO - 1
			LIST_CLOTH_SELECT_NO = 0
		ELSE
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_LAST_NO - 1
		ENDIF
	ENDIF
;カテゴリ
ELSEIF INRANGE(INPUT_NO, 0, VARSIZE("CATEGORY") - 1)
	IF 簡易情報
		DRAW_TYPE '= "簡易情報"
	ELSE
		DRAW_TYPE '= "リスト"
		簡易情報 = 0
		カタログ = 0
	ENDIF
	SELECT_CATEGORY = INPUT_NO
	PAGE = 0
	LIST_CLOTH_SELECT_NO = 0
;カタログ
ELSEIF INPUT_NO == 10
	IF カタログ
		カタログ = 0
		DRAW_TYPE '= "リスト"
	ELSE
		カタログ = 1
		簡易情報 = 0
		DRAW_TYPE '= "カタログ"
	ENDIF
	PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
;簡易情報
ELSEIF INPUT_NO == 11
	IF 簡易情報
		簡易情報 = 0
		DRAW_TYPE '= "リスト"
	ELSE
		簡易情報 = 1
		カタログ = 0
		DRAW_TYPE '= "簡易情報"
	ENDIF
	PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
;フィルタ
ELSEIF INRANGE(INPUT_NO, FILTER_INPUT_OFFSET, MIN(FILTER_INPUT_OFFSET + VARSIZE("LABEL") - 1, FILTER_INPUT_OFFSET + 19)) && (DRAW_TYPE == "リスト" || DRAW_TYPE == "簡易情報")
	INVERTBIT FILTER, INPUT_NO - FILTER_INPUT_OFFSET
	PAGE = 0
	LIST_CLOTH_SELECT_NO = 0
;衣装
ELSEIF INRANGE(INPUT_NO, 100, 1000) && ((ITEM:INPUT_NO == 0 && ITEMPRICE:INPUT_NO > 0) || (SHOW_TYPE == "表示画面"))
	LIST_CLOTH_SELECT_NO = FINDELEMENT(LIST_CLOTH, INPUT_NO)
	IF LIST_CLOTH_SELECT_NO == -1
		INPUT_NO = -1
		GOTO INPUT_LOOP
	ENDIF
	IF DRAW_TYPE == "リスト" || DRAW_TYPE == "簡易情報"
		DRAW_TYPE '= カタログ ? "カタログ" # "単体"
		簡易情報 = 0
		PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
	ELSEIF SHOW_TYPE == "購入画面"
		CALL BUY_CLOTH(LIST_CLOTH, INPUT_NO)
		IF RESULT == 1
			PRINTFORML %ITEMNAME:INPUT_NO%を買いました
			;購入した衣装が最後尾の物なら空欄ページの回避の為一つ前の物を渡す
			IF LIST_CLOTH_SELECT_NO == LIST_CLOTH_LAST_NO - 1
				PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO - 1, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
			ELSE
				PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
			ENDIF
		ELSEIF RESULT == -1
			PRINTFORML 無効な値です
		ELSEIF !RESULT
			PRINTFORML お金が足りません
		ENDIF
		PRINTW
	ENDIF
ELSEIF INPUT_NO != -1
	PRINTW 想定外の値が入力されました
	PRINTL
	INPUT_NO = -1
	GOTO INPUT_LOOP
ENDIF

;売り物っぽい衣装の配列を作る
IF SHOW_TYPE == "購入画面"
	CALL LIST_CLOTH_NOTHAVE(LIST_CLOTH, LIST_CLOTH_LAST_NO, CATEGORY:SELECT_CATEGORY)
;所持衣装の配列を作る（変身衣装は除外）
ELSEIF SHOW_TYPE == "表示画面"
	CALL LIST_CLOTH_HAVE(LIST_CLOTH, LIST_CLOTH_LAST_NO, CATEGORY:SELECT_CATEGORY)
ENDIF
IF !RESULT
	PRINTFORMW 想定外のカテゴリーが指定されました(%CATEGORY:SELECT_CATEGORY%)
	SELECT_CATEGORY = 0
	GOTO INPUT_LOOP
ENDIF

;有効なフィルタボタンでフィルタ
SIF FILTER
	CALL FILTER_CLOTH_HOSEI(LIST_CLOTH, LIST_CLOTH_LAST_NO, HOSEI_NAME, CLOTH_HOSEI_ACTIVE, SELECT_CATEGORY, FILTER)

;1ページに表示する衣装の数と配列の中身の数から最大ページを取得
MAXPAGE = GET_CLOTH_MAXPAGE(DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)

;衣装表示前の描画
CALL SHOW_CLOTH_HEADER(SHOW_TYPE, LABEL, CLOTH_HOSEI_ACTIVE, DRAW_TYPE, SELECT_CATEGORY)

;衣装の描画
CALL SHOW_CLOTH_LIST(SHOW_TYPE, LIST_CLOTH, LIST_CLOTH_LAST_NO, DRAW_TYPE, SELECT_CATEGORY, PAGE, SHOW_COUNT_LIST, LABEL, HOSEI_NAME, CLOTH_HOSEI_ACTIVE)

;衣装表示後の描画
CALL SHOW_CLOTH_FOOTER(DRAW_TYPE, CATEGORY, SELECT_CATEGORY, カタログ, 簡易情報, LABEL, CLOTH_HOSEI_ACTIVE, FILTER, FILTER_INPUT_OFFSET, MAXPAGE)

;表示を再開して入力を待つ
REDRAW 2
INPUT
REDRAW 0

INPUT_NO = RESULT

GOTO INPUT_LOOP

;----------------------------------------------------------
;表示するページ数を返す
;----------------------------------------------------------
@GET_CLOTH_PAGE(CLOTH_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM CLOTH_NO
#DIMS DRAW_TYPE
#DIM LIST_CLOTH_LAST_NO
#DIM REF SHOW_COUNT_LIST

SIF CLOTH_NO < 1
	RETURNF 0
SIF LIST_CLOTH_LAST_NO < 1
	RETURNF 0
SELECTCASE DRAW_TYPE
	CASE "リスト"
		RETURNF CLOTH_NO / SHOW_COUNT_LIST
	CASE "単体"
		RETURNF CLOTH_NO / SHOW_COUNT_LIST:1
	CASE "カタログ"
		RETURNF CLOTH_NO / SHOW_COUNT_LIST:2
	CASE "簡易情報"
		RETURNF CLOTH_NO / SHOW_COUNT_LIST:3
ENDSELECT

;----------------------------------------------------------
;最大ページ数を返す
;----------------------------------------------------------
@GET_CLOTH_MAXPAGE(DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DRAW_TYPE
#DIM LIST_CLOTH_LAST_NO
#DIM REF SHOW_COUNT_LIST

SIF !LIST_CLOTH_LAST_NO
	RETURNF 0
SELECTCASE DRAW_TYPE
	CASE "リスト"
		RETURNF (LIST_CLOTH_LAST_NO - 1) / SHOW_COUNT_LIST
	CASE "単体"
		RETURNF (LIST_CLOTH_LAST_NO - 1) / SHOW_COUNT_LIST:1
	CASE "カタログ"
		RETURNF (LIST_CLOTH_LAST_NO - 1) / SHOW_COUNT_LIST:2
	CASE "簡易情報"
		RETURNF (LIST_CLOTH_LAST_NO - 1) / SHOW_COUNT_LIST:3
ENDSELECT

;----------------------------------------------------------
;ヘッダの描画
;----------------------------------------------------------
@SHOW_CLOTH_HEADER(SHOW_TYPE, LABEL, CLOTH_HOSEI_ACTIVE, DRAW_TYPE, SELECT_CATEGORY)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS SHOW_TYPE
#DIMS REF LABEL
#DIM REF CLOTH_HOSEI_ACTIVE
#DIMS DRAW_TYPE
#DIM SELECT_CATEGORY

#DIM WIDTH

CALL LB
DRAWLINE
IF SHOW_TYPE == "購入画面"
	IF SELECT_CATEGORY == 0
		PRINTL アウターを購入します
	ELSEIF SELECT_CATEGORY == 1
		PRINTL カスタマイズパーツを購入します(注意：変身コスチューム専用)
	ELSEIF SELECT_CATEGORY == 2
		PRINTL インナーを購入します
	ELSEIF SELECT_CATEGORY == 3
		PRINTL その他装備を購入します
	ENDIF
	PRINTFORML 資金：{MONEY}＄
	DRAWLINE
ELSEIF SHOW_TYPE == "表示画面"
	PRINTL 所持衣装一覧
	DRAWLINE
ENDIF
IF DRAW_TYPE == "簡易情報"
	WIDTH = WIDTH_CLOTH + WIDTH_MONEY
	SIF SHOW_TYPE == "購入画面"
		WIDTH += 11
	PRINTFORM %LOCALS, WIDTH%|
	FOR LOCAL, 0, VARSIZE("CLOTH_HOSEI_ACTIVE")
		PRINTFORM %LABEL:(CLOTH_HOSEI_ACTIVE:LOCAL)%|
	NEXT
	PRINTL
ENDIF

RETURN 1

;----------------------------------------------------------
;フッタの描画
;----------------------------------------------------------
@SHOW_CLOTH_FOOTER(DRAW_TYPE, CATEGORY, SELECT_CATEGORY,  カタログ, 簡易情報, LABEL, CLOTH_HOSEI_ACTIVE, FILTER, FILTER_INPUT_OFFSET, MAXPAGE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DRAW_TYPE
#DIMS REF CATEGORY
#DIM SELECT_CATEGORY
#DIM カタログ
#DIM 簡易情報
#DIMS REF LABEL
#DIM REF CLOTH_HOSEI_ACTIVE
#DIM FILTER
#DIM FILTER_INPUT_OFFSET
#DIM MAXPAGE

#DIM CONST WIDTH = STRLENS("カスタマイズパーツ") + 1
#DIM CATEGORY_NO

DRAWLINE
SIF DRAW_TYPE == "リスト" || DRAW_TYPE == "簡易情報"
	CALL SHOW_CLOTH_FILTERBUTTON(LABEL, CLOTH_HOSEI_ACTIVE, FILTER, FILTER_INPUT_OFFSET)
PRINTL
DRAWLINE

FOR CATEGORY_NO, 0, VARSIZE("CATEGORY")
	LOCALS '= CATEGORY:CATEGORY_NO
	LOCAL = CATEGORY:SELECT_CATEGORY != LOCALS ? 1 # 2
	LOCALS '= @"%LOCALS, WIDTH, LEFT%"
	CALL CLOTH_BUTTON(LOCALS, CATEGORY_NO, LOCAL, 0xFFFF00)
NEXT
PRINTL

LOCALS '= @"%"カタログ", WIDTH, LEFT%"
CALL CLOTH_TOGGLEBUTTON(LOCALS, 10, カタログ)

LOCALS '= @"%"簡易情報", WIDTH, LEFT%"
CALL CLOTH_TOGGLEBUTTON(LOCALS, 11, 簡易情報)
PRINTL

CALL CLOTH_BUTTON("前へ　　", 90, !MAXPAGE ? 0 # 1)
CALL CLOTH_BUTTON("次へ", 91, !MAXPAGE ? 0 # 1)
PRINTL

PRINTBUTTON "[99]戻る", 99
PRINTL

RETURN 1

;----------------------------------------------------------
;フィルタボタンの描画
;----------------------------------------------------------
@SHOW_CLOTH_FILTERBUTTON(LABEL, CLOTH_HOSEI_ACTIVE, FILTER, FILTER_INPUT_OFFSET)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS REF LABEL
#DIM REF CLOTH_HOSEI_ACTIVE
#DIM FILTER
#DIM FILTER_INPUT_OFFSET

FOR LOCAL, 0, VARSIZE("CLOTH_HOSEI_ACTIVE")
	GETBIT FILTER, LOCAL
	CALL CLOTH_TOGGLEBUTTON(LABEL:(CLOTH_HOSEI_ACTIVE:LOCAL), FILTER_INPUT_OFFSET + LOCAL, RESULT)
	PRINT 　
NEXT

;----------------------------------------------------------
;衣装リストの描画
;----------------------------------------------------------
@SHOW_CLOTH_LIST(SHOW_TYPE, LIST_CLOTH, LIST_CLOTH_LAST_NO, DRAW_TYPE, SELECT_CATEGORY, PAGE, SHOW_COUNT_LIST, LABEL, HOSEI_NAME, CLOTH_HOSEI_ACTIVE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS SHOW_TYPE
#DIM REF LIST_CLOTH
#DIM LIST_CLOTH_LAST_NO
#DIMS DRAW_TYPE
#DIM SELECT_CATEGORY
#DIM PAGE
#DIM REF SHOW_COUNT_LIST
#DIMS REF LABEL
#DIMS REF HOSEI_NAME
#DIM REF CLOTH_HOSEI_ACTIVE

#DIM NOTHAVE_COUNT
#DIM CLOTH_NO_FROM
#DIM CLOTH_NO_TO
#DIM SHOW_COUNT
#DIM BIT_POS
#DIM 閾値
#DIM KEEP_CFLAG
#DIMS CALL_HOSEI_NAME
#DIM HOSEI_VAL

IF !LIST_CLOTH_LAST_NO
	IF SHOW_TYPE == "購入画面"
		PRINTL     - SOLD OUT -
	ELSE
		PRINTL 未所持
	ENDIF
	RETURN 1
ENDIF

IF DRAW_TYPE == "リスト"
	SHOW_COUNT = SHOW_COUNT_LIST:0
ELSEIF DRAW_TYPE == "単体"
	SHOW_COUNT = SHOW_COUNT_LIST:1
ELSEIF DRAW_TYPE == "カタログ"
	SHOW_COUNT = SHOW_COUNT_LIST:2
ELSEIF DRAW_TYPE == "簡易情報"
	SHOW_COUNT = SHOW_COUNT_LIST:3
ENDIF

IF SELECT_CATEGORY == 0 || SELECT_CATEGORY == 2
	CALL_HOSEI_NAME '= "CLOTH_HOSEI"
ELSEIF SELECT_CATEGORY == 1
	CALL_HOSEI_NAME '= "CLOTH_CUSTOM_HOSEI"
ENDIF

CLOTH_NO_FROM = PAGE * SHOW_COUNT
CLOTH_NO_TO = MIN(PAGE * SHOW_COUNT + SHOW_COUNT, LIST_CLOTH_LAST_NO)
HOSEI_VAL = 0

IF DRAW_TYPE == "リスト"
	NOTHAVE_COUNT = 0
	FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
		SIF (NOTHAVE_COUNT % 2) == 1
			PRINT 　
		PRINTFORM [%TOSTR(LIST_CLOTH:LOCAL)%] %ITEMNAME:(LIST_CLOTH:LOCAL), WIDTH_CLOTH, LEFT%
		IF SHOW_TYPE == "購入画面"
			SIF MONEY < ITEMPRICE:(LIST_CLOTH:LOCAL)
				SETCOLOR 0x808080
			PRINTFORM ({ITEMPRICE:(LIST_CLOTH:LOCAL), WIDTH_MONEY, RIGHT}＄)
			RESETCOLOR
		ENDIF
		NOTHAVE_COUNT ++
		SIF (NOTHAVE_COUNT % 2) == 0
			PRINTL
	NEXT
	SIF (NOTHAVE_COUNT % 2) == 1
		PRINTL
ELSEIF DRAW_TYPE == "簡易情報"
	KEEP_CFLAG = CFLAG:1
	FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
		PRINTFORM [%TOSTR(LIST_CLOTH:LOCAL)%] %ITEMNAME:(LIST_CLOTH:LOCAL), WIDTH_CLOTH, LEFT%
		IF SHOW_TYPE == "購入画面"
			LOCALS '= TOSTR(ITEMPRICE:(LIST_CLOTH:LOCAL))
			PRINTFORM (%LOCALS, WIDTH_MONEY, RIGHT%＄) 
		ENDIF
		PRINT |
		FOR BIT_POS, 0, VARSIZE("CLOTH_HOSEI_ACTIVE")
			IF SELECT_CATEGORY == 0
				SELECTCASE CLOTH_HOSEI_ACTIVE:BIT_POS
					CASE 0, 1, 2, 3, 7
						閾値 = 100
					CASE 8, 9, 10, 14, 15, 16, 17, 18
						閾値 = 0
					CASE 4, 5
						閾値 = -95
					CASE 6, 11, 12, 13
						閾値 = -100
				ENDSELECT
			ELSEIF SELECT_CATEGORY == 1
				SELECTCASE CLOTH_HOSEI_ACTIVE:BIT_POS
					CASE 4, 5, 11, 12
						閾値 = -100
					CASEELSE
						閾値 = 0
				ENDSELECT
			ELSEIF SELECT_CATEGORY == 2
				SELECTCASE CLOTH_HOSEI_ACTIVE:BIT_POS
					CASE 0, 1, 2, 3, 7
						閾値 = 100
					CASE 8, 9, 10, 14, 15, 16, 17, 18
						閾値 = 0
					CASE 6
						閾値 = -95
					CASE 4, 5, 11, 12, 13
						閾値 = -100
				ENDSELECT
			ELSE
				閾値 = 0
			ENDIF
			CFLAG:1 = 0
			TRYCCALLFORM %CALL_HOSEI_NAME%_%HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS)%_{LIST_CLOTH:LOCAL}
				HOSEI_VAL = RESULT
			CATCH
				PRINT 　|
				CONTINUE
			ENDCATCH
			CFLAG:1 = 1
			TRYCCALLFORM %CALL_HOSEI_NAME%_%HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS)%_{LIST_CLOTH:LOCAL}
				IF 閾値 < 0
					HOSEI_VAL = MIN(RESULT, RESULT, HOSEI_VAL)
				ELSE
					HOSEI_VAL = MAX(RESULT, RESULT, HOSEI_VAL)
				ENDIF
			CATCH
			ENDCATCH
			CALL SHOW_CLOTH_HOSEI_MARK(HOSEI_VAL, 閾値)
			PRINT |
		NEXT
		PRINTL
	NEXT
	CFLAG:1 = KEEP_CFLAG
ELSE
	FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
		PRINTFORM %ITEMNAME:(LIST_CLOTH:LOCAL), WIDTH_CLOTH, LEFT%
		IF SHOW_TYPE == "購入画面"
			LOCALS '= TOSTR(ITEMPRICE:(LIST_CLOTH:LOCAL)) + "＄"
			PRINTFORM %LOCALS, WIDTH_MONEY + 2, RIGHT%
			PRINT 　
			CALL CLOTH_BUTTON("買う", LIST_CLOTH:LOCAL, ITEMPRICE:(LIST_CLOTH:LOCAL) <= MONEY ? 1 # 0)
		ENDIF
		PRINTL
		TRYCALLFORM CLOTH_DESCRIPTION_{LIST_CLOTH:LOCAL}
		CALL SHORTLINE
	NEXT
ENDIF

RETURN 1

;----------------------------------------------------------
;未所持の売り物っぽい衣装を配列に入れる
;----------------------------------------------------------
@LIST_CLOTH_NOTHAVE(LIST_CLOTH, LIST_CLOTH_LAST_NO, CATEGORY)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LIST_CLOTH
#DIM REF LIST_CLOTH_LAST_NO
#DIMS CATEGORY

#DIM CLOTH_NO_FROM
#DIM CLOTH_NO_TO
#DIM NOTHAVE_COUNT

CALL GET_CLOTH_CATEGORY_FROM_TO(CATEGORY, CLOTH_NO_FROM, CLOTH_NO_TO)
SIF !RESULT
	RETURN 0

VARSET LIST_CLOTH
NOTHAVE_COUNT = 0

FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
	IF ITEMPRICE:LOCAL > 0 && ISCHECK_CLOTH(LOCAL)
		LIST_CLOTH:NOTHAVE_COUNT = LOCAL
		NOTHAVE_COUNT ++
	ENDIF
NEXT

LIST_CLOTH_LAST_NO = FINDELEMENT(LIST_CLOTH, 0)

RETURN 1

;----------------------------------------------------------
;所持している衣装を配列に入れる
;----------------------------------------------------------
@LIST_CLOTH_HAVE(LIST_CLOTH, LIST_CLOTH_LAST_NO, CATEGORY)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LIST_CLOTH
#DIM REF LIST_CLOTH_LAST_NO
#DIMS CATEGORY

#DIM CLOTH_NO_FROM
#DIM CLOTH_NO_TO
#DIM HAVE_COUNT

CALL GET_CLOTH_CATEGORY_FROM_TO(CATEGORY, CLOTH_NO_FROM, CLOTH_NO_TO)
SIF !RESULT
	RETURN 0

VARSET LIST_CLOTH
HAVE_COUNT = 0

FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
	IF ITEM:LOCAL
		LIST_CLOTH:HAVE_COUNT = LOCAL
		HAVE_COUNT ++
	ENDIF
NEXT

LIST_CLOTH_LAST_NO = FINDELEMENT(LIST_CLOTH, 0)

RETURN 1

;----------------------------------------------------------
;カテゴリー別で衣装の範囲を返す
;----------------------------------------------------------
@GET_CLOTH_CATEGORY_FROM_TO(CATEGORY, CLOTH_NO_FROM, CLOTH_NO_TO)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS CATEGORY
#DIM REF CLOTH_NO_FROM
#DIM REF CLOTH_NO_TO
#DIMS SHOW_TYPE

SELECTCASE CATEGORY
	CASE "アウター"
		CLOTH_NO_FROM = 100
		CLOTH_NO_TO = 200
	CASE "カスタマイズパーツ"
		CLOTH_NO_FROM = 600
		CLOTH_NO_TO = 700
	CASE "インナー"
		CLOTH_NO_FROM = 300
		CLOTH_NO_TO = 400
	CASE "その他"
		CLOTH_NO_FROM = 500
		CLOTH_NO_TO = 600
	CASEELSE
		RETURN 0
ENDSELECT

RETURN 1

;----------------------------------------------------------
;フィルタに対応した能力値の補正を行う関数を探してフィルタリング
;----------------------------------------------------------
@FILTER_CLOTH_HOSEI(LIST_CLOTH, LIST_CLOTH_LAST_NO, HOSEI_NAME, CLOTH_HOSEI_ACTIVE, SELECT_CATEGORY, FILTER)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LIST_CLOTH
#DIM REF LIST_CLOTH_LAST_NO
#DIMS REF HOSEI_NAME
#DIM REF CLOTH_HOSEI_ACTIVE
#DIM SELECT_CATEGORY
#DIM FILTER

#DIM BIT_POS
#DIM NEW_LIST_CLOTH, 100
#DIM NEW_LIST_CLOTH_NO
#DIM CLOTH_NO
#DIM FILTER_FLAG
#DIMS CALL_HOSEI_NAME

SIF !LIST_CLOTH_LAST_NO
	RETURN 0

VARSET NEW_LIST_CLOTH
NEW_LIST_CLOTH_NO = 0
FILTER_FLAG = 1

IF SELECT_CATEGORY == 0 || SELECT_CATEGORY == 2
	CALL_HOSEI_NAME '= "CLOTH_HOSEI"
ELSEIF SELECT_CATEGORY == 1
	CALL_HOSEI_NAME '= "CLOTH_CUSTOM_HOSEI"
ENDIF

;有効になっている全てのフィルタの効果に補正をかける関数がある物を取得
FOR CLOTH_NO, 0, LIST_CLOTH_LAST_NO
	FOR BIT_POS, 0, VARSIZE("CLOTH_HOSEI_ACTIVE")
		GETBIT FILTER, BIT_POS
		SIF !RESULT
			CONTINUE
		TRYCCALLFORM %CALL_HOSEI_NAME%_%HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS)%_{LIST_CLOTH:CLOTH_NO}
		CATCH
			FILTER_FLAG = 0
			BREAK
		ENDCATCH
		;直撃補正フィルタで表示するのはポリススカートのみ
		IF HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS) == "HIT" && LIST_CLOTH:CLOTH_NO != 112
			FILTER_FLAG = 0
			BREAK
		ENDIF
		IF SELECT_CATEGORY == 0
			IF HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS) == "TAIRYOKU" || HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS) == "KIRYOKU"
				IF RESULT >= 95
					FILTER_FLAG = 0
					BREAK
				ENDIF
			ENDIF
		ELSEIF SELECT_CATEGORY == 2
			IF HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS) == "SEITAISEI"
				IF RESULT >= 95
					FILTER_FLAG = 0
					BREAK
				ENDIF
			ENDIF
		ENDIF
	NEXT
	IF FILTER_FLAG
		NEW_LIST_CLOTH:NEW_LIST_CLOTH_NO = LIST_CLOTH:CLOTH_NO
		NEW_LIST_CLOTH_NO ++
	ELSE
		FILTER_FLAG = 1
	ENDIF
NEXT

VARSET LIST_CLOTH

FOR LOCAL, 0, NEW_LIST_CLOTH_NO
	LIST_CLOTH:LOCAL = NEW_LIST_CLOTH:LOCAL
NEXT

LIST_CLOTH_LAST_NO = FINDELEMENT(LIST_CLOTH, 0)

;----------------------------------------------------------
@ISCHECK_CLOTH, ARG
#FUNCTION
SIF ARG > 999
	RETURNF 0
SIF ITEMNAME:ARG != "" && ITEM:ARG == 0
	RETURNF 1
RETURNF 0

;----------------------------------------------------------
;購入処理
;----------------------------------------------------------
@BUY_CLOTH(LIST_CLOTH, INPUT_NO)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LIST_CLOTH
#DIM INPUT_NO

SIF FINDELEMENT(LIST_CLOTH, INPUT_NO) == -1
	RETURN -1
SIF MONEY < ITEMPRICE:INPUT_NO
	RETURN 0
MONEY -= ITEMPRICE:INPUT_NO
ITEM:INPUT_NO = 1

RETURN 1

;----------------------------------------------------------
;簡易情報の補正マークを描画
;----------------------------------------------------------
@SHOW_CLOTH_HOSEI_MARK(VAL, 閾値)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM VAL
#DIM 閾値

IF 閾値 < 0
	閾値 *= -1
	IF 閾値 < VAL
		SETCOLOR 255, 96, 96
		PRINT ▼
		RESETCOLOR
	ELSEIF VAL < 閾値
		SETCOLOR 96, 96, 255
		PRINT ▲
		RESETCOLOR
	ELSE
		PRINT 　
	ENDIF
ELSE
	IF VAL < 閾値
		SETCOLOR 255, 96, 96
		PRINT ▼
		RESETCOLOR
	ELSEIF 閾値 < VAL
		SETCOLOR 96, 96, 255
		PRINT ▲
		RESETCOLOR
	ELSE
		PRINT 　
	ENDIF
ENDIF

RETURN

;----------------------------------------------------------
;有効無効のボタン
;----------------------------------------------------------
@CLOTH_BUTTON(LABEL, INPUT_VAL, BUTTON = 1, COLOR = 0x808080, 括弧 = 1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS LABEL
#DIM INPUT_VAL
#DIM BUTTON
#DIM COLOR
#DIM 括弧

IF 括弧
	LOCALS '= @"%TOSTR(INPUT_VAL), 2%"
	LOCALS '= "[" + LOCALS + "]" + LABEL
ELSE
	LOCALS '= LABEL
ENDIF

IF BUTTON == 1
	PRINTBUTTON @"%LOCALS%", INPUT_VAL
ELSEIF BUTTON == 2
	GETCOLOR
	SETCOLOR COLOR
	PRINTBUTTON @"%LOCALS%", INPUT_VAL
	SETCOLOR RESULT
ELSE
	GETCOLOR
	SETCOLOR COLOR
	PRINTPLAINFORM %LOCALS%
	SETCOLOR RESULT
ENDIF

RETURN 1

;----------------------------------------------------------
;ON/OFFのボタン
;----------------------------------------------------------
@CLOTH_TOGGLEBUTTON(LABEL, INPUT_VAL, SELECT = 0, COLOR = 0x00FF00, 括弧 = 1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS LABEL
#DIM INPUT_VAL
#DIM SELECT
#DIM COLOR
#DIM 括弧

IF 括弧
	LOCALS '= @"%TOSTR(INPUT_VAL), 2%"
	LOCALS '= "[" + LOCALS + "]" + LABEL
ELSE
	LOCALS '= LABEL
ENDIF
IF SELECT
	GETCOLOR
	SETCOLOR COLOR
	PRINTBUTTON @"%LOCALS%", INPUT_VAL
	SETCOLOR RESULT
ELSE
	PRINTBUTTON @"%LOCALS%", INPUT_VAL
ENDIF
