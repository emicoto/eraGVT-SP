@EVENTTRAIN
#DIM CHISEI_CAL
#DIM CHISEI_SUM
#DIM CHISEI_ENEMY
#DIM INITIATIVE
#DIM LCOUNT
#DIM CCOUNT
REDRAW 1
;戦闘開始時に戦闘距離を遠距離に設定する
TCVAR:0 = 3
FOR LCOUNT, 0, TCRLENGTH
	TCREPORT:LCOUNT = 0
NEXT
;避妊効果中に受けた射精量をリセット
TFLAG:6 = 0
;行動予約フラグを折る
TFLAG:16 = -1
TFLAG:17 = -1
;追加責め関係のフラグを折る
VARSET EX_COM
VARSET SH_COM
VARSET INSERT
;拡張度関係のフラグを折る
VARSET TENTACLE_SIZE
VARSET TENTACLE_NUM

;最初から堕落
SIF (MESSAGE_BRANCH_F(TARGET) & 堕落)
	TFLAG:97 |= 堕落
;最初から快楽蕩け
SIF (MESSAGE_BRANCH_F(TARGET) & 快楽蕩け)
	TFLAG:97 |= 快楽蕩け
;最初から絶望
SIF (MESSAGE_BRANCH_F(TARGET) & 絶望)
	TFLAG:97 |= 絶望
;最初から性抵抗
SIF (MESSAGE_BRANCH_F(TARGET) & 性抵抗)
	TFLAG:97 |= 性抵抗
;最初から苦戦
SIF (MESSAGE_BRANCH_F(TARGET) & 苦戦)
	TFLAG:97 |= 苦戦

;PALAM最大値をリセット
VARSET MAX_PALAM

;射精ゲージ、噴乳ゲージのリセット
BASE:射精 = 0
BASE:噴乳 = 0

;空中ダッシュ回数をリセット
;衣装による空中性能強化
CALL CLOTH_BATTLE_HOSEI, "AIRPLUS",TARGET
SIF RESULT > 0
	MAXBASE:空中ダッシュ += 1
;立体機動装置の効果###
SIF CFLAG:43 == 510
	MAXBASE:空中ダッシュ += 1
;FEATによる空中ダッシュ増加
SIF TALENT:有翼 > 0
	MAXBASE:空中ダッシュ += 1
BASE:空中ダッシュ = MAXBASE:空中ダッシュ
SIF BASE:空中ダッシュ >= 8
	BASE:空中ダッシュ = 8

;FEAT効果によるEXゲージ増加
IF TALENT:魔力貯蔵 > 0
	SETCOLOR 255,128,0
	PRINTFORMW [魔力貯蔵]の効果で%CALLNAME%のEXゲージが2つチャージされた！
	RESETCOLOR
	TCVAR:4 += 200
ENDIF

;衣装の耐久度を設定
CALL CLOTH_BATTLE_SETHP
;衣装の情報を更新
CALL REFRESH_CLOTH_DATA

;ボス触手の行動選択
IF FLAG:110 == 1
	CALL SELECT_ENEMY_ACTION
ELSE
	CALL SELECT_TENTACLE_ACTION
ENDIF

;戦闘中フラグを立てる
FLAG:700 = 1

;解析度を読み込む
IF FLAG:10 == 0
	FLAG:20 = FLAG:(500 + FLAG:11)
ELSEIF FLAG:10 == 1
	FLAG:20 = FLAG:(600 + FLAG:11)
ELSEIF FLAG:10 == 2
	FLAG:20 = 100
ENDIF

;実績
CHISEI_SUM = 0
CHISEI_CAL = 0
;戦闘支援による補正
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER || CCOUNT > 6
		CONTINUE
	CALL CHECK_ACTION("支援",CCOUNT)
	SIF RESULT == 0
		CONTINUE
	IF CFLAG:CCOUNT:100 == 106
		CHISEI_CAL = MAXBASE:CCOUNT:知性 / 4
		;衣装による補正
		CALL CLOTH_BATTLE_HOSEI, "CHISEI", CCOUNT
		CHISEI_CAL = CHISEI_CAL * RESULT / 100
		;インカム
		SIF CFLAG:CCOUNT:43 == 511
			CHISEI_CAL = CHISEI_CAL * 110 / 100 + 5
		;支援者が献身的
		SIF TALENT:CCOUNT:献身的 > 0
			CHISEI_CAL = CHISEI_CAL * 110 / 100 + 5
		;対象者が自分勝手
		SIF TALENT:TARGET:自分勝手 > 0
			CHISEI_CAL = CHISEI_CAL * 25 / 100
		CHISEI_SUM += CHISEI_CAL
	ENDIF
NEXT
IF FLAG:110 == 1
	CHISEI_ENEMY = MAXBASE:(FLAG:111):知性
	SIF CHISEI_ENEMY < 100
		CHISEI_ENEMY = 100
ELSE
	CALL TENTACLE_ACCESS, "CHISEI"
	CHISEI_ENEMY = RESULT
ENDIF
IF PERCENT_CAL_F(CHISEI_SUM, CHISEI_ENEMY) >= 200 && GLOBAL:168 == 0
	PRINTL 【実績：タクティカルオーダー】を達成しました！
	PRINTW 
	GLOBAL:168 = 1
	SAVEGLOBAL
ENDIF

;先制攻撃の判定
INITIATIVE = MIN(SQRT(EXP:戦闘経験), 20) + MIN(SQRT(PERCENT_CAL_F(CHISEI_SUM, CHISEI_ENEMY)), 25) + 5
;生き残っているボスの数を見る
CALL TENTACLE_SURVIVE, "NUM"
;１体目のボス撃破前は先制率アップ
SIF FLAG:3 == RESULT
	INITIATIVE += 15
;２体目のボス撃破前は先制率アップ
SIF FLAG:3 - 1 == RESULT
	INITIATIVE += 15
;FEAT効果による先制率アップ
SIF TALENT:精霊交信 > 0
	INITIATIVE += 15
SIF TALENT:狩人の勘 > 0
	INITIATIVE += 15
;雑魚敵が相手の場合は先制率２倍
SIF FLAG:10 == 2
	INITIATIVE *= 2
;判定に成功したら確率を半分にして再度判定を行う
$INITIATIVE_LOOP
IF FLAG:999 > 0
	SETCOLOR 96,96,96
	PRINTL 
	PRINTFORML 　先制率　{INITIATIVE}％
	PRINTL 
	RESETCOLOR
ENDIF
IF RAND:100 < INITIATIVE
	TFLAG:24 += 1
	INITIATIVE = INITIATIVE * 3 / 4
	SIF INITIATIVE > 0
		GOTO INITIATIVE_LOOP
ENDIF
IF TFLAG:24 > 0
	IF TALENT:精霊交信 > 0
		SETCOLOR 255,128,0
		PRINTFORMW [精霊交信]の効果で先制率が上昇！
		RESETCOLOR
	ENDIF
	IF TALENT:狩人の勘 > 0
		SETCOLOR 255,128,0
		PRINTFORMW [狩人の勘]の効果で先制率が上昇！
		RESETCOLOR
	ENDIF

	PRINTL 
	FONTBOLD
	PRINT 　先制攻撃
	SIF TFLAG:24 > 1
		PRINTFORM (+{TFLAG:24 - 1})
	PRINT ！ 
	FONTREGULAR
	IF TFLAG:24 == 1
		PRINTW 敵はこのターン動けない！
	ELSE
		PRINTFORMW 敵は%TOSTR(TFLAG:24)%ターンの間動けない！
	ENDIF
	PRINTL 
;応援判定　先制攻撃中は発生しない
ELSEIF CONFIG_CHECK_EVENT_F(3) > 0
	CALL PERFORM_CHEERS_FIRST_HANTEI
ENDIF




@SHOW_USERCOM
RESETCOLOR
PRINTL 
IF CONFIG_CHECK_SCREEN_F(2) > 0
	PRINTFORML 　┌──────\@TCVAR:8 == 0 ? ─ # ┬ \@─────────────────────────────────────┐
	PRINTPLAIN 　│
	PRINT  [810] 特殊 
	;特殊コマンド選択中
	IF TCVAR:8 == 0 && TCVAR:0 == 0
		TCVAR:8 += 10
		PRINTPLAIN 　
		CALL PRINT_COMNAME, 44
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 45
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 46
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┐
		CALL PRINT_COMNAME, 72
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 73
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 47
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [820] 性技 
		ELSE
			PRINT  [820] 攻撃 
		ENDIF
		PRINTPLAIN │
		CALL PRINT_COMNAME, 69
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 99
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┤
		CALL PRINT_COMNAME, 70
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 71
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 72
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [830] 拘束 
		ELSE
			PRINT  [830] 補助 
		ENDIF
		PRINTPLAIN │
		TCVAR:8 -= 10
	ELSEIF TCVAR:8 == 0
		TCVAR:8 += 10
		PRINTPLAIN 　
		IF CFLAG:1
			CALL PRINT_COMNAME, 73
		ELSE
			CALL PRINT_COMNAME, 0
		ENDIF
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 69
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 99
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┐
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [820] 性技 
		ELSE
			PRINT  [820] 攻撃 
		ENDIF
		PRINTPLAIN │
		CALL PRINT_COMNAME, 16
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 17
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┤
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 71
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 72
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [830] 拘束 
		ELSE
			PRINT  [830] 補助 
		ENDIF
		PRINTPLAIN │
		TCVAR:8 -= 10
	;性技コマンド選択中
	ELSEIF  TCVAR:8 == 1 && TCVAR:0 == 0
		TCVAR:8 += 10
		PRINTPLAIN │
		CALL PRINT_COMNAME, 100
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 101
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 102
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┘
		CALL PRINT_COMNAME, 103
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 104
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [820] 性技 
		ELSE
			PRINT  [820] 攻撃 
		ENDIF
		PRINTPLAIN 　
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┐
		CALL PRINT_COMNAME, 70
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 71
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 72
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [830] 拘束 
		ELSE
			PRINT  [830] 補助 
		ENDIF
		PRINTPLAIN │
		TCVAR:8 -= 10
	;攻撃コマンド選択中
	ELSEIF  TCVAR:8 == 1
		TCVAR:8 += 10
		PRINTPLAIN │
		CALL PRINT_COMNAME, 1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 2
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 3
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┘
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [820] 性技 
		ELSE
			PRINT  [820] 攻撃 
		ENDIF
		PRINTPLAIN 　
		CALL PRINT_COMNAME, 16
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 17
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┐
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 71
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 72
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [830] 拘束 
		ELSE
			PRINT  [830] 補助 
		ENDIF
		PRINTPLAIN │
		TCVAR:8 -= 10
	;拘束コマンド選択中
	ELSEIF  TCVAR:8 == 2 && TCVAR:0 == 0
		TCVAR:8 += 10
		PRINTPLAIN │
		IF (TCVAR:12 & 強拘束)
			CALL PRINT_COMNAME, 40
		ELSE
			CALL PRINT_COMNAME, 8
		ENDIF
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 9
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 10
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┤
		CALL PRINT_COMNAME, 11
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 12
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 13
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [820] 性技 
		ELSE
			PRINT  [820] 攻撃 
		ENDIF
		PRINTPLAIN │
		CALL PRINT_COMNAME, 14
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 15
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┘
		CALL PRINT_COMNAME, 70
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 71
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 72
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [830] 拘束 
		ELSE
			PRINT  [830] 補助 
		ENDIF
		PRINTPLAIN 　
		TCVAR:8 -= 10
	;補助コマンド選択中
	ELSEIF  TCVAR:8 == 2
		TCVAR:8 += 10
		PRINTPLAIN │
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 4
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 5
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┤
		CALL PRINT_COMNAME, 6
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 7
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [820] 性技 
		ELSE
			PRINT  [820] 攻撃 
		ENDIF
		PRINTPLAIN │
		CALL PRINT_COMNAME, 16
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 17
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　├──────┘
		CALL PRINT_COMNAME, -1
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 71
		PRINTPLAIN 　 
		CALL PRINT_COMNAME, 72
		PRINTPLAIN 　│
		PRINTL 

		PRINTPLAIN 　│
		IF TCVAR:0 == 0
			PRINT  [830] 拘束 
		ELSE
			PRINT  [830] 補助 
		ENDIF
		PRINTPLAIN 　
		TCVAR:8 -= 10
	ENDIF
	PRINTPLAIN 　　　　　　　　　　　　　　　　
	PRINT ステータス表示[800]
	PRINTPLAIN  　　　　　 
	LOCALS = 
	SIF CHECK_CAN_RETREAT_F() == 0
		SETCOLOR 128,128,128
	SIF (TCVAR:0 > 0 && FLAG:45 == 0 && (TCVAR:12 & 気絶) == 0) || FLAG:999 == 1
		LOCALS = 撤退[999]

	PRINTFORM %LOCALS, 9%
	RESETCOLOR
	PRINTPLAIN 　│
	PRINTL 
	PRINTFORML 　└──────\@TCVAR:8 == 2 ? ─ # ┴ \@─────────────────────────────────────┘
ENDIF

SIF CONFIG_CHECK_SCREEN_F(2) == 0
	PRINTC ステータス表示[800]

SIF CHECK_CAN_RETREAT_F() == 0
	SETCOLOR 128,128,128
SIF (TCVAR:0 > 0 && FLAG:45 == 0 && CONFIG_CHECK_SCREEN_F(2) == 0 && (TCVAR:12 & 気絶) == 0) || (CONFIG_CHECK_SCREEN_F(2) == 0 && FLAG:999 == 1)
	PRINTC 撤退[999]
RESETCOLOR

@USERCOM
IF RESULT == 999 && ((TCVAR:0 > 0 && FLAG:45 == 0 && (TCVAR:12 & 気絶) == 0) || FLAG:999 == 1)
	IF CHECK_CAN_RETREAT_F()
		PRINTL 
		FONTBOLD
		SETCOLOR 0, 255, 150
		PRINTL ********** 自分の行動 **********
		RESETCOLOR
		FONTREGULAR
		PRINTL 

		;撤退
		IF FLAG:110 == 0
			CALL ACT_HANTEI_TETTAI_TENTACLE
			IF RESULT == 1
				;地の文：撤退成功
				CALL MESSAGE_BATTLE_CHARA_TETTAI_SUCCESS
				;地の文：洗脳/悪堕ちキャラが操作キャラに撤退されてしまった
				SIF FLAG:110 == 1
					CALL MESSAGE_OTHER_BATTLE_CHARA_TETTAI_SUCCESS

				TRYCALL KYUSHUTU_SUCCESS_HANTEI
				;ラスボス強化
				;TRYCALLFORM LASTBOSS_KYOUKA_{FLAG:11}_BATTLEEND,0
				BEGIN AFTERTRAIN
			ELSE
				;地の文：撤退失敗
				CALL MESSAGE_BATTLE_CHARA_TETTAI_FALSE
				;地の文：洗脳/悪堕ちキャラが操作キャラの撤退を防いだ
				SIF FLAG:110 == 1
					CALL MESSAGE_OTHER_BATTLE_CHARA_TETTAI_FALSE
				JUMP SOURCE_CHECK
			ENDIF
		ELSE
			CALL ACT_HANTEI_TETTAI_TENTACLE
			IF RESULT == 1
				;地の文：撤退成功
				CALL MESSAGE_BATTLE_CHARA_TETTAI_SUCCESS
				;地の文：洗脳/悪堕ちキャラが操作キャラに撤退されてしまった
				SIF FLAG:110 == 1
					CALL MESSAGE_OTHER_BATTLE_CHARA_TETTAI_SUCCESS
				
				BEGIN AFTERTRAIN
			ELSE
				;地の文：撤退失敗
				CALL MESSAGE_BATTLE_CHARA_TETTAI_FALSE
				;地の文：洗脳/悪堕ちキャラが操作キャラの撤退を防いだ
				SIF FLAG:110 == 1
					CALL MESSAGE_OTHER_BATTLE_CHARA_TETTAI_FALSE
				JUMP SOURCE_CHECK
			ENDIF
		ENDIF
	ELSE
	ENDIF
ELSEIF RESULT == 800
	DRAWLINE
	CALL SHOW_STATUS_CHARA_SELECT, TARGET
ELSEIF RESULT == 810
	TCVAR:8 = 0
ELSEIF RESULT == 820
	TCVAR:8 = 1
ELSEIF RESULT == 830
	TCVAR:8 = 2
ELSEIF RESULT == 400
	;DEBUG
	IF FLAG:999 == 0
		PRINTL 
		PRINTL 
		PRINTL DEBUG ON
		FLAG:999 = 1
		SETBGCOLOR 0,0,40
	ELSEIF FLAG:999 == 1
		PRINTL 
		PRINTL 
		PRINTL DEBUG OFF
		FLAG:999 = 0
		RESETBGCOLOR
	ENDIF
ELSEIF RESULT < 400
	LOCAL = RESULT
	TCVAR:8 += 10
	RESULT = 0
	TRYCALLFORM COM_ABLE{LOCAL}
	TCVAR:8 -= 10
	SIF RESULT
		DOTRAIN LOCAL
ENDIF

@EVENTCOM
;EX系は表示しない
SIF SELECTCOM == 16 || SELECTCOM == 17 || SELECTCOM == 71 || SELECTCOM == 72
	RETURN 0
;射精部位フラグを0に
TFLAG:4 = 0
;防御フラグを消す
TCVAR:2 = 体勢：通常
;受け渡し用PALAMリセット
VARSET COMMON_PALAM

PRINTL 
FONTBOLD
SETCOLOR 0, 255, 150
PRINTL ********** 自分の行動 **********
RESETCOLOR
FONTREGULAR
PRINTL 

@EVENTCOMEND
#DIM LCOUNT
#DIM CCOUNT
;計算用
#DIM CHISEI_CAL
;PALAM減衰率
#DIM AT_VAR
CALL BATTLE_REPORT
;心境の持続ターンをカウント
SIF TCVAR:1 > 0
	TCVAR:11 += 1

;着地中または拘束中なら空中フラグをリセット
IF GETBIT(TCVAR:216, 2) || TCVAR:0 == 0
	TCVAR:216 = 0
	BASE:空中ダッシュ = MAXBASE:空中ダッシュ
	SIF BASE:空中ダッシュ>= 8
		BASE:空中ダッシュ = 8
;滞空状態で続けて空中ダッシュを使用しないなら着地状態になる
ELSEIF GETBIT(TCVAR:216, 1) && GETBIT(TCVAR:216, 0) == 0
	SETBIT TCVAR:216,2
	SIF GETBIT(TCVAR:216, 1)
		TCVAR:216 -= 2
ENDIF

;空中ダッシュゲージが切れていたら空中攻撃フラグをリセット
SIF GETBIT (TCVAR:216, 0) && BASE:空中ダッシュ == 0
	TCVAR:216 -= 1

;バースト攻撃フラグは常にリセット
TCVAR:217 = 0

;拘束中でなければ丸飲みフラグを解除
SIF TCVAR:0 != 0
	TFLAG:23 = 0

;拘束中なら同一距離フラグを解除
SIF TCVAR:0 == 0
	TFLAG:30 = 0
;拘束中ならチェインリセット
SIF TCVAR:0 == 0
	TFLAG:31 = 0
;防御コマンドを使用した次のターンはカウンターアタック
IF SELECTCOM == 4
	TFLAG:32 = 1
ELSE
	TFLAG:32 = 0
ENDIF

;衣装の情報を更新
CALL REFRESH_CLOTH_DATA

;ゲージ増減関連
IF CFLAG:1 == 2
	TCVAR:6 += -17
ELSE
	TCVAR:6 += 12
	;状態異常の場合、ゲージ増加量が上昇
	SIF (TCVAR:12 & 気絶)
		TCVAR:6 += 23
	SIF (TCVAR:12 & 排卵)
		TCVAR:6 += 3
	SIF (TCVAR:12 & 発情)
		TCVAR:6 += 3
	SIF (TCVAR:12 & 麻痺)
		TCVAR:6 += 8
	SIF (TCVAR:12 & べとべと)
		TCVAR:6 += 5
	SIF (TCVAR:12 & 腰くだけ)
		TCVAR:6 += 8
	SIF (TCVAR:12 & 恍惚)
		TCVAR:6 += 5
	SIF (TCVAR:12 & 強拘束)
		TCVAR:6 += 2
	;増加量アップ系の補正
	IF TCVAR:6 > 0
		LOCAL = 0
		;変身可能キャラで変身前なら増加量2倍
		SIF TALENT:変身能力 > 0 && CFLAG:1 == 0
			LOCAL += 100
		;やる気スイッチ#####
		SIF CFLAG:43 == 503
			LOCAL += 30
		;FEAT効果によるゲージ増加量低下
		SIF TALENT:不屈 > 0
			LOCAL -= 15
		SIF TALENT:スタミナ > 0
			LOCAL -= 15
		;衣装によるゲージ増幅効果
		CALL CLOTH_BATTLE_HOSEI, "EXBOOST"
		LOCAL += RESULT
		SIF LOCAL < -100
			LOCAL = -100
		TCVAR:6 = TCVAR:6 * (100 + LOCAL) / 100
	ENDIF
ENDIF

IF CFLAG:1 == 2
	TCVAR:5 += TCVAR:6
ELSE
	TCVAR:4 = LIMIT(TCVAR:4 + TCVAR:6, 0, 600)
	TCVAR:4 = LIMIT(TCVAR:4 + TCVAR:7, -100, 500)
ENDIF

;SP変身解除
IF TCVAR:5 <= 0 && CFLAG:1 == 2
	IF TALENT:変身能力
		CALL TRANSFORM, 1
	ELSE
		CALL TRANSFORM, 0
	ENDIF
	PRINTL 
	FONTBOLD
	;地の文：ＳＰ変身解除
	PRINTFORML ＳＰ変身の効果時間が終了した！
	FONTREGULAR
	;行動の反動で疲労が蓄積する
	CFLAG:99 += TFLAG:99
	RESULTS = {TFLAG:99}
	TOFULL RESULTS
	PRINTFORML %PRINT_CALLNAME(TARGET)%の身体の底に疲労が蓄積した……（＋%RESULTS%）
	PRINTW 
	TFLAG:99 = 0
ENDIF

;ゲージ使用状況フラグをリセット
TCVAR:3 = 0
;双方の増減をリセット
TCVAR:6 = 0
TCVAR:7 = 0

;触手サイズを初期化
VARSET TENTACLE_SIZE

;絶頂による変身解除、性耐性が少ないほど発生率が上がる
LOCAL:2 = 0
REPEAT 感覚数
	LOCAL:2 += NOWEX:(COUNT)
REND
CALL PERCENT_CAL, BASE:気力, MAXBASE:気力
LOCAL:4 = RESULT
CALL PERCENT_CAL, BASE:性耐性, MAXBASE:性耐性
LOCAL:3 = 100 - (RESULT * 4 / 3)

;気力が尽きて変身が強制的に解けた場合の処理
IF CFLAG:1 == 1 && BASE:気力 <= 0 && CONFIG_CHECK_BALANCE_F(6) == 0 && (TCVAR:12 & 強拘束) == 0
	PRINTL 
	;地の文：気力が尽きて変身が強制的に解けた
	CALL MESSAGE_BATTLE_CHARA_TRANSRELEASE
	;地の文：洗脳/悪堕ちキャラが操作キャラを変身強制解除させた
	SIF FLAG:110 == 1
		CALL MESSAGE_OTHER_BATTLE_CHARA_TRANSRELEASE
	CALL TRANSFORM, 0
	
	;心境変化
	LOCAL = RAND:100
	IF LOCAL < 30
		CALL SHINKYOU_CHANGE, "IKARI_TEIKAN"
	ELSEIF LOCAL < 60
		CALL SHINKYOU_CHANGE, "REISEI_DOUYOU"
	ELSEIF LOCAL < 90
		CALL SHINKYOU_CHANGE, "KOUYOU_SYOUTIN"
	ENDIF
ELSEIF CFLAG:1 == 1 && LOCAL:2 > 0 && RAND:100 < LOCAL:3 && RESULT < 50 && CONFIG_CHECK_BALANCE_F(6) == 0 && (TCVAR:12 & 強拘束) == 0
	PRINTL 
	;地の文：絶頂で変身が強制的に解けた
	CALL MESSAGE_BATTLE_CHARA_TRANSRELEASE_ECS
	;地の文：洗脳/悪堕ちキャラが絶頂で変身強制解除させた
	SIF FLAG:110 == 1
		CALL MESSAGE_OTHER_BATTLE_CHARA_TRANSRELEASE_ECS
	CALL TRANSFORM, 0

	;心境変化
	LOCAL = RAND:100
	IF LOCAL < 30
		CALL SHINKYOU_CHANGE, "IKARI_TEIKAN"
	ELSEIF LOCAL < 60
		CALL SHINKYOU_CHANGE, "REISEI_DOUYOU"
	ELSEIF LOCAL < 90
		CALL SHINKYOU_CHANGE, "KOUYOU_SYOUTIN"
	ENDIF
ELSEIF CFLAG:1 == 1 && LOCAL:2 > 0 && RESULT < 50 && (TCVAR:12 & 気絶) == 0
	PRINTL 
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は気力を振り絞り何とか変身を維持した！
ENDIF

;ボスの解析度を知性の基礎値に応じて上昇させる（実数値ではない）
IF FLAG:110 == 0 && FLAG:20 < 100
	LOCAL = MAX(BASE:知性 - 100, 1)
	;戦闘支援による補正
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER || CCOUNT > 6
			CONTINUE
		CALL CHECK_ACTION("支援",CCOUNT)
		SIF RESULT == 0
			CONTINUE
		IF CFLAG:CCOUNT:100 == 106
			CHISEI_CAL = MAX((BASE:CCOUNT:知性 - 100),2) / 2
			;衣装による補正
			CALL CLOTH_BATTLE_HOSEI, "CHISEI", CCOUNT
			CHISEI_CAL = CHISEI_CAL * RESULT / 100
			;インカム
			SIF CFLAG:CCOUNT:43 == 511
				CHISEI_CAL = CHISEI_CAL * 110 / 100 + 5
			;支援者が献身的
			SIF TALENT:CCOUNT:献身的 > 0
				CHISEI_CAL = CHISEI_CAL * 110 / 100 + 5
			;対象者が自分勝手
			SIF TALENT:TARGET:自分勝手 > 0
				CHISEI_CAL = CHISEI_CAL * 25 / 100
			LOCAL += CHISEI_CAL
		ENDIF
	NEXT
	;心境による増減
	IF TCVAR:1 == 2
		LOCAL /= 2
	ELSEIF TCVAR:1 == 4
		LOCAL += 5
	ELSEIF TCVAR:1 == 5
		LOCAL /= 3
	ENDIF
	LOCAL = RAND:5 + LOCAL/4 + LOCAL:1
	CALL TENTACLE_LEVEL
	SIF RESULT < 10
		LOCAL += 10 - RESULT / 3
	SIF TCVAR:2 != 体勢：通常
		LOCAL += 2 + RAND:5
	;スカウター#####
	SIF CFLAG:TARGET:43 == 500
		LOCAL += 5 + RAND:6
	PRINTL 
	IF LOCAL > 0
		DRAWLINE
		PRINTFORML 敵の解析度が{LOCAL}％上がった！
	ELSE
		DRAWLINE
		PRINTFORML 敵の解析度は上がらなかった…
	ENDIF
	SIF FLAG:20 < 25 && FLAG:20 + LOCAL >= 25
		PRINTL 敵の体力最大値が判明した！
	SIF FLAG:20 < 50 && FLAG:20 + LOCAL >= 50
		PRINTL 敵の体力現在値が判明した！
	SIF FLAG:20 < 75 && FLAG:20 + LOCAL >= 75
		PRINTL 敵の能力基礎値が判明した！
	IF FLAG:20 < 100 && FLAG:20 + LOCAL >= 100
		PRINTL 敵の行動補正値が判明した！
		PRINTL 敵の油断度が判明した！
		PRINTL 敵の解析が完了した！
	ENDIF
	FLAG:20 += LOCAL
	SIF FLAG:20 > 100
		FLAG:20 = 100
	PRINTL 
ENDIF
;EXにNOWEXを加算
REPEAT 感覚数
	EX:(COUNT) += NOWEX:(COUNT)
REND

PREVCOM = SELECTCOM

;絶頂禁止カウント
SIF TCVAR:0 == 0 && TCVAR:40 > 0
	TCVAR:40 -= 1

SETCOLOR 96, 96, 96
SIF FLAG:999 == 1
	PRINTFORML /* debug */ 絶頂禁止残ターン{TCVAR:40}
RESETCOLOR

AT_VAR = LIMIT(950 + PALAMLV_F(PALAM:恭順) * 3 + PALAMLV_F(PALAM:欲情) * 3 + PALAMLV_F(PALAM:屈服) * 2 + PALAMLV_F(PALAM:恐怖) * 2, 950, 995)

;PALAM最大値を保存およびPALAMの減衰
FOR LCOUNT, 0, PALAM終点
	SIF PALAM:(LCOUNT) > MAX_PALAM:(LCOUNT)
		MAX_PALAM:(LCOUNT) = PALAM:(LCOUNT)

	SIF LCOUNT == 潤滑
		CONTINUE
	PALAM:(LCOUNT) = PALAM:(LCOUNT) * AT_VAR / 1000
NEXT

;野次馬が減る
LOCAL:1 = 0
FOR LOCAL,0,(FLAG:70 + FLAG:71)
	IF RAND:100 < 5 + TFLAG:0 + (TIME == 0) * 5 && FLAG:70
		FLAG:70 -= 1
		LOCAL:1 += 1
	ENDIF
	IF RAND:100 < -10 + TFLAG:0 + (TIME == 0) * 5 && FLAG:71
		FLAG:71 -= 1
		LOCAL:1 += 1
	ENDIF
NEXT
IF LOCAL:1
	IF FLAG:72
		PRINTFORML 危険を感じた観衆が{LOCAL:1}人避難した！
	ELSE
		PRINTFORML {LOCAL:1}人の一般人が安全な場所まで避難できたようだ・・・
	ENDIF
	SIF FLAG:70 + FLAG:71 == 0
		PRINTFORML どうやら周囲の避難が完了したようだ！
	PRINTW 
ENDIF

