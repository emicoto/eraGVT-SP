;背後に回る
@COM6
;刻印による行動制限判定　１で行動できない
CALL ACT_LIMIT
SIF RESULT == 1
	RETURN 1
;空中攻撃フラグを進める
CALL ATTACK_AIR_FLAG
;べとべと中だと失敗することがある
IF (TCVAR:12 & べとべと) && RAND:100 < 50 && GETBIT(TCVAR:216, 1) == 0
	;距離表示
	CALL PRINT_DISTANCE
	PRINTL 
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は脚を滑らせてしまった！
	PRINTFORML 移動を伴う行動が失敗した！
	PRINTW 
	RETURN 1
ENDIF

;同一距離フラグを折る
TFLAG:30 = 0
;チェインコンボをリセット
TFLAG:31 = 0

;ゲージが増加
IF CFLAG:99 < 5
	TCVAR:6 += 30 + RAND:16
ELSEIF CFLAG:99 < 15
	TCVAR:6 += 20 + RAND:11
ELSE
	TCVAR:6 += 10 + RAND:6
ENDIF

;距離を近にする
TCVAR:0 = 1
;距離表示
CALL PRINT_DISTANCE
PRINTL 

;疲労が少し蓄積
TFLAG:99 += 1
;FEAT効果による反動の増加
SIF TALENT:空中浮遊 > 0
	TFLAG:99 += 1

;地の文：背後に回る
CALL MESSAGE_BATTLE_CHARA_STEPIN
;地の文：洗脳/悪堕ちキャラが操作キャラの背後に回るを見た
SIF FLAG:110 == 1
	CALL MESSAGE_OTHER_BATTLE_CHARA_STEPIN

;成否判定
LOCAL:0 = MAXBASE:知性

;衣装による補正
CALL CLOTH_BATTLE_HOSEI, "CHISEI"
LOCAL:0 = LOCAL:0 * RESULT / 100

;心境による補正
CALL SHINKYOU_CHECK, "CHISEI", LOCAL:0
LOCAL:0 = RESULT

;ボス触手/敵キャラの知性
IF FLAG:110
	LOCAL:1 = MAXBASE:(FLAG:111):知性
ELSE
	CALL TENTACLE_ACCESS, "CHISEI"
	LOCAL:1 = RESULT
ENDIF

CALL PERCENT_CAL, LOCAL:0, LOCAL:1
IF RAND:360 + 90 < RESULT || TFLAG:10 == 4
	LOCAL = 500 + RAND:RESULT / 2
	LOCALS = {LOCAL}
	TOFULL LOCALS
	TFLAG:3 += LOCAL
	PRINTFORML 相手は一瞬こちらを見失って取り乱した！
	PRINTFORML 油断度＋（%RESULTS%）
	TFLAG:1 = 1
ELSEIF RAND:240 + 60 < RESULT
	LOCAL = 250 + RAND:RESULT / 2
	LOCALS = {LOCAL}
	TOFULL LOCALS
	TFLAG:3 += LOCAL
	PRINTFORML うまく相手を翻弄することができた！
	PRINTFORML 油断度＋（%RESULTS%）
ELSEIF RAND:180 + 30 < RESULT
	LOCAL = 100 + RAND:RESULT / 4
	LOCALS = {LOCAL}
	TOFULL LOCALS
	TFLAG:3 += LOCAL
	PRINTFORML 相手のペースを乱した！
	PRINTFORML 油断度＋（%RESULTS%）
ELSE
	PRINTFORML しかしうまくいかなかった・・・
ENDIF
PRINTFORML EXゲージが大きく溜まった！

;心境が高揚になりやすい
IF RAND:100 < (TCVAR:11 * TCVAR:11) || TCVAR:1 == 0
	SIF RAND:2 == 0
		CALL SHINKYOU_CHANGE, "KOUYOU"
ELSE
	TCVAR:11 ++
ENDIF
PRINTL 
RETURN 1
