
;攻撃コマンドの共通処理
@COM_ATTACK_COMMON
#DIM ATTACK_NUM
#DIM HIT_FLAG
VARSET LOCAL
ATTACK_NUM = 0
HIT_FLAG = 0


;戦闘スタイルチェンジスキーム
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_BATTLE_STYLE_CHANGE")

;[連続]スタイルによる攻撃回数アップ
SIF FSTYLE_NAME_F(TARGET, TCVAR:0) == "連続"
	ATTACK_NUM += 1
;[連続]バーストによる攻撃回数アップ
SIF GETBIT(TCVAR:217, 0) && FSTYLE_NAME_F(TARGET, TCVAR:0) == "連続"
	ATTACK_NUM += 4
;[使役]スタイルによる攻撃回数アップ
SIF GETBIT(TCVAR:217, 0) == 0 && FSTYLE_NAME_F(TARGET, TCVAR:0) == "使役"
	ATTACK_NUM += 1

;距離表示
CALL PRINT_DISTANCE
PRINT 　
;武器詳細表示
FONTBOLD
SIF CSTR:(TCVAR:0 + 4) != ""
	PRINTFORM 『%CSTR:(TCVAR:0 + 4)%』
PRINTFORML <<%FSTYLE_NAME_F(TARGET,TCVAR:0)%>>
FONTREGULAR
;コンボ表示
CALL PRINT_COMBO
PRINTL 

;地の文
IF TCVAR:0 == 1
	CALL MESSAGE_BATTLE_CHARA_ATTACK_RANGE_SHORT
ELSEIF TCVAR:0 == 2
	CALL MESSAGE_BATTLE_CHARA_ATTACK_RANGE_MIDDLE
ELSEIF TCVAR:0 == 3
	CALL MESSAGE_BATTLE_CHARA_ATTACK_RANGE_LONG
ENDIF

$ATTACK_AGAIN
;命中判定
IF TCVAR:0 == 1
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "ATTACK_RANGE_SHORT"
ELSEIF TCVAR:0 == 2
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "ATTACK_RANGE_MIDDLE"
ELSEIF TCVAR:0 == 3
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "ATTACK_RANGE_LONG"
ENDIF
IF FLAG:10 == 1 && FLAG:11 == 2 && FLAG:21 == 1 && (TFLAG:13 & TCVAR:0) == 0
	PRINTFORML 待ち構えていた触手に本体への攻撃を阻まれた……
	PRINTL 
	RESULT = 0
ENDIF
IF RESULT == 0
	;カス当たりの判定
	IF TCVAR:0 == 1
		CALL ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, "ATTACK_RANGE_SHORT"
	ELSEIF TCVAR:0 == 2
		CALL ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, "ATTACK_RANGE_MIDDLE"
	ELSEIF TCVAR:0 == 3
		CALL ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, "ATTACK_RANGE_LONG"
	ENDIF

	IF RESULT
		;カス当たりフラグ
		HIT_FLAG = -1
		;チェイン加算
		TFLAG:31 += 1
		;ダメージ計算
		IF TCVAR:0 == 1
			CALL DAMAGE, "ATTACK_RANGE_SHORT"
			;[広範]スタイルはカス当たりの威力が高い
			IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "広範"
				LOCAL:1 = MIN(RESULT * 100 * MIN(BASE:体力 * 100 / MAXBASE:体力 + 20, 40) / 10000 + 100, RESULT) * (RAND:26 + 75) / 100
			ELSE
				LOCAL:1 = MIN(RESULT * 25 * MIN(BASE:体力 * 100 / MAXBASE:体力 + 15, 30) / 10000 + 50, RESULT)
			ENDIF
		ELSEIF TCVAR:0 == 2
			CALL DAMAGE, "ATTACK_RANGE_MIDDLE"
			;[広範]スタイルはカス当たりの威力が高い
			IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "広範"
				LOCAL:1 = MIN(RESULT * 200 * MIN(BASE:体力 * 100 / MAXBASE:体力 + 15, 30) / 10000 + 100, RESULT) * (RAND:51 + 50) / 100
			ELSE
				LOCAL:1 = MIN(RESULT * 100 * MIN(BASE:体力 * 100 / MAXBASE:体力 + 10, 20) / 10000 + 50, RESULT)
			ENDIF
		ELSEIF TCVAR:0 == 3
			CALL DAMAGE, "ATTACK_RANGE_LONG"
			;[広範]スタイルはカス当たりの威力が高い
			IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "広範"
				LOCAL:1 = MIN(RESULT * 300 * MIN(BASE:体力 * 100 / MAXBASE:体力 + 10, 20) / 10000 + 100, RESULT) * (RAND:76 + 25) / 100
			ELSE
				LOCAL:1 = MIN(RESULT * 300 * MIN(BASE:体力 * 100 / MAXBASE:体力 + 5, 10) / 10000 + 50, RESULT)
			ENDIF
		ENDIF
		FLAG:13 -= LOCAL:1
		;地の文：カス当たり
		CALL MESSAGE_BATTLE_CHARA_ATTACK_GUARD, ATTACK_NUM
		;地の文：洗脳/悪堕ちキャラが操作キャラにカス当たりされた
		SIF FLAG:110 == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_GUARD, ATTACK_NUM
		FONTBOLD
		PRINTFORML {LOCAL:1}のダメージを与えた！
		FONTREGULAR
		PRINTW 
		IF LOCAL:1 >= 5000 && GLOBAL:163 == 0
			PRINTL 【実績：破壊神の一撃】を達成しました！
			PRINTW 
			GLOBAL:163 = 1
			SAVEGLOBAL
		ENDIF
		LOCAL:2 = 1
	ELSE
		;攻撃ミスフラグ
		HIT_FLAG = 0
		;チェインリセット
		TFLAG:31 = 0
		;地の文：攻撃失敗
		CALL MESSAGE_BATTLE_CHARA_ATTACK_FALSE, ATTACK_NUM
		;地の文：洗脳/悪堕ちキャラが操作キャラの攻撃を回避した
		SIF FLAG:110 == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_FALSE, ATTACK_NUM
	ENDIF

	;心境変化：高揚か消沈
	SIF TCVAR:1 == 0 && ATTACK_NUM == 0
		CALL SHINKYOU_CHANGE, "KOUYOU_SYOUTIN"

	;油断度上昇の追加効果
	LOCAL:3 = 0
	;[設置]スタイル
	IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "設置" && LOCAL:2
		LOCAL:3 += 125
		;[設置]バーストの効果
		SIF GETBIT(TCVAR:217, 0)
			LOCAL:3 += 125
	ENDIF
	IF LOCAL:3 > 0
		IF (FLAG:110 == 0)
			CALL TENTACLE_ACCESS, "NAME"
		ELSEIF (FLAG:110 == 1)
			PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
		ENDIF
		LOCALS = {LOCAL:3}
		TOFULL LOCALS
		PRINTFORML の油断度が少し上昇した……（＋%RESULTS%）
		PRINTW 
		FLAG:17 += LOCAL:3
	ENDIF
	;[撹乱]バーストの怯み効果10％
	IF GETBIT(TCVAR:217, 0) && FSTYLE_NAME_F(TARGET,TCVAR:0) == "撹乱" && RAND:10 == 0 && HIT_FLAG == -1
		TFLAG:1 = 1
		PRINTFORML うまく相手の体勢を崩した！
	ENDIF
ELSE
	;直撃時の処理

	;チェイン加算
	TFLAG:31 += 1
	;衣装による補正
	RESULT = 0
	CALL CLOTH_BATTLE_HOSEI, "CRITICAL"
	;FEATによるクリティカル補正
	SIF TALENT:獣性の証 > 0
		RESULT += 6
	SIF TALENT:秘められし力 > 0 && PERCENT_CAL_F(BASE:体力 + BASE:気力, MAXBASE:体力 + MAXBASE:気力) <= 25
		RESULT += 12
	SIF TALENT:心眼 > 0
		RESULT += 3
	;[撹乱]スタイルのクリティカル補正
	SIF FSTYLE_NAME_F(TARGET,TCVAR:0) == "撹乱"
		RESULT += 5
	;[知略]バーストのクリティカル率アップ
	SIF GETBIT(TCVAR:217, 0) && FSTYLE_NAME_F(TARGET,TCVAR:0) == "知略"
		RESULT += 5
	;EVADERのクリティカル補正
	SIF TFLAG:33 > 1
		RESULT += GET_EVADER_CRT(TFLAG:33 - 1)
	;防御力によるクリティカル補正（隠し効果）
	LOCAL:0 = MAX(BASE:防御 - 100, 0)
	IF LOCAL:0 < 0
		LOCAL:0 = ABS(LOCAL:0)
		RESULT -= MIN(SQRT(LOCAL:0) * 2 / 3 , 12)
	ELSE
		RESULT += MIN(SQRT(LOCAL:0) * 2 / 3 , 12)
	ENDIF
	;近距離はクリティカルしやすい
	SIF TCVAR:0 == 1
		RESULT += 3
	;中距離は少しだけクリティカルしやすい
	SIF TCVAR:0 == 2
		RESULT += 1
	;クリティカル判定
	IF RAND:100 < 5 + RESULT
		LOCAL:0 = 15
		;バースト攻撃時はクリティカル倍率が上昇
		SIF GETBIT(TCVAR:217, 0)
			LOCAL:0 += 5
		;秘められし力の隠し補正
		SIF TALENT:秘められし力 > 0 && PERCENT_CAL_F(BASE:体力 + BASE:気力, MAXBASE:体力 + MAXBASE:気力) <= 25
			RESULT += 5
		SIF TALENT:秘められし力 > 0 && PERCENT_CAL_F(BASE:体力 + BASE:気力, MAXBASE:体力 + MAXBASE:気力) <= 10
			RESULT += 5
	ELSE
		LOCAL:0 = 10
	ENDIF

	;ダメージ計算
	IF TCVAR:0 == 1
		CALL DAMAGE, "ATTACK_RANGE_SHORT"
	ELSEIF TCVAR:0 == 2
		CALL DAMAGE, "ATTACK_RANGE_MIDDLE"
	ELSEIF TCVAR:0 == 3
		CALL DAMAGE, "ATTACK_RANGE_LONG"
	ENDIF

	LOCAL:1 = RESULT * LOCAL:0 / 10
	FLAG:13 -= LOCAL:1
	LOCAL:2 = LOCAL:1
	RESULT = 0
	IF LOCAL:0 > 10
		;クリティカルフラグ
		HIT_FLAG = 2
		;地の文：攻撃がクリティカルヒット
		CALL MESSAGE_BATTLE_CHARA_ATTACK_CRITICAL_HIT, ATTACK_NUM
		;地の文：洗脳/悪堕ちキャラが操作キャラの攻撃をクリティカルヒットされた
		SIF FLAG:110 == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_CRITICAL_HIT, ATTACK_NUM
	ELSE
		;直撃フラグ
		HIT_FLAG = 1
		;地の文：攻撃が通常ヒット
		CALL MESSAGE_BATTLE_CHARA_ATTACK_HIT, ATTACK_NUM
		;地の文：洗脳/悪堕ちキャラが操作キャラの攻撃を通常ヒットされた
		SIF FLAG:110 == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_HIT, ATTACK_NUM
	ENDIF
	;とどめ演出用
	SIF FLAG:13 <= 0 && RESULT == 999
		LOCAL:1 = LOCAL:1 * 16
	FONTBOLD
	PRINTFORML {LOCAL:1}のダメージを与えた！
	FONTREGULAR
	PRINTW 
	IF LOCAL:2 >= 5000 && GLOBAL:163 == 0
		PRINTL 【実績：破壊神の一撃】を達成しました！
		PRINTW 
		GLOBAL:163 = 1
		SAVEGLOBAL
	ENDIF

	;油断度上昇の追加効果
	LOCAL:3 = 0
	;[設置]スタイル
	IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "設置"
		LOCAL:3 += 300
		;[設置]バーストの効果
		SIF GETBIT(TCVAR:217, 0)
			LOCAL:3 += 300
	ENDIF
	IF LOCAL:3 > 0
		IF (FLAG:110 == 0)
			CALL TENTACLE_ACCESS, "NAME"
		ELSEIF (FLAG:110 == 1)
			PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
		ENDIF
		LOCALS = {LOCAL:3}
		TOFULL LOCALS
		PRINTFORML の油断度が少し上昇した……（＋%RESULTS%）
		PRINTW 
		FLAG:17 += LOCAL:3
	ENDIF
	;[撹乱]バーストの怯み効果50％、クリティカルで100％
	IF GETBIT(TCVAR:217, 0) && FSTYLE_NAME_F(TARGET,TCVAR:0) == "撹乱"
		IF HIT_FLAG == 2 || RAND:2 == 0
			TFLAG:1 = 1
			PRINTFORML うまく相手の体勢を崩した！
		ENDIF
	ENDIF
ENDIF

;追撃の発動
IF ATTACK_NUM
	ATTACK_NUM --
	GOTO ATTACK_AGAIN
ENDIF

;[全力]スタイルの反動
SIF FSTYLE_NAME_F(TARGET,TCVAR:0) == "全力"
	TFLAG:99 += 2

;バースト攻撃の反動
IF GETBIT(TCVAR:217, 0)
	SELECTCASE FSTYLE_NAME_F(TARGET, TCVAR:0)
		CASE "連続"
			TFLAG:99 += 2
		CASE "装甲"
			TFLAG:99 += 2
		CASE "撹乱"
			TFLAG:99 += 2
		CASE "重撃"
			TFLAG:99 += 3
		CASE "広範"
			TFLAG:99 += 3
		CASE "全力"
			TFLAG:99 += 4
		CASE "知略"
			TFLAG:99 += 1
		CASE "設置"
			TFLAG:99 += 1
		CASE "使役"
			TFLAG:99 += 3
		CASE "通常"
			TFLAG:99 += 1
	ENDSELECT
	;FEAT効果による反動増加
	SIF TALENT:ホットスタート > 0
		TFLAG:99 += 1
	SIF TALENT:フルバースト > 0
		TFLAG:99 += 1

	;[装甲]バーストのオートガード効果
	IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "装甲"
		TCVAR:2 = 体勢：防御
		PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は防御態勢になった！
	ENDIF
	;[知略]バーストの見切り効果
	IF FSTYLE_NAME_F(TARGET,TCVAR:0) == "知略"
		TFLAG:25 = 1
		PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は見切り状態になった！
	ENDIF
ENDIF

;サディストの効果
IF TALENT:サド気質 > 0 && HIT_FLAG > 0
	;気力が微量に回復
	LOCAL:1 = RAND:(MAXBASE:気力 * HIT_FLAG * 8 / 100) + 10
	SIF BASE:気力 + LOCAL:1 >= MAXBASE:気力
		LOCAL:1 = MAXBASE:気力 - BASE:気力
	BASE:気力 += LOCAL:1
	;欲情が微量に増加
	COMMON_PALAM:7 += LOCAL:1 * 10
	SIF LOCAL:1 > 0
		PRINTFORML サディスティックな快楽で%BASENAME:1%が{LOCAL:1}回復した
ENDIF

RETURN 1



;空中攻撃フラグ管理
@ATTACK_AIR_FLAG
IF GETBIT(TCVAR:216, 1) && GETBIT(TCVAR:216, 0) == 0
	TCVAR:216 = 4
	;FEAT効果による着地時の反動
	SIF TALENT:有翼 > 0
		TFLAG:99 += 1
ELSEIF GETBIT(TCVAR:216, 0) && BASE:空中ダッシュ
	SETBIT TCVAR:216, 1
	BASE:空中ダッシュ -= 1
	;空中攻撃の反動
	TFLAG:99 += 1
	;FEAT効果による反動の軽減
	SIF TALENT:空中浮遊 > 0
		TFLAG:99 -= 1
	;FEAT効果による反動の増加
	SIF TALENT:エアマスター > 0
		TFLAG:99 += 1
ENDIF


