;CFLAG:221前回出産からの膣出回数
;CFLAG:222妊娠日数*2
;CFLAG:223触手日霊
;CFLAG:224育児日数
;TALENT:妊娠 妊娠中


;------------------------------
;受精判定
;------------------------------
@NINSIN_HANTEI,ARG:0,ARG:1,ARG:2 = 0
#DIM PAPA_ID
#DIM PREG_PER

SIF TALENT:オトコ > 0
	RETURN 0
SIF TALENT:未熟 > 0
	RETURN 0
SIF TALENT:妊娠 > 0
	RETURN 0
;常時避妊モード
SIF CONFIG_CHECK_OTHER_F(5) > 0
	RETURN 0
;避妊効果
IF CHECK_HININ_F(TARGET, ARG:2) == 1
	;戦闘中かつ避妊に成功した場合は射精量をプールする
	SIF FLAG:700 > 0
		TFLAG:6 += ARG:0
	RETURN 0
ELSE
	;戦闘中かつ避妊に失敗した場合はプールした射精量を戻す
	SIF FLAG:700 > 0
		ARG:0 += TFLAG:6
	TFLAG:6 = 0
ENDIF
;父親のＩＤを取得
;幽閉中の場合は幽閉相手
IF (CFLAG:0 == 1 || CFLAG:0 == 2 || CFLAG:0 == 3 || CFLAG:0 == 9) && ARG:2 == 0
	PAPA_ID = CFLAG:21 + (CFLAG:20 == 1) * 100
;戦闘中の場合
ELSEIF FLAG:700 > 0 && ARG:2 == 0
	;洗脳/悪堕ちキャラの場合、そのキャラのＩＤ
	IF FLAG:110 > 0
		PAPA_ID = CFLAG:(FLAG:111):240 * -1 - 100
		;寄生ふたなりの場合
		SIF TALENT:(FLAG:111):ふたなり == 2 && RAND:4 != 0
			PAPA_ID = 200
	;その他の場合
	ELSE
		;戦闘中の触手のＩＤ
		PAPA_ID = FLAG:11
		;ラスボス触手の場合
		IF FLAG:10 == 1
			PAPA_ID += 100
		;雑魚敵の場合
		ELSEIF FLAG:10 == 2
			PAPA_ID += 200
		ENDIF
	ENDIF
;それ以外の場合
ELSE
	PAPA_ID = ARG:2
	;寄生ふたなりの場合
	SIF TALENT:(CHARAID_F(ARG:2 * -1 - 100)):ふたなり == 2 && RAND:4 != 0
		PAPA_ID = 200
ENDIF
;中出し累積回数を加算
CFLAG:221 += ARG:0
;精液の種類ごとに累積数を加算
;触手の場合
IF PAPA_ID > 0
	CFLAG:232 += ARG:0
	SQRT (ARG:1 * CFLAG:232 * (EXP:出産経験 + 1) / 2)
	PREG_PER = RESULT
;仲間キャラの場合
ELSEIF PAPA_ID <= -100
	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		;寄生されてるなら触手、そうでなければ人間扱い
		IF CFLAG:LOCAL:240 == (PAPA_ID * -1 - 100) && TALENT:LOCAL:寄生 > 0
			CFLAG:232 += ARG:0
			SQRT (ARG:1 * CFLAG:232 * (EXP:出産経験 + 1) / 2)
			PREG_PER = RESULT
		ELSEIF CFLAG:LOCAL:240 == (PAPA_ID * -1 - 100)
			CFLAG:233 += ARG:0
			SQRT (ARG:1 * CFLAG:233 * (EXP:出産経験 + 1) / 2)
			PREG_PER = RESULT
		ENDIF
	NEXT
;一般人の場合
ELSEIF PAPA_ID <= -1
	CFLAG:233 += ARG:0
	SQRT (ARG:1 * CFLAG:233 * (EXP:出産経験 + 1) / 2)
	PREG_PER = RESULT
ENDIF
;妊娠確率補正
SIF FLAG:700 == 1
	PREG_PER = PREG_PER * 2
SIF TALENT:苗床化
	PREG_PER = PREG_PER * 4
;妊娠確率アップの効果
SIF FLAG:909
	PREG_PER = PREG_PER * 2
;避妊結界持ちが破られた場合、かつ相手が触手の場合は妊娠確率アップ
SIF TALENT:避妊結界 > 0 && PAPA_ID > 0
	PREG_PER = PREG_PER * 2
;FEAT効果による妊娠確率補正
SIF TALENT:獣性の証 > 0
	PREG_PER = PREG_PER * 125 / 100
SIF TALENT:祝福 > 0
	PREG_PER = PREG_PER * 150 / 100
SIF TALENT:不老長寿 > 0
	PREG_PER = PREG_PER * 75 / 100
SIF (TCVAR:12 & 発情)
	PREG_PER = PREG_PER * 2
SIF (TCVAR:12 & 排卵)
	PREG_PER = 1000
;debug妊娠確率表示
IF FLAG:999 == 1
	PRINTL 
	PRINTFORML 基本妊娠確率を入力してください。（負の数で変更なし）
	INPUT
	IF RESULT < 0
	ELSE
		PREG_PER = RESULT * 10
	ENDIF
	SETCOLOR 128,128,128
	PRINTL 
	PRINTFORML 　　妊娠確率 {(PREG_PER + 100 * (CFLAG:0 == 0)) / 10}％
	PRINTL 
	RESETCOLOR
ENDIF
;幽閉されてなければ10%増し
IF RAND:1000 < (PREG_PER + 100 * (CFLAG:0 == 0)) && (TALENT:妊娠 == 0)
	;屈服の珠の増加
	CALL NINSIN_SUBMIT
	;妊娠したら累積中出回数を0に
	CFLAG:221 = 0
	CFLAG:232 = 0
	CFLAG:233 = 0
	;ここで父親の記録を行う
	CFLAG:230 = PAPA_ID
	CALL NINSIN_FLAG
	IF FLAG:700 == 1
		CALL TENTACLE_ACCESS, "GETNAME"
		IF RESULTS == "Ｈ触手" && (TCVAR:12 & 排卵)
			PRINTFORML Ｈ触手が広げた薄膜のモニターには%PRINT_TRANSCALLNAME(TARGET)%の胎内の様子が映し出されており、
			PRINTFORML 成熟した卵子の周りには無数の触手の精子が群がっている。
			PRINTFORML そのうちの一匹が表皮を突き破って悲願を達成し、爆発的な細胞の増殖が始まった。
			PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%は目の前で異種間受精の瞬間を見せつけられてしまった・・・
			PRINTW 
			TALENT:妊娠 = 1
			PRINTFORMW %PRINT_TRANSCALLNAME(TARGET)%は[妊娠]した
			CALL NINSIN_TS_FIX
		ENDIF
	ENDIF
	RETURN 1
ENDIF
RETURN 0
;------------------------------
;戦闘終了時の妊娠チェック
;------------------------------
@NINSIN_CHECK_AFTER
;戦闘終了後に初めて地の文が流れる
IF TALENT:妊娠 == 2 && TALENT:オトコ <= 0
	IF CONFIG_CHECK_OTHER_F(0) > 0
		;苗床化されている場合は75%で普通の触手が産まれてしまう
		IF TALENT:苗床化 && RAND:4 != 0
			TALENT:妊娠 = 1
			CFLAG:227 = NUM_CHILD_TENTACLE(TARGET)
			CFLAG:228 = PREGNANT_RANDOM_SIZE(TARGET)
		ELSE
			TALENT:妊娠 = 3
			CFLAG:228 = PREGNANT_RANDOM_SIZE(TARGET)
		ENDIF
	ELSE
		TALENT:妊娠 = 1
		CFLAG:227 = NUM_CHILD_TENTACLE(TARGET)
		CFLAG:228 = PREGNANT_RANDOM_SIZE(TARGET)
	ENDIF
	PRINTL 
	CALL MESSAGE_NINNSIN
	CALL NINSIN_TS_FIX
ENDIF

;------------------------------
;妊娠フラグを立てて地の文表示
;------------------------------
@NINSIN_FLAG
#DIM CCOUNT
;仲間が父親の場合
IF CFLAG:230 <= -100
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		;父親を判定
		IF CFLAG:CCOUNT:240 == CFLAG:230 * -1 - 100
			;父親も母親も寄生されていない場合
			IF TALENT:CCOUNT:寄生 == 0 && TALENT:寄生 == 0
				TALENT:妊娠 = 4
				CFLAG:228 = PREGNANT_RANDOM_SIZE(TARGET)
			ELSE
				TALENT:妊娠 = 2
			ENDIF
		ENDIF
	NEXT
;普通の人間が父親の場合
ELSEIF CFLAG:230 <= -1
	;母親が寄生されていない場合
	IF TALENT:寄生 == 0
		TALENT:妊娠 = 4
		CFLAG:228 = PREGNANT_RANDOM_SIZE(TARGET)
	ELSE
		TALENT:妊娠 = 2
	ENDIF
;それ以外の場合は通常の妊娠フラグを立てる
ELSE
	TALENT:妊娠 = 2
ENDIF
;幽閉中の場合、または戦闘中でない場合は即時に妊娠する
IF CFLAG:21 > 0 || (FLAG:700 == 0 && TALENT:妊娠 == 2)
	IF CONFIG_CHECK_OTHER_F(0) > 0
		;苗床化されている場合は75%で普通の触手が産まれてしまう
		IF ((TALENT:苗床化 && RAND:4 != 0) || CFLAG:22 != 0) && CFLAG:0 == 1
			TALENT:妊娠 = 1
			CFLAG:227 = NUM_CHILD_TENTACLE(TARGET)
			CFLAG:228 = PREGNANT_RANDOM_SIZE(TARGET)
		ELSE
			TALENT:妊娠 = 3
			CFLAG:228 = PREGNANT_RANDOM_SIZE(TARGET)
		ENDIF
	ELSE
		TALENT:妊娠 = 1
		CFLAG:227 = NUM_CHILD_TENTACLE(TARGET)
		CFLAG:228 = PREGNANT_RANDOM_SIZE(TARGET)
	ENDIF
	PRINTL 
	CALL MESSAGE_NINNSIN
	CALL NINSIN_TS_FIX
ENDIF
;ＴＳ変身時の正常妊娠の場合、オトコに戻れなくなる
IF TALENT:オトコ <= 0 && TALENT:変身時ＴＳ > 0 && CFLAG:1 > 0 && TALENT:妊娠 == 4
	PRINTW 
	PRINTFORML %PRINT_CALLNAME(TARGET,1)%の様子がおかしい…
	PRINTFORML なぜか分からないが、身体の奥底に何か違和感を感じる…
	PRINTL 
	PRINTFORML %PRINT_CALLNAME(TARGET,1)%は[オトコ]に戻れなくなった
	PRINTW 

	TALENT:変身時ＴＳ = -1
	TALENT:変身時非処女 = 0
	TALENT:性別変化 = 1
	TALENT:ふたなり = TALENT:変身時ふたなり

	BASE:身長 = MAXBASE:身長
	BASE:体重 = MAXBASE:体重
	BASE:胸囲 = MAXBASE:胸囲
	BASE:胴囲 = MAXBASE:胴囲
	BASE:腰囲 = MAXBASE:腰囲
	BASE:年齢 = MAXBASE:年齢
	CSTR:13 = %CSTR:14%
	CSTR:30 = %CSTR:31%
	CSTR:32 = %CSTR:34%
	CSTR:33 = %CSTR:35%
	CSTR:36 = %CSTR:37%
ENDIF

;------------------------------
;ＴＳ魔法少女の場合、妊娠するとオトコに戻れなくなるor一時的にオトコに変身できなくなる
;------------------------------
@NINSIN_TS_FIX
IF TALENT:オトコ <= 0 && TALENT:変身時ＴＳ > 0 && CFLAG:1 > 0
	;地の文
	CALL MESSAGE_NINNSIN_TS_FIX

	TALENT:変身時ＴＳ = -1
	TALENT:変身時非処女 = 0
	TALENT:性別変化 = 1
	TALENT:ふたなり = TALENT:変身時ふたなり
	TALENT:外見 = TALENT:変身時外見


	CALL GENERATE_CHAR_SIZE, TARGET, 0
	MAXBASE:身長 = RESULT:2
	MAXBASE:体重 = RESULT:3
	MAXBASE:胸囲 = RESULT:4
	MAXBASE:胴囲 = RESULT:5
	MAXBASE:腰囲 = RESULT:6
	MAXBASE:年齢 = BASE:年齢
	CSTR:13 = %CSTR:14%
	CSTR:30 = %CSTR:31%
	CSTR:32 = %CSTR:34%
	CSTR:33 = %CSTR:35%
	CSTR:36 = %CSTR:37%
ELSEIF TALENT:オトコ <= 0 && TALENT:変身時ＴＳ > 0 && CFLAG:1 == 0
	;地の文
	CALL MESSAGE_NINNSIN_TS_FIX
ENDIF

;------------------------------
;出産判定
;------------------------------
@BIRTH_HANTEI
#DIM CCOUNT
LOCAL:0 = TARGET
FOR CCOUNT,0,CHARANUM
	TARGET = CCOUNT
	SIF CFLAG:0 == 9
		CONTINUE

	;妊娠中なら妊娠日数を増加
	IF TALENT:妊娠 == 1 || TALENT:妊娠 == 3
		;幽閉中でなく出産直前なら　コマンド入力できない
		IF CFLAG:0 == 0 && (CFLAG:222 > 9 || (TALENT:苗床化 > 0 && CFLAG:222 > 3))
			CFLAG:0 = 10
			CALL MESSAGE_MATANITY
			CALL SET_PARTYMEMBER
			DRAWLINE
		ENDIF
		;苗床化は３倍速で成長する
		IF TALENT:苗床化 > 0
			CFLAG:222 += 3
		ELSE
			CFLAG:222 += 1
		ENDIF
		;幽閉中はさらに成長が早まる
		SIF CFLAG:0 == 1
			CFLAG:222 += 2
		;出産可能かどうか
		IF CFLAG:222 > 10
			;フィルタ×でなく、母乳体質でないなら母乳体質にする
			IF TALENT:母乳体質 == 0 && FILTER_CHECK_F(2) == 0
				TALENT:母乳体質 = 1
				PRINTFORML %PRINT_CALLNAME(TARGET,1)%の胸が張ってきた…
				PRINTL
				PRINTFORML %PRINT_CALLNAME(TARGET,1)%は[%TALENTNAME:1%]になった…
			ENDIF
			;確率で出産　最短5日最長7日半。ただし幽閉中に苗床化されてると常に1/2の確率で即出産
			IF CFLAG:222 >= 30
				LOCAL:1 = 0
			ELSE
				LOCAL:1 = RAND:(30 - CFLAG:222)
			ENDIF
			IF (LOCAL:1 == 0 && CFLAG:222 >= 10) || CFLAG:222 >= 15 || (CFLAG:0 == 1 && TALENT:苗床化 > 0 && RAND:2 == 0)
				IF TALENT:妊娠 == 3 && CONFIG_CHECK_OTHER_F(0) > 0
					CALL CHILD_BIRTH
				ELSE
					CALL TENTACLE_BIRTH
				ENDIF
			ENDIF
		ENDIF
	;正常妊娠の場合
	ELSEIF TALENT:妊娠 == 4
		;苗床化は３倍速で成長する
		IF TALENT:苗床化 > 0
			CFLAG:222 += 3
		ELSE
			CFLAG:222 += 1
		ENDIF
		IF CFLAG:222 >= 20
			PRINTFORML %PRINT_CALLNAME(TARGET,1)%の腹部が明らかに膨らんできている…
			PRINTFORML もう妊娠していることは誰の目にも明らかだ…
			IF TALENT:オトコ == 0 && TALENT:変身時ＴＳ > 0 && CFLAG:1 == 0
				PRINTW 
				PRINTFORML %PRINT_CALLNAME(TARGET)%は妊娠している間は変身できなくなります
			ENDIF
			PRINTW 
			TALENT:妊娠 = 5
		ELSEIF CFLAG:222 == 12
			PRINTFORML %PRINT_CALLNAME(TARGET,1)%の腹部が膨らんできたような気がする…
			PRINTFORML 今まで先延ばしにしてきたが、ちゃんと検査をしておいた方が良いだろう…
			PRINTW 
		ELSEIF CFLAG:222 == 9
			PRINTFORML %PRINT_CALLNAME(TARGET,1)%の様子がおかしい…
			PRINTFORML 時折強い吐き気を感じているようだ…
			PRINTW 
		ELSEIF CFLAG:222 == 6
			PRINTFORML %PRINT_CALLNAME(TARGET,1)%の様子がおかしい…
			IF RAND:2 == 0
				PRINTFORML 前回から随分と生理が遅れているようだ…
			ELSE
				PRINTFORML 何やら軽い吐き気のようなものを感じているようだ…
			ENDIF
			PRINTFORML 心当たりがあるなら、医務室で検査してみた方が良いかもしれない…
			PRINTW 
		ENDIF
	ELSEIF TALENT:妊娠 == 5
		;幽閉中でなく出産直前なら　コマンド入力できない
		IF CFLAG:0 == 0 && CFLAG:222 > 42
			CFLAG:0 = 10
			CALL MESSAGE_MATANITY
			CALL SET_PARTYMEMBER
			DRAWLINE
		ENDIF
		;苗床化は３倍速で成長する
		IF TALENT:苗床化 > 0
			CFLAG:222 += 3
		ELSE
			CFLAG:222 += 1
		ENDIF
		IF CFLAG:222 >= 16
			;フィルタ×でなく、母乳体質でないなら母乳体質にする
			IF TALENT:母乳体質 == 0 && FILTER_CHECK_F(2) == 0
				TALENT:母乳体質 = 1
				PRINTFORML %PRINT_CALLNAME(TARGET,1)%の胸が張ってきた…
				PRINTL
				PRINTFORML %PRINT_CALLNAME(TARGET,1)%は[%TALENTNAME:1%]になった…
				PRINTW 
			ENDIF
		ENDIF
		;出産可能化どうか
		;確率で出産　最短26日最長28日半。
		IF CFLAG:222 >= 83
			LOCAL:1 = 0
		ELSE
			LOCAL:1 = RAND:(83 - CFLAG:222)
		ENDIF
		SIF (LOCAL:1 == 0 && CFLAG:222 >= 53) || CFLAG:222 >= 56
			CALL CHILD_BIRTH_NORMAL
	ELSE
		CONTINUE
	ENDIF

NEXT
TARGET =LOCAL:0

;------------------------------
;出産(触手)
;------------------------------
;LOCAL:1 触手の数
@TENTACLE_BIRTH
;生存時に再び行動可能に復帰する
SIF CFLAG:0 == 10
	CALL RECOVER_TO_PARTY,TARGET
;幽閉中とそれ以外で地の文が違う
LOCAL:1 = CFLAG:227
IF CFLAG:21 != 0
	;異形出産描写
	IF FILTER_CHECK_F(4) == 0
		;育児機能オンなら合いの子率アップ
		IF RAND:4 == 0 || (CONFIG_CHECK_OTHER_F(0) > 0 && RAND:4 != 0)
			CALL MESSAGE_PRISON_AINOKO_SHUSSAN,LOCAL:1
		ELSE
			CALL MESSAGE_PRISON_SHUSSAN,LOCAL:1
		ENDIF
	ELSE
		CALL MESSAGE_PRISON_SHUSSAN,LOCAL:1
	ENDIF
	CFLAG:223 = 1
	;今回の出産数を加算
	CFLAG:220 += LOCAL:1
	FLAG:44 += LOCAL:1
ELSE
	CALL MESSAGE_SHUSSAN,LOCAL:1
	PRINTL
	;組織にいる場合は触手の欠片追加
	FLAG:200 += LOCAL:1
	PRINTFORML 触手の欠片＋{LOCAL:1}
ENDIF

CALL ABL_UP_BIRTH,LOCAL:1
CALL _ABLUP,1

;妊娠日数中だし回数のリセット、素質[妊娠]の削除
CFLAG:221 = 0
CFLAG:222 = 0
CFLAG:227 = 0
CFLAG:228 = 0
CFLAG:232 = 0
CFLAG:233 = 0
TALENT:妊娠 = 0

;------------------------------
;出産時に上がる能力の計算
;------------------------------
@ABL_UP_BIRTH,ARG
#DIM LCOUNT
VARSET LOCAL

;体力気力の減少処理
BASE:体力 = MAX(BASE:体力/2-500,1)
BASE:気力 = MAX(BASE:体力/2-500,1)

LOCAL = 100

;《従順》をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.20
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.50
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 0.70
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.50
ELSE
	TIMES LOCAL , 2.00
ENDIF

;《触手中毒》をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL , 1.00
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL , 1.10
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL , 1.30
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL , 1.50
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL , 2.00
ELSE
	TIMES LOCAL , 2.50
ENDIF

;《出産経験》をみる
IF EXP:出産経験 < 1
	TIMES LOCAL , 0.50
ELSEIF EXP:出産経験 < 10
	TIMES LOCAL , 0.70
ELSEIF EXP:出産経験 < 30
	TIMES LOCAL , 1.00
ELSEIF EXP:出産経験 < 50
	TIMES LOCAL , 2.00
ELSE
	TIMES LOCAL , 4.00
ENDIF
;修練P入手
CALL GET_SYUREN,LOCAL/10
;組織にいると威力3倍
SIF CFLAG:0 == 10
	TIMES LOCAL , 3.00
;珠ゲット
	LOCAL:1 += (ABL:Ｖ感覚 + 1) * 10 * LOCAL * (1 + (TALENT:Ｖ敏感 > 0) + (TALENT:淫壷 > 0)) / (1+(TALENT:Ｖ鈍感 > 0))
	LOCAL:3 += (ABL:噴乳中毒 + 1) * (ABL:Ｂ感覚 + 1) * 10 * LOCAL * (1 + (TALENT:Ｂ敏感 > 0) + (TALENT:淫乳 > 0)) / (1 + (TALENT:Ｂ鈍感 > 0))
JUEL:快Ｖ = LOCAL:1
JUEL:快Ｂ = LOCAL:3
JUEL:恭順 += (ABL:従順 + 1) * 100 * LOCAL
JUEL:欲情 += (ABL:欲望 + 1) * 25 * LOCAL
JUEL:屈服 += (ABL:従順 + 1) * 250 * LOCAL
JUEL:恥情 += (ABL:露出癖 + 1) * 25 * LOCAL
SQRT (EXP:Ｖ拡張経験+1)
JUEL:苦痛 += (ABL:マゾっ気 + 1) * 300 * LOCAL / RESULT
SQRT (EXP:出産経験+1)
JUEL:恐怖 += 100 * LOCAL / RESULT
;V経験
LOCAL:120 = LOCAL / 10
;絶頂経験
LOCAL:122 = (LOCAL:1 + LOCAL:3) / 1000
;苦痛経験
LOCAL:132 = LOCAL / 50
;噴乳経験
SIF TALENT:母乳体質 == 1
	LOCAL:154 = LOCAL:3 / (6 - ABL:噴乳中毒) / 1000
;V拡張
LOCAL:151 = 1

;出産経験とV拡張経験追加
;初めての出産なら異常経験が入り、体力気力が少し上昇する
IF EXP:出産経験 == 0
	LOCAL:150 += 1
	CFLAG:204 = 1
	MAXBASE:体力 += MAXBASE:体力/4
	MAXBASE:気力 += MAXBASE:気力/4
	BASE:体力基礎 += BASE:体力基礎/4
	BASE:気力基礎 += BASE:気力基礎/4
	PRINTFORML 出産の苦痛に耐えた%PRINT_CALLNAME(TARGET)%は体力と気力が増加しました
	PRINTL 
ENDIF
;出産経験
LOCAL:157 = ARG

;経験数の増加
FOR LCOUNT, 100, 200
	CALL COMMON_PRISON_EXP, LCOUNT, LOCAL:LCOUNT
NEXT

;拡張度の上昇（３サイズを設定していない場合や極端な拡張を抑制している場合は発生しない）
IF CFLAG:34 > 0 && FILTER_CHECK_F(17) > 0
	TENTACLE_SIZE:1:快Ｖ = PREGNANCY_BELLY_EXPAND(TARGET) / 2
	CALL V_GAPING(GAPING_SIZE_TO_POINT("Ｖ"), TARGET)
	IF RESULT > 0 && FILTER_CHECK_F(16) == 0
		PRINTFORML 　膣径：＋{RESULT / 10}.{RESULT % 10} cm	
		PRINTL 
	ENDIF
ENDIF
PRINTL 

;------------------------
;産む触手の数
;------------------------
@NUM_CHILD_TENTACLE(ARG)
#FUNCTION
LOCAL = 0
;出産経験により増える
IF EXP:出産経験 < 1
	LOCAL = 1 + RAND:4
ELSEIF EXP:出産経験 < 10
	LOCAL = 2 + RAND:5
ELSEIF EXP:出産経験 < 30
	LOCAL = 3 + RAND:6
ELSEIF EXP:出産経験 < 50
	LOCAL = 4 + RAND:7
ELSE
	LOCAL = 5 + RAND:8
ENDIF
;苗床化されていれば更に増える
SIF TALENT:苗床化
	LOCAL += 1 + RAND:2
SIF CFLAG:0 == 9
	LOCAL = MAX(LOCAL / 2, 1)


RETURNF LOCAL

;----------------------------
;ターン終了時に子触手が増殖
;----------------------------
@AUTO_BIRTH
VARSET LOCAL
;常時避妊モード
SIF CONFIG_CHECK_OTHER_F(5) > 0
	RETURN 0
;防衛力が低いと、どこかで子触手が産み落とされる
LOCAL = FLAG:852 / 100
LOCAL:1 = 0
SIF LOCAL <= 2
	LOCAL = 2
IF RAND:LOCAL == 0 && FLAG:852 < 10000
	DRAWLINE
	LOCAL:1 += 1
	LOCAL:2 = 1
	PRINTL 【避けられた悲劇】
	PRINTW 　どこかで誰かが襲われ、子触手が産み落とされたようだ...
ENDIF
;苗床化＋死亡したキャラが居るなら、ランダムで出産
FOR LOCAL, 0, CHARANUM
	SIF LOCAL == MASTER
		CONTINUE
	IF CFLAG:LOCAL:0 == 9 && TALENT:LOCAL:苗床化 && RAND:4 == 0
		IF LOCAL:2 == 0
			DRAWLINE
			LOCAL:2 = 1
		ENDIF
		RESULT = NUM_CHILD_TENTACLE(LOCAL)
		PRINTL 【苗床出産】
		PRINTFORMW 　触手に取り込まれた%CALLNAME:LOCAL%が、{RESULT}匹の子触手を産み落としたようだ...
		LOCAL:1 += RESULT
		SIF FILTER_CHECK_F(2) == 0
			TALENT:母乳体質 = 1
	ENDIF
NEXT
IF LOCAL:1 > 0
	FLAG:44 += LOCAL:1
ENDIF


;----------------------------
;妊娠時の屈服増加
;----------------------------
@NINSIN_SUBMIT
VARSET LOCAL
IF TALENT:触手の虜
	JUEL:恭順 += 1000
ELSEIF EXP:出産経験 >= 10
	JUEL:屈服 += 100
ELSEIF EXP:出産経験 >= 5
	JUEL:屈服 += 400
	JUEL:恐怖 += 50
ELSEIF EXP:出産経験 >= 1
	JUEL:屈服 += 1000
	JUEL:恐怖 += 300
ELSE
	JUEL:屈服 += 2500
	JUEL:恐怖 += 1500
ENDIF



;--------------------------------------------------
;避妊効果の有無を判定する式中関数
;ARG:1 = 相手のＩＤ
@CHECK_HININ_F(ARG, ARG:1)
#FUNCTION
LOCAL = 0
;戦闘中のみの効果
IF FLAG:700 > 0
	;変身中かつソフトボディスーツの場合
	IF CFLAG:ARG:41 == 299 && CFLAG:1 > 0
		LOCAL = 100
	;避妊用ピルの効果
	ELSEIF CFLAG:ARG:241 > 0 && FLAG:110 > 0
		LOCAL = 80
	ELSEIF CFLAG:ARG:241 > 0
		LOCAL = 5
	ENDIF
	;避妊結界
	SIF TALENT:ARG:避妊結界 > 0 && BASE:ARG:性耐性 > 0
		LOCAL = 100
;戦闘中以外
ELSE
	;避妊用ピルの効果
	IF CFLAG:ARG:241 > 0 && ARG:1 < 0
		LOCAL = 80
	ELSEIF CFLAG:ARG:241 > 0
		LOCAL = 5
	ENDIF
	;避妊結界
	SIF TALENT:ARG:避妊結界 > 0 && BASE:ARG:性耐性 > 0 && ARG:1 > 0
		LOCAL = 100
ENDIF
IF RAND:100 < LOCAL
	LOCAL:1 = 1
ELSE
	LOCAL:1 = 0
ENDIF
RETURNF LOCAL:1


;--------------------------------------------------
;妊娠中かどうかを判定する式中関数
@CHECK_PREGNANT_F(ARG)
#FUNCTION
IF TALENT:ARG:妊娠 == 1 || TALENT:ARG:妊娠 == 3 || (TALENT:ARG:妊娠 == 5 && CFLAG:ARG:222 >= 11)
	LOCAL = 1
ELSE
	LOCAL = 0
ENDIF
RETURNF LOCAL



;--------------------------------------------------
;妊娠時の腹囲最大サイズをランダム決定
@PREGNANT_RANDOM_SIZE(ARG)
#FUNCTION
LOCAL = 0
SIF TALENT:ARG:妊娠 == 0
	RETURNF 0
;触手幼体の場合
IF TALENT:ARG:妊娠 == 1
	FOR LOCAL:1, 0, CFLAG:ARG:227
		LOCAL += 88
	NEXT
ELSE
	LOCAL = 266
ENDIF
LOCAL = LOCAL * (80 + RAND:41) / 100
RETURNF LOCAL

;--------------------------------------------------
;妊娠時のバストの変化
@PREGNANCY_BOOB_EXPAND(ARG)
#FUNCTION
LOCAL = 0
SIF TALENT:ARG:妊娠 == 0
	RETURNF 0
;触手幼体の場合
IF TALENT:ARG:妊娠 == 1
	LOCAL = 20 * CFLAG:ARG:222 / 10
;ヒトの胎児（異常）の場合
ELSEIF TALENT:ARG:妊娠 == 3
	LOCAL = 20 * CFLAG:ARG:222 / 10
;ヒトの胎児（正常）の場合
ELSEIF TALENT:ARG:妊娠 == 5
	LOCAL = 20 * (CFLAG:ARG:222 - 6) / 40
ENDIF
RETURNF LOCAL

;--------------------------------------------------
;妊娠時の腹囲の変化
@PREGNANCY_BELLY_EXPAND(ARG)
#FUNCTION
LOCAL = 0
SIF TALENT:ARG:妊娠 == 0
	RETURNF 0
;触手幼体の場合
IF TALENT:ARG:妊娠 == 1
	LOCAL = CFLAG:ARG:228 * (CFLAG:ARG:222) / 10
;ヒトの胎児（異常）の場合
ELSEIF TALENT:ARG:妊娠 == 3
	LOCAL = CFLAG:ARG:228 * (CFLAG:ARG:222 + 5) / 15
;ヒトの胎児（正常）の場合
ELSEIF TALENT:ARG:妊娠 == 4 || TALENT:ARG:妊娠 == 5
	IF CFLAG:ARG:222 < 14
		LOCAL = CFLAG:ARG:228 * 15 / 100
	ELSEIF CFLAG:ARG:222 < 22
		LOCAL = CFLAG:ARG:228 * 30 / 100
	ELSEIF CFLAG:ARG:222 < 42
		LOCAL = CFLAG:ARG:228 * 80 / 100
	ELSE
		LOCAL = CFLAG:ARG:228
	ENDIF
	LOCAL = LOCAL * MAX(CFLAG:ARG:222 - 8,0) / 48
ENDIF
;FEATによる胎児サイズへの影響
IF TALENT:ARG:小さな体躯 > 0
	LOCAL = LOCAL * 85 / 100
	SIF TALENT:ARG:妖精族 > 0
		LOCAL = LOCAL * 70 / 100
ENDIF
RETURNF LOCAL


;--------------------------------------------------
;アフターピルで緊急避妊
@AFTER_PILL, ARG, ARG:1, ARG:2
#DIM LCOUNT
SIF CONFIG_CHECK_OTHER_F(6) == 0
	RETURN 0
SIF TALENT:ARG:オトコ > 0
	RETURN 0
SIF TALENT:ARG:未熟 > 0
	RETURN 0
SIF TALENT:ARG:妊娠 == 1 || TALENT:ARG:妊娠 == 3 || TALENT:ARG:妊娠 == 5
	RETURN 0
SIF CFLAG:ARG:241 > 0
	RETURN 0
;常時避妊モード
SIF CONFIG_CHECK_OTHER_F(5) > 0
	RETURN 0
PRINTL 
PRINTW ・・
PRINTW ・・・・
PRINTW ・・・・・・
PRINTL 
PRINTFORML %PRINT_CALLNAME(ARG,1)%は膣内射精されたことを自覚している…
IF ARG:1 < 25
	PRINTFORM 時間が経ち過ぎて効果がないかもしれないが、
ELSE
	PRINTFORM このままでは妊娠してしまうかもしれないが、
ENDIF
PRINTFORML $500支払って緊急用アフターピルを飲んでおくべきだろうか？
PRINTFORML [0]アフターピルは飲まない
PRINTFORML [1]アフターピルを飲んで避妊する（所持金:${MONEY}）
LCOUNT = LINECOUNT
$INPUT_LOOP_0
INPUT
IF RESULT == 0
	PRINTFORML %PRINT_CALLNAME(ARG)%はアフターピルを飲まないことにした…
ELSEIF RESULT == 1
	IF MONEY >= 500
		MONEY -= 500
		PRINTFORML %PRINT_CALLNAME(ARG)%はアフターピルを飲んだ…
		PRINTFORML 身体の底に疲労が蓄積した……（＋１５）
		CFLAG:ARG:99 += 15
		IF RAND:100 < ARG:1
			CFLAG:ARG:241 = 1
			SIF CHECK_HININ_F(ARG, ARG:2) > 0 && CFLAG:ARG:222 == 0
				TALENT:ARG:妊娠 = 0
		ENDIF
		IF FLAG:999 == 1
			SETCOLOR 96,96,96
			PRINTL 
			PRINTFORML アフターピル成功率 {ARG:1}％
			PRINTFORML 避妊 = {CFLAG:ARG:241}　/　妊娠 = {TALENT:ARG:妊娠}
			PRINTL 
			RESETCOLOR
		ENDIF
	ELSE
		PRINTFORML なんと、所持金が足りない！
	ENDIF
ELSE
	PRINTFORML 妊娠の可能性に慌ててしまう気持ちも分かるが、ここは冷静になるべきだろう…
	PRINTW 
	CLEARLINE LINECOUNT - LCOUNT
	GOTO INPUT_LOOP_0
ENDIF



