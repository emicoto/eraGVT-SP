;**********************************************************
;基本ルール
;加筆修正は、自由に行ってください
;ただし、前の方が書いた文章は出来る限り消さないでください
;
;口述での「陰核」なので、吐息や詰まった表現などはありです
;体力や発情など、ステータスでどもったりする様にしましょう
;この変数を呼び出すときは、吃ることも含めてお使いください
;
;増やして欲しい隠語は、アウトプットしてリクエストなりしましょう
;未登録の中に放り込むなり、掲示板にアップするなりすれば、
;実装される可能性があります
;
;by 雑魚の人 2018/02/11
;**********************************************************
;Clitoris(クリトリス)の淫語
;**********************************************************
@S_SLANG_C(ARG)
#FUNCTIONS
#DIMS S_SLANGWORD_C

;初期化
S_SLANGWORD_C=

;オトコやふたなりは、ペニスをコール
IF TALENT:オトコ > 0 ||  TALENT:ふたなり > 0
	S_SLANGWORD_C=%S_SLANG_P(TARGET)%
ELSE
	IF TALENT:淫核
		S_SLANGWORD_C+="淫核"
	ELSE
		SELECTCASE RAND:3
			CASE 2
				S_SLANGWORD_C+="お豆さん"
			CASE 1
				S_SLANGWORD_C+="エッチな突起"
			CASEELSE
				S_SLANGWORD_C+="クリトリス"
		ENDSELECT
	ENDIF
ENDIF
RETURNF S_SLANGWORD_C

;*************【淫語覚書（未登録）】***********************
;
;肉豆
;肉芽
;陰核
;
;*************【淫語覚書（未登録）】***********************



