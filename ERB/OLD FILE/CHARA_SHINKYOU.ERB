;--------------------------------------------------
;キャラの心境に関する処理
;--------------------------------------------------
;心境による補正等
@SHINKYOU_CHECK, ARGS, ARG
IF TCVAR:1 == 0
	SELECTCASE ARGS
		CASE "PRINT"
			PRINT 普通
		CASE "PRINTSTATE"
			PRINT 普通（補正なし）
	ENDSELECT
	RETURN ARG
ELSEIF TCVAR:1 == 1
	SELECTCASE ARGS
		CASE "PRINT"
			PRINT 倒錯
		CASE "PRINTSTATE"
			PRINT 倒錯（攻・防・敏ダウン）
		CASE "KOUGEKI"
			TIMES ARG, 0.80
		CASE "BOUGYO"
			TIMES ARG, 0.80
		CASE "BINSYOU"
			TIMES ARG, 0.80
		CASE "CHISEI"
	ENDSELECT
	RETURN ARG
ELSEIF TCVAR:1 == 2
	SELECTCASE ARGS
		CASE "PRINT"
			PRINT 怒り
		CASE "PRINTSTATE"
			PRINT 怒り（攻撃・防御アップ）
		CASE "KOUGEKI"
			TIMES ARG, 1.20
		CASE "BOUGYO"
			TIMES ARG, 1.20
		CASE "BINSYOU"
		CASE "CHISEI"
			TIMES ARG, 0.80
	ENDSELECT
	RETURN ARG
ELSEIF TCVAR:1 == 3
	SELECTCASE ARGS
		CASE "PRINT"
			PRINT 諦観
		CASE "PRINTSTATE"
			PRINT 諦観（攻撃・防御ダウン）
		CASE "KOUGEKI"
			TIMES ARG, 0.80
		CASE "BOUGYO"
			TIMES ARG, 0.80
		CASE "BINSYOU"
		CASE "CHISEI"
			TIMES ARG, 0.80
	ENDSELECT
	RETURN ARG
ELSEIF TCVAR:1 == 4
	SELECTCASE ARGS
		CASE "PRINT"
			PRINT 冷静
		CASE "PRINTSTATE"
			PRINT 冷静（防御・敏捷アップ）
		CASE "KOUGEKI"
		CASE "BOUGYO"
			TIMES ARG, 1.20
		CASE "BINSYOU"
			TIMES ARG, 1.20
		CASE "CHISEI"
			TIMES ARG, 1.20
	ENDSELECT
	RETURN ARG
ELSEIF TCVAR:1 == 5
	SELECTCASE ARGS
		CASE "PRINT"
			PRINT 動揺
		CASE "PRINTSTATE"
			PRINT 動揺（防御・敏捷ダウン）
		CASE "KOUGEKI"
		CASE "BOUGYO"
			TIMES ARG, 0.80
		CASE "BINSYOU"
			TIMES ARG, 0.80
		CASE "CHISEI"
			TIMES ARG, 1.20
	ENDSELECT
	RETURN ARG
ELSEIF TCVAR:1 == 6
	SELECTCASE ARGS
		CASE "PRINT"
			PRINT 高揚
		CASE "PRINTSTATE"
			PRINT 高揚（攻撃・敏捷アップ）
		CASE "KOUGEKI"
			TIMES ARG, 1.20
		CASE "BOUGYO"
		CASE "BINSYOU"
			TIMES ARG, 1.20
		CASE "CHISEI"
	ENDSELECT
	RETURN ARG
ELSEIF TCVAR:1 == 7
	SELECTCASE ARGS
		CASE "PRINT"
			PRINT 消沈
		CASE "PRINTSTATE"
			PRINT 消沈（攻撃・敏捷ダウン）
		CASE "KOUGEKI"
			TIMES ARG, 0.80
		CASE "BOUGYO"
		CASE "BINSYOU"
			TIMES ARG, 0.80
		CASE "CHISEI"
	ENDSELECT
	RETURN ARG
ENDIF


;--------------------------------------------------
;心境変化のチェック

;--------------------------------------------------
;共通処理
@SHINKYOU_CHANGE, ARGS
LOCAL = 0
;心境強度の判定
SELECTCASE TCVAR:1
	;倒錯
	CASE 1
		SIF (MESSAGE_BRANCH_F(TARGET) & 堕落)
			LOCAL += 1
		SIF (MESSAGE_BRANCH_F(TARGET) & 快楽蕩け)
			LOCAL += 2
		SIF (MESSAGE_BRANCH_F(TARGET) & 性抵抗)
			LOCAL += 1
		SIF TCVAR:11 < 1
			LOCAL = 20
	;怒り
	CASE 2
		SIF (MESSAGE_BRANCH_F(TARGET) & 苦戦) == 0
			LOCAL += 1
		SIF ARGS == "REISEI"
			LOCAL += 1
		SIF TCVAR:0 == 0
			LOCAL += 1
		SIF TCVAR:11 < 2
			LOCAL = 20
	;諦観
	CASE 3
		SIF (MESSAGE_BRANCH_F(TARGET) & 絶望)
			LOCAL += 2
		SIF (MESSAGE_BRANCH_F(TARGET) & 苦戦)
			LOCAL += 1
		SIF ARGS != "TOUSAKU" && ARGS != "DOUYOU" && ARGS != "SYOUTIN"
			LOCAL += 1
		SIF TCVAR:0 == 0
			LOCAL += 2
		SIF TCVAR:11 < 1
			LOCAL = 20
	;冷静
	CASE 4
		SIF (MESSAGE_BRANCH_F(TARGET) & 絶望)
			LOCAL += 2
		SIF (MESSAGE_BRANCH_F(TARGET) & 性抵抗) == 0
			LOCAL += 1
		SIF ARGS == "KOUYOU"
			LOCAL += 1
		SIF TCVAR:11 < 2
			LOCAL = 20
	;動揺
	CASE 5
		SIF (MESSAGE_BRANCH_F(TARGET) & 絶望)
			LOCAL += 1
		SIF (MESSAGE_BRANCH_F(TARGET) & 苦戦)
			LOCAL += 1
		SIF TCVAR:0 == 0
			LOCAL += 2
		SIF TCVAR:11 < 1
			LOCAL = 20
	;高揚
	CASE 6
		SIF (MESSAGE_BRANCH_F(TARGET) & 苦戦) == 0
			LOCAL += 1
		SIF (MESSAGE_BRANCH_F(TARGET) & 性抵抗) == 0
			LOCAL += 1
		SIF TCVAR:11 < 2
			LOCAL = 20
		SIF TCVAR:0 == 0
			LOCAL = 0
	;消沈
	CASE 7
		SIF (MESSAGE_BRANCH_F(TARGET) & 絶望)
			LOCAL += 1
		SIF (MESSAGE_BRANCH_F(TARGET) & 苦戦)
			LOCAL += 1
		SIF TCVAR:11 < 1
			LOCAL = 20
ENDSELECT
;心境強度に応じて心境が変わりにくくなる
SIF RAND:100 < LOCAL * 5
		RETURN 0

TRYCALLFORM SHINKYOU_CHANGE_%ARGS%

;--------------------------------------------------
;普通
;呼び出されたら無条件で普通に戻る
@SHINKYOU_CHANGE_NORMAL
SIF TCVAR:1 == 0
	RETURN
TCVAR:1 = 0
PRINTL 
PRINTFORM %PRINT_CALLNAME(TARGET,1)%の心境が
CALL SHINKYOU_CHECK, "PRINT", 0
PRINTL に戻った
;心境の持続ターンをリセット
TCVAR:11 = 0

;--------------------------------------------------
;倒錯
;体力60%以下かつ気力60%以下で倒錯になる判定をかける
@SHINKYOU_CHANGE_TOUSAKU
LOCAL:0 = 0
LOCAL:1 = 0
CALL PERCENT_CAL, BASE:体力, MAXBASE:体力
LOCAL:0 = RESULT
CALL PERCENT_CAL, BASE:気力, MAXBASE:気力
LOCAL:1 = RESULT
;SP変身中は気力体力の減少による影響を受けない
IF CFLAG:1 == 2
	LOCAL:0 = 100
	LOCAL:1 = 100
ENDIF
IF LOCAL:0 <= 60 && LOCAL:1 <= 60
CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", TARGET
	CALL SEIKAKU_HOSEI_SHINKYOU, RESULT, "TOUSAKU"
	IF RAND:100 < RESULT
		IF TCVAR:1 != 1
			TCVAR:1 = 1
			CALL MESSAGE_SHINKYOU_CHANGE_TOUSAKU
			;心境の持続ターンをリセット
			TCVAR:11 = 0
		ENDIF
	ENDIF
ENDIF


;--------------------------------------------------
;怒り/諦観
;体力60%以上かつ気力60%以上で怒りになる判定をかける
;体力20%以下かつ気力20%以下で諦観になる判定をかける
@SHINKYOU_CHANGE_IKARI_TEIKAN
LOCAL:0 = 0
LOCAL:1 = 0
CALL PERCENT_CAL, BASE:体力, MAXBASE:体力
LOCAL:0 = RESULT
CALL PERCENT_CAL, BASE:気力, MAXBASE:気力
LOCAL:1 = RESULT
;SP変身中は気力体力の減少による影響を受けない
IF CFLAG:1 == 2
	LOCAL:0 = 100
	LOCAL:1 = 100
ENDIF
IF LOCAL:0 >= 60 && LOCAL:1 >= 60
	CALL SHINKYOU_CHANGE_IKARI
ELSEIF LOCAL:0 <= 20 && LOCAL:1 <= 20
	CALL SHINKYOU_CHANGE_TEIKAN
ENDIF

@SHINKYOU_CHANGE_IKARI
CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", TARGET
CALL SEIKAKU_HOSEI_SHINKYOU, RESULT, "IKARI"
IF RAND:100 < RESULT
	IF TCVAR:1 != 2
		TCVAR:1 = 2
		;地の文：怒り
		CALL MESSAGE_SHINKYOU_CHANGE_IKARI
		;心境の持続ターンをリセット
		TCVAR:11 = 0
	ENDIF
ENDIF

@SHINKYOU_CHANGE_TEIKAN
CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", TARGET
CALL SEIKAKU_HOSEI_SHINKYOU, RESULT, "TEIKAN"
IF RAND:100 < RESULT
	IF TCVAR:1 != 3
		TCVAR:1 = 3
		;地の文：諦観
		CALL MESSAGE_SHINKYOU_CHANGE_TEIKAN
		;心境の持続ターンをリセット
		TCVAR:11 = 0
	ENDIF
ENDIF

;--------------------------------------------------
;冷静/動揺
;体力60%以上かつ気力60%以上で冷静になる判定をかける
;体力20%以下かつ気力20%以下で動揺になる判定をかける
@SHINKYOU_CHANGE_REISEI_DOUYOU
LOCAL:0 = 0
LOCAL:1 = 0
CALL PERCENT_CAL, BASE:体力, MAXBASE:体力
LOCAL:0 = RESULT
CALL PERCENT_CAL, BASE:気力, MAXBASE:気力
LOCAL:1 = RESULT
;SP変身中は気力体力の減少による影響を受けない
IF CFLAG:1 == 2
	LOCAL:0 = 100
	LOCAL:1 = 100
ENDIF
IF LOCAL:0 >= 60 && LOCAL:1 >= 60
	CALL SHINKYOU_CHANGE_REISEI
ELSEIF LOCAL:0 <= 20 && LOCAL:1 <= 20
	CALL SHINKYOU_CHANGE_DOUYOU
ENDIF

@SHINKYOU_CHANGE_REISEI
CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", TARGET
CALL SEIKAKU_HOSEI_SHINKYOU, RESULT, "REISEI"
IF RAND:100 < RESULT
	IF TCVAR:1 != 4
		TCVAR:1 = 4
		;地の文：冷静
		CALL MESSAGE_SHINKYOU_CHANGE_REISEI
		;心境の持続ターンをリセット
		TCVAR:11 = 0
	ENDIF
ENDIF

@SHINKYOU_CHANGE_DOUYOU
CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", TARGET
CALL SEIKAKU_HOSEI_SHINKYOU, RESULT, "DOUYOU"
IF RAND:100 < RESULT
	IF TCVAR:1 != 5
		TCVAR:1 = 5
		;地の文：動揺
		CALL MESSAGE_SHINKYOU_CHANGE_DOUYOU
		;心境の持続ターンをリセット
		TCVAR:11 = 0
	ENDIF
ENDIF

;--------------------------------------------------
;高揚/消沈
;体力60%以上かつ気力60%以上で高揚になる判定をかける
;体力20%以下かつ気力20%以下で消沈になる判定をかける
@SHINKYOU_CHANGE_KOUYOU_SYOUTIN
LOCAL:0 = 0
LOCAL:1 = 0
CALL PERCENT_CAL, BASE:体力, MAXBASE:体力
LOCAL:0 = RESULT
CALL PERCENT_CAL, BASE:気力, MAXBASE:気力
LOCAL:1 = RESULT
;SP変身中は気力体力の減少による影響を受けない
IF CFLAG:1 == 2
	LOCAL:0 = 100
	LOCAL:1 = 100
ENDIF
IF LOCAL:0 >= 60 && LOCAL:1 >= 60
	CALL SHINKYOU_CHANGE_KOUYOU
ELSEIF LOCAL:0 <= 20 && LOCAL:1 <= 20
	CALL SHINKYOU_CHANGE_SYOUTIN
ENDIF

@SHINKYOU_CHANGE_KOUYOU
CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", TARGET
CALL SEIKAKU_HOSEI_SHINKYOU, RESULT, "KOUYOU"
IF RAND:100 < RESULT
	IF TCVAR:1 != 6
		TCVAR:1 = 6
		;地の文：高揚
		CALL MESSAGE_SHINKYOU_CHANGE_KOUYOU
		;心境の持続ターンをリセット
		TCVAR:11 = 0
	ENDIF
ENDIF

@SHINKYOU_CHANGE_SYOUTIN
CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", TARGET
CALL SEIKAKU_HOSEI_SHINKYOU, RESULT, "SYOUTIN"
IF RAND:100 < RESULT
	IF TCVAR:1 != 7
		TCVAR:1 = 7
		;地の文：消沈
		CALL MESSAGE_SHINKYOU_CHANGE_SYOUTIN
		;心境の持続ターンをリセット
		TCVAR:11 = 0
	ENDIF
ENDIF


