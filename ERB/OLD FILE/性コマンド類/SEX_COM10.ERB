;手淫 弄るのと同時に弄られる

;性コマンド分類
@SEX_TYPE_COM10
RETURN 通常

@SEX_COM10,ARG = 0,ARG:1 = 0
;EX_COM	追加責め実行部位（ビット）
;	1：Ｃ　2：Ｖ　4：Ａ　8：Ｂ
;追加責め（特定部位なし）
CALL SEX_COMEX_RANDOM, 0, ARG:1
EX_COM = RESULT:0
SH_COM = RESULT:1

;射精部位フラグ:手を立てる
TFLAG:4 = TFLAG:4 | 手
;我慢：確率で射精部位フラグを折る
SIF RAND:100 < 75
	TFLAG:4 = 0

;悪堕ちキャラのペニス位置をセット
SIF FLAG:110 == 1 && (TALENT:(FLAG:111):オトコ > 0 || TALENT:(FLAG:111):ふたなり > 0)
	TFLAG:18 = 手

;取得パラメータ計算
;ベース値の明示的初期化
REPEAT 12
	LOCAL:COUNT = 0
REND

;油断度の上昇
TFLAG:3 += 25

;射精値 150
CALL TENTACLE_SYASEI_UP, 150

;触手サイズの設定
CALL SET_TENTACLE_SIZE, FLAG:10, FLAG:11, FLAG:110, FLAG:111, 10

IF FLAG:10 == 2
	CALLFORM MESSAGE_MOB_{ARG}_COM10, EX_COM, SH_COM
ELSE
	;地の文：洗脳/悪堕ちキャラが操作キャラに手淫をさせた
	SIF FLAG:110 == 1
		CALL MESSAGE_OTHER_SEX_COM10
	;地の文：COM10
	CALL MESSAGE_SEX_COM10, EX_COM, SH_COM
ENDIF

;結界による触手本数の変動
CALL SET_TENTACLE_POOL
;服へのダメージ
CALL CLOTH_BATTLE_DAMAGE, 5

;ベース
EXP:奉仕快楽経験 += 1

;恭順
IF ABL:従順 <= 2
	LOCAL:5 = 200
ELSEIF ABL:従順 == 3
	LOCAL:5 = 500
ELSEIF ABL:従順 == 4
	LOCAL:5 = 1000
ELSEIF ABL:従順 >= 5
	LOCAL:5 = 2000
ENDIF


;習得
IF ABL:奉仕精神 == 0
	LOCAL:6 = 50
ELSEIF ABL:奉仕精神 == 1
	LOCAL:6 = 100
ELSEIF ABL:奉仕精神 == 2
	LOCAL:6 = 200
ELSEIF ABL:奉仕精神 == 3
	LOCAL:6 = 500
ELSEIF ABL:奉仕精神 == 4
	LOCAL:6 = 1000
ELSEIF ABL:奉仕精神 >= 5
	LOCAL:6 = 2000
ENDIF

;奉仕精神に応じてボーナス
IF ABL:奉仕精神 < 3
ELSEIF ABL:奉仕精神 < 4
	LOCAL:5 += 500
	LOCAL:6 += 200
ELSEIF ABL:奉仕精神 < 5
	LOCAL:5 += 1000
	LOCAL:6 += 500
ELSE
	LOCAL:5 += 2000
	LOCAL:6 += 1000
ENDIF

;追加責め実行
IF EX_COM
	CALL SEX_COMEX, 0, 0, EX_COM | SH_COM
	REPEAT 12
		LOCAL:COUNT += RESULT:COUNT
	REND
ENDIF

;行動予約なし
TFLAG:17 = -1
;触手の行動を記録
TFLAG:20 = 10

;習得基本値
LOCAL:6 += 2000

;欲情基本値
LOCAL:7 += 0

;屈服基本値
LOCAL:8 += 400

;恥情基本値
LOCAL:9 += 100

;苦痛基本値
LOCAL:10 += 0

;恐怖基本値
LOCAL:11 += 0

;体力消費基本値
LOCAL:12 += 50

;計算へ
CALL PALAM_CAL, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12
