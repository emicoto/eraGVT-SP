;V攻め強

;性コマンド分類
@SEX_TYPE_COM3
RETURN 挿入

@SEX_COM3,ARG = 0,ARG:1 = 0
;EX_COM	追加責め実行部位（ビット）
;	1：Ｃ　2：Ｖ　4：Ａ　8：Ｂ
;追加責め
CALL SEX_COMEX_RANDOM, Ｖ, ARG:1
EX_COM = RESULT:0
SH_COM = RESULT:1

;清純派なら１度だけ自動でＶ防御
SIF TALENT:清純派 > 0 && TCVAR:2 != 100
	CALL AUTO_V_DEFENCE, TARGET

;射精部位フラグ:膣を立てる、Ｖ防御しているなら口を立てる
IF TCVAR:2 == 体勢：Ｖ防御 || BASE:Ｖ結界耐久力 > 0
	TFLAG:4 = TFLAG:4 | 口
	;結界がなければワレメ射精フラグを立てる
	SIF BASE:Ｖ結界耐久力 <= 0
		TFLAG:4 = TFLAG:4 | ワレメ
ELSE
	TFLAG:4 = TFLAG:4 | 膣
	INSERT = INSERT | 膣
ENDIF
;Ａ追加攻めがあるならアナル射精フラグを立てる
IF (EX_COM & Ａ)
	TFLAG:4 = TFLAG:4 | アナル
	INSERT = INSERT | アナル
ENDIF

;悪堕ちキャラのペニス位置をセット
SIF FLAG:110 == 1 && (TALENT:(FLAG:111):オトコ > 0 || TALENT:(FLAG:111):ふたなり > 0)
	TFLAG:18 = 膣

;取得パラメータ計算
;ベース値の明示的初期化
REPEAT 12
	LOCAL:COUNT = 0
REND

;油断度の上昇
TFLAG:3 += 50

;射精値 250
CALL TENTACLE_SYASEI_UP, 250

;触手サイズの設定
CALL SET_TENTACLE_SIZE, FLAG:10, FLAG:11, FLAG:110, FLAG:111, 3

IF FLAG:10 == 2
	CALLFORM MESSAGE_MOB_{ARG}_COM3, EX_COM, SH_COM
	;処女なら苦痛が大量に入る
	IF TALENT:処女 > 0 && TCVAR:2 != 体勢：Ｖ防御 && BASE:Ｖ結界耐久力 <= 0
		LOCAL:10 = 10000
		TALENT:処女 = -1
		CFLAG:206 = 2
		TCVAR:1 = 1
	ELSE
		LOCAL:10 = 0
	ENDIF
ELSE
	;処女なら苦痛が大量に入る
	IF TALENT:処女 > 0 && TCVAR:2 != 体勢：Ｖ防御 && BASE:Ｖ結界耐久力 <= 0
		LOCAL:10 = 10000
		CALL LOSTVIRGIN
	ELSE
		LOCAL:10 = 0
	ENDIF
	;地の文：洗脳/悪堕ちキャラが操作キャラにV攻め強をした
	SIF FLAG:110 == 1
		CALL MESSAGE_OTHER_SEX_COM3
	;地の文:COM3
	CALL MESSAGE_SEX_COM3, EX_COM, SH_COM
ENDIF

;結界による触手本数の変動
CALL SET_TENTACLE_POOL
;服へのダメージ
CALL CLOTH_BATTLE_DAMAGE, 8

;ベース
IF TCVAR:2 == 体勢：Ｖ防御
	EXP:Ｖ経験 += 1
	SIF FLAG:15 >= FLAG:14
		EXP:フェラ経験 += 1
ELSEIF EX_COM & Ｖ
	EXP:Ｖ経験 += 2
	SIF FLAG:110 == 0 && INCEST_F(TARGET, FLAG:10 * 100 + FLAG:11, 1) > 0
		EXP:近親交配経験 += 1
	SIF FLAG:110 == 1 && INCEST_F(TARGET, FLAG:111) > 0
		EXP:近親交配経験 += 1
ENDIF

;快V
IF ABL:Ｖ感覚 == 0
	LOCAL:1 = 400
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL:1 = 1000
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL:1 = 2000
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL:1 = 4000
ELSEIF ABL:Ｖ感覚 == 4
	LOCAL:1 = 10000
ELSEIF ABL:Ｖ感覚 >= 5
	LOCAL:1 = 20000
ENDIF
;Ｖ防御効果
SIF TCVAR:2 == 体勢：Ｖ防御
	LOCAL:1 /= 4

;恭順
IF ABL:従順 <= 2
	LOCAL:5 = 200
ELSEIF ABL:従順 == 3
	LOCAL:5 = 500
ELSEIF ABL:従順 == 4
	LOCAL:5 = 1000
ELSEIF ABL:従順 >= 5
	LOCAL:5 = 2000
ENDIF

;習得
IF ABL:奉仕精神 == 0
	LOCAL:6 = 20
ELSEIF ABL:奉仕精神 == 1
	LOCAL:6 = 50
ELSEIF ABL:奉仕精神 == 2
	LOCAL:6 = 100
ELSEIF ABL:奉仕精神 == 3
	LOCAL:6 = 200
ELSEIF ABL:奉仕精神 == 4
	LOCAL:6 = 500
ELSEIF ABL:奉仕精神 >= 5
	LOCAL:6 = 1000
ENDIF

;潤滑が低いと苦痛が入る
IF PALAM:潤滑 < 1000
	LOCAL:10 += 5000
ELSEIF PALAM:潤滑 < 2000
	LOCAL:10 += 1000
ENDIF
SIF BASE:Ｖ結界耐久力 > 0
	LOCAL:10 = 0

;V感覚が低いと恐怖が入る
IF ABL:Ｖ感覚 == 0
	LOCAL:11 = 4000
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL:11 = 1000
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL:11 = 400
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL:11 = 200
ELSE
ENDIF
SIF BASE:Ｖ結界耐久力 > 0
	LOCAL:11 /= 2

;追加責め実行
IF EX_COM
	CALL SEX_COMEX, Ｖ, 1, EX_COM | SH_COM
	REPEAT 12
		LOCAL:COUNT += RESULT:COUNT
	REND
ENDIF

;性耐性が残り50%以上なら派生しにくい
IF PERCENT_CAL_F(BASE:性耐性, MAXBASE:性耐性) >= 50 && RAND:2 == 0
;ＶＡ挿入成功＆射精ゲージ不足なら両穴ピストン＋強拘束状態
ELSEIF (EX_COM & Ｖ) && (EX_COM & Ａ) && FLAG:15 < FLAG:14 && BASE:Ｖ結界耐久力 == 0 && BASE:Ａ結界耐久力 == 0 && TCVAR:2 != 体勢：Ｖ防御 && FLAG:10 != 2 && TALENT:オトコ <= 0
	TFLAG:17 = 19
	;状態異常：強拘束
	CALL STATE_CHANGE_PKOUSOKU, 100
;Ｖ挿入成功＆射精ゲージ不足なら種付けピストン＋強拘束状態
ELSEIF FLAG:15 < FLAG:14 && BASE:Ｖ結界耐久力 == 0 && TCVAR:2 != 体勢：Ｖ防御 && FLAG:10 != 2 && TALENT:オトコ <= 0
	TFLAG:17 = 15
	;状態異常：強拘束
	CALL STATE_CHANGE_PKOUSOKU, 100
ELSE
	;行動予約なし
	TFLAG:17 = -1
ENDIF
;触手の行動を記録
TFLAG:20 = 3

;習得基本値
LOCAL:6 += 1500

;欲情基本値
LOCAL:7 += 0

;屈服基本値
LOCAL:8 += 2500

;恥情基本値
LOCAL:9 += 200

;苦痛基本値
LOCAL:10 += 100

;恐怖基本値
LOCAL:11 += 1000

;体力消費基本値
LOCAL:12 += 200

;計算へ
CALL PALAM_CAL, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12
