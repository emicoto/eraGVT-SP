;種付けピストン（Ｖ挿入系統からの派生限定行動）

;性コマンド分類
@SEX_TYPE_COM15
RETURN 挿入

@SEX_COM15,ARG = 0,ARG:1 = 0
;EX_COM	追加責め実行部位（ビット）
;	1：Ｃ　2：Ｖ　4：Ａ　8：Ｂ
;追加責め
CALL SEX_COMEX_RANDOM, Ｖ, ARG:1
EX_COM = RESULT:0
SH_COM = RESULT:1

;状態異常：強拘束
SIF (TCVAR:12 & 強拘束) == 0
	CALL STATE_CHANGE_PKOUSOKU, 100

;悪堕ちキャラのペニス位置をセット
SIF FLAG:110 == 1 && (TALENT:(FLAG:111):オトコ > 0 || TALENT:(FLAG:111):ふたなり > 0)
	TFLAG:18 = 膣

;取得パラメータ計算
;ベース値の明示的初期化
REPEAT 12
	LOCAL:COUNT = 0
REND

;油断度の上昇
TFLAG:3 += 5

;射精値 450
CALL TENTACLE_SYASEI_UP, 450

;触手サイズの設定
CALL SET_TENTACLE_SIZE, FLAG:10, FLAG:11, FLAG:110, FLAG:111, 15

IF FLAG:10 == 2
	CALLFORM MESSAGE_MOB_{ARG}_COM15, EX_COM, SH_COM
ELSE
	;地の文：洗脳/悪堕ちキャラが操作キャラに種付けピストンをした
	SIF FLAG:110 == 1
		CALL MESSAGE_OTHER_SEX_COM15
	;地の文:COM2
	CALL MESSAGE_SEX_COM15, EX_COM, SH_COM
ENDIF

;結界による触手本数の変動
CALL SET_TENTACLE_POOL
;服へのダメージ
CALL CLOTH_BATTLE_DAMAGE, 2

;ベース
EXP:Ｖ経験 += 1
SIF FLAG:110 == 0 && INCEST_F(TARGET, FLAG:10 * 100 + FLAG:11, 1) > 0
	EXP:近親交配経験 += 1
SIF FLAG:110 == 1 && INCEST_F(TARGET, FLAG:111) > 0
	EXP:近親交配経験 += 1

;快V
IF ABL:Ｖ感覚 == 0
	LOCAL:1 = 40
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL:1 = 100
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL:1 = 200
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL:1 = 400
ELSEIF ABL:Ｖ感覚 == 4
	LOCAL:1 = 1000
ELSEIF ABL:Ｖ感覚 >= 5
	LOCAL:1 = 2000
ENDIF

;恭順
IF ABL:従順 <= 2
	LOCAL:5 = 20
ELSEIF ABL:従順 == 3
	LOCAL:5 = 50
ELSEIF ABL:従順 == 4
	LOCAL:5 = 100
ELSEIF ABL:従順 >= 5
	LOCAL:5 = 200
ENDIF

;習得
IF ABL:奉仕精神 == 0
	LOCAL:6 = 10
ELSEIF ABL:奉仕精神 == 1
	LOCAL:6 = 25
ELSEIF ABL:奉仕精神 == 2
	LOCAL:6 = 50
ELSEIF ABL:奉仕精神 == 3
	LOCAL:6 = 100
ELSEIF ABL:奉仕精神 == 4
	LOCAL:6 = 250
ELSEIF ABL:奉仕精神 >= 5
	LOCAL:6 = 500
ENDIF

;潤滑が低いと苦痛が入る
IF PALAM:潤滑 < 1000
	LOCAL:10 += 1000
ELSEIF PALAM:潤滑 < 2000
	LOCAL:10 += 200
ENDIF

;V感覚が低いと恐怖が入る
IF ABL:Ｖ感覚 == 0
	LOCAL:11 = 400
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL:11 = 100
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL:11 = 40
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL:11 = 20
ELSE
ENDIF

;追加責め実行
IF EX_COM
	CALL SEX_COMEX, Ｖ, 1, EX_COM | SH_COM
	REPEAT 12
		LOCAL:COUNT += RESULT:COUNT
	REND
ENDIF

;大量射精可能ならフィニッシュに移行
IF FLAG:15 >= FLAG:14 * 2
	TFLAG:17 = 16
;Ａ追加責めが立っていれば両穴ピストンに移行
ELSEIF (EX_COM & Ａ) && FLAG:15 >= FLAG:14 && BASE:Ａ結界耐久力 == 0 && FLAG:10 != 2 && (TALENT:オトコ <= 0 || FILTER_CHECK_F(5) == 0)
	TFLAG:17 = 19
;それ以外なら種付け続行
ELSE
	TFLAG:17 = 15
ENDIF
;触手の行動を記録
TFLAG:20 = 15

;習得基本値
LOCAL:6 += 150

;欲情基本値
LOCAL:7 += 100

;屈服基本値
LOCAL:8 += 100

;恥情基本値
LOCAL:9 += 100

;苦痛基本値
LOCAL:10 += 50

;恐怖基本値
LOCAL:11 += 150

;体力消費基本値
LOCAL:12 += 1

;計算へ
CALL PALAM_CAL, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12
