;------------------------------
;育児判定(適当
;------------------------------
@GROW_HANTEI
#DIM CCOUNT
LOCAL = TARGET
FOR CCOUNT, 0, CHARANUM
	TARGET = CCOUNT
	;育児中なら育児日数を増加
	IF CFLAG:0 == 11 || CFLAG:22 > 0
		CFLAG:224 += 1
		;確率で育児終了　最短2日最長5日
		SIF (CFLAG:224 >= 4 && RAND:2 == 0) || CFLAG:224 >= 10
			CALL ADD_CHILD,TARGET
	ENDIF
	IF TALENT:性徴停滞 > 0
	ELSEIF CFLAG:225 > 14
		CALL CHILD_GROW_2
		CFLAG:225 = 0
	ELSEIF CFLAG:225 == 7
		CALL CHILD_GROW_1
		CFLAG:225 += 1
	ELSEIF CFLAG:225 > 0
		CFLAG:225 += 1
	ENDIF
NEXT
TARGET = LOCAL

;-------------------------
;鍛錬変化時の表示
;-------------------------
@TRAINING_HOSEI_CHILD,ARG
LOCAL:4 = MARK:血族補正
SIF LOCAL:4 <= 0
	RETURN ARG
SIF LOCAL:4 >= 12
	LOCAL:4 = 12
LOCAL:2 = 3 + LOCAL:4
LOCAL:3 = 2 + LOCAL:4
LOCAL:1 = ARG
FOR LOCAL,0,LOCAL:4
	LOCAL:1 = LOCAL:1 * LOCAL:2
NEXT
FOR LOCAL,0,LOCAL:4
	LOCAL:1 = LOCAL:1 / LOCAL:3
NEXT
RETURN LOCAL:1

;------------------------------
;出産(娘)
;------------------------------
;妊娠日数のリセット、素質[妊娠]の削除
@CHILD_BIRTH

;出産するキャラは疲労度が蓄積する
CFLAG:99 += 45
LOCAL:0 = 0
DRAWLINE
FONTBOLD
PRINTFORML 出産（%PRINT_CALLNAME(TARGET,1)%）
FONTREGULAR
IF CFLAG:0 != 10
	CALL MESSAGE_PRISON_CHILD_SHUSSAN
ELSE
	CALL MESSAGE_CHILD_SHUSSAN
ENDIF

CALL ABL_UP_BIRTH,1
CALL _ABLUP,1

;妊娠日数中だし回数のリセット、素質[妊娠]の削除
CFLAG:221 = 0
CFLAG:222 = 0
CFLAG:227 = 0
CFLAG:228 = 0
CFLAG:232 = 0
CFLAG:233 = 0
TALENT:妊娠 = 0

IF CFLAG:0 == 10 && MONEY >= 5000 && CHARANUM < (登録最大人数 + 1)
	DRAWLINE
	PRINTFORML 娘が産まれた
	PRINTFORML 娘を育てるためには養育費として$5000かかります
	PRINTFORML 娘を育てますか？
	PRINTL
	PRINTFORML [0]いいえ
	PRINTFORML [1]はい（所持金:${MONEY}）
	$INPUT_CHILD_CRADLE
	INPUT
	IF RESULT == 1
		MONEY -= 5000
		CALL MESSAGE_CHILD_CRADLE
		CFLAG:0 = 11
	ELSEIF RESULT == 0
		CALL MESSAGE_CHILD_ASLYM
		CALL RECOVER_TO_PARTY,TARGET
	ELSE
		PRINTL 正しい数値を入力してください
		GOTO INPUT_CHILD_CRADLE
	ENDIF
ELSEIF CFLAG:0 == 1 || CFLAG:0 == 2 || CFLAG:0 == 3
	IF CHARANUM < (登録最大人数 + 1) && CFLAG:22 == 0
		DRAWLINE
		PRINTL 娘が産まれた
		IF CFLAG:0 == 1
			PRINTFORML 今の%PRINT_CALLNAME(TARGET)%にはどうすることもできないが、
			IF FLAG:0 == 2
				PRINTL いずれ助け出すチャンスが巡ってくるかもしれない。
			ELSEIF FLAG:0 != 2 && CHARANUM_ALIVE()
				PRINTL 仲間が娘を救出してくれるかもしれない。
			ELSE
				PRINTL いつか誰かが助けてくれるかもしれない。
			ENDIF
		ELSEIF CFLAG:0 == 2 && TALENT:主観視点 > 0
			PRINTFORML %PRINT_CALLNAME(TARGET)%の胸の奥底にはまだ微かに我が子を思う心が残っている……
		ELSEIF CFLAG:0 == 3 && TALENT:主観視点 > 0
			PRINTFORML %PRINT_CALLNAME(TARGET)%は甘えるような声で触手に"娘を育てたい"と懇願した……
		ELSEIF CFLAG:0 == 2
			PRINTFORML 虚ろな瞳のまま反応を示さないように見える%PRINT_CALLNAME(TARGET)%だが、
			PRINTFORML その胸の奥底にはまだ微かに我が子を思う心が残っているようだ……
		ELSEIF CFLAG:0 == 3
			PRINTFORML %PRINT_CALLNAME(TARGET)%は甘えるような声で触手に"娘を育てたい"と懇願している。
			PRINTFORML 闇に堕ちた身であっても我が子は愛しいようだ。
		ENDIF
		PRINTL 無事を祈って名前を付けてあげますか？
		PRINTL
		PRINTL [0]いいえ
		PRINTL [1]はい
		$INPUT_CHILD_PRAYER
		INPUT
		IF RESULT == 1
			$INPUT_CHILD_NAME_YN
			PRINTL キャラの名前を自分で決めますか？
			PRINTL [0]いいえ（ランダム生成）
			PRINTL [1]はい（手動入力）
			INPUT
			IF RESULT == 0
				$INPUT_LOOP_CHILD_NAME_RANDOM
				PRINTL [0]日本語で構成
				PRINTL [1]英語で構成
				PRINTL [2]フランス語で構成
				PRINTL [3]ドイツ語で構成
				PRINTL [4]イタリア語で構成
				PRINTL [5]ロシア語で構成
				PRINTL [99]完全ランダム
				INPUT
				LOCAL:99 = RESULT
				$INPUT_LOOP_NAME_KOSEI
				IF LOCAL:99  == 99
					;名前
					LOCAL:1 = RAND:4000 + 12000
					WHILE STR:(LOCAL:1) == ""
						LOCAL:1 = RAND:4000 + 12000
					WEND
					LOCALS:1 = %STR:(LOCAL:1)%
				ELSEIF LOCAL:99  == 0
					;名前
					LOCAL = RAND:500 + 12000
					WHILE STR:LOCAL == ""
						LOCAL = RAND:500 + 12000
					WEND
					LOCALS:1 = %STR:LOCAL%
				ELSEIF LOCAL:99  == 1
					;名前
					LOCAL = RAND:1000 + 12500
					WHILE STR:LOCAL == ""
						LOCAL = RAND:1000 + 12500
					WEND
					LOCALS:1 = %STR:LOCAL%
				ELSEIF LOCAL:99  == 2
					;名前
					LOCAL = RAND:500 + 13500
					WHILE STR:LOCAL == ""
						LOCAL = RAND:500 + 13500
					WEND
					LOCALS:1 = %STR:LOCAL%
				ELSEIF LOCAL:99  == 3
					;名前
					LOCAL = RAND:1000 + 14000
					WHILE STR:LOCAL == ""
						LOCAL = RAND:1000 + 14000
					WEND
					LOCALS:1 = %STR:LOCAL%
				ELSEIF LOCAL:99  == 4
					;名前
					LOCAL = RAND:500 + 15000
					WHILE STR:LOCAL == ""
						LOCAL = RAND:500 + 15000
					WEND
					LOCALS:1 = %STR:LOCAL%
				ELSEIF LOCAL:99  == 5
					;名前
					LOCAL = RAND:500 + 15500
					WHILE STR:LOCAL == ""
						LOCAL = RAND:500 + 15500
					WEND
					LOCALS:1 = %STR:LOCAL%
				ELSE
					GOTO INPUT_LOOP_CHILD_NAME_RANDOM
				ENDIF
			ELSEIF RESULT == 1
				$INPUT_CHILD_NAME_INPUT
				PRINTL キャラの名前を入力してください
				INPUTS
				IF RESULTS == ""
					GOTO INPUT_CHILD_NAME_INPUT
				ELSE
					LOCALS:1 = %RESULTS%
				ENDIF
			ELSE
				PRINTL 正しい数値を入力してください
				GOTO INPUT_CHILD_NAME_YN
			ENDIF
			PRINTFORML キャラの名前は 『%LOCALS:1%』 でよろしいですか？
			PRINTL [0]いいえ
			PRINTL [1]はい
			$INPUT_CHILD_NAME_YN_1
			INPUT
			IF RESULT == 0
				GOTO INPUT_CHILD_NAME_YN
			ELSEIF RESULT == 1
				CSTR:11 = %LOCALS:1%
				CFLAG:22 = CFLAG:21
			ELSE
				GOTO INPUT_CHILD_NAME_YN_1
			ENDIF
			CALL MESSAGE_CHILD_PRAYER
		ELSEIF RESULT == 0
			CALL MESSAGE_CHILD_GIVEUP
		ELSE
			PRINTL 正しい数値を入力してください
			GOTO INPUT_CHILD_PRAYER
		ENDIF
	ENDIF
ELSEIF MONEY < 5000 || CHARANUM >= (登録最大人数 + 1)
	IF CHARANUM >= (登録最大人数 + 1)
		PRINTL （これ以上仲間を増やせないので選択肢を省略します）
	ELSEIF MONEY < 5000
		PRINTL （資金不足なので選択肢を省略します）
	ENDIF
	CALL MESSAGE_CHILD_ASLYM
	SIF CFLAG:0 == 10
		CALL RECOVER_TO_PARTY,TARGET
ENDIF

;------------------------
;子供の能力決定
;------------------------
;ARG = 母親のＩＤ
@ADD_CHILD,ARG
#DIM PAPA_POWER
#DIM PAPA_CHARA
#DIM CCOUNT
#DIM RACE
#DIM FEAT_SELECT, 300
#DIM FEAT_NUM
#DIMS CHILD_SEX
VARSET LOCAL
PAPA_CHARA = 0
CHILD_SEX = ""
PRINTL 
PRINTL 
IF CFLAG:ARG:0 == 11
	PRINTFORML %PRINT_CALLNAME(ARG)%の子供が異常な速度で育ち、十分に闘えるようになりました。
ELSEIF CFLAG:ARG:22
	CALL TENTACLE_BITVALUE, CFLAG:ARG:22
	CALL TENTACLE_SURVIVE_CHECK, RESULT
	IF RESULT > 0
		PRINTFORML %PRINT_CALLNAME(ARG)%の子供が異常な速度で育ち、幼児期を終えました。
	ELSE
		PRINTFORML %PRINT_CALLNAME(ARG)%と生き別れた子供は、ついに見つかりませんでした……。
		CSTR:ARG:11 = 
		CFLAG:ARG:22 = 0
		CFLAG:ARG:224 = 0
		RETURN
	ENDIF
ENDIF
ADDCHARA 0
FLAG:8 ++
CFLAG:(CHARANUM - 1):240 = FLAG:8
TARGET = CHARANUM -1
CFLAG:7 = CFLAG:ARG:240
;子供の性別がオトコの場合
IF CFLAG:ARG:226 > 0
	TALENT:オトコ = 1
	CHILD_SEX = ♂
ELSE
	CHILD_SEX = ♀
ENDIF
;親が持つ子供の性別情報をリセット
CFLAG:ARG:226 = 0

;名前関係-------------------------------------------------------------------------
IF CSTR:ARG:11 == ""
	$INPUT_CHILD_NAME_YN
	PRINTFORML キャラの名前を自分で決めますか？（性別：%CHILD_SEX%）
	PRINTL [0]いいえ（ランダム生成）
	PRINTL [1]はい（手動入力）
	INPUT
	IF RESULT == 0
		$INPUT_LOOP_CHILD_NAME_RANDOM
		PRINTL [0]日本語で構成
		PRINTL [1]英語で構成
		PRINTL [2]フランス語で構成
		PRINTL [3]ドイツ語で構成
		PRINTL [4]イタリア語で構成
		PRINTL [5]ロシア語で構成
		PRINTL [99]完全ランダム
		INPUT
		LOCAL:99 = RESULT
		$INPUT_LOOP_NAME_KOSEI
		IF LOCAL:99  == 99
			;名前
			LOCAL:1 = RAND:4000 + 12000
			WHILE STR:(LOCAL:1) == ""
				LOCAL:1 = RAND:4000 + 12000
			WEND
			LOCALS:1 = %STR:(LOCAL:1)%
		ELSEIF LOCAL:99  == 0
			;名前
			IF TALENT:オトコ > 0
				LOCAL = RAND:500 + 16000
				WHILE STR:LOCAL == ""
					LOCAL = RAND:500 + 16000
				WEND
				LOCALS:1 = %STR:LOCAL%
			ELSE
				LOCAL = RAND:500 + 12000
				WHILE STR:LOCAL == ""
					LOCAL = RAND:500 + 12000
				WEND
				LOCALS:1 = %STR:LOCAL%
			ENDIF
		ELSEIF LOCAL:99  == 1
			;名前
			LOCAL = RAND:1000 + 12500
			WHILE STR:LOCAL == ""
				LOCAL = RAND:1000 + 12500
			WEND
			LOCALS:1 = %STR:LOCAL%
		ELSEIF LOCAL:99  == 2
			;名前
			LOCAL = RAND:500 + 13500
			WHILE STR:LOCAL == ""
				LOCAL = RAND:500 + 13500
			WEND
			LOCALS:1 = %STR:LOCAL%
		ELSEIF LOCAL:99  == 3
			;名前
			LOCAL = RAND:1000 + 14000
			WHILE STR:LOCAL == ""
				LOCAL = RAND:1000 + 14000
			WEND
			LOCALS:1 = %STR:LOCAL%
		ELSEIF LOCAL:99  == 4
			;名前
			LOCAL = RAND:500 + 15000
			WHILE STR:LOCAL == ""
				LOCAL = RAND:500 + 15000
			WEND
			LOCALS:1 = %STR:LOCAL%
		ELSEIF LOCAL:99  == 5
			;名前
			LOCAL = RAND:500 + 15500
			WHILE STR:LOCAL == ""
				LOCAL = RAND:500 + 15500
			WEND
			LOCALS:1 = %STR:LOCAL%
		ELSE
			GOTO INPUT_LOOP_CHILD_NAME_RANDOM
		ENDIF
	ELSEIF RESULT == 1
		$INPUT_CHILD_NAME_INPUT
		PRINTL キャラの名前を入力してください
		INPUTS
		IF RESULTS == ""
			GOTO INPUT_CHILD_NAME_INPUT
		ELSE
			LOCALS:1 = %RESULTS%
		ENDIF
	ELSE
		PRINTL 正しい数値を入力してください
		GOTO INPUT_CHILD_NAME_YN
	ENDIF
	;親の苗字を引き継ぐ
	LOCALS:2 = %CSTR:ARG:10%
	LOCALS:3 = %CSTR:ARG:10%
	;父親が仲間の場合
	IF CFLAG:ARG:230 <= -100
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:240 == CFLAG:ARG:230 * -1 - 100
				LOCALS:3 = %CSTR:CCOUNT:10%
		NEXT
	ENDIF
	PRINTFORML キャラの名前は 『%LOCALS:1%』 でよろしいですか？
	PRINTL [0]いいえ
	PRINTL [1]はい
	$INPUT_CHILD_NAME_YN_1
	INPUT
	IF RESULT == 0
		GOTO INPUT_CHILD_NAME_YN
	ELSEIF RESULT ==1
		IF LOCALS:2 != "" || LOCALS:3 != ""
			PRINTFORML キャラのフルネームを決めてください。
			PRINTL ･････････････････････････････････････････････････････
			PRINTFORML [0]%LOCALS:1% (苗字なし)
			IF LOCALS:2 != LOCALS:3
				PRINTL
				PRINTL 母姓
			ENDIF
			PRINTFORML [1]%LOCALS:2% %LOCALS:1%
			PRINTFORML [2]%LOCALS:1% %LOCALS:2%
			IF LOCALS:2 != LOCALS:3
				PRINTL
				PRINTL 父姓
				PRINTFORML [3]%LOCALS:3% %LOCALS:1%
				PRINTFORML [4]%LOCALS:1% %LOCALS:3%
			ENDIF
			$INPUT_LOOP_CHILD_NARABI
			INPUT
			IF RESULT == 0
				NAME = %LOCALS:1%
				CSTR:10 = 
			ELSEIF RESULT == 1
				NAME = %LOCALS:2% %LOCALS:1%
				CSTR:10 = %LOCALS:2%
			ELSEIF RESULT == 2
				NAME = %LOCALS:1% %LOCALS:2%
				CSTR:10 = %LOCALS:2%
			ELSEIF RESULT == 3 && LOCALS:2 != LOCALS:3
				NAME = %LOCALS:3% %LOCALS:1%
				CSTR:10 = %LOCALS:3%
			ELSEIF RESULT == 4 && LOCALS:2 != LOCALS:3
				NAME = %LOCALS:1% %LOCALS:3%
				CSTR:10 = %LOCALS:3%
			ELSE
				GOTO INPUT_LOOP_CHILD_NARABI
			ENDIF
			CALLNAME = %LOCALS:1%
		ELSE
			NAME = %LOCALS:1%
			CALLNAME = %LOCALS:1%
		ENDIF
	ELSE
		GOTO INPUT_CHILD_NAME_YN_1
	ENDIF

;幽閉された娘の場合
ELSE
	;親の苗字を引き継ぐ
	CSTR:10 = %CSTR:ARG:10%
	NAME = %CSTR:ARG:11%
	CALLNAME = %CSTR:ARG:11%
	CFLAG:20 = FLAG:10
	CFLAG:21 = CFLAG:ARG:22
ENDIF

;一人称の設定
IF RAND:20 == 0 || TALENT:オトコ > 0
	IF RAND:2 == 0
		CFLAG:8 = 25 + RAND:3
	ELSE
		CFLAG:8 = 30 + RAND:3
	ENDIF
ENDIF
CALL FIRSTSETTING_CHARA_SELFCALL, TARGET



;---------------------------------------------------------------------------------

;種族の設定,親の種族を覚える
FOR LOCAL,201,250
	SIF TALENT:ARG:LOCAL > 0
		CFLAG:231 = LOCAL
NEXT
;母親依存の種族になる
IF TALENTNAME:(CFLAG:231) == "ロボっ子"
	TALENT:人間 = 1	
ELSE
	TALENT:(CFLAG:231) = 1
ENDIF
;フィートの設定開始
CALL SYUZOKU_CHECK,"GET_SYUZOKU_VALUE",TARGET
RACE = RESULT
;種族の開始番号
LOCAL:1 = 0
;フィートの開始番号
LOCAL:2 = 1000
;種族の数
LOCAL:3 = 11
PRINTFORML %PRINT_CALLNAME(TARGET)%の種族は『%TALENTNAME:RACE%』です
PRINTFORML フィートを設定しますか？
PRINTFORML [0]はい
PRINTFORML [1]いいえ
PRINTFORML [2]ランダムに設定する
$INPUT_LOOP_0
INPUT
IF RESULT == 0
	;フィートの初期化
	VARSET FEAT_SELECT
	FOR LOCAL, 100, 300
		SIF FEAT_ABLE_F(RACE + LOCAL:1, LOCAL + LOCAL:2) == 0
			FEAT_SELECT:LOCAL = 0
	NEXT
	CALL LB
	$MASTER_LOOP_1
	;ヴァンパイアならば夜魔の貴族を必ず取得
	SIF RACE == 9
		FEAT_SELECT:110 = 1
	;フィートの数をカウント
	FEAT_NUM = 0
	FOR LOCAL, 100, 300
		SIF FEAT_SELECT:LOCAL > 0
			FEAT_NUM ++
	NEXT
	PRINTFORML %TALENTNAME:(RACE + LOCAL:1)%
	CALL SHORTLINE
	CALL SYUZOKU_INFO, (RACE + LOCAL:1)
	PRINTL 
	PRINTL (取得可能フィート)          選択
	FOR LOCAL, 100, 300
		SIF FEAT_ABLE_F(RACE + LOCAL:1, LOCAL + LOCAL:2) == 0
			CONTINUE
		IF LOCAL < 200
			PRINT 　★
		ELSE
			PRINT 　☆
		ENDIF
		PRINTFORM %TALENTNAME:(LOCAL + LOCAL:2),22,LEFT%  [{LOCAL}]  
		IF LOCAL == 110 && FEAT_SELECT:LOCAL > 0
			SETCOLOR 255,0,0
			PRINT ◎
			RESETCOLOR
			PRINTPLAIN  固定取得
		ELSEIF FEAT_SELECT:LOCAL > 0
			SETCOLOR 255,128,0
			PRINT ○
			RESETCOLOR
		ELSE
			PRINT ×
		ENDIF
		PRINTL 
	NEXT
	PRINTL 
	PRINTFORML 選択したフィート({FEAT_NUM}/3)
	SIF FEAT_NUM > 3
		SETCOLOR 128,128,128
	PRINTL [0]決定
	RESETCOLOR
	$INPUT_LOOP_1
	INPUT
	IF RESULT == 0 && FEAT_NUM <= 3
	ELSEIF RESULT >= 100 && RESULT <= 300 && FEAT_ABLE_F(RACE + LOCAL:1, RESULT + LOCAL:2) > 0
		LOCAL = RESULT
		CALL LB
		CALL FEAT_INFO, (LOCAL + LOCAL:2)
		PRINTL 
		IF FEAT_SELECT:LOCAL > 0
			FEAT_SELECT:LOCAL = 0
		ELSE
			FEAT_SELECT:LOCAL = 1
		ENDIF
		GOTO MASTER_LOOP_1
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF
	;フィートの設定
	FOR LOCAL, 100, 300 
		IF FEAT_SELECT:LOCAL > 0
			TALENT:(LOCAL + LOCAL:2) = 1
		ELSE
			TALENT:(LOCAL + LOCAL:2) = 0
		ENDIF
	NEXT
	;プロフィールに反映
	CALL SET_PROFILE, TARGET
ELSEIF RESULT == 1
	PRINTFORML %PRINT_CALLNAME(TARGET)%にはフィートを設定しません
	PRINTW 
ELSEIF RESULT == 2
	;フィートのランダム設定
	CALL SET_FEAT_DEFAULT, TARGET, RACE
	;プロフィールに反映
	CALL SET_PROFILE, TARGET
	PRINTFORML ランダムにフィートを設定しました
	PRINTW 
ELSE
	GOTO INPUT_LOOP_0
ENDIF
;---------------------------------------------------------------------------------

IF CFLAG:ARG:22 > 0
	;幽閉された娘は教育を受けていないので非戦闘員
	TALENT:変身能力 = -1
	;服は着ていない状態
	CFLAG:40 = 0
	CFLAG:42 = 0
ELSE
	;服を着せる
	CFLAG:40 = 100
	CFLAG:42 = 300
	;変身能力の設定
	CFLAG:10 = CFLAG:ARG:10
	CFLAG:11 = CFLAG:ARG:11
	IF TALENT:ARG:人間 == 1
		IF RAND:4 != 0
			TALENT:変身能力 = 1
			CFLAG:41 = 200
		ENDIF
	ELSE 
		IF RAND:4 == 0
			TALENT:変身能力 = 1
			CFLAG:41 = 200
		ENDIF
	ENDIF
ENDIF
IF TALENT:変身能力 == 1
	PRINTFORML %PRINT_CALLNAME(TARGET)%には変身能力が付きました
	;キャラの変身後名前の設定
	CALL FIRSTSETTING_CHARA_TRANSAFTERNAME, TARGET
	
	;キャラの変身後呼び名の設定
	IF CFLAG:2 == 1
		;キャラの変身後呼び名フラグを立てる
		CFLAG:3 = 1
		CALL FIRSTSETTING_CHARA_TRANSAFTERCALLNAME, TARGET
	ENDIF
	
	;キャラの変身時のかけ声の設定
	CALL FIRSTSETTING_CHARA_TRANSCALL, TARGET
	
	;キャラの名乗り口上の設定
	CALL FIRSTSETTING_CHARA_NANORI, TARGET
ENDIF
IF TALENT:オトコ <= 0
	;処女
	TALENT:処女 = 1
ENDIF
;バイパン
TALENT:パイパン = 1
;未熟
TALENT:未熟 = 1

;父親に見やった才能
;生まれたてはそれ以外鈍感
FOR LOCAL,0,4
	TALENT:(101 + LOCAL * 2) = 1
NEXT
;父親ＩＤを記録
CFLAG:9 = CFLAG:ARG:230

;Ｃ触手
IF CFLAG:ARG:230 == 1
	TALENT:Ｃ敏感 = 1
	TALENT:Ｃ鈍感 = 0
	PAPA_POWER = 10
	SETBIT RELATION:TARGET:TARGET, Ｃ触手
;Ｖ触手
ELSEIF CFLAG:ARG:230 == 2
	TALENT:Ｖ敏感 = 1
	TALENT:Ｖ鈍感 = 0
	PAPA_POWER = 10
	SETBIT RELATION:TARGET:TARGET, Ｖ触手
;Ａ触手
ELSEIF CFLAG:ARG:230 == 3
	TALENT:Ａ敏感 = 1
	TALENT:Ａ鈍感 = 0
	PAPA_POWER = 10
	SETBIT RELATION:TARGET:TARGET, Ａ触手
;Ｂ触手
ELSEIF CFLAG:ARG:230 == 4
	TALENT:Ｂ敏感 = 1
	TALENT:Ｂ鈍感 = 0
	PAPA_POWER = 10
	SETBIT RELATION:TARGET:TARGET, Ｂ触手
;Ｓ触手
ELSEIF CFLAG:ARG:230 == 5
	ABL:マゾっ気 = 3
	PAPA_POWER = 10
	SETBIT RELATION:TARGET:TARGET, Ｓ触手
;Ｐ触手
ELSEIF CFLAG:ARG:230 == 6
	ABL:露出癖 = 3
	PAPA_POWER = 10
	SETBIT RELATION:TARGET:TARGET, Ｐ触手
;Ｈ触手
ELSEIF CFLAG:ARG:230 == 7
	ABL:奉仕精神 = 3
	PAPA_POWER = 10
	SETBIT RELATION:TARGET:TARGET, Ｈ触手
;ラスボス系
ELSEIF CFLAG:ARG:230 >= 100 && CFLAG:ARG:230 < 200
	;全部位敏感
	TALENT:Ｃ敏感 = 1
	SIF TALENT:オトコ <= 0
		TALENT:Ｖ敏感 = 1
	TALENT:Ａ敏感 = 1
	TALENT:Ｂ敏感 = 1
	;全部位から鈍感削除
	TALENT:Ｃ鈍感 = 0
	TALENT:Ｖ鈍感 = 0
	TALENT:Ａ鈍感 = 0
	TALENT:Ｂ鈍感 = 0
	PAPA_POWER = 15
	SETBIT RELATION:TARGET:TARGET, 血統不明
;雑魚敵汎用
ELSEIF CFLAG:ARG:230 >= 200
	PAPA_POWER = 1
;仲間キャラ
ELSEIF CFLAG:ARG:230 <= -100
	PAPA_CHARA = 0
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF CFLAG:CCOUNT:240 == CFLAG:ARG:230 * -1 - 100
			PAPA_CHARA = CCOUNT
	NEXT
	;父親になったキャラの変身能力で分岐
	IF TALENT:PAPA_CHARA:変身能力 == 1
		PAPA_POWER = 6
	ELSEIF TALENT:PAPA_CHARA:変身能力 == -1
		PAPA_POWER = 2
	ELSE
		PAPA_POWER = 4
	ENDIF
;一般人
ELSEIF CFLAG:ARG:230 <= -1
	PAPA_POWER = 0
ELSE
	PRINTL 父親不明!?
	PRINTFORML 触手番号{CFLAG:ARG:230}が父親です
	
ENDIF
;男の場合、Ｖ関係の素質は付かない
IF TALENT:オトコ > 0
	IF TALENT:Ｖ敏感 > 0
		TALENT:Ｃ敏感 = 1
		TALENT:Ｃ鈍感 = 0
	ENDIF
	TALENT:Ｖ敏感 = 0
	TALENT:Ｖ鈍感 = 0
ENDIF

;親の能力に応じて基礎値にボーナス
IF PAPA_POWER > 0
	BASE:TARGET:体力基礎 = BASE:ARG:体力基礎*6/10 + 15 * PAPA_POWER + RAND:(40 * PAPA_POWER)
	BASE:TARGET:気力基礎 = BASE:ARG:気力基礎*6/10 + 15 * PAPA_POWER + RAND:(40 * PAPA_POWER)
	BASE:TARGET:性耐性基礎 = BASE:ARG:性耐性基礎*6/10 + 3 * PAPA_POWER + RAND:(4 * PAPA_POWER)
	BASE:TARGET:攻撃 = MAX(100,BASE:ARG:攻撃*6/10 + 3 * PAPA_POWER + RAND:(4 * PAPA_POWER))
	BASE:TARGET:防御 = MAX(100,BASE:ARG:防御*6/10 + 3 * PAPA_POWER + RAND:(4 * PAPA_POWER))
	BASE:TARGET:敏捷 = MAX(100,BASE:ARG:敏捷*6/10 + 3 * PAPA_POWER + RAND:(4 * PAPA_POWER))
	BASE:TARGET:知性 = MAX(100,BASE:ARG:知性*6/10 + 3 * PAPA_POWER + RAND:(4 * PAPA_POWER))
ELSEIF PAPA_POWER < 0
	PAPA_POWER *= -1
	BASE:TARGET:体力基礎 = BASE:ARG:体力基礎*6/10 - 15 * PAPA_POWER - RAND:(40 * PAPA_POWER)
	BASE:TARGET:気力基礎 = BASE:ARG:気力基礎*6/10 - 15 * PAPA_POWER - RAND:(40 * PAPA_POWER)
	BASE:TARGET:性耐性基礎 = BASE:ARG:性耐性基礎*6/10 - 3 * PAPA_POWER - RAND:(4 * PAPA_POWER)
	BASE:TARGET:攻撃 = MAX(10,BASE:ARG:攻撃*6/10 - 3 * PAPA_POWER - RAND:(4 * PAPA_POWER))
	BASE:TARGET:防御 = MAX(10,BASE:ARG:防御*6/10 - 3 * PAPA_POWER - RAND:(4 * PAPA_POWER))
	BASE:TARGET:敏捷 = MAX(10,BASE:ARG:敏捷*6/10 - 3 * PAPA_POWER - RAND:(4 * PAPA_POWER))
	BASE:TARGET:知性 = MAX(80,BASE:ARG:知性*6/10 - 3 * PAPA_POWER - RAND:(4 * PAPA_POWER))
ELSE
	BASE:TARGET:体力基礎 = BASE:ARG:体力基礎*6/10
	BASE:TARGET:気力基礎 = BASE:ARG:気力基礎*6/10
	BASE:TARGET:性耐性基礎 = BASE:ARG:性耐性基礎*6/10
	BASE:TARGET:攻撃 = MAX(100,BASE:ARG:攻撃*6/10)
	BASE:TARGET:防御 = MAX(100,BASE:ARG:防御*6/10)
	BASE:TARGET:敏捷 = MAX(100,BASE:ARG:敏捷*6/10)
	BASE:TARGET:知性 = MAX(100,BASE:ARG:知性*6/10)
ENDIF

MAXBASE:TARGET:体力 = BASE:体力基礎
MAXBASE:TARGET:気力 = BASE:気力基礎
MAXBASE:TARGET:性耐性 = BASE:性耐性基礎

;子供の基礎値を記録
CFLAG:60 = MAXBASE:0 - SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 0, CSVBASE_F(NO:TARGET,0))
CFLAG:61 = MAXBASE:1 - SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 1, CSVBASE_F(NO:TARGET,1))
CFLAG:62 = MAXBASE:2 - SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 2, CSVBASE_F(NO:TARGET,2))
CFLAG:63 = BASE:10 - SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 10, CSVBASE_F(NO:TARGET,10))
CFLAG:64 = BASE:11 - SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 11, CSVBASE_F(NO:TARGET,11))
CFLAG:65 = BASE:12 - SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 12, CSVBASE_F(NO:TARGET,12))
CFLAG:66 = BASE:13 - SEIKAKU_HOSEI_F(SEIKAKU_CHECK_F(TARGET), 13, CSVBASE_F(NO:TARGET,13))

;一応baseを最大値に
BASE:体力 = MAXBASE:体力
BASE:気力 = MAXBASE:気力
BASE:性耐性 = MAXBASE:性耐性
MAXBASE:体力基礎 = BASE:体力基礎
MAXBASE:気力基礎 = BASE:気力基礎
MAXBASE:性耐性基礎 = BASE:性耐性基礎
MAXBASE:攻撃 = BASE:攻撃
MAXBASE:防御 = BASE:防御
MAXBASE:敏捷 = BASE:敏捷
MAXBASE:知性 = BASE:知性
CALL LEVELSTATUS,TARGET
;性格
IF CFLAG:ARG:230 == 5
	IF RAND:4 == 0
		TALENT:臆病 = 1
	ELSEIF RAND:3 ==0
		TALENT:乱暴者 = 1
	ELSE
		TALENT:(RAND:18+10) = 1
	ENDIF
ELSEIF CFLAG:ARG:230 == 6
	IF RAND:4 == 0
		TALENT:恥ずかしがり屋 = 1
	ELSEIF RAND:3 ==0
		TALENT:古風 = 1
	ELSE
		TALENT:(RAND:18+10) = 1
	ENDIF
ELSE
	TALENT:(RAND:18+10) = 1
ENDIF
;体力などのBASE値の計算
LOCAL = SEIKAKU_CHECK_F(TARGET)
LOCAL:1 = 0
LOCAL:2 = 0
FOR LOCAL:3, 0, 7
	LOCAL:1 = LOCAL:3
	SIF LOCAL:3 > 2
		LOCAL:1 = LOCAL:3 + 7
	LOCAL:2 = BASE:(LOCAL:1)
	;性格補正
	BASE:(LOCAL:1) = SEIKAKU_HOSEI_F(LOCAL, LOCAL:1, LOCAL:2)
	MAXBASE:(LOCAL:1) = BASE:(LOCAL:1)
NEXT

;妊娠中の膣出回数によって精液中毒が決まる
IF CFLAG:ARG:221 == 0
ELSEIF CFLAG:ARG:221 < 10
	ABL:精液中毒 = 1
ELSEIF CFLAG:ARG:221 < 25
	ABL:精液中毒 = 2
ELSEIF CFLAG:ARG:221 < 75
	ABL:精液中毒 = 3
ELSEIF CFLAG:ARG:221 < 200
	ABL:精液中毒 = 4
ELSE
	ABL:精液中毒 = 5
ENDIF
;その他の素質

;貧乳
SIF TALENT:オトコ <= 0
	TALENT:貧乳 = 1
;濡れにくい
TALENT:濡れにくい = 1
;回復はやい
TALENT:回復早い = 1
;小柄
TALENT:小柄 = 1

;距離得意を決める
IF RAND:5 == 0
	LOCAL:2 = RAND:3
	TALENT:(180 + LOCAL:2 * 2) = 1
ENDIF
;距離不得意を決める
IF RAND:5 == 0
	LOCAL:2 = RAND:3
	SIF TALENT:(180 + LOCAL:2 * 2) != 1
		TALENT:(181 + LOCAL:2 * 2) = 1
ENDIF
;触手の血族レベル
MARK:血族補正 = MARK:ARG:血族補正+1
;成長日数
CFLAG:225 = 1

;プロフィールの設定
;ヘアスタイルの設定
CSTR:12 = %STR:(30000 + RAND:9)%
CSTR:13 = %STR:(30100 + RAND:15)%
IF TALENT:変身能力 > 0
	CSTR:14 = %CSTR:13%
	SIF RAND:4 == 0
		CSTR:14 = %STR:(30100 + RAND:15)%
ENDIF

;各種色を両親から継承

FOR LOCAL, 30, 38
	IF CFLAG:ARG:34 > 0 || (PAPA_CHARA > 0 && CFLAG:PAPA_CHARA:34 > 0 && RAND:2 == 0)
		CSTR:LOCAL = %CSTR:ARG:LOCAL%
		LOCAL:LOCAL = 1
	ELSEIF PAPA_CHARA > 0 && CFLAG:PAPA_CHARA:34 > 0
		CSTR:LOCAL = %CSTR:PAPA_CHARA:LOCAL%
		LOCAL:LOCAL = 1
	ELSE
		LOCAL:LOCAL = 0
	ENDIF
NEXT

;LOCAL:0	ループ変数
;LOCAL:1	親と変わるかの判定
;LOCAL:2	色A
;LOCAL:3	色B
FOR LOCAL, 30, 36
	;通常時髪
	IF LOCAL == 30
		LOCAL:1 = RAND:4
		LOCAL:2 = RAND:7
	;変身時髪
	ELSEIF LOCAL == 31
		IF LOCAL:1 == 0 && RAND:4 == 0
			LOCAL:1 = RAND:4
			LOCAL:2 = RAND:7
		ENDIF
	;通常時右目
	ELSEIF LOCAL == 32
		LOCAL:1 = RAND:4
		LOCAL:2 = RAND:7
	;通常時左目
	ELSEIF LOCAL == 33
		;右目が変化した
		IF LOCAL:1 == 0
			LOCAL:1 = (RAND:3 == 0)
		;右目が変化していない
		ELSE
			LOCAL:1 = RAND:3
			LOCAL:2 = RAND:7
		ENDIF
	;変身時右目
	ELSEIF LOCAL == 34
		LOCAL:1 = RAND:4
		IF CSTR:32 == CSTR:33
			IF LOCAL:1 == 0
				LOCAL:2 = RAND:7
				LOCAL:3 = LOCAL:2
			ENDIF
		;元からオッドアイの場合
		ELSE
			;変化なし
			LOCAL:1 = 9
		ENDIF
	;変身時左目
	ELSEIF LOCAL == 35
		;元からオッドアイの場合
		IF LOCAL:1 == 9
			;変化なし
			LOCAL:1 = 9
		;変化ありの場合
		ELSEIF LOCAL:1 == 0
			LOCAL:2 = LOCAL:3
			SIF RAND:3 == 0
				LOCAL:2 = RAND:7
		ELSEIF RAND:3 == 0
			LOCAL:2 = RAND:7
		ENDIF
	ENDIF
	SIF LOCAL:LOCAL != 1
		LOCAL:1 = 0
	IF LOCAL:1 == 0
		IF LOCAL:2 == 0
			CSTR:LOCAL = 赤
		ELSEIF LOCAL:2 == 1
			CSTR:LOCAL = 緑
		ELSEIF LOCAL:2 == 2
			CSTR:LOCAL = 青
		ELSEIF LOCAL:2 == 3
			CSTR:LOCAL = 黄
		ELSEIF LOCAL:2 == 4
			CSTR:LOCAL = 紫
		ELSEIF LOCAL:2 == 5
			CSTR:LOCAL = 橙
		ELSEIF LOCAL:2 == 6
			CSTR:LOCAL = 桃
		ENDIF
	ENDIF
NEXT

;肌の色
IF RAND:4 == 0 || CFLAG:ARG:34 == 0
	IF RAND:4 == 0
		CSTR:36 = 黒
		CSTR:37 = 黒
	ELSEIF RAND:3 == 0
		CSTR:36 = 褐色
		CSTR:37 = 褐色
	ELSEIF RAND:2 == 0
		CSTR:36 = 色白
		CSTR:37 = 色白
	ELSE
		CSTR:36 = 肌色
		CSTR:37 = 肌色
	ENDIF
ENDIF

BASE:年齢 = RAND:5 + 6
MAXBASE:年齢 = BASE:年齢
;スリーサイズ等設定
CALL CHARA_SIZE_DEFAULT, TARGET
BASE:実年齢 = 0

;パーソナリティのランダム設定
IF RAND:12 == 0
	LOCAL = 1
ELSEIF RAND:6 == 0
	LOCAL = 2
ELSE
	LOCAL = 3
ENDIF
$PERSONALITY_0
IF LOCAL > 0
	CSTR:40 = %STR:(30500 + RAND:500)%
	SIF CSTR:40 == "" || CSTR:40 == "CSTR:41" || CSTR:40 == "CSTR:42"
		GOTO PERSONALITY_0
ENDIF
$PERSONALITY_1
IF LOCAL > 1
	CSTR:41 = %STR:(30500 + RAND:500)%
	SIF CSTR:41 == "" || CSTR:41 == "CSTR:40" || CSTR:40 == "CSTR:42"
		GOTO PERSONALITY_1
ENDIF
$PERSONALITY_2
IF LOCAL > 2
	CSTR:42 = %STR:(30500 + RAND:500)%
	SIF CSTR:42 == "" || CSTR:42 == "CSTR:40" || CSTR:42 == "CSTR:41"
		GOTO PERSONALITY_2
ENDIF

;設定画面を１度呼び出す
SIF CFLAG:34 == 0
	CALL GENERATE_BODYLINE, TARGET
CALL SIZE_SETTING, TARGET

;処理終了
IF CFLAG:ARG:0 == 11
	PRINTFORML %PRINT_CALLNAME(ARG)%は育児を終了しました。通常状態に復帰します
	PRINTFORML %PRINT_CALLNAME(TARGET)%が組織に加入しました
	PRINTL 
	CALL RECOVER_TO_PARTY,ARG
	CFLAG:ARG:224 = 0
	CALL RECOVER_TO_PARTY,TARGET

	;相関関係の全探索
	CALL CHECK_ALL_RELATION
ELSEIF CFLAG:ARG:22 > 0
	PRINTFORML %PRINT_CALLNAME(TARGET)%は種付けの対象として認識されたようです。
	PRINTFORML %PRINT_CALLNAME(TARGET)%は幽閉されました・・・
	PRINTW 
	CFLAG:0 = 1
	CSTR:ARG:11 = 
	CFLAG:ARG:22 = 0
	CFLAG:ARG:224 = 0
	;救出されるまで口上は表示されない
	CFLAG:6 = -1

	;相関関係の全探索
	CALL CHECK_ALL_RELATION
ENDIF
RETURN


;-----------------------------
;一次性徴
;-----------------------------
@CHILD_GROW_1
PRINTL 
FONTBOLD
PRINTL 一次性徴
FONTREGULAR
PRINTFORML %PRINT_CALLNAME(TARGET)%は成長して体質が変わった
;未熟を失う
IF TALENT:未熟 == 1
	IF TALENT:オトコ > 0
		PRINTFORML %PRINT_CALLNAME(TARGET)%は射精できるようになった
	ELSE
		PRINTFORML %PRINT_CALLNAME(TARGET)%は妊娠できるようになった
	ENDIF
	TALENT:未熟 = 0
ENDIF
PRINTW 

;パイパン？　25%でパイパンでなくなる
SIF RAND:4 == 0
	TALENT:パイパン = 0
;敏感、鈍感の設定 
;一次性徴で8割の確率で鈍感が取れる
FOR LOCAL,0,4
	SIF RAND:100 < 80
		TALENT:(101 + 2 * LOCAL) = 0
NEXT

;身体の成長
LOCAL = RAND:5 + 1
BASE:年齢 = BASE:年齢 + LOCAL
MAXBASE:年齢 = MAXBASE:年齢 + LOCAL
CALL CHARA_SIZE_DEFAULT, TARGET
DRAWLINE

;-----------------------------
;二次性徴
;-----------------------------
@CHILD_GROW_2
PRINTL 
FONTBOLD
PRINTL 二次性徴
FONTREGULAR

PRINTFORML %PRINT_CALLNAME(TARGET)%は成長して体質がさらに変わった
PRINTW 
;パイパン？　75%でパイパンでなくなる
SIF RAND:4 != 0
	TALENT:パイパン = 0
;鈍感、敏感
FOR LOCAL,0,4
	IF (RAND:100 < 80) && (TALENT:(101 + 2 * LOCAL) == 1)
			TALENT:(101 + 2 * LOCAL) = 0
	ELSEIF (RAND:100 > 80) && (TALENT:(100 + 2 * LOCAL) == 0)
			TALENT:(100 + 2 * LOCAL) = 1
	ENDIF
NEXT
;胸膨らむか
IF TALENT:オトコ <= 0
	RESULT = RAND:100
	IF (RESULT < 70) && (TALENT:貧乳 > 0)
		TALENT:貧乳 -= 1
	ELSEIF (RESULT > 70) && (TALENT:貧乳 == 0) && (TALENT:巨乳 < 5)
		TALENT:巨乳 += 1
	ENDIF
	;濡れやすいか
	RESULT = RAND:100
	IF (RESULT < 70) && (TALENT:濡れにくい == 1)
		TALENT:濡れにくい = 0
	ELSEIF (RESULT > 70) && (TALENT:濡れにくい == 0)
		TALENT:濡れやすい = 1
	ENDIF
ENDIF
;回復変わるか
RESULT = RAND:100
IF (RESULT < 70) && (TALENT:回復早い == 1)
	TALENT:回復早い = 0
ELSEIF (RESULT > 70) && (TALENT:回復早い == 0)
	TALENT:回復遅い = 1
ENDIF
;体の大きさ
RESULT = RAND:100
IF (RESULT < 70) && (TALENT:小柄 > 0)
	TALENT:小柄 = 0
ELSEIF (RESULT > 70) && (TALENT:小柄 == 0)
	TALENT:長身 = 1
ENDIF

;身体の成長
LOCAL = RAND:5 + 1
BASE:年齢 = BASE:年齢 + LOCAL
MAXBASE:年齢 = MAXBASE:年齢 + LOCAL
CALL CHARA_SIZE_DEFAULT, TARGET
DRAWLINE

;-------------------------------
;娘の救出
;-------------------------------
@RESCUE_CHILD,ARG
#DIM CCOUNT
VARSET LOCAL
;母親サーチ
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	SIF CFLAG:CCOUNT:240 == CFLAG:ARG:7
		LOCAL = CCOUNT
NEXT
IF MONEY >= 5000 && CHARANUM < (登録最大人数 + 1)
	DRAWLINE
	PRINTFORML 助け出されたまだ幼い少女が、
	PRINTFORML カタコトで『%CALLNAME:ARG%』と名乗っている。
	PRINTFORML どうやら%CALLNAME:LOCAL%の娘らしいが・・・
	PRINTL 
	PRINTL 仲間に加えるためには養育費として$5000かかります
	PRINTL 仲間に加えますか？
	PRINTL
	PRINTL [0]いいえ
	PRINTL [1]はい
	$INPUT_CHILD_CRADLE
	INPUT
	IF RESULT == 1
		IF CFLAG:LOCAL:0 == 0
			PRINTFORML 母と子の間に言葉は要らなかった。
			PRINTFORML %PRINT_CALLNAME(LOCAL)%は何も言わずに%CALLNAME:ARG%を強く抱きしめ、
			PRINTFORML %CALLNAME:ARG%もまた、そっと抱き返すのだった・・・
		ELSE
			PRINTFORML 行く宛ても無い幼い少女を野に放り出すわけにもいかない。
			PRINTFORML 共に闘う仲間として引き取ることにした・・・
		ENDIF
		PRINTW 
		IF CSTR:ARG:10 != ""
			PRINTFORML キャラのフルネームを決めてください。
			PRINTFORML [0]%CSTR:ARG:10% %CALLNAME:ARG%
			PRINTFORML [1]%CALLNAME:ARG% %CSTR:ARG:10%
			$INPUT_LOOP_CHILD_NARABI
			INPUT
			IF RESULT == 0
				NAME = %CSTR:ARG:10% %CALLNAME:ARG%
			ELSEIF RESULT == 1
				NAME = %CALLNAME:ARG% %CSTR:ARG:10%
			ELSE
				GOTO INPUT_LOOP_CHILD_NARABI
			ENDIF
		ENDIF
		PRINTFORML %CALLNAME:ARG%は驚異的な知能を発揮して短期間で言葉を覚えた！
		MONEY -= 5000
		CALL RECOVER_TO_PARTY,ARG
		CFLAG:ARG:6 = 0
	ELSEIF RESULT == 0
		IF CFLAG:LOCAL:0 == 0
			PRINTFORML %PRINT_CALLNAME(LOCAL)%は%CALLNAME:ARG%が自分の娘だと知りながらも
			PRINTFORML 戦いに巻き込むわけにはいかないと判断し、施設へと預けることにした・・・
		ELSE
			PRINTFORML 触手との厳しい戦いにこんな子供を巻き込むわけにはいかない。
			PRINTFORML %CALLNAME:ARG%と名乗る少女を施設へと預けることにした・・・
		ENDIF
		DELCHARA ARG
		;FOR CHARANUM内でDELする関係上、外部のCCOUNTを減らす
		LOCAL:99 --
	ELSE
		PRINTL 正しい数値を入力してください
		GOTO INPUT_CHILD_CRADLE
	ENDIF
ELSEIF MONEY < 5000 || CHARANUM >= (登録最大人数 + 1)
	DRAWLINE
	PRINTFORML 助け出されたまだ幼い少女が、
	PRINTFORML カタコトで『%CALLNAME:ARG%』と名乗っている。
	PRINTFORML どうやら%CALLNAME:LOCAL%の娘らしいが・・・
	PRINTL 
	IF CHARANUM >= (登録最大人数 + 1)
		PRINTL （これ以上仲間を増やせないので選択肢を省略します）
	ELSEIF MONEY < 5000
		PRINTL （資金不足なので選択肢を省略します）
	ENDIF
	IF CFLAG:LOCAL:0 == 0
		PRINTFORML %PRINT_CALLNAME(LOCAL)%は%CALLNAME:ARG%が自分の娘だと知りながらも
		PRINTFORML 戦いに巻き込むわけにはいかないと判断し、施設へと預けることにした・・・
	ELSE
		PRINTFORML 触手との厳しい戦いにこんな子供を巻き込むわけにはいかない。
		PRINTFORML %CALLNAME:ARG%と名乗る少女を施設へと預けることにした・・・
	ENDIF
	DELCHARA ARG
	;FOR CHARANUM内でDELする関係上、外部のCCOUNTを減らす
	LOCAL:99 --
ENDIF
DRAWLINE
RETURN LOCAL:99






