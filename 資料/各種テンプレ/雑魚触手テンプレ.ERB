;------------------------------------------------------------------------------------------------------------------------
;★雑魚敵用テンプレ
;　　雑魚敵を追加するには幾つかの制約があるので、そのチュートリアルも兼ねたテンプレ用ファイルです。
;　　ユーザー側で雑魚敵を追加したい場合は目を通しておいてください。
;
;------------------------------------------------------------------------------------------------------------------------
;★基本の仕組み
;　　雑魚敵は基本的にボス戦の処理を流用していますが、幾つか異なる点があります。
;　　 1.FLAG:10の値が2になる(この値でボスか雑魚敵か判別している)
;　　 2.SAVESTR:13の中身が"MOB"になる
;　　 3.雑魚敵はSPCOM(IDが1000〜1999の性コマンド)が使用できない
;　　 4.雑魚敵の使用するコマンドは専用の地の文を表示させることができる
;
;　　このうち4.の仕様を利用して雑魚敵ごとの個性を出していくことになります。
;
;------------------------------------------------------------------------------------------------------------------------
;★このテンプレの使い方
;　　雑魚敵には固有のIDを割り振る必要があるので、
;　　既存の雑魚敵と被らない番号を選んでテンプレ関数名の###と置き換えてください。
;　　例えばID = 25の雑魚敵を追加するなら、@TENTACLE_MOB_25となるようにしてください。
;　　機械的な置換を行うとミスが少ないでしょう。
;
;　　また、各種パラメータの下限は 1です。 0にするとエラーになる可能性があるので注意してください。
;
;------------------------------------------------------------------------------------------------------------------------

;==================================================
;【基本】
;--------------------------------------------------
;↓触手の存在確認用関数　何も処理しない
@TENTACLE_MOB_###
;--------------------------------------------------
;名前
@TENTACLE_MOB_###_GETNAME
RESULTS = 触手敵

;--------------------------------------------------
;解説
;雑魚敵とエンカウントした際に表示されます。
;外見などの描写はここで行ってください。
@TENTACLE_MOB_###_DEFENITION
PRINTFORML （触手！）
PRINTFORML （触手！！）
PRINTFORML （触手！！！）


;==================================================
;【ステータス】
;--------------------------------------------------
;HP
;500ポイントにつき１ターン程度生き延びると考えてください。
;ワンパンで倒れるとエロいことができないので多少は時間を与えてあげましょう。
@TENTACLE_MOB_###_HP
LOCAL = 1000
CALL TENTACLE_STATUS_HOSEI, LOCAL, 500
RETURN RESULT

;--------------------------------------------------
;射精限界値
;この値が高いほど射精が遅くなり、持久力が増します。
@TENTACLE_MOB_###_SYASEI
CALL TENTACLE_LEVEL
LOCAL = 480 + RESULT * 5
RETURN LOCAL

;--------------------------------------------------
;搾精ダメージ補正
;この値が高いほど射精時に受ける搾精ダメージが大きくなります。
;自分から大量射精する敵の場合などは値を小さくして自滅を防ぎましょう。
@TENTACLE_MOB_###_SAKUSEI
LOCAL = 200
RETURN LOCAL

;--------------------------------------------------
;油断度閾値
;この値が高いほど油断しにくくなります。
@TENTACLE_MOB_###_YUDAN
LOCAL = 500
RETURN LOCAL

;--------------------------------------------------
;攻撃
;100を基準に、高ければ高いほど打撃による攻撃の威力が上がります。
;ダメージ計算は乗算によって求められるので値を2倍にすればダメージも2倍になります。
@TENTACLE_MOB_###_KOUGEKI
LOCAL = 100
CALL TENTACLE_STATUS_HOSEI, LOCAL
RETURN RESULT

;--------------------------------------------------
;防御
;100を基準に、高ければ高いほど打撃で受けるダメージが減ります。
;HPと相談して値を決めると良いでしょう。
@TENTACLE_MOB_###_BOUGYO
LOCAL = 100
CALL TENTACLE_STATUS_HOSEI, LOCAL
RETURN RESULT

;--------------------------------------------------
;敏捷
;100を基準に、高ければ高いほど命中率と回避率が上がります。
;ボスと違ってHPが少ないので、敏捷が低いと意外と早く沈みます。
@TENTACLE_MOB_###_BINSYOU
LOCAL = 100
CALL TENTACLE_STATUS_HOSEI, LOCAL
RETURN RESULT

;--------------------------------------------------
;知性
;50を基準に、高ければ高いほど攻撃予測が困難になります。
;知性に割り振るプレイヤーは少ないので、このパラメータだけ基準値が低めです。
@TENTACLE_MOB_###_CHISEI
LOCAL = 50
CALL TENTACLE_STATUS_HOSEI, LOCAL,10
RETURN RESULT

;--------------------------------------------------
;近距離
;命中率補正です。近・中・遠の３種類があります。
;基準は100で値が高いほど命中率が上昇します。
@TENTACLE_MOB_###_SHORT
RESULT = 100
RETURN RESULT

;--------------------------------------------------
;中距離
@TENTACLE_MOB_###_MIDDLE
RESULT = 100
RETURN RESULT

;--------------------------------------------------
;遠距離
@TENTACLE_MOB_###_LONG
RESULT = 100
RETURN RESULT

;--------------------------------------------------
;拘束力
;キャラクターの「振り解く」を失敗させやすくする補正です。
;基準は100で値が高いほど成功率が低下します。
@TENTACLE_MOB_###_HOLD
RESULT = 100
RETURN RESULT

;--------------------------------------------------
;PALAM補正 百分率で返す
;性コマンドなどでキャラクターに与えるソースの量を変動させる補正です。
;この補正が高いと性コマンドで与えるダメージが上昇します。
;
;　体力を減少させるソース
;　　苦痛
;　気力を減少させるソース
;　　屈服、恥情、恐怖
;　性耐性を減少させるソース
;　　快C、快V、快A、快B、恭順、欲情
;
@TENTACLE_MOB_###_PALAM_HOSEI
;快C
LOCAL:0 = 100
;快V
LOCAL:1 = 100
;快A
LOCAL:2 = 100
;快B
LOCAL:3 = 100
;潤滑
LOCAL:4 = 50
;恭順
LOCAL:5 = 50
;取得
LOCAL:6 = 50
;欲情
LOCAL:7 = 50
;屈服
LOCAL:8 = 50
;恥情
LOCAL:9 = 50
;苦痛
LOCAL:10 = 50
;恐怖
LOCAL:11 = 50

RETURN LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11



;==================================================
;【専用ルーチン（戦闘）】
;--------------------------------------------------
;キャラを拘束していないときの通常戦闘コマンドの選択ルーチンです。
;弱い敵ほど拘束攻撃を使う確率を上げると良いでしょう。
;
;　0 = ランダム選択
;　1 = 通常攻撃		攻撃力依存の体力ダメージ。
;　2 = 絡みつく		拘束攻撃
;　3 = 粘液攻撃		固定ダメージ攻撃、体力へのダメージが大きめ。
;　4 = 距離を取る	回復行動
;　5 = 使用禁止
;　6 = 邪悪な波動	固定ダメージ攻撃、気力へのダメージが大きめ。
;
@TENTACLE_MOB_###_ATTACK_ROUTINE
LOCAL = RAND:100
IF LOCAL < 25
	RETURN 2
ELSE
	RETURN 0
ENDIF


;==================================================
;【専用ルーチン（性行為）】
;--------------------------------------------------
;拘束時に性コマンドを使用する頻度を決定する思考ルーチンです。
;選択条件を与え、性コマンドのIDをRETURNで返します。例えば、0ならC攻め弱、3ならV攻め強が選択されます。
;
;注意点として、性行為ルーチンでは必ずRETURNで値を返すようにしてください。
;RETURNを省略するとRETURN 0となりC攻め弱が返されてしまいます。-1以下の値を返すことも禁止です。
;また、ID1000〜1999のSPCOMはボス専用なので使用させないでください。バグります。
;
;基本の性コマンドで不足の場合はオリジナルのコマンドを作成することができます。
;その場合はID2000〜2999を利用してください。
;
@TENTACLE_MOB_###_SEX_ROUTINE
LOCAL = RAND:100
;これはサンプルです。
;15%で専用コマンド「ディープキス」実行
IF LOCAL < 15
	RETURN 2000
;それ以外の場合は「種付け」実行
ELSE
	;衣装が残っていれば「脱衣攻撃」を実行
	IF CLOTH_OUTER_PER > 90 || CLOTH_INNER_PER > CLOTH_INNER_DEF
		RETURN 14
	ELSE
		;相手がオトコなら「Ａ攻め強」を実行
		IF TALENT:オトコ > 0
			RETURN 5
		;「種付け」実行
		ELSE
			RETURN 3
		ENDIF
	ENDIF
ENDIF
;必ずルーチン内で0以上の数値をRETURNで返すようにしてください。
;RETURN -1を返すと@ENEMY_ACTIONの汎用ルーチンを呼び出せますが、
;MOB用地の文が存在しないコマンドが選択されるとエラーで強制終了するので使用禁止です。

;==================================================
;【性コマンドレファレンス】
;--------------------------------------------------
;ARG=1　素股焦らし成功時の行動指定
;ARG=2　素股焦らし失敗時の行動指定
;ARG=3　フェラチオ誘発時の行動指定
;
;性コマンドに関する補足情報です。
;主にキャラクターが「素股焦らし」を失敗した際の反撃に用いる挿入系コマンドを指定します。
;挿入系コマンドを持たない雑魚敵なら、RETURN0を返すかこの関数自体をコメントアウトしてください。
;
;コメントアウトまたはRETURN0を返した場合はキャラクターの「素股焦らし」が使用不能になります。
;
@TENTACLE_MOB_###_REACTION_REF, ARG = 0
IF ARG == 1
	;ここは3固定でＯＫ
	RETURN 3
ELSEIF ARG == 2
	;50%でＶ攻め強、50%でＡ攻め強で反撃
	IF RAND:2 == 0
		RETURN 3
	ELSE
		RETURN 5
	ENDIF
ELSEIF ARG == 3
	;フェラチオが可能ならフェラチオを返す
	RETURN 11
ENDIF
RETURN -1



;==================================================
;【性コマンド関係】
;--------------------------------------------------
;ちょっと複雑なので注意して読んでください。
;
;☆コマンド実行の仕組みについて
;まずは性コマンドの分類判定が呼び出され、続いて性コマンドの実行処理が呼び出されます。
;性コマンド実行処理の内部で地の文が呼び出され、ここでようやくゲーム画面に文章が表示されます。
;既存コマンドを利用する場合は性コマンド分類、地の文、追加攻め部位指定のみ用意しておけばＯＫですが、
;専用コマンドを利用する場合はさらにコマンドの実行処理も自分で用意する必要があります。
;
;☆コマンドのＩＤの指定
;関数名の***を性コマンドのIDに置換してください。
;C攻め強ならCOM0、V攻め強ならCOM3、専用コマンド2000番ならCOM2000としてください。
;
;☆専用コマンドのCOMABLE
;ID2000以上の専用コマンドにはCOMABLEがありません。
;使用条件を持たせたい場合は性コマンド選択ルーチン内に条件分岐を直接仕込んでください。
;

;@SEX_TYPE_MOB_###_COM***
;@SEXCOM_MOB_###_COM***,ARG = 0,ARG:1 = 0
;@MESSAGE_MOB_###_COM***, ARG:0, ARG:1
;@SEXCOM_OPTION_MOB_###_COM***
;でひとまとめ
;@SEX_TYPE_MOB_###_COM***+1
;@SEXCOM_MOB_###_COM***+1,ARG = 0,ARG:1 = 0
;@MESSAGE_MOB_###_COM***+1, ARG:0, ARG:1
;@SEXCOM_OPTION_MOB_###_COM***+1
;でひとまとめ。としておいた方が「あの関数書き忘れてた！」というミスを防げて、後で見返しやすい。
;--------------------------------------------------
;性コマンド分類
;性コマンドの種類を判別するための処理です。
;通常(分類なし)、フェラ(フェラ攻撃で反撃可能)、挿入(足コキ攻撃で反撃可能)の３つの内どれかを指定してください。
@SEX_TYPE_MOB_###_COM***
RETURN 通常

;--------------------------------------------------
;コマンド実行処理
;雑魚専用コマンドを追加する場合はIDを2000〜2999にしてください。
;コマンドIDが0〜999までの値ならこの部分は不要です。コメントアウトするなり削除するなりしてください。
;全部自分で設定すると大変なので、既存コマンドの処理をコピペして改変していくのが良さそうです。
;--------------------------------------------------
@SEXCOM_MOB_###_COM***,ARG = 0,ARG:1 = 0
;コマンドのＩＤ
LOCAL:0 = ***

;メイン部位（なし0 Ｃ1 Ｖ2 Ａ4 Ｂ8）
LOCAL:2 = Ｖ

;油断度上昇量
LOCAL:3 = 0

;射精フラグ（なし0 膣2 アナル4 胸8 口16 手32 ワレメ64 暴発128 ビット演算）
LOCAL:4 = 膣+口+手

;射精ゲージ上昇量
LOCAL:5 = 0

;服へのダメージ
LOCAL:6 = 0

;コマンドの強度（弱=0 強=1 SP=2）
;追加責めの効果とPALAM影響度の両方が強化される
LOCAL:7 = 0

;処女喪失の有無（なし0 あり1 あり、地の文なし2）
LOCAL:8 = 0

;特殊効果
;1 = Ｖ挿入防御貫通（Ｖ挿入防御していても挿入される）
;2 = 拡張責め（ＶまたはＡ強度が1以上なら屈服、恥情、苦痛、恐怖が入る）
LOCAL:9 = 0

;PALAM影響度　影響なしなら=0　基本値=5
;-------------------------
;快Ｃ強度
LOCAL:10 = 0
;快Ｖ強度
LOCAL:11 = 0
;快Ａ強度
LOCAL:12 = 0
;快Ｂ強度
LOCAL:13 = 0

;マゾによる屈服、恐怖影響度
LOCAL:14 = 0

;挿入による恭順影響度
LOCAL:15 = 0

;習得影響度
LOCAL:16 = 0

;露出癖による欲情、恥情、恐怖影響度（羞恥プレイなど）
LOCAL:17 = 0

;強制奉仕による屈服影響度（フェラ、イラマチオ、パイズリなど）
LOCAL:18 = 0

;フェラ経験
EXP:フェラ経験 += 0
;放尿経験
EXP:放尿経験 += 0

;共通処理を呼び出し
VARSET RESULT
CALL MOB_CREATE_COM, LOCAL:0, ARG:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18

;触手の行動を記録
TFLAG:20 = LOCAL:0
;行動予約なし
TFLAG:17 = -1

;潤滑基本値
RESULT:4 += 0

;習得基本値
RESULT:6 += 0

;欲情基本値
RESULT:7 += 0

;屈服基本値
RESULT:8 += 0

;恥情基本値
RESULT:9 += 0

;苦痛基本値
RESULT:10 += 0

;恐怖基本値
RESULT:11 += 0

;体力消費基本値
RESULT:12 += 0

;計算へ
CALL PALAM_CAL, RESULT:0, RESULT:1, RESULT:2, RESULT:3, RESULT:4, RESULT:5, RESULT:6, RESULT:7, RESULT:8, RESULT:9, RESULT:10, RESULT:11, RESULT:12


;--------------------------------------------------
;地の文の描写
;動画撮影フラグについて：
;地の文内で立てる必要がある撮影フラグは、愛撫(1)、口淫(2)、挿入(3)、嬲りものにされる(7)の４つです。
;射精(4と5)、精液ぶっかけ(6)、絶頂(8)はSOURCE内で自動的に付与されるので、地の文内でフラグを立てる必要はありません。
;--------------------------------------------------
@MESSAGE_MOB_###_COM***, ARG:0, ARG:1
;サンプル
FONTBOLD
PRINT 全身愛撫
FONTREGULAR
CALL MESSAGE_SEX_STATE, ARG:0, ARG:1, Ｃ
PRINTL 
PRINTFORML 無数の触手が%PRINT_TRANSCALLNAME(TARGET)%の全身に絡みつき、肌の上を這っている・・・
PRINTW 
;口上
TRYCALLFORM KOJO_ROOT(CFLAG:6, "SEX_COM0")
PRINTL 
;動画撮影フラグの処理
IF FLAG:71
	;愛撫されたところを見られた
	SIF (ARG & Ｃ) || (ARG & Ｖ) || (ARG & Ａ) || (ARG & Ｂ)
		SETBIT TFLAG:21,1
ENDIF

;--------------------------------------------------
;追加攻め部位指定
;ビット演算でＶＣＡＢを指定します。
;例えば膣とアナルの追加攻めを行わせたいなら RETURN 6 または RETURN Ｖ+Ａ としてください。
;追加攻めを行わないのであれば-1を返せばＯＫです。
;--------------------------------------------------
@SEXCOM_OPTION_MOB_###_COM***
RETURN -1


;-----------------------------------------------------------------------------------------------------------------------------------------------
;以下の処理は必要に応じて地の文を書きかえるだけでＯＫです。

;--------------------------------------------------
;性コマンド
;--------------------------------------------------
;==================================================
;【衣装を破く】
;--------------------------------------------------
;性コマンド分類
@SEX_TYPE_MOB_###_COM14
RETURN 通常

@MESSAGE_MOB_###_COM14, ARG:0, ARG:1
FONTBOLD
PRINTL 衣装を破く
FONTREGULAR
CALL TENTACLE_ACCESS, "NAME"
IF RAND:3 == 0
	PRINTFORML は触手の先端から濁った色の粘液を勢いよく吐き出した！
	PRINTFORML 異臭のする粘液が%PRINT_TRANSCALLNAME(TARGET)%の全身に降りかかり、
	PRINTFORML 衣装がみるみる溶かされていく・・・
	PRINTL 
	;口上
	TRYCALLFORM KOJO_{CFLAG:6}_SEX_COM14
	PRINTL 
	;状態異常：べとべと
	CALL STATE_CHANGE_BETOBETO, 15
ELSE
	PRINTFORML は%PRINT_TRANSCALLNAME(TARGET)%の全身に絡まり、激しく蠢いている！
	PRINTFORML もみくちゃにされた%PRINT_TRANSCALLNAME(TARGET)%の衣装が引き裂かれていく・・・
	PRINTL 
	;口上
	TRYCALLFORM KOJO_{CFLAG:6}_SEX_COM14
	PRINTL 
ENDIF


;--------------------------------------------------
;通常コマンド
;--------------------------------------------------
;==================================================
;【通常攻撃】
;--------------------------------------------------
;コメントアウトすると通常の地の文が表示される
;MESSAGE_BATTLE.ERBの@MESSAGE_BATTLE_TENTACLE_ATTACKを参考に
;@MESSAGE_BATTLE_MOB_###_TENTACLE_ATTACK

;==================================================
;【体液を吐く】
;--------------------------------------------------
;コメントアウトすると通常の地の文が表示される
:MESSAGE_BATTLE.ERBの@MESSAGE_BATTLE_TENTACLE_TAIEKIを参考に
;@MESSAGE_BATTLE_MOB_###_TENTACLE_TAIEKI

;==================================================
;【邪悪な波動】
;--------------------------------------------------
;コメントアウトすると通常の地の文が表示される
:MESSAGE_BATTLE.ERBの@MESSAGE_BATTLE_TENTACLE_HADOUを参考に
;@MESSAGE_BATTLE_MOB_###_TENTACLE_HADOU

;==================================================
;【絡みつく】
;--------------------------------------------------
@MESSAGE_BATTLE_MOB_###_KARAMITUKU
PRINT 無数の触手幼体たち
IF TFLAG:12 == 3
	PRINTFORM が全身をバネにして、%PRINT_TRANSCALLNAME(TARGET)%の四方八方から飛びかかってきた！
ELSEIF RAND:2 == 0
	PRINTFORM が全身をバネにして、%PRINT_TRANSCALLNAME(TARGET)%の脚元から飛びかかってきた！
ELSE
	PRINTFORM が%PRINT_TRANSCALLNAME(TARGET)%の頭上から飛びかかってきた！
ENDIF
PRINT （
IF TFLAG:11 & 1
	PRINT 近
ELSE
	PRINT 　
ENDIF
IF TFLAG:11 & 2
	PRINT 中
ELSE
	PRINT 　
ENDIF
IF TFLAG:11 & 4
	PRINT 遠
ELSE
	PRINT 　
ENDIF
PRINT ）
IF TFLAG:11 & 8
	PRINT 対空
ELSE
	PRINT 　　
ENDIF
PRINTL 


;==================================================
;【絡みつく成功】
;--------------------------------------------------
@MESSAGE_BATTLE_MOB_###_KARAMITUKU_SUCCESS
PRINTFORM 避けきれなかった%PRINT_TRANSCALLNAME(TARGET)%は
CALL TENTACLE_ACCESS, "NAME"
PRINTFORML に絡み付かれ、
PRINTFORML 群れの中に取り込まれてしまった！





;--------------------------------------------------
;魔法少女側　性コマンド
;--------------------------------------------------
;==================================================
;【手淫攻撃】
;--------------------------------------------------
;コメントアウトすると通常の地の文が表示される
:MESSAGE_SEX.ERBの@MESSAGE_BATTLE_CHARA_SEX_ATTACK100を参考に
@MESSAGE_BATTLE_CHARA_MOB_###_SEX_ATTACK100

;==================================================
;【フェラ攻撃】
;--------------------------------------------------
;コメントアウトすると通常の地の文が表示される
:MESSAGE_SEX.ERBの@MESSAGE_BATTLE_CHARA_SEX_ATTACK101を参考に
@MESSAGE_BATTLE_CHARA_MOB_###_SEX_ATTACK101

;==================================================
;【足コキ攻撃】
;--------------------------------------------------
;コメントアウトすると通常の地の文が表示される
:MESSAGE_SEX.ERBの@MESSAGE_BATTLE_CHARA_SEX_ATTACK102を参考に
@MESSAGE_BATTLE_CHARA_MOB_###_SEX_ATTACK102

;==================================================
;【素股焦らし】
;--------------------------------------------------
;コメントアウトすると通常の地の文が表示される
:MESSAGE_SEX.ERBの@MESSAGE_BATTLE_CHARA_SEX_ATTACK103を参考に
@MESSAGE_BATTLE_CHARA_MOB_###_SEX_ATTACK103

;==================================================
;【搾り取る】
;--------------------------------------------------
;コメントアウトすると通常の地の文が表示される
:MESSAGE_SEX.ERBの@MESSAGE_BATTLE_CHARA_SEX_ATTACK104を参考に
@MESSAGE_BATTLE_CHARA_MOB_###_SEX_ATTACK104

